	;------------------------------------------------------
	; useful equates
	;------------------------------------------------------
wboot	equ	0	; jump to BIOS warm boot routine
bdos	equ 5	; BDOS entry
conout	equ 2	; BDOS console output function
putstr	equ	9	; BDOS print string function
ndoors	equ	100
	;
	org	100h
	lxi	sp,stack  ; set our own stack
	lxi	d,signon  ; print signon message
	mvi	c,putstr
	call bdos
	;
	; generate sequence of squares from 1 to specified limit
	;
gensqr:	
    lxi	h,1	; starting value of square
	lxi	d,3	; starting value of increment
	lxi	b,ndoors+1
sqrs2:	
    call cmpbchl  ; have we exceeded the limit?
	jnc	done	  ; we're finished
	call putdec	  ; otherwise print current square
	mvi	a,' '	  ; separate with a space
	call putchr
	dad	d	      ; square += incrememnt
	inx	d	      ; increment += 2
	inx	d
	jmp	sqrs2	; repeat until finished	
	;
done:	jmp	wboot	; exit to command prompt
	;
	;---------------------------------------------------
	; 16-bit unsigned comparison of HL and BC
	; if HL = BC then Z flag set
	; if HL < BC then CY flag set (NC if HL >= BC)
	;------------------------------------------------------
cmpbchl:
	mov	a,h
	cmp	b
	rnz
	mov	a,l
	cmp	c
	ret
	;---------------------------------------------------
	; console output of char in A register
	;---------------------------------------------------
putchr:	push h
	push d
	push b
	mov	e,a
	mvi	c,conout
	call bdos
	pop	b
	pop	d
	pop	h
	ret
	;---------------------------------------------------
	; Output decimal number to console
	; HL holds 16-bit unsigned binary number to print
	;---------------------------------------------------
putdec: push b
	push d
	push h
	lxi	b,-10
	lxi	d,-1
putdec2:
	dad	b
	inx	d
	jc	putdec2
	lxi	b,10
	dad	b
	xchg
	mov	a,h
	ora	l
	cnz	putdec		; recursive call
	mov	a,e
	adi	'0'
	call putchr
	pop	h
	pop	d
	pop	b
	ret
	;---------------------------------------------------
	; data area and stack
	;---------------------------------------------------
signon: db	'The open doors are: $'
stack	equ	$+128		; 64-level stack
	;
	end
