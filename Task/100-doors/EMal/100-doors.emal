type Door:State
enum do int CLOSED, OPEN end
type Door
model
  int id
  Door:State state
  new by int ←id, Door:State ←state do end
  fun toggle ← <|me.state ← when(me.state æ Door:State.CLOSED, Door:State.OPEN, Door:State.CLOSED)
  fun asText ← <|"Door #" + me.id + " is " + when(me.state æ Door:State.CLOSED, "closed", "open")
end
type Main
^|There are 100 doors in a row that are all initially closed.|^
List doors ← Door[].with(100, <int i|Door(i + 1, Door:State.CLOSED))
^|You make 100 passes by the doors.|^
for int pass ← 0; pass < 100; ++pass
  for int i ← pass; i < 100; i += pass + 1
    doors[i].toggle()
  end
end
^|Which are open, which are closed?|^
for each Door door in doors
  if door.state æ Door:State.CLOSED do continue end
  writeLine(door)
end
