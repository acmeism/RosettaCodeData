/* Solution to the 100 doors problem in PLI-80 */

hundred_doors:
  procedure options (main);

    %replace
      open_door by '1'b,
      closed_door by '0'b,
      numdoors by 100;

    dcl
      doors(1:numdoors) bit(1),
      (i, j) fixed bin(15);

    /* all doors are initially closed */
    do i = 1 to numdoors;
      doors(i) = closed_door;
    end;

    /* cycle through at increasing intervals and flip doors */
    do i = 1 to numdoors;
      j = i;
      do while (j <= numdoors);
        doors(j) = ^doors(j);
        j = j + i;
      end;
    end;

    /* show results - open doors should all be perfect squares */
    put skip list ('The open doors are:');
    do i = 1 to numdoors;
      if doors(i) = open_door then
        put edit (i) (F(4));
    end;

end hundred_doors;
