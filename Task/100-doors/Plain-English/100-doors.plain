A flag list is a doubly linked list with a flag.
A door is a flag list.
A pass is a number.

To run:
  Start up.
  Pass doors given 1000 and 1000 passes.
  Shut down.

To pass doors given a count and some passes:
  Create some doors given the count.
  Loop.
    Add 1 to a counter.
    If the counter is greater than the passes, break.
    Go through the doors given the counter and the passes.
  Repeat.
  Output the states of the doors.
  Destroy the doors.

To create some doors given a count:
  Loop.
    Add 1 to a counter.
    If the counter is greater than the count, exit.
    Allocate memory for a door.
    Clear the door's flag.
    Append the door to the doors.
  Repeat.

To go through some doors given a number and some passes:
  Put 0 into a counter.
  Loop.
    Add the number to the counter.
    If the counter is greater than the passes, exit.
    Pick a door from the doors given the number.
    Invert the door's flag.
  Repeat.

To pick a door from some doors given a number:
  Loop.
    Add 1 to a counter.
    If the counter is greater than the number, exit.
    Get the door from the doors.
    If the door is nil, exit.
  Repeat.

To output the states of some doors:
  Loop.
    Bump a counter.
    Get a door from the doors.
    If the door is nil, exit.
    If the door's flag is set,
      Write "Door " then the counter then " is open"
        then the CRLF string to StdOut;
      Repeat.
    \Write "Door " then the counter then " is closed"
      \then the CRLF string to StdOut.
  Repeat.
