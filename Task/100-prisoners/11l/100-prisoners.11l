F play_random(n)
   V pardoned = 0
   V in_drawer = Array(0.<100)
   V sampler = Array(0.<100)
   L 0 .< n
      random:shuffle(&in_drawer)
      V found = 0B
      L(prisoner) 100
         found = 0B
         L(reveal) random:sample(sampler, 50)
            V card = in_drawer[reveal]
            I card == prisoner
               found = 1B
               L.break
         I !found
            L.break
      I found
         pardoned++
   R Float(pardoned) / n * 100

F play_optimal(n)
   V pardoned = 0
   V in_drawer = Array(0.<100)
   L 0 .< n
      random:shuffle(&in_drawer)
      V found = 0B
      L(prisoner) 100
         V reveal = prisoner
         found = 0B
         L 50
            V card = in_drawer[reveal]
            I card == prisoner
               found = 1B
               L.break
            reveal = card
         I !found
            L.break
      I found
         pardoned++
   R Float(pardoned) / n * 100

V n = 100'000
print(‘ Simulation count: ’n)
print(‘ Random play wins: #2.1% of simulations’.format(play_random(n)))
print(‘Optimal play wins: #2.1% of simulations’.format(play_optimal(n)))
