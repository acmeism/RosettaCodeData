board$ = "123456789ABCDEF0"
solve$ = board$
pos = 16

sub print_board()
    local i, n$
	
    clear screen
    for i = 1 to len(board$)
    	if i = pos then
            print "   ";
    	else
    	    n$ = str$(dec(mid$(board$, i, 1)), "###")
    	    print n$;
        end if
        if mod(i, 4) = 0 print
    next
    print
end sub

sub move(d)
    local new_pos, delta(4)

    delta(1) = 4 : delta(2) = 1 : delta(3) = -1 : delta(4) = -4

    new_pos = pos + delta(d)
    if new_pos >= 1 and new_pos <= 16 and (mod(pos, 4) = mod(new_pos, 4) or floor((pos - 1) / 4) = floor((new_pos - 1) / 4)) then
    	mid$(board$, pos, 1) = mid$(board$, new_pos, 1)
    	mid$(board$, new_pos, 1) = "0"
        pos = new_pos
    end if
end sub

for i = 1 to 100 : move(int(ran(4))+1) : next
do
    print_board()
    if board$ = solve$ break
    c = ((instr("esc  up   left rightdown ", inkey$) - 1) / 5)
    if c < 1 break
    move(c)
loop
print "solved!\n"
