class TwentyOne {
  @quit : Bool;
  @player_total : Int;
  @computer_total : Int;

  function : Main(args : String[]) ~ Nil {
    TwentyOne->New()->Play();
  }

  New() {
  }

  method : Play() ~ Nil {
    player_first := Int->Random(1) = 1;

    "Enter 'q' to quit\n==="->PrintLine();
    do {
      if(player_first) {
        PlayerTurn();
        if(<>@quit) {
          "---"->PrintLine();
          ComputerTurn();
        };
      }
      else {
        ComputerTurn();
        "---"->PrintLine();
        PlayerTurn();
      };
      "==="->PrintLine();
    }
    while(<>@quit);
  }

  method : ComputerTurn() ~ Nil {
    input := Int->Random(1, 3);

    "Computer choose: {$input}"->PrintLine();
    @computer_total += input;

    if(@computer_total = 21) {
      "Computer Wins!"->PrintLine();
      @quit := true;
    }
    else if(@computer_total > 21) {
      "Computer Loses."->PrintLine();
      @quit := true;
    }
    else {
      "Computer total is {$@computer_total}."->PrintLine();
    };
  }

  method : PlayerTurn() ~ Nil {
    input := GetInput();

    if(input = -1) {
      "Quit"->PrintLine();
      @quit := true;
    }
    else if(input = 0) {
      "Invalid Input!"->PrintLine();
    }
    else {
      @player_total += input;
    };

    if(@player_total = 21) {
      "Player Wins!"->PrintLine();
      @quit := true;
    }
    else if(@player_total > 21) {
      "Player Loses."->PrintLine();
      @quit := true;
    }
    else {
      "Player total is {$@player_total}."->PrintLine();
    };
  }

  function : GetInput() ~ Int {
    "Choosing a number beween 1-3: "->Print();

    input := System.IO.Console->ReadString();
    if(input->Size() = 1) {
      if(input->Get(0) = 'q') {
        return -1;
      };

      return input->ToInt();
    };

    return 0;
  }
}
