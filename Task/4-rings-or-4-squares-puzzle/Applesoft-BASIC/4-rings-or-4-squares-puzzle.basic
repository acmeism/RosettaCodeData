 100 TRUE =  NOT FALSE
 110 PLO = 1:PHI = 7:PUNIQUE = TRUE:PSHOW = TRUE: GOSUB 150"FOURSQUARES"
 120 PLO = 3:PHI = 9:PUNIQUE = TRUE:PSHOW = TRUE: GOSUB 150"FOURSQUARES"
 130 PLO = 0:PHI = 9:PUNIQUE = FALSE:PSHOW = FALSE: GOSUB 150"FOURSQUARES"
 140  END
 150 LO = PLO
 160 HI = PHI
 170 UNIQUE = PUNIQUE
 180 SHOW = PSHOW
 190 S = 0: REM SOLUTIONS
 200  PRINT
 210  GOSUB 270"ACD"
 220  PRINT
 230  PRINT S" ";
 240  IF  NOT UNIQUE THEN  PRINT "NON-";
 250  PRINT "UNIQUE SOLUTIONS IN "LO" TO "HI
 260  RETURN
 270  FOR C = LO TO HI
 280      FOR D = LO TO HI
 290          IF ( NOT UNIQUE) OR (C <  > D) THEN A = C + D: IF (A >  = LO) AND (A <  = HI) AND (( NOT UNIQUE) OR ((C <  > 0) AND (D <  > 0))) THEN  GOSUB 320"GE"
 300  NEXT D,C
 310  RETURN
 320  FOR E = LO TO HI
 330      IF ( NOT UNIQUE) OR ((E <  > A) AND (E <  > C) AND (E <  > D)) THEN G = D + E: IF (G >  = LO) AND (G <  = HI) AND (( NOT UNIQUE) OR ((G <  > A) AND (G <  > C) AND (G <  > D) AND (G <  > E))) THEN  GOSUB 360"BF"
 340  NEXT E
 350  RETURN
 360  FOR F = LO TO HI
 370      IF (( NOT UNIQUE) OR ((F <  > A) AND (F <  > C) AND (F <  > D) AND (F <  > G) AND (F <  > E))) THEN  GOSUB 400
 380  NEXT F
 390  RETURN
 400 B = E + F - C: IF ((B >  = LO) AND (B <  = HI) AND (( NOT UNIQUE) OR ((B <  > A) AND (B <  > C) AND (B <  > D) AND (B <  > G) AND (B <  > E) AND (B <  > F)))) THEN S = S + 1: IF (SHOW) THEN  PRINT A" "B" "C" "D" "E" "F" "G
 410  RETURN
