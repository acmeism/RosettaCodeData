import system'routines;
import extensions;
import extensions'routines;
import extensions'text;

extension bottleOp
{
    bottleDescription()
        = self.Printable + (self != 1).iif(" bottles"," bottle");

    bottleEnumerator() = new Variable(self).doWith:(n)
    {
        ^ new Enumerator:
        {
            bool next() = n > 0;

            get() = new StringWriter()
                    .printLine(n.bottleDescription()," of beer on the wall")
                    .printLine(n.bottleDescription()," of beer")
                    .printLine("Take one down, pass it around")
                    .printLine((n.reduce:1).bottleDescription()," of beer on the wall");

            reset() {}

            enumerable() = __target;
        }
    };
}

public program()
{
    var bottles := 99;

    bottles.bottleEnumerator().forEach:printingLn
}
