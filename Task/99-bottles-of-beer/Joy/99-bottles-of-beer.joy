LIBRA

	_beerlib == true;

HIDE
	beer == "of beer " putchars;
	wall == "on the wall" putchars;
	take1 == "Take one down and pass it around, " putchars;
	dup3 == dup dup dup;
	comma == ", " putchars;
	period == ". " putchars;
	bottles == [small]
		[[null] [pop "no more bottles " putchars] [put "bottle " putchars] ifte]
		[put "bottles " putchars] ifte;
	sing-verse == dup3 bottles beer wall comma
		bottles beer ".\n" putchars
		take1 pred bottles beer wall period newline newline;
	sing-verse-0 == "No more bottles of beer on the wall, no more bottles of beer\n" putchars
			"Go to the store and buy some more, " putchars
			99 bottles pop beer wall period newline

IN
	(* n -- *)
	sing-verses == [null]
			[sing-verse-0]
			[sing-verse pred] tailrec
END.

99 sing-verses.
