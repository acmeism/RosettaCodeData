;
;          99 BOTTLES OF BEER
;          WRITTEN  BY:  BILL GUNSHANNON
;

           .MCALL  .PRINT .EXIT
           .RADIX  10



MESG1:
CNUM1:     .ASCII  "  "
           .ASCII  " BOTTLES OF BEER ON THE WALL, "
CNUM2:     .ASCII  "  "
           .ASCII  " BOTTLES OF BEER."<13><10>
           .ASCII  "TAKE ONE DOWN AND PASS IT AROUND, "
CNUM3:     .ASCII  "  "
           .ASCIZ  " BOTTLES OF BEER ON THE WALL."<13><10><10>

MESG2:     .ASCII  " 2 BOTTLES OF BEER ON THE WALL, "
           .ASCII  " 2 BOTTLES OF BEER."<13><10>
           .ASCII  "TAKE ONE DOWN AND PASS IT AROUND, "
           .ASCIZ  " 1 BOTTLE OF BEER ON THE WALL."<13><10><10>

MESG3:     .ASCII  " 1 BOTTLE OF BEER ON THE WALL, "
           .ASCII  " 1 BOTTLE OF BEER."<13><10>
           .ASCII  "TAKE ONE DOWN AND PASS IT AROUND, "
           .ASCIZ  "NO MORE BOTTLES OF BEER ON THE WALL."<13><10><10>

MESG4:     .ASCII  "NO MORE BOTTLES OF BEER ON THE WALL, "
           .ASCII  "NO MORE BOTTLES OF BEER."<13><10>
           .ASCII  "GO TO THE STORE AND BUY SOME MORE, "
           .ASCIZ  "99 BOTTLES OF BEER ON THE WALL."<13><10><10>

           .EVEN

COUNT1:    .WORD   99      ;  STARTING NUMBER OF BOTTLES
PV:        .WORD   10.,1,  ;  POSITION VALUES FOR 2 DIGITS


START:
           MOV     COUNT1, R0
           MOV     R0, R5
           DEC     R5
AGAIN:

           MOV     #CNUM1, R1

;       CONVERT NUMBER OF BOTTLES TO ASCII FOR THE FIRST FIELD

           MOV     R0,-(SP)
           MOV     R1,-(SP)
           MOV     R2,-(SP)
           MOV     R3,-(SP)
           MOV     R4,-(SP)
           MOV     R5,-(SP)

           MOV     #PV, R3
           MOV     #2, R2
A1$:       MOV     #-1,R4
A2$:       INC     R4
           SUB     (R3), R0
           BCC     A2$
           ADD     (R3)+, R0
           ADD     #48, R4
           MOVB    R4, (R1)+
A21$:      SOB     R2, A1$

;   REMOVE LEADING ZERO - JUST LOOKS BETTER THAT WAY

           MOV     #CNUM1, R1
           CMPB    (R1), #48
           BNE     CONT1
           MOVB    #32, (R1)
CONT1:     MOV     #CNUM2, R0

;    MOVE THE ASCII NUMBER TO FIELD TWO RATHER THAN COMPUTING IT AGAIN

           MOVB    (R1)+, (R0)+
           MOVB    (R1), (R0)

           MOV     R5, R0
           MOV     #CNUM3, R1

;       CONVERT NUMBER OF BOTTLES TO ASCII FOR THE THIRD FIELD

           MOV     #PV, R3
           MOV     #2, R2
B1$:       MOV     #-1,R4
B2$:       INC     R4
           SUB     (R3), R0
           BCC     B2$
           ADD     (R3)+, R0
           ADD     #48, R4
           MOVB    R4, (R1)+
B21$:      SOB     R2, B1$

;   REMOVE LEADING ZERO - JUST LOOKS BETTER THAT WAY

           MOV     #CNUM3, R1
           CMPB    (R1), #48
           BNE     CONT2
           MOVB    #32, (R1)
CONT2:


           MOV     (SP)+,R5
           MOV     (SP)+,R4
           MOV     (SP)+,R3
           MOV     (SP)+,R2
           MOV     (SP)+,R1
           MOV     (SP)+,R0


;       HANDLE SPECIAL GRAMMER CASES

           CMP     R0, #2
           BNE     ONE
           .PRINT  #MESG2
           BR      DONE
ONE:       CMP     R0, #1
           BNE     GREATER
           .PRINT  #MESG3
           BR      DONE
GREATER:
           .PRINT  #MESG1

DONE:

;        DECREMENT THE COUNT AND START AGAIN

           MOV     R5, R0
           DEC     R5
           TST     R0
           BNE     AGAIN

;    OUT OF BEER, SEND THEM TO THE STORE

           .PRINT  #MESG4

;   CLEAN UP AND GO BACK TO KMON

           .EXIT


           .END     START
