     H/TITLE 99 Bottles of Beer on the Wall - RPGIII (IBM System/38)
     F********************************************************************
     F*
     F* Lines with an asterisk in column 7 (such as this one) are comments.
     F*
     F* The character in column 6 specifies the form type. Before the first
     F* use of each form type is a comment with the template for the form.
     F*
     F********************************************************************
     F* File Description Specifications
     F*
     F* Specify a default printer file for output, program described,
     F* 132 columns wide, and an overflow indicator OV. The overflow
     F* indicator is turned on when data is output to the last usable
     F* line (line 60, for the default printer file QSYSPRT).
     F*
     F*ilenameIPEAF....RlenLK1AIOvKlocEDevice+......KExit++Entry+A....U1
     FQSYSPRT O   F     132     OV     PRINTER
     F*
     C********************************************************************
     C* If there were an input file, the RPG cycle would automatically
     C* execute the output lines; since there is no input file, output
     C* lines are produced as "exception" output with the EXCPT opcode.
     C*
     C* Calculation Specifications
     C* vvvvvvvvv-- conditionally executes lines if indicators are on/off
     C*0N01N02N03Factor1+++OpcdeFactor2+++ResultLenDHHiLoEqComments+++++++
     C*
     C* Zero, then add 99 to a variable named #BOTLS, which is defined
     C* as packed decimal, 3 positions, 0 decimal places.
     C                     Z-ADD99        #BOTLS  30
     C*
     C* Do until #BOTLS = 0. Each loop produces one complete verse.
     C           #BOTLS    DOUEQ0
     C*
     C* When the overflow indicator is turned on, start a new page.
     C* The indicator is automatically turned on when the overflow
     C* line is printed on, and it is automatically turned off
     C* when the heading is printed.
     C   OV                EXCPTNEWPAG
     C*
     C* Print exception lines with names "LYRIC1" and "LYRIC2".
     C                     EXCPTLYRIC1
     C                     EXCPTLYRIC2
     C*
     C* Subtract 1 from #BOTLS, and turn on indicator LR (Last Record)
     C* if the result is equal to zero (Eq); if LR is on, the program
     C* will terminate at the end of the current cycle.
     C                     SUB  1         #BOTLS         LR
     C*
     C* Compare #BOTLS to 1, and turn on indicator 01 if equal.
     C           #BOTLS    COMP 1                        01
     C*
     C* Print exception lines with name "LYRIC1".
     C                     EXCPTLYRIC1
     C*
     C* If LR is not on, print exception lines with name "SKIPLN"
     C* (which, in this case, will produce a blank line between verses).
     C  NLR                EXCPTSKIPLN
     C*
     C                     END                             end do
     C*
     O********************************************************************
     O* Output Specifications
     O*       .-- E means "Exception" ("H" Header, "D" Detail, "T" Total)
     O*       v   vv-- before printing, skip to line # 1 (of the next page)
     O*ame++++DFBASbSaN01N02N03Excnam...........................................
     OQSYSPRT E    1           NEWPAG
     O*          v-- space 1 line after printing
     O        E  1             LYRIC1
     O*
     O*               .........-- conditionally print lines if indicators on/off
     O*               vvvvvvvvv      v-- edit code Z (suppress leading Zeroes)
     O*...............N01N02N03Field+YBEnd+PConstant/editword+++++++++
     O                         #BOTLSZ    3
     O                N01NLR             11 'bottles'
     O                N01NLR             31 'of beer on the wall'
     O                 01NLR             10 'bottle'
     O                 01NLR             30 'of beer on the wall'
     O                 LR                16 'No more bottles'
     O                 LR                36 'of beer on the wall'
     O        E  1             LYRIC2
     O                         #BOTLSZ    3
     O                N01                19 'bottles of beer'
     O                 01                18 'bottle of beer'
     O        E  1             LYRIC2
     O                                   14 'Take one down'
     O        E  1             LYRIC2
     O                                   15 'Pass it around'
     O        E  1             SKIPLN
