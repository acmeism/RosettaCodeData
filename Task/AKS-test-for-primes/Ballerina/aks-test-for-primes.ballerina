import ballerina/io;

int[] e = "²³⁴⁵⁶⁷".toCodePointInts();

function bc(int p) returns int[] {
    int[] c = [];
    c.setLength(p + 1);
    int r = 1;
    int half = p / 2;
    foreach int i in 0...half {
        c[i] = r;
        c[p - i] = r;
        r = r * (p - i) / (i + 1);
    }
    foreach int i in int:range(p - 1, -1, -2) {
        c[i] = -c[i];
    }
    return c;
}

function pp(int[] c) returns string|error {
    if c.length() == 1 { return c[0].toString(); }
    int p = c.length() - 1;
    string s = "";
    if c[p] != 1 { s = c[p].toString(); }
    foreach int i in int:range(p, 0, -1) {
        s += "x";
        if i != 1 { s += check string:fromCodePointInt(e[i - 2]); }
        int d = c[i - 1];
        if d < 0 {
            s += " - " + (-d).toString();
        } else {
            s += " + " + d.toString();
        }
    }
    return s;
}

function aks(int p) returns boolean {
    int[] c = bc(p);
    c[p] -= 1;
    c[0] += 1;
    foreach int d in c {
        if d % p != 0 { return false; }
    }
    return true;
}

public function main() {
    foreach int p in 0...7 {
        io:println(p, ":  ", pp(bc(p)));
    }
    io:println("\nAll primes under 50:");
    foreach int p in 2...49 {
        if aks(p) { io:print(p, " "); }
    }
    io:println();
}
