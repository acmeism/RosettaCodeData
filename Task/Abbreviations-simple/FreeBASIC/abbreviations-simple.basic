Function Token(cad As String, tokens() As String) As Integer
    Dim As Integer n = 0
    Dim As String temp = ""
    Dim As Integer i

    For i = 1 To Len(cad)
        If Mid(cad, i, 1) <> " " Then
            temp &= Mid(cad, i, 1)
        Elseif temp <> "" Then
            n += 1
            Redim Preserve tokens(1 To n)
            tokens(n) = temp
            temp = ""
        End If
    Next

    If temp <> "" Then
        n += 1
        Redim Preserve tokens(1 To n)
        tokens(n) = temp
    End If

    Return n
End Function

Dim As String c = "add 1  alter 3  backup 2  bottom 1  Cappend 2  change 1  Schange  Cinsert 2 Clast 3" & _
" compress 4 copy 2 count 3 Coverlay 3 cursor 3  delete 3 Cdelete 2  down 1  duplicate" & _
" 3 xEdit 1 expand 3 extract 3  find 1 Nfind 2 Nfindup 6 NfUP 3 Cfind 2 findUP 3 fUP 2" & _
" forward 2  get  help 1 hexType 4  input 1 powerInput 3  join 1 split 2 spltJOIN load" & _
" locate 1 Clocate 2 lowerCase 3 upperCase 3 Lprefix 2  macro  merge 2 modify 3 move 2" & _
" msg  next 1 overlay 1 parse preserve 4 purge 3 put putD query 1 quit  read recover 3" & _
" refresh renum 3 repeat 3 replace 1 Creplace 2 reset 3 restore 4 rgtLEFT right 2 left" & _
" 2  save  set  shift 2  si  sort  sos  stack 3 status 4 top  transfer 3  type 1  up 1"
c = Ucase(c)

Dim As String chunk()
Dim As Integer i, j, n, v

n = Token(c, chunk())

Dim As Integer lonc(1 To n)

For i = 1 To n - 1
    v = Val(chunk(i + 1))
    If v Then
        lonc(i) = v
        chunk(i + 1) = ""
    Else
        lonc(i) = Len(chunk(i))
    End If
Next

Dim As String test = "riG   rePEAT copies   put mo   rest    types   fup.    6       poweRin"
test = Ucase(test)

Dim As String w()
Dim As Integer x = Token(test, w())

Dim As Integer lonw(1 To x)

For i = 1 To x
    lonw(i) = Len(w(i))
Next

Dim As String r = ""

For j = 1 To x
    Dim As String p = ""
    For i = 1 To n
        Dim As String lc = Left(chunk(i), lonw(j))
        Dim As String lw = w(j)
        If (lw = lc) And (lonc(i) <= lonw(j)) Then
            p = chunk(i)
            Exit For
        End If
    Next
    If p = "" Then p = "*error*"
    r &= " " & p
Next

Print r

Sleep
