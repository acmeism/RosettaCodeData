V grid = [[0] * 10] * 10
grid[5][5] = 64

print(‘Before:’)
L(row) grid
   print(row.map(c -> ‘#3’.format(c)).join(‘’))

F simulate(&grid)
   L
      V changed = 0B
      L(arr) grid
         V ii = L.index
         L(val) arr
            V jj = L.index
            I val > 3
               grid[ii][jj] -= 4
               I ii > 0
                  grid[ii - 1][jj]++
               I ii < grid.len - 1
                  grid[ii + 1][jj]++
               I jj > 0
                  grid[ii][jj - 1]++
               I jj < grid.len - 1
                  grid[ii][jj + 1]++
               changed = 1B
      I !changed
         L.break

simulate(&grid)

print("\nAfter:")
L(row) grid
   print(row.map(c -> ‘#3’.format(c)).join(‘’))

grid = [[0] * 65] * 65
grid[32][32] = 64 * 64

simulate(&grid)

V ppm = File(‘sand_pile.ppm’, WRITE)
ppm.write_bytes(("P6\n#. #.\n255\n".format(grid.len, grid.len)).encode())
V colors = [[Byte(0),   0, 0],
            [Byte(255), 0, 0],
            [Byte(0), 255, 0],
            [Byte(0), 0, 255]]
L(row) grid
   L(c) row
      ppm.write_bytes(colors[c])
