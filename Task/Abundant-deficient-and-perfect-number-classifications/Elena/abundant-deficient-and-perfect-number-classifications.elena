import extensions;

classifyNumbers(int bound, ref int abundant, ref int deficient, ref int perfect)
{
    int a := 0;
    int d := 0;
    int p := 0;
    int[] sum := new int[](bound + 1);

    for(int divisor := 1; divisor <= bound / 2; divisor += 1)
    {
        for(int i := divisor + divisor; i <= bound; i += divisor)
        {
            sum[i] := sum[i] + divisor
        }
    };

    for(int i := 1; i <= bound; i += 1)
    {
        int t := sum[i];

        if (sum[i]<i)
        {
            d += 1
        }
        else
        {
            if (sum[i]>i)
            {
                a += 1
            }
            else
            {
                p += 1
            }
        }
    };

    abundant := a;
    deficient := d;
    perfect := p
}

public program()
{
    int abundant := 0;
    int deficient := 0;
    int perfect := 0;
    classifyNumbers(20000, ref abundant, ref deficient, ref perfect);
    console.printLine("Abundant: ",abundant,", Deficient: ",deficient,", Perfect: ",perfect)
}
