num_deficient%=0
num_perfect%=0
num_abundant%=0
'
FOR current%=1 TO 20000
  sum_divisors%=@sum_proper_divisors(current%)
  IF sum_divisors%<current%
    num_deficient%=num_deficient%+1
  ELSE IF sum_divisors%=current%
    num_perfect%=num_perfect%+1
  ELSE ! sum_divisors%>current%
    num_abundant%=num_abundant%+1
  ENDIF
NEXT current%
'
' Display results on a window
'
OPENW 1
CLEARW 1
PRINT "Number deficient ";num_deficient%
PRINT "Number perfect   ";num_perfect%
PRINT "Number abundant  ";num_abundant%
~INP(2)
CLOSEW 1
'
' Compute the sum of proper divisors of given number
'
FUNCTION sum_proper_divisors(n%)
  LOCAL i%,sum%,root%
  '
  IF n%>1 ! n% must be 2 or higher
    sum%=1 ! start with 1
    root%=SQR(n%) ! note that root% is an integer
    ' check possible factors, up to sqrt
    FOR i%=2 TO root%
      IF n% MOD i%=0
        sum%=sum%+i% ! i% is a factor
        IF i%*i%<>n% ! check i% is not actual square root of n%
          sum%=sum%+n%/i% ! so n%/i% will also be a factor
        ENDIF
      ENDIF
    NEXT i%
  ENDIF
  RETURN sum%
ENDFUNC
