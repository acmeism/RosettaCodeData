  10 LET abundant=0: LET deficient=0: LET perfect=0
  20 FOR j=1 TO 20000
  30 GO SUB 120
  40 IF sump<j THEN LET deficient=deficient+1: GO TO 70
  50 IF sump=j THEN LET perfect=perfect+1: GO TO 70
  60 LET abundant=abundant+1
  70 NEXT j
  80 PRINT "Perfect: ";perfect
  90 PRINT "Abundant: ";abundant
 100 PRINT "Deficient: ";deficient
 110 STOP
 120 IF j=1 THEN LET sump=0: RETURN
 130 LET sum=1
 140 LET root=SQR j
 150 FOR i=2 TO root
 160 IF j/i=INT (j/i) THEN LET sum=sum+i: IF (i*i)<>j THEN LET sum=sum+j/i
 170 NEXT i
 180 LET sump=sum
 190 RETURN
