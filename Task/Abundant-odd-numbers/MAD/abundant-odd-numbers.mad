            NORMAL MODE IS INTEGER

            INTERNAL FUNCTION(ND)
            ENTRY TO ODDSUM.
            SUM = 1
            SQN = SQRT.(ND)
            THROUGH CHECK, FOR CN=3, 2, CN.G.SQN
            TM = ND/CN
            WHENEVER TM*CN.E.ND
                SUM = SUM + CN
                WHENEVER TM.NE.CN, SUM = SUM + TM
CHECK       END OF CONDITIONAL
            FUNCTION RETURN SUM
            END OF FUNCTION

            SEEN = 0
            NUM = 1

            THROUGH SHOW, FOR NUM=1, 2, SEEN.G.1000
            WHENEVER NUM.L.ODDSUM.(NUM)
                SEEN = SEEN + 1
                WHENEVER SEEN.LE.25 .OR. SEEN.E.1000,
          0         PRINT FORMAT OUTFMT,SEEN,NUM,ODDSUM.(NUM)
SHOW        END OF CONDITIONAL

BILION      THROUGH BILION, FOR NUM=NUM, 2,
          0     NUM.G.1000000000 .AND. NUM.L.ODDSUM.(NUM)

            PRINT FORMAT HUGENO,NUM,ODDSUM.(NUM)

            VECTOR VALUES OUTFMT =
          0     $4HNO. ,I4,S1,3HIS ,I6,S1,7HDIVSUM ,I6*$
            VECTOR VALUES HUGENO =
          0     $25HFIRST ABOVE 1 BILLION IS ,I10,S1,7HDIVSUM ,I10*$
            END OF PROGRAM
