10  'Almost prime
20  FOR k = 1 TO 5
30  PRINT "k = "; k; ":";
40  LET i = 2
50  LET c = 0
60  WHILE c < 10
70   LET an = i: GOSUB 150
80   IF iskprime <> 0 THEN PRINT USING " ###"; i; : LET c = c + 1
90   LET i = i + 1
100 WEND
110 PRINT
120 NEXT k
130 END
140 ' Check if n (AN) is a k (K) prime
150 LET f = 0
160 FOR j = 2 TO an
170 WHILE an MOD j = 0
180   IF f = k THEN LET iskprime = 0: RETURN
190   LET f = f + 1
200   LET an = INT(an / j)
210 WEND
220 NEXT j
230 LET iskprime = (f = k)
240 RETURN
