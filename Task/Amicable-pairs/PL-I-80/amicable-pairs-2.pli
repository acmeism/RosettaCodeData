amicable: procedure options (main);

    %replace
      search_limit by 20000;

    dcl sumf( 1 : search_limit ) fixed bin;
    dcl (a, b, found) fixed bin;

    put skip list ('Searching for amicable pairs up to ');
    put edit (search_limit) (f(5));

    do a = 1 to search_limit; sumf( a ) = 1; end;
    do a = 2 to search_limit;
        do b = a + a to search_limit by a;
            sumf( b ) = sumf( b ) + a;
        end;
    end;

    found = 0;
    do a = 2 to search_limit;
      b = sumf(a);
      if (b > a) then
        do;
          if (sumf(b) = a) then
            do;
              found = found + 1;
              put skip edit (a,b) (f(7));
            end;
        end;
    end;
    put skip list (found, ' pairs were found');
    stop;

end amicable;
