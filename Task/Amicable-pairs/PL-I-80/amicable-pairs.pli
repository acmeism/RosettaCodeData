amicable: procedure options (main);

    %replace
      search_limit by 20000;

    dcl (a, b, found) fixed bin;

    put skip list ('Searching for amicable pairs up to ');
    put edit (search_limit) (f(5));
    found = 0;
    do a = 2 to search_limit;
      b = sumf(a);
      if (b > a) then
        do;
          if (sumf(b) = a) then
            do;
              found = found + 1;
              put skip edit (a,b) (f(7));
            end;
        end;
    end;
    put skip list (found, ' pairs were found');
    stop;


/* return sum of the proper divisors of n */
sumf:
    procedure(n) returns (fixed bin);

    dcl (n, sum, f1, f2) fixed bin;

    sum = 1;  /* 1 is a proper divisor of every number */
    f1 = 2;
    do while ((f1 * f1) < n);
      if mod(n, f1) = 0 then
        do;
          sum = sum + f1;
          f2 = n / f1;
          /* don't double count identical co-factors! */
          if f2 > f1 then sum = sum + f2;
        end;
      f1 = f1 + 1;
    end;
    return (sum);
end sumf;

end amicable;
