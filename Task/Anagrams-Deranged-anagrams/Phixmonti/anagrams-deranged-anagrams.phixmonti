/# Rosetta Code problem: http://rosettacode.org/wiki/Anagrams/Deranged_anagrams
by Galileo, 06/2022 #/

include ..\Utilitys.pmt

"unixdict.txt" "r" fopen var f

( )

true while
    f fgets
    dup -1 == if
        drop
        f fclose
        false
    else
        -1 del
        dup sort swap 2 tolist
        0 put
        true
    endif
endwhile

sort

0 var largest
( ) var candidate

( len 2 swap ) for var i
    ( i 1 ) sget >ps
    ( i 1 - 1 ) sget ps> == if
        ( i 2 ) sget >ps
        ( i 1 - 2 ) sget ps> len >ps
        true var test
        tps for var j
            j get rot j get rot == if false var test exitfor endif
        endfor
        test tps largest > and if
            ps> var largest
            2 tolist var candidate
        else
            ps> drop drop drop
        endif
    endif
endfor

candidate print
