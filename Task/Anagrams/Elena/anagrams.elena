import system'routines;
import system'calendar;
import system'io;
import system'collections;
import extensions;
import extensions'routines;
import extensions'text;
import algorithms;

extension op
{
     string normalized()
         = self.toArray().ascendant().summarize(new StringWriter());
}

public program()
{
    var start := Now;

    auto dictionary := new Map<string,object>();

    File.assign("unixdict.txt").forEachLine::(word)
    {
        var key := word.normalized();
        var item := dictionary[key];
        if (nil == item)
        {
            item := new ArrayList();
            dictionary[key] := item
        };

        item.append(word)
    };

    dictionary.Values
        .quickSort::(former,later => former.Item2.Length > later.Item2.Length )
        .top(20)
        .forEach::(pair){ Console.printLine(pair.Item2) };

    var end := Now;

    var diff := end - start;

    Console.printLine("Time elapsed in msec:",diff.Milliseconds);

    Console.readChar()
}
