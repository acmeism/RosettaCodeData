--
-- demo\rosetta\animate_pendulum2.exw
-- ==================================
--
include pGUI.e

Ihandle dlg, canvas, timer
cdCanvas cddbuffer, cdcanvas

constant g = 50

atom alpha = PI/2,
     omega = 0

function redraw_cb(Ihandle /*ih*/, integer /*posx*/, integer /*posy*/)
    integer {w, h} = IupGetIntInt(canvas, "DRAWSIZE")
    cdCanvasActivate(cddbuffer)
    cdCanvasClear(cddbuffer)
    -- new suspension point and length
    integer sX = floor(w/2)
    integer sY = floor(h/8)
    integer len = sX-30
    atom dt = 1/w
    -- move:
    atom epsilon = -len*sin(alpha)*g
    omega += dt*epsilon
    alpha += dt*omega
    -- repaint:
    integer eX = floor(len*sin(alpha)+sX)
    integer eY = floor(len*cos(alpha)+sY)
    cdCanvasSetForeground(cddbuffer, CD_DARK_GREY)
    cdCanvasLine(cddbuffer, sX, h-sY, eX, h-eY)
    cdCanvasSetForeground(cddbuffer, CD_BLACK)
    cdCanvasSector(cddbuffer, eX, h-eY, 35, 35, 0, 360)
    cdCanvasFlush(cddbuffer)
    return IUP_DEFAULT
end function

function timer_cb(Ihandle /*ih*/)
    IupUpdate(canvas)
    return IUP_IGNORE
end function

function map_cb(Ihandle ih)
    cdcanvas = cdCreateCanvas(CD_IUP, ih)
    cddbuffer = cdCreateCanvas(CD_DBUFFER, cdcanvas)
    cdCanvasSetBackground(cddbuffer, CD_GREY)
    return IUP_DEFAULT
end function

function esc_close(Ihandle /*ih*/, atom c)
    if c=K_ESC then return IUP_CLOSE end if
    return IUP_CONTINUE
end function

procedure main()
    IupOpen()

    canvas = IupCanvas(NULL)
    IupSetAttribute(canvas, "RASTERSIZE", "640x380")
    IupSetCallback(canvas, "MAP_CB", Icallback("map_cb"))
    IupSetCallback(canvas, "ACTION", Icallback("redraw_cb"))

    timer = IupTimer(Icallback("timer_cb"), 20)

    dlg = IupDialog(canvas)
    IupSetAttribute(dlg, "TITLE", "Animated Pendulum")
    IupSetCallback(dlg, "K_ANY", Icallback("esc_close"))

    IupShow(dlg)
    IupSetAttribute(canvas, "RASTERSIZE", NULL)
    IupMainLoop()
    IupClose()
end procedure

main()
