PROC CreateLog(CHAR ARRAY fname)
  CHAR ARRAY header="account:password:UID:GID:fullname,office,extension,homephone,email:directory:shell"
  BYTE dev=[1]

  Close(dev)
  Open(dev,fname,8)
  PrintDE(dev,header)
  Close(dev)
RETURN

PROC AppendLog(CHAR ARRAY fname,line)
  BYTE dev=[1]

  Close(dev)
  Open(dev,fname,9)
  PrintDE(dev,line)
  Close(dev)
RETURN

PROC ReadFile(CHAR ARRAY fname)
  CHAR ARRAY line(255)
  BYTE dev=[1]

  Close(dev)
  Open(dev,fname,4)
  WHILE Eof(dev)=0
  DO
    InputSD(dev,line)
    IF line(0) THEN
      PutE() Print(line)
    FI
  OD
  Close(dev)
RETURN

PROC PrintInv(CHAR ARRAY a)
  BYTE i

  IF a(0)>0 THEN
    FOR i=1 TO a(0)
    DO
      Put(a(i)%$80)
    OD
  FI
RETURN

PROC Main()
  CHAR ARRAY fname="D:PASSWD"
  BYTE LMARGIN=$52,oldLMARGIN

  oldLMARGIN=LMARGIN
  LMARGIN=0 ;remove left margin on the screen
  Put(125) PutE() ;clear the screen

  CreateLog(fname)
  AppendLog(fname,"jsmith:x:1001:1000:Joe Smith,Room 1007,(234)555-8917,(234)555-0077,jsmith@rosettacode.org:/home/jsmith:/bin/bash")
  AppendLog(fname,"jdoe:x:1002:1000:Jane Doe,Room 1004,(234)555-8914,(234)555-0044,jdoe@rosettacode.org:/home/jdoe:/bin/bash")

  PrintInv("Initial log:")
  ReadFile(fname)

  AppendLog(fname,"xyz:x:1003:1000:X Yz,Room 1003,(234)555-8913,(234)555-0033,xyz@rosettacode.org:/home/xyz:/bin/bash")

  PutE() PrintInv("Updated log:")
  ReadFile(fname)

  LMARGIN=oldLMARGIN ;restore left margin on the screen
RETURN
