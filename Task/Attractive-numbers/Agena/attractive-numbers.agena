scope # find some attractive numbers - numbers whose prime factor counts are prime, n must be > 1

    local constant maxNumber := 120; # maximum number we will consider

    printf( "The attractve numbers up to %d\n", maxNumber );
    local aCount := 0;
    for i from 2 to maxNumber do
        local v, fCount := i, 0;
        while v mod 2 = 0 do
            fCount +:= 1;
            v      \:= 2
        od;
        for j from 3 to maxNumber by 2 while v > 1 do
            while v > 1 and v mod j = 0 do
                fCount +:= 1;
                v      \:= j
            od
        od;
        if fCount > 0 then
            if numtheory.isprime( fCount ) then
                printf( " %3d", i );
                aCount +:= 1;
                if aCount mod 20 = 0 then print() fi
            fi
        fi
    od;
    printf( "\nFound %d attractive numbers\n", aCount )
end
