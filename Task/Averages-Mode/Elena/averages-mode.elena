#define system.
#define system'collections.
#define extensions'io.

// Averages/Mode

#symbol mode = &&:anArray
[
    #var aCountMap := Dictionary new &default:0.
    control foreach:anArray &do: &&:anItem
    [
        aCountMap @ anItem << ((aCountMap @ anItem) + 1).
    ].

    listControl sort:aCountMap &with: &&:p:n
        [ p Value > n Value ].

    #var aResult := List new.

    #var aMax := aCountMap First Value.
    control foreach:aCountMap &do: &&:anItem
    [
        aMax == anItem Value
            ? [ aResult += anItem Key. ].
    ].

    ^ listControl toArray:aResult.
].

#symbol program =
[
    #var anArray1 := (1, 1, 2, 4, 4).
    #var anArray2 := (1, 3, 6, 6, 6, 6, 7, 7, 12, 12, 17).

    #var aMode1 := mode:anArray1.
    #var aMode2 := mode:anArray2.

    consoleEx write:"mode of (" write::anArray1 write:") is (" write::aMode1 writeLine:")".
    consoleEx write:"mode of (" write::anArray2 write:") is (" write::aMode2 writeLine:")".
].
