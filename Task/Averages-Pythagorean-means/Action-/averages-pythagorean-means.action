INCLUDE "D2:REAL.ACT" ;from the Action! Tool Kit

PROC InverseI(INT a,result)
  REAL one,x

  IntToReal(1,one)
  IntToReal(a,x)
  RealDiv(one,x,result)
RETURN

PROC ArithmeticMean(INT ARRAY a INT count REAL POINTER result)
  INT i
  REAL x,sum,tmp

  IntToReal(0,sum)
  FOR i=0 TO count-1
  DO
    IntToReal(a(i),x)
    RealAdd(sum,x,tmp)
    RealAssign(tmp,sum)
  OD
  IntToReal(count,tmp)
  RealDiv(sum,tmp,result)
RETURN

PROC GeometricMean(INT ARRAY a INT count REAL POINTER result)
  INT i
  REAL x,prod,tmp

  IntToReal(1,prod)
  FOR i=0 TO count-1
  DO
    IntToReal(a(i),x)
    RealMult(prod,x,tmp)
    RealAssign(tmp,prod)
  OD
  InverseI(count,tmp)
  Power(prod,tmp,result)
RETURN

PROC HarmonicMean(INT ARRAY a INT count REAL POINTER result)
  INT i
  REAL x,sum,tmp

  IntToReal(0,sum)
  FOR i=0 TO count-1
  DO
    InverseI(a(i),x)
    RealAdd(sum,x,tmp)
    RealAssign(tmp,sum)
  OD
  IntToReal(count,tmp)
  RealDiv(tmp,sum,result)
RETURN

PROC Main()
  BYTE i
  INT ARRAY a=[1 2 3 4 5 6 7 8 9 10]
  REAL result

  Put(125) PutE() ;clear screen

  ArithmeticMean(a,10,result)
  Print("Arithmetic mean=") PrintRE(result)
  GeometricMean(a,10,result)
  Print(" Geometric mean=") PrintRE(result)
  HarmonicMean(a,10,result)
  Print("  Harmonic mean=") PrintRE(result)
RETURN
