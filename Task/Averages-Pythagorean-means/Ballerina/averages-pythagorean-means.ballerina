import ballerina/io;

public function main() {
    final int n = 10;
    var arr = from int i in 1...n select <float>i;
    float len = <float>arr.length();

    var sum  = function(float total, float next) returns float => total + next;
    var prod = function(float total, float next) returns float => total * next;
    var rsum = function(float total, float next) returns float => total + 1.0 / next;

    var am = arr.reduce(sum, 0.0) / len;
    var gm = arr.reduce(prod, 1.0).pow(1.0 / len);
    var hm = arr.reduce(rsum, 0.0).pow(-1.0) * len;

    io:println("For the numbers 1 to ", n, ":");
    io:println("  Arithmetic mean = ", am);
    io:println("  Geometric mean  = ", gm);
    io:println("  Harmonic mean   = ", hm);
    io:println("  A >= G >= H     = ", am >= gm && gm >= hm);
}
