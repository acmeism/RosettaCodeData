100 PROGRAM "Averages.bas"
110 NUMERIC ARR(1 TO 10)
120 FOR I=LBOUND(ARR) TO UBOUND(ARR)
130   LET ARR(I)=I
140 NEXT
150 PRINT "Arithmetic mean =";ARITHM(ARR)
160 PRINT "Geometric mean  =";GEOMETRIC(ARR)
170 PRINT "Harmonic mean   =";HARMONIC(ARR)
180 DEF ARITHM(REF A)
190   LET T=0
200   FOR I=LBOUND(A) TO UBOUND(A)
210     LET T=T+A(I)
220   NEXT
230   LET ARITHM=T/SIZE(A)
240 END DEF
250 DEF GEOMETRIC(REF A)
260   LET T=1
270   FOR I=LBOUND(A) TO UBOUND(A)
280     LET T=T*A(I)
290   NEXT
300   LET GEOMETRIC=T^(1/SIZE(A))
310 END DEF
320 DEF HARMONIC(REF A)
330   LET T=0
340   FOR I=LBOUND(A) TO UBOUND(A)
350     LET T=T+(1/A(I))
360   NEXT
370   LET HARMONIC=SIZE(A)/T
380 END DEF
