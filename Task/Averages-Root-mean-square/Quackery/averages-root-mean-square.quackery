[ $ "bigrat.qky" loadfile ] now!

[ [] swap
  witheach
    [ unpack 2dup v*
      join nested join ] ] is squareall (   [ --> [   )

[ dup size n->v rot
  0 n->v rot
  witheach
    [ unpack v+ ]
  2swap v/ ]               is arithmean (   [ --> n/d )

[ dip
  [ squareall arithmean ]
  vsqrt drop ]             is rms       ( [ n --> n/d )

say "The RMS of the integers 1 to 10, to 80 decimal places with rounding." cr
say "(Checked on Wolfram Alpha. The final digit is correctly rounded up.)" cr cr

' [ [ 1 1 ] [ 2 1 ] [ 3 1 ] [ 4 1 ] [  5 1 ]
    [ 6 1 ] [ 7 1 ] [ 8 1 ] [ 9 1 ] [ 10 1 ] ]

( ^^^ the integers 1 to 10 represented as a nest of nested rational numbers )

80 rms
80 point$ echo$
