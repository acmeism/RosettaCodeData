#define system.
#define system'routines.
#define system'collections.
#define extensions.

#class SMA
{
    #field thePeriod.
    #field theList.

    #constructor new : aPeriod
    [
        thePeriod := aPeriod.
        theList := List new.
    ]

    #method append : aNumber
    [
        theList += aNumber.

        #var aCount := theList length.
        ^ aCount =>
            0 ?  [ 0.0r ]
            ! [
                (aCount > thePeriod)?
                [
                    theList remove &index:0.

                    aCount := thePeriod.
                ].

                #var aSum := theList summarize:(Real new &int:0).

                ^ aSum / aCount.
            ].
    ]
}

#symbol program =
[
    #var SMA3 := SMA new:3.
    #var SMA5 := SMA new:5.

    1 to:5 &doEach: (:i)
    [
        console write:"sma3 + " :i :" = ": (SMA3 += i) &paddingRight:30 &with:#32.
        console writeLine:"sma5 + " :i :" = ": (SMA5 += i).
    ].

    5 to:1 &doEach: (:i)
    [
        console write:"sma3 + " :i :" = ": (SMA3 += i) &paddingRight:30 &with:#32.
        console writeLine:"sma5 + " :i :" = ": (SMA5 += i).
    ].
].
