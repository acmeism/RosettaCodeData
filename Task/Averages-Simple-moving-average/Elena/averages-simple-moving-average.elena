import system'routines.
import system'collections.
import extensions.

class SMA
{
    object thePeriod.
    object theList.

    constructor new : aPeriod
    [
        thePeriod := aPeriod.
        theList := List new.
    ]

    append : aNumber
    [
        theList append:aNumber.

        var aCount := theList length.
        aCount =>
            0 [ ^0.0r ];
            ! [
                if (aCount > thePeriod)
                [
                    theList removeAt:0.

                    aCount := thePeriod
                ].

                var aSum := theList summarize(Real new).

                ^ aSum / aCount
            ]
    ]
}

public program =
[
    var SMA3 := SMA new:3.
    var SMA5 := SMA new:5.

    1 to:5 do(:i)
    [
        console printPaddingRight(30, "sma3 + ", i, " = ", SMA3 append:i).
        console printLine("sma5 + ", i, " = ", SMA5 append:i)
    ].

    5 to:1 do(:i)
    [
        console printPaddingRight(30, "sma3 + ", i, " = ", SMA3 append:i).
        console printLine("sma5 + ", i, " = ", SMA5 append:i)
    ].

    console readChar.
].
