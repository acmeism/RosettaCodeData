fastfunc findj a norm .
   j = floor sqrt norm + 1
   repeat
      b = j * j
      if a + b < norm : return -1
      if a + b = norm : return j
      until j = 0
      j -= 1
   .
   return -1
.
proc babylon_spiral nsteps &pts[][] .
   pts[][] = [ [ 0 0 ] [ 0 1 ] ]
   norm = 1
   last[] = pts[$][]
   for k to nsteps - 2
      theta = atan2 last[2] last[1]
      cands[][] = [ ]
      while len cands[][] = 0
         norm += 1
         for i = 0 to nsteps
            a = i * i
            if a > norm div 2 : break 1
            j = findj a norm
            if j <> -1
               cands[][] &= [ i, j ]
               cands[][] &= [ -i, j ]
               cands[][] &= [ i, -j ]
               cands[][] &= [ -i, -j ]
               cands[][] &= [ j, i ]
               cands[][] &= [ -j, i ]
               cands[][] &= [ j, -i ]
               cands[][] &= [ -j, -i ]
            .
         .
      .
      min = 1 / 0
      for i to len cands[][]
         h = (theta - atan2 cands[i][2] cands[i][1]) mod 360
         if h < min
            min = h
            mini = i
         .
      .
      last[] = cands[mini][]
      pts[][] &= pts[$][]
      pts[$][1] += last[1]
      pts[$][2] += last[2]
   .
.
babylon_spiral 40 pts[][]
print pts[][]
#
babylon_spiral 10000 pts[][]
for i to len pts[][]
   minx = lower minx pts[i][1]
   maxx = higher maxx pts[i][1]
   miny = lower miny pts[i][2]
   maxy = higher maxy pts[i][2]
.
scx = 100 / (maxx - minx) * 0.96
scy = 100 / (maxy - miny) * 0.96
ty = -miny * scy + 2
tx = -minx * scx + 2
glinewidth 0.1
for i to len pts[][] - 1
   gline 0 ty 100 ty
   gline tx 0 tx 100
   gline pts[i][1] * scx + tx, pts[i][2] * scy + ty, pts[i + 1][1] * scx + tx, pts[i + 1][2] * scy + ty
.
