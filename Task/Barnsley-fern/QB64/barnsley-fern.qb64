_Title "Barnsley Fern"
Dim As Integer sw, sh
sw = 400: sh = 600
Screen _NewImage(sw, sh, 8)

Dim As Long i, ox, oy
Dim As Single sRand
Dim As Double x, y, x1, y1, sx, sy
sx = 60: sy = 59
ox = 180: oy = 4
Randomize Timer

x = 0
y = 0
For i = 1 To 400000
    sRand = Rnd
    Select Case sRand
        Case Is < 0.01
            x1 = 0: y1 = 0.16 * y
        Case Is < 0.08
            x1 = 0.2 * x - 0.26 * y: y1 = 0.23 * x + 0.22 * y + 1.6
        Case Is < 0.15
            x1 = -0.15 * x + 0.28 * y: y1 = 0.26 * x + 0.24 * y + 0.44
        Case Else
            x1 = 0.85 * x + 0.04 * y: y1 = -0.04 * x + 0.85 * y + 1.6
    End Select
    x = x1
    y = y1
    PSet (x * sx + ox, sh - (y * sy) - oy), 10
Next

Sleep
System
