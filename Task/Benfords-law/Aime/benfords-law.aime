text
sum(text a, text b)
{
    data d;
    integer e, f, n, r;

    e = ~a;
    f = ~b;

    r = 0;

    n = min(e, f);
    while (n) {
        n -= 1;
        e -= 1;
        f -= 1;
        r += a[e] - '0';
        r += b[f] - '0';
        b_insert(d, 0, r % 10 + '0');
        r /= 10;
    }

    if (f) {
        e = f;
        a = b;
    }

    while (e) {
        e -= 1;
        r += a[e] - '0';
        b_insert(d, 0, r % 10 + '0');
        r /= 10;
    }

    if (r) {
        b_insert(d, 0, r + '0');
    }

    d;
}

text
fibs(list l, integer n)
{
    integer c, i;
    text a, b, w;

    l[1] = 1;

    a = "0";
    b = "1";
    i = 1;
    while (i < n) {
        w = sum(a, b);
        a = b;
        b = w;
        c = w[0] - '0';
        l[c] = 1 + l[c];
        i += 1;
    }

    w;
}

integer
main(void)
{
    integer i, n;
    list f;
    real m;

    n = 1000;

    f.pn_integer(0, 10, 0);

    fibs(f, n);

    m = 100r / n;

    o_text("\t\texpected\t   found\n");
    i = 0;
    while (i < 9) {
        i += 1;
        o_form("%8d/p3d3w16//p3d3w16/\n", i, 100 * log10(1 + 1r / i), f[i] * m);
    }

    0;
}
