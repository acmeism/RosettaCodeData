(fofl, size, subrg):
Benford: procedure options(main);                /* 20 October 2013 */
   declare sc(1000) char(1), f(1000) float (16);
   declare d fixed (1);

   call Fibonacci(f);
   call digits(sc, f);

   put skip list ('digit  expected     obtained');
   do d= 1 upthru 9;
      put skip edit (d, log10(1 + 1/d), tally(sc, trim(d))/1000)
         (f(3), 2 f(13,8) );
   end;

Fibonacci: procedure (f);
   declare f(*) float (16);
   declare i fixed binary;

   f(1), f(2) = 1;
   do i = 3 to 1000;
      f(i) = f(i-1) + f(i-2);
   end;
end Fibonacci;

digits: procedure (sc, f);
   declare sc(*) char(1), f(*) float (16);
   sc = substr(trim(f), 1, 1);
end digits;

tally: procedure (sc, d) returns (fixed binary);
   declare sc(*) char(1), d char(1);
   declare (i, t) fixed binary;
   t = 0;
   do i = 1 to 1000;
      if sc(i) = d then t = t + 1;
   end;
   return (t);
end tally;
end Benford;
