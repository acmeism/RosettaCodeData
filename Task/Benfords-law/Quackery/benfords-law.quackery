  [ $ "bigrat.qky" loadfile ] now!

  [ ' [ 1 1 ]
    swap 2 - times
      [ dup -1 peek
        over -2 peek
        + join ] ]            is fibonacci  (   n --> [ )

  [ 0 swap witheach + ]       is sum        (   [ --> n )

  [ [ 10 /mod over while
      drop again ] nip ]      is msd        (   n --> n )

  [ 2dup peek 1+ unrot poke ] is tallydigit ( [ n --> [ )

  [ 0 10 of swap
    witheach
      [ msd tallydigit ] ]    is msdcount   (   [ --> [ )

  [ [ table
      $ "0.30103" $ "0.17609"
      $ "0.12494" $ "0.09691"
      $ "0.07918" $ "0.06695"
      $ "0.05799" $ "0.05115"
      $ "0.04576" ] do echo$ ] is expected  (     -->   )

  say "n  expected counted" cr
  1000 fibonacci msdcount
  behead drop
  dup sum swap
  witheach
    [ i^ 1+ echo sp sp
      i^ expected sp sp
      over reduce
      5 point$ echo$ cr ]
  drop
