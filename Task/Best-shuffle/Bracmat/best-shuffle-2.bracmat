    ( shuffle
    =   m car cdr todo a z count M string tried
      .     !arg:(@(?:%?car ?cdr).?todo)
          & !Count:?count
          & :?tried
          & (   @( !todo
                 :   ?a
                     ( %@?M
                     & ~(!tried:? !M ?)
                     & !M !tried:?tried
                     & !M:~!car
                     )
                     ( ?z
                     &   shuffle$(!cdr.str$(!a !z))
                       : (<!count:?count.?string)
                     & !M:?m
                     & ~
                     )
                 )
              | !count:<!Count
              |   @(!todo:%?m ?z)
                & shuffle$(!cdr.!z):(?count.?string)
                & !count+1
            . !m !string
            )
        | (0.)
    )
  & abracadabra seesaw elk grrrrrr up a:?words
  &   whl
    ' ( !words:%?word ?words
      & @(!word:? [?Count)
      & out$(!word shuffle$(!word.!word))
      )
  & Done
