#define system.
#define system'routines.

#symbol shuffle = (:aLiteral)
[
    #var aLength := aLiteral length.
    #var aShuffled := literalControl toArray:aLiteral.

    control from:0 &till:aLength &do: i
    [
        #var aChar := aLiteral@i.
        (aChar == (aShuffled@i)) ?
        [
            control from:0 &till:aLength &do: j
            [
                #var anAltChar := aShuffled@j.
                (anAltChar != aChar)and:[ anAltChar != (aLiteral@i) ]and:[aChar != (aLiteral@j)] ?
                    [
                        arrayControl exchange:i &with:j &in:aShuffled.

                        #break nil.
                    ].
            ].
        ].
    ].

    ^ Summing new:(String new) foreach:aShuffled literal.
].

#symbol scoreShuffle = (:anOriginal:aShuffled)
[
    #var aScore := Integer new.

    control from:0 &till:(anOriginal length) &do: i
        [ (anOriginal @ i) == (aShuffled @ i) ? [ aScore += 1. ]. ].

    ^ aScore number.
].

#symbol program =
[
    control foreach:("abracadabra", "seesaw", "grrrrrr", "pop", "up", "a") &do: aWord
    [
        #var aShuffled := shuffle:aWord.

        console write:"The best shuffle of " write:aWord write:" is " write:aShuffled write:"("
            write:(scoreShuffle:aWord:aShuffled) writeLine:")".
    ].

    console readChar.
].
