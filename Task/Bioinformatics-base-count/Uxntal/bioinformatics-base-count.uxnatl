%\n { 0a }  %\s { 20 }  %\0 { 00 }
%newline { [ LIT2 \n -Console/write ] DEO }

|18 @Console/write

|100

;data base-count

BRK

@base-count ( data* -- )
	LDAk
	DUP #0a NEQ ?{ !/next }
	DUP [ LIT "A ] NEQ ?{
		[ LIT2 &adenine $2 ]  INC2 ,/adenine  STR2 !/resume }
	DUP [ LIT "C ] NEQ ?{
		[ LIT2 &cytosine $2 ] INC2 ,/cytosine STR2 !/resume }
	DUP [ LIT "G ] NEQ ?{
		[ LIT2 &guanine $2 ]  INC2 ,/guanine  STR2 !/resume }
	DUP [ LIT "T ] NEQ ?{
		[ LIT2 &thymine $2 ]  INC2 ,/thymine  STR2 }

	&resume
		[ LIT2 &total $2 ]    INC2 ,/total    STR2

	&next
		POP
		INC2 LDAk ?base-count

	POP2

	;msgs/sequence print/str
	;data          print/str
	;msgs/header   print/str
	;msgs/adenine  print/str ,/adenine  LDR2 print/dec newline
	;msgs/cytosine print/str ,/cytosine LDR2 print/dec newline
	;msgs/guanine  print/str ,/guanine  LDR2 print/dec newline
	;msgs/thymine  print/str ,/thymine  LDR2 print/dec newline
	;msgs/total    print/str ,/total    LDR2 print/dec newline
	
	JMP2r

@print/str ( str* -- )
	LDAk .Console/write DEO
	INC2 LDAk ?/str
	POP2 JMP2r

@print/dec ( short* -- )
	#000a SWP2 [ LITr ff ]
	
	&dec/get
		SWP2k DIV2k MUL2 SUB2 STH
		POP OVR2 DIV2 ORAk ?/dec/get
	POP2 POP2
	
	&dec/put
		STHr INCk ?{ POP JMP2r }
		[ LIT "0 ] ADD .Console/write DEO !/dec/put

@data [
	"CGTAAAAAATTACAACGTCCTTTGG "CTATCTCTTAAACTCCTGCTAAATG \n
	"CTCGTGCTTTCCAATTATGTAAGCG "TTCCGAGACGGGGTGGTCGATTCTG \n
	"AGGACAAAGGTCAAGATGGAGCGCA "TCGAACGCAATAAGGATCATTTGAT \n
	"GGGACGTTTCGTCGACAAAGTCTTG "TTTCGAGAGTAACGGCTACCGTCTT \n
	"CGATTCTGCTTATAACACTATGTTC "TTATGAAATGGATGTTCTGAGTTGG \n
	"TCAGTCCCAATGTGCGGGGTTTCTT "TTAGTACGTCGGGAGTGGTATTATA \n
	"TTTAATTTTTCTATATAGCGATCTG "TATTTAAGCAATTCATTTAGGTTAT \n
	"CGCCGCGATGCTCGGTTCGGACCGC "CAAGCATCTGGCTCCACTGCTAGTG \n
	"TCCTAAATTTGAATGGCAAACACAA "ATAAGATTTAGCAATTCGTGTAGAC \n
	"GACCGGGGACTTGCATGATGGGAGC "AGCTTTGTTAAACTACGAACGTAAT \n \0 ]

@msgs [
	&header   \n "BASE \s "COUNT: \n \0
	&sequence "SEQUENCE: \n \0
	&adenine  \s \s \s "Adenine: \s \0
	&cytosine \s \s "Cytosine: \s \0
	&guanine  \s \s \s "Guanine: \s \0
	&thymine  \s \s \s "Thymine: \s \0
	&total    \s \s \s \s \s "Total: \s \0 ]
