F isPrime(n)
   I n % 2 == 0
      R n == 2
   I n % 3 == 0
      R n == 3
   V d = 5
   L d * d <= n
      I n % d == 0
         R 0B
      I n % (d + 2) == 0
         R 0B
      d += 6
   R 1B

F sameDigits(=n, b)
   V d = n % b
   n I/= b
   I d == 0
      R 0B
   L n > 0
      I n % b != d
         R 0B
      n I/= b
   R 1B

F isBrazilian(n)
   I n < 7
      R 0B
   I (n [&] 1) == 0
      R 1B
   L(b) 2 .. n - 2
      I sameDigits(n, b)
         R 1B
   R 0B

F printList(title, check)
   print(title)
   V n = 7
   [Int] l
   L
      I check(n) & isBrazilian(n)
         l.append(n)
         I l.len == 20 {L.break}
      n++
   print(l.join(‘, ’))
   print()

printList(‘First 20 Brazilian numbers:’, n -> 1B)
printList(‘First 20 odd Brazilian numbers:’, n -> (n [&] 1) != 0)
printList(‘First 20 prime Brazilian numbers:’, n -> isPrime(n))
