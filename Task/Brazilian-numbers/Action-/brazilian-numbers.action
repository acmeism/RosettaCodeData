INCLUDE "H6:SIEVE.ACT"

BYTE FUNC SameDigits(INT x,b)
  INT d

  d=x MOD b
  x==/b
  WHILE x>0
  DO
    IF x MOD b#d THEN
      RETURN (0)
    FI
    x==/b
  OD
RETURN (1)

BYTE FUNC IsBrazilian(INT x)
  INT b

  IF x<7 THEN RETURN (0) FI
  IF x MOD 2=0 THEN RETURN (1) FI
  FOR b=2 TO x-2
  DO
    IF SameDigits(x,b) THEN
      RETURN (1)
    FI
  OD
RETURN (0)

PROC Main()
  DEFINE COUNT="20"
  DEFINE MAXNUM="3000"
  BYTE ARRAY primes(MAXNUM+1)
  INT i,x,c
  CHAR ARRAY s

  Put(125) PutE() ;clear the screen
  Sieve(primes,MAXNUM+1)

  FOR i=0 TO 2
  DO
    IF i=0 THEN
      s=" "
    ELSEIF i=1 THEN
      s=" odd "
    ELSE
      s=" prime "
    FI
    PrintF("First %I%SBrazilian numbers:%E",COUNT,s)
    c=0 x=7
    DO
      IF IsBrazilian(x) THEN
        PrintI(x) Put(32)
        c==+1
        IF c=COUNT THEN EXIT FI
      FI
      IF i=0 THEN
        x==+1
      ELSEIF i=1 THEN
        x==+2
      ELSE
        DO
          x==+2
        UNTIL primes(x)
        OD
      FI
    OD
    PutE() PutE()
  OD
RETURN
