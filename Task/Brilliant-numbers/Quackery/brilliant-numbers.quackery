  [ 1 swap
    [ 10 / dup iff
        [ dip 1+ ]
      else done
      again ]
    drop ]                 is digits (   n --> n )

  [ over size 0 swap 2swap
    bsearchwith < drop ]   is search ( [ n --> n )

  1010 eratosthenes
  1 temp put
  [] [] []
  1010 times
    [ i^ isprime if
      [ temp share
        i^ digits < if
          [ nested join
            []
            1 temp tally ]
        i^ join ] ]
  nested join
  temp release
  witheach
    [ dup witheach
        [ over witheach
          [ over *
            dip rot join unrot ]
          drop behead drop ]
      drop ]
  sort
  say "First 100 brilliant numbers:" cr
  dup 100 split drop
  unbuild
  2 split nip -2 split drop
  nest$ 40 wrap$ cr cr
  6 times
    [ dup dup 10 i^ 1+ **
      say "First > "
      dup 1 - echo
      say " is "
      search tuck peek echo
      say " at position " 1+ echo
      say "." cr ]
  drop
