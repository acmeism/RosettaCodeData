100 D$ = "123456789"
110 FOR I = 1 TO 4
120    P = INT(RND(1) * LEN(D$)) + 1
130    N$ = N$ + MID$(D$, P, 1)
140    D$ = MID$(D$, 1, P - 1) + MID$(D$, P + 1, 8)
150 NEXT
160 PRINT "A RANDOM NUMBER HAS BEEN CREATED.
170 PRINT "THE NUMBER HAS FOUR DIGITS FROM 1 TO 9, WITHOUT DUPLICATION."
200 FOR Q = 0 TO 1 STEP 0
210     INPUT "GUESS THE NUMBER: "; G%
220     G$ = STR$(G%)
230     M = LEN(G$) <> 4 OR G% = 0
240     IF NOT M THEN FOR I = 2 TO 4 : M = MID$(G$, I, 1) = "0" : IF NOT M THEN NEXT I
250     IF NOT M THEN FOR I = 1 TO 3 : FOR J = I + 1 TO 4 : M = MID$(G$, I, 1) = MID$(G$, J, 1) : IF NOT M THEN NEXT J, I
260     IF M THEN PRINT "THE GUESS IS MALFORMED." : NEXT Q
270     B = 0
280     C = 0
300     FOR I = 1 TO 4
310         C$ = MID$(N$, I, 1)
320         BULL = MID$(G$, I, 1) = C$
330         COW = 0
340         IF NOT BULL THEN FOR J = 1 TO 4 : COW = MID$(G$, J, 1) = C$ : IF NOT COW THEN NEXT J
350         B = B + BULL
360         C = C + COW
370     NEXT I
380     PRINT B " BULLS, " C " COWS"
390     Q = G$ = N$
400 NEXT Q
