dig[] = [ 1 2 3 4 5 6 7 8 9 ]
for i = 1 to 4
   h = i - 1 + randint (10 - i)
   swap dig[i] dig[h]
.
# print dig[]
len g[] 4
attempts = 0
repeat
   repeat
      ok = 0
      s$[] = strchars input
      if len s$[] = 4
         ok = 1
         for i = 1 to 4
            g[i] = number s$[i]
            if g[i] = 0
               ok = 0
            .
         .
      .
      until ok = 1
   .
   print g[]
   attempts += 1
   bulls = 0
   cows = 0
   for i = 1 to 4
      if g[i] = dig[i]
         bulls += 1
      else
         for j = 1 to 4
            if dig[j] = g[i]
               cows += 1
            .
         .
      .
   .
   print "bulls:" & bulls & " cows:" & cows
   until bulls = 4
.
print "Well done! " & attempts & " attempts needed."
