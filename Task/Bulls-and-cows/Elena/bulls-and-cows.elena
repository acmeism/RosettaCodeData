#define system.
#define system'routines.
#define extensions.

#class GameMaster
{
    #field theNumbers.
    #field theAttempt.

    #constructor new
    [
        // generate secret number
        #var aRandomNumbers := randomControl randomize:9 &array:(1,2,3,4,5,6,7,8,9).

        theNumbers := arrayControl Subarray:aRandomNumbers &from:0 &length:4.
        theAttempt := Integer new:1.
    ]

    #method ask
    [
        #var aRow := consoleEx write:"Your Guess #":theAttempt:" ?" readLine.

        ^ literalControl toArray:aRow.
    ]

    #method proceed : aGuess
    [
        #var aCows := Integer new:0.
        #var aBulls := Integer new:0.

        (aGuess length != 4)
            ? [ aBulls << -1. ]
            ! [
                control forrange &int:0 &int:3 &do: (&int:i)
                [
                    #var ch := aGuess@i.
                    #var aNumber := convertor toInt:(ch literal).

                    // check range
                    (aNumber > 0)and:(aNumber < 10)
                        ! [ #throw InvalidArgumentException new. ].

                    // check duplicates
                    #var duplicate := arrayControl seek:aGuess &until: x [ (x == ch)and:[ x equal &reference:ch not ] ].
                    nil != duplicate ?
                    [
                        #throw InvalidArgumentException new.
                    ].

                    (aNumber == (theNumbers@i))
                        ? [ aBulls += 1. ]
                        ! [
                            arrayControl ifExist:aNumber &in:theNumbers
                                ? [ aCows += 1. ].
                        ].
                ]
                | ifFailed:
                [
                    aBulls << -1.
                ].
            ].

        ^ aBulls =>
            -1 ? [ consoleEx writeLine:"Not a valid guess.". ^ true. ]
             4 ? [ consoleEx writeLine:"Congratulations! You have won!". ^ false. ]
             ! [
                 theAttempt += 1.

                 consoleEx writeLine:"Your Score is " : aBulls : " bulls and " : aCows : " cows".

                 ^ true.
             ].
    ]
}

#symbol program =
[
    #var aGameMaster := GameMaster new.

    control while:[ aGameMaster proceed:(aGameMaster ask) ].
].
