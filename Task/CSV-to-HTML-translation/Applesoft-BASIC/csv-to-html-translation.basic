 100  DATA "Character,Speech"
 110  DATA "The multitude,The messiah! Show us the messiah!"
 120  DATA "Brian's mother,<angry>Now you listen here! He's not the messiah; he's a very naughty boy! Now go away!</angry>"
 130  DATA "The multitude,Who are you?"
 140  DATA "Brian's mother,I'm his mother; that's who!"
 150  DATA "The multitude,Behold his mother! Behold his mother!"
 160  DATA
 170  LET M$ =  CHR$ (13)
 180  LET Q$ =  CHR$ (34)
 190  LET TRUE =  NOT FALSE
 200  LET HEADER = TRUE
 210  DIM C(255),H$(4,1)
 220  LET H$(1,0) = "</TD><TD>"
 230  LET H$(2,0) = "&LT;"
 240  LET H$(3,0) = "&GT;"
 250  LET H$(4,0) = "&AMP;"
 260  FOR I = 1 TO 4
 270      LET C( ASC ( MID$ (",<>&",I,1))) = I
 280      LET H$(I,HEADER) = H$(I,0)
 290  NEXT I
 300  LET H$(1,1) = "</TH><TH>"
 310  PRINT "<!DOCTYPE HTML>"M$"<HTML>"M$"<HEAD>"M$"</HEAD>"M$"<BODY>"
 320  PRINT "<TABLE BORDER="Q$"1"Q$" CELLPADDING="Q$"10"Q$" CELLSPACING="Q$"0"Q$">"
 330  READ CSV$
 340  FOR Q = 0 TO 1 STEP 0
 350      PRINT "<TR><T" MID$ ("DH",1 + HEADER,1)">";
 360      FOR I = 1 TO  LEN (CSV$)
 370          LET C$ =  MID$ (CSV$,I,1)
 380          LET H = C( ASC (C$))
 390          PRINT H$(H,HEADER) MID$ (C$,1,H = 0);
 400      NEXT I
 410      PRINT "</T" MID$ ("DH",1 + HEADER,1)"></TR>"
 420      LET HEADER = FALSE
 430      READ CSV$
 440      LET Q = CSV$ = ""
 450  NEXT Q
 460  PRINT "</TABLE>"M$"</BODY>"M$"</HTML>"
