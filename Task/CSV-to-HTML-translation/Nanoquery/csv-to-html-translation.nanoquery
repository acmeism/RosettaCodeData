// a method that converts a csv row into a html table row as a string
def toHtmlRow(record, tag)
	htmlrow = "\t<tr>\n"

	// loop through the values in the current csv row
	for i in range(1, len(record))
		htmlrow = htmlrow + "\t\t<" + tag + ">" + (record ~ i) + "</" + tag + ">\n"
	end for

	return htmlrow + "\t</tr>\n"
end def

// get the name of the csv file then open it
print "filename: "
input fname
open fname

// allocate a string to hold the table
htmltable = "<table>\n"

// add the column names to the table (#0 returns column names as a record object)
htmltable = (htmltable + toHtmlRow(#0, "th"))

// add all other rows to the table
for i in range(1, $dbsize)
	htmltable = (htmltable + toHtmlRow(#i, "td"))
end for

// close the html table
htmltable = htmltable+"</table>"

println htmltable
