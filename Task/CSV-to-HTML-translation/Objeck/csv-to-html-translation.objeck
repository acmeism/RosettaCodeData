use System.IO.File;
use Data.CSV;

class CsvToHtml {
  function : Main(args : String[]) ~ Nil {
    if(args->Size() = 1) {
      table := CsvTable->New(FileReader->ReadFile(args[0]));
      if(table->IsParsed()) {
        buffer := "<html><body><table>";
        Header(table->GetHeaders(), buffer);
        for(i := 1; i < table->Size(); i += 1;) {
          Data(table->Get(i), buffer);
        };
        buffer += "</table></body></html>";
        buffer->PrintLine();
      };
    };
  }

  function : Header(row : CsvRow, buffer : String) ~ Nil {
    buffer += "<tr>";
    each(i : row) {
      buffer += "<th>";
      buffer += Encode(row->Get(i));
      buffer += "</th>";
    };
    buffer += "</tr>";
  }

  function : Data(row : CsvRow, buffer : String) ~ Nil {
    buffer += "<tr>";
    each(i : row) {
      buffer += "<td>";
      buffer += Encode(row->Get(i));
      buffer += "</td>";
    };
    buffer += "</tr>";
  }

  function : Encode(in : String) ~ String {
    out := "";

    each(i : in) {
      c := in->Get(i);
      select(c) {
        label '&': {
          out->Append("&amp;");

        }

        label '\'': {
          out->Append("&apos;");
        }

        label '<': {
          out->Append("&lt;");
        }

        label '>': {
          out->Append("&gt;");
        }

        other: {
          out->Append(c);
        }
      };
    };

    return out;
  }
}
