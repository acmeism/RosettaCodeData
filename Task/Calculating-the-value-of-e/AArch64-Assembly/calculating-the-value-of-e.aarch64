#include <stdio.h>

.globl main
.section .text

/*
        eulers constant is the sum of the
        series Σ_0 ^ ∞ 1/n! = 1 + 1/1 + 1/2 + 1/2*3 ...
*/

main:
        stp x29, x30, [sp, #-16]!

        fmov d0, #1.0
        fadd d0, d0, d0 // sum accumulator = 2 to start

        fmov d1, #1.0   // fraction value
        fmov d4, #1.0   // permanent 1.0

        mov  x0, #16 // max step
        mov  x1, #1  // start value

        1:
        add   x1, x1, #1

        scvtf d2, x1
        fmul  d1, d1, d2
        fdiv  d3, d4, d1
        fadd  d0, d0, d3

        cmp  x1, x0
        b.lt 1b

        adr x0, string
        bl printf

        ldp x29, x30, [sp], 16
        ret

.section .rodata
string: .asciz "euler's constant e ≈ %18.16f\n"
