import system'text.
import system'routines.
import system'calendar.
import extensions.
import extensions'routines.

const MonthNames = ("JANUARY","FEBRUARY","MARCH","APRIL","MAY","JUNE","JULY","AUGUST","SEPTEMBER","OCTOBER","NOVEMBER","DECEMBER").
const DayNames = ("MO", "TU", "WE", "TH", "FR", "SA", "SU").

class CalendarMonthPrinter
{
    object theDate.
    object theLine.
    object theMonth.
    object theYear.
    object theRow.

    constructor year:aYear month:aMonth
    [
        theMonth := aMonth.
        theYear := aYear.
        theLine := TextBuilder new.
        theRow := Integer new(0).
    ]

    writeTitle
    [
        theRow value := 0.
        theDate := Date new(theYear, theMonth, 1).
        DayNames forEach(:aName)
            [ theLine print(" ",aName) ]
    ]

    writeLine
    [
        theLine clear.

        if (theDate month == theMonth)
            [
                theLine write:"   " length((theDate dayOfWeek == 0)iif(7,theDate dayOfWeek) - 1).

                [
                    theLine write:(theDate day; literal) paddingLeft:3 with:$32.
                    theDate := theDate addDays:1.
                ]
                repeatUntil:$((theDate month != theMonth)||(theDate dayOfWeek == 1)).
            ].

        int aLength := theLine length.
        if (aLength < 21)
            [ theLine write:" " length(21 - aLength). ].

        theRow append(1).
    ]

    indexer = BaseIndexer::
    {
        bool available = theRow < 7.

        readIndexTo(ref<int> anIndex) [ theRow readValueTo(anIndex) ]

        writeIndex(int anIndex)
        [
            if (anIndex <= theRow)
                [ self writeTitle ].

            while (anIndex > theRow)
                [ self writeLine ]
        ]

        appendIndex(int anIndex)
            <= writeIndex(theRow value + anIndex).

        readLengthTo(ref<int> retVal) [ retVal value := 7 ]

        get = self.

        set : o [ NotSupportedException new; raise ]
    }.

    printTitleTo : anOutput
    [
        anOutput write(MonthNames[theMonth - 1]) padding:21 with:$32.
    ]

    printTo : anOutput
    [
        anOutput write(theLine literal).
    ]
}

class Calendar
{
    int theYear.
    int theRowLength.

    constructor new : aYear
    [
        theYear := aYear.
        theRowLength := 3.
    ]

    printTo:anOutput
    [
        anOutput write:"[SNOOPY]" padding:(theRowLength * 25) with:$32.
        anOutput writeLine.
        anOutput write:(theYear literal) padding:(theRowLength * 25) with:$32.
        anOutput writeLine; writeLine.

        var aRowCount := 12 / theRowLength.
        var Months := Array new(aRowCount); populate(:i)
            (Array new:theRowLength;
                populate(:j)
                    ( CalendarMonthPrinter year:theYear month(i * theRowLength + j + 1))).

        Months forEach(:aRow)
        [
            aRow forEach(:aMonth)
            [
                aMonth printTitleTo:anOutput.
                anOutput write:"   ".
            ].

            anOutput writeLine.

            ParallelEnumerator new:aRow; forEach(:aLine)
            [
                aLine forEach(:aPrinter)
                [
                    aPrinter printTo:anOutput.
                    anOutput write:"   "
                ].

                anOutput writeLine
            ].
        ].
    ]
}

public program =
[
    var aCalender := Calendar new(console write:"ENTER THE YEAR:"; readLineTo(Integer new)).

    aCalender printTo:console.

    console readChar.
].
