#define system.
#define system'text.
#define system'routines.
#define system'calendar.
#define extensions.
#define extensions'math.
#define extensions'routines.

#symbol MonthNames = ("JANUARY","FEBRUARY","MARCH","APRIL","MAY","JUNE","JULY","AUGUST","SEPTEMBER","OCTOBER","NOVEMBER","DECEMBER").
#symbol DayNames = ("MO", "TU", "WE", "TH", "FR", "SA", "SU").

#class CalendarMonthPrinter
{
    #field theDate.
    #field theLine.
    #field theMonth.
    #field theYear.
    #field theRow.

    #constructor new &year:aYear &month:aMonth
    [
        theMonth := aMonth.
        theYear := aYear.
        theLine := TextBuffer new.
        theRow := Integer new &int:0.
    ]

    #method writeTitle
    [
        theRow << 0.
        theDate := Date new &year:(theYear int) &month:(theMonth int) &day:1.
        DayNames run &each: aName
            [ theLine writeLiteral:" ":aName ].
    ]

    #method writeLine
    [
        theLine clear.

        (theDate month == theMonth)
            ? [
                theLine write:"   " &length:(((theDate dayOfWeek) => 0 ? [ 7 ] ! [ theDate dayOfWeek ]) - 1).

                control do:
                [
                    theLine write:(theDate day literal) &paddingLeft:3 &with:#32.

                    theDate := theDate add &days:1.
                ]
                &until:[(theDate month != theMonth)or:[theDate dayOfWeek == 1]].
            ].

        #var(type:int) aLength := theLine length.
        (aLength < 21)
            ? [ theLine write:" " &length:(21 - aLength). ].

        theRow += 1.
    ]

    #method iterator = Iterator
    {
        available = theRow < 7.

        readIndex &vint:anIndex [ anIndex << theRow int. ]

        write &index:anIndex
        [
            (anIndex <= theRow)
                ? [ self writeTitle. ].

            #loop (anIndex > theRow) ?
                [ self writeLine. ].
        ]

        get = self.
    }.

    #method printTitleTo : anOutput
    [
        anOutput write:(MonthNames @(theMonth - 1)) &padding:21 &with:#32.
    ]

    #method printTo : anOutput
    [
        anOutput write:(theLine literal).
    ]
}

#class Calendar
{
    #field(type:int) theYear.
    #field(type:int) theRowLength.

    #constructor new : aYear
    [
        theYear := aYear int.
        theRowLength := 3.
    ]

    #method printTo:anOutput
    [
        anOutput write:"[SNOOPY]" &padding:(theRowLength * 25) &with:#32.
        anOutput writeLine.
        anOutput write:(theYear literal) &padding:(theRowLength * 25) &with:#32.
        anOutput writeLine writeLine.

        #var aRowCount := 12 / theRowLength.
        #var Months := Array new &length:aRowCount set &every:(&index:i)
            [ Array new &length:theRowLength set &every:(&index:j)
                [ CalendarMonthPrinter new &year:(theYear int) &month:((i * theRowLength + j + 1) int) ]].

        Months run &each: aRow
        [
            aRow run &each: aMonth
            [
                aMonth printTitleTo:anOutput.

                anOutput write:"   ".
            ].

            anOutput writeLine.

            ParallelEnumerator new:aRow run &each: aLine
            [
                aLine run &each: aPrinter
                [
                    aPrinter printTo:anOutput.

                    anOutput write:"   ".
                ].

                anOutput writeLine.
            ].
        ].
    ]
}

#symbol program =
[
    #var aCalender := Calendar new:(console write:"ENTER THE YEAR:" readLine:(Integer new)).

    aCalender printTo:console.

    console readChar.
].
