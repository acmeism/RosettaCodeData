      INSTALL @lib$+"DATELIB"
      VDU 23,22,640;570;8,15,16,128

      year% = 1969
      PRINT TAB(38); year%
      DIM dom%(2), mjd%(2), dim%(2)

      FOR day% = 1 TO 7
        days$ += LEFT$(FN_date$(FN_mjd(day%, 1, 1905), "ddd"), 2) + " "
      NEXT

      FOR month% = 1 TO 10 STEP 3
        PRINT
        FOR col% = 0 TO 2
          mjd%(col%) = FN_mjd(1, month% + col%, year%)
          month$ = FN_date$(mjd%(col%), "MMMM")
          PRINT TAB(col%*24 + 16 - LEN(month$)/2) month$;
        NEXT
        FOR col% = 0 TO 2
          PRINT TAB(col%*24 + 6) days$;
          dim%(col%) = FN_dim(month% + col%, year%)
        NEXT
        dom%() = 1
        col% = 0
        REPEAT
          dow% = FN_dow(mjd%(col%))
          IF dom%(col%)<=dim%(col%) THEN
            PRINT TAB(col%*24 + dow%*3 + 6); dom%(col%);
            dom%(col%) += 1
            mjd%(col%) += 1
          ENDIF
          IF dow%=6 OR dom%(col%)>dim%(col%) col% = (col% + 1) MOD 3
        UNTIL dom%(0)>dim%(0) AND dom%(1)>dim%(1) AND dom%(2)>dim%(2)
        PRINT
      NEXT
