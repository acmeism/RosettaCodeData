100H: /* DRAW A CANTOR SET USING ASCII                                       */

   /* BDOS SYSTEM CALL AND I/O ROUTINES                                      */
   BDOS: PROCEDURE( F, A ); DECLARE F BYTE, A ADDRESS; GOTO 5;   END;
   PR$CHAR: PROCEDURE( C ); DECLARE C BYTE; CALL BDOS( 2, C );   END;
   PR$NL:   PROCEDURE; CALL PR$CHAR( 0DH ); CALL PR$CHAR( 0AH ); END;

   /* DRAW A CANTOR SET                                                      */

   DECLARE LINES LITERALLY '4';
   DECLARE WIDTH LITERALLY '27';                     /* MUST BE 3**(LINES-1) */

   DECLARE LINE    (WIDTH)BYTE;
   DECLARE ( I, L, C, W, S, SEGMENTS ) BYTE;

   DO I = 0 TO LAST( LINE );
      LINE( I ) = 023H;
      CALL PR$CHAR( LINE( I ) );
   END;
   CALL PR$NL;
   W        = WIDTH;
   SEGMENTS = 1;
   DO L = 2 TO LINES;
      W        = W / 3;
      SEGMENTS = SEGMENTS * 3;
      C        = 0;
      DO S = 1 TO SEGMENTS;
         DO I = 1 TO W;
            IF NOT S THEN LINE( C ) = ' ';       /* EVEN SEGMENT - BLANK IT */
            CALL PR$CHAR( LINE( C ) );
            C = C + 1;
         END;
      END;
      CALL PR$NL;
   END;

EOF
