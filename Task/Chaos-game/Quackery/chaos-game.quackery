  [ $ "turtleduck.qky" loadfile ] now!

  [ 2 3 circle ]              is dot        (     -->     )

  [ 1 fly
    -1 4 turn
    1 fly
    1 4 turn ]                is toxy       ( n n -->     )

  [ swap 2dup toxy
    dot
    1 2 turn
    toxy
    1 2 turn ]                is plot       ( n n -->     )

  [ 200 random
    346 random
    over 346 *
    over 200 * < if
      [ dip [ 400 + ]
        692 swap - ] ]        is intriangle (     --> n n )

  [ 3 random
    [ table
      [ 2 /
        dip [ 2 / ] ]
      [ 2 /
        dip [ 800 + 2 / ] ]
      [ 692 + 2 /
        dip [ 400 + 2 / ] ] ]
    do ]                      is tovertex   ( n n -->     )

   turtle
  0 frames
  -400 1 fly
  1 4 turn
  340 1 fly
  -1 4 turn
  intriangle
  10000 times
    [ i^ 100 mod 0= if frame
      2dup plot
      tovertex ]
  2drop
  1 frames
