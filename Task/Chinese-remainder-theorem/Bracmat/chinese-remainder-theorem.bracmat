( ( mul-inv
  =   a b b0 q x0 x1
    .   !arg:(?a.?b:?b0)
      & ( !b:1
        |   0:?x0
          & 1:?x1
          &   whl
            ' ( !a:>1
              &   (!b.mod$(!a.!b):?q.!x1+-1*!q*!x0.!x0)
                : (?a.?b.?x0.?x1)
              )
          & ( !x1:<0&!b0+!x1
            | !x1
            )
        )
  )
& ( chinese-remainder
  =   n a as p ns ni prod sum
    .   !arg:(?n.?a)
      & 1:?prod
      & 0:?sum
      & !n:?ns
      & whl'(!ns:%?ni ?ns&!prod*!ni:?prod)
      & !n:?ns
      & !a:?as
      &   whl
        ' ( !ns:%?ni ?ns
          & !as:%?ai ?as
          & div$(!prod.!ni):?p
          & !sum+!ai*mul-inv$(!p.!ni)*!p:?sum
          )
      & mod$(!sum.!prod):?arg
      & !arg
  )
& 3 5 7:?n
& 2 3 2:?a
& put$(str$(chinese-remainder$(!n.!a) \n))
);
