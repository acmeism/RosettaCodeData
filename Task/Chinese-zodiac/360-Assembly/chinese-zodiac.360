*        Chinese zodiac            10/03/2019
CHINEZOD CSECT
         USING  CHINEZOD,R13       base register
         B      72(R15)            skip savearea
         DC     17F'0'             savearea
         SAVE   (14,12)            save previous context
         ST     R13,4(R15)         link backward
         ST     R15,8(R13)         link forward
         LR     R13,R15            set addressability
         LA     R6,1               i=1
       DO WHILE=(C,R6,LE,=A(NY))   do i=1 to hbound(years)
         LR     R1,R6                i
         SLA    R1,2                 *4
         L      R2,YEARS-4(R1)       ~
         ST     R2,YEAR              year=years(i)
         SH     R2,=H'4'             -4
         LR     R7,R2                year-4
         SRDA   R2,32                ~
         D      R2,=F'10'            /10
         SRA    R2,1                 /2
         MH     R2,=H'6'             *6
         LA     R1,ELEMENTS(R2)      ~
         MVC    ELEMENT,0(R1)      element=elements(mod(year-4,10)/2+1)
         LR     R2,R7                year-4
         SRDA   R2,32                ~
         D      R2,=F'12'            /12
         SLA    R2,3                 *8
         LA     R1,ANIMALS(R2)       ~
         MVC    ANIMAL,0(R1)         animal=animals(mod(year-4,12)+1)
         L      R2,YEAR              year
         SRDA   R2,32                ~
         D      R2,=F'2'             /2
         SLA    R2,2                 *4
         LA     R1,YINYANGS(R2)      ~
         MVC    YINYANG,0(R1)        yinyang=yinyangs(mod(year,2)+1)
         LR     R2,R7                year-4
         SRDA   R2,32                ~
         D      R2,=F'60'            /60
         LA     R2,1(R2)             nn=mod(year-4,60)+1
         L      R1,YEAR              year
         XDECO  R1,XDEC              edit year
         MVC    PG+00(4),XDEC+8      output year
         MVC    PG+24(6),ELEMENT     output element
         MVC    PG+31(8),ANIMAL      output animal
         MVC    PG+41(4),YINYANG     output yinyang
         XDECO  R2,XDEC              edit nn
         MVC    PG+49(2),XDEC+10     output nn
         XPRNT  PG,L'PG              print buffer
         LA     R6,1(R6)             i++
       ENDDO    ,                  enddo i
         L      R13,4(0,R13)       restore previous savearea pointer
         RETURN (14,12),RC=0       restore registers from calling sav
NY       EQU    (ANIMAL-YEARS)/4
ANIMALS  DC     CL8'Rat',CL8'Ox',CL8'Tiger',CL8'Rabbit'
         DC     CL8'Dragon',CL8'Snake',CL8'Horse',CL8'Goat'
         DC     CL8'Monkey',CL8'Rooster',CL8'Dog',CL8'Pig'
ELEMENTS DC     CL6'Wood',CL6'Fire',CL6'Earth',CL6'Metal',CL6'Water'
YINYANGS DC     CL4'Yang',CL4'Yin'
YEARS    DC     F'1935',F'1938',F'1968',F'1972',F'1976',F'1984',F'2017'
ANIMAL   DS     CL8
ELEMENT  DS     CL6
YINYANG  DS     CL4
YEAR     DS     F
PG    DC   CL80':::: is the year of the :::::: :::::::: (::::).  ::/60'
XDEC     DS     CL12               temp for xdeco
         REGEQU
         END    CHINEZOD
