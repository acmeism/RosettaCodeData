CNZODIAC
=LAMBDA(y,
    APPENDCOLS(
        APPENDCOLS(
            APPENDCOLS(
                CNYEARNAME(y)
            )(
                CNYEARELEMENT(y)
            )
        )(
            CNYEARANIMAL(y)
        )
    )(
        CNYEARYINYANG(y)
    )
)


CNYEARANIMAL
=LAMBDA(y,
    LET(
        shengxiao, {
         "鼠","shǔ","rat";
         "牛","niú","ox";
         "虎","hǔ","tiger";
         "兔","tù","rabbit";
         "龍","lóng","dragon";
         "蛇","shé","snake";
         "馬","mǎ","horse";
         "羊","yáng","goat";
         "猴","hóu","monkey";
         "鸡","jī","rooster";
         "狗","gǒu","dog";
         "豬","zhū","pig"
         },

        iYear, y - 4,
        iBranch, 1 + MOD(iYear, 12),

        TRANSPOSE(
            INDEX(shengxiao, iBranch)
        )
    )
)


CNYEARELEMENT
=LAMBDA(y,
    LET(
        wuxing, {
         "木","mù","wood";
         "火","huǒ","fire";
         "土","tǔ","earth";
         "金","jīn","metal";
         "水","shuǐ","water"
         },

        iYear, y - 4,
        iStem, MOD(iYear, 10),

        TRANSPOSE(
            INDEX(
                wuxing,
                1 + QUOTIENT(
                    iStem,
                    2
                )
            )
        )
    )
)


CNYEARNAME
=LAMBDA(y,
    LET(
        tiangan, {
         "甲","jiă";
         "乙","yĭ";
         "丙","bĭng";
         "丁","dīng";
         "戊","wù";
         "己","jĭ";
         "庚","gēng";
         "辛","xīn";
         "壬","rén";
         "癸","gŭi"
         },

         dizhi, {
         "子","zĭ";
         "丑","chŏu";
         "寅","yín";
         "卯","măo";
         "辰","chén";
         "巳","sì";
         "午","wŭ";
         "未","wèi";
         "申","shēn";
         "酉","yŏu";
         "戌","xū";
         "亥","hài"
         },


        iYear, y - 4,
        iStem, 1 + MOD(iYear, 10),
        iBranch, 1 + MOD(iYear, 12),
        iIndex, 1 + MOD(iYear, 60),

        stem, INDEX(tiangan, iStem),
        branch, INDEX(dizhi, iBranch),

        APPENDROWS(
            APPENDROWS(
                CONCAT(INDEX(stem, 1), INDEX(branch, 1))
            )(
                CONCAT(INDEX(stem, 2), INDEX(branch, 2))
            )
        )(iIndex & "/60")
    )
)


CNYEARYINYANG
=LAMBDA(y,
    LET(
        yinyang, {
         "阳","yáng", "bright";
         "阴","yīn", "dark"
         },

        TRANSPOSE(
            INDEX(
                yinyang,
                1 + MOD(y - 4, 2)
            )
        )
    )
)
