$ include "seed7_05.s7i";
  include "console.s7i";

const array string: animals is [0] ("Rat", "Ox", "Tiger", "Rabbit", "Dragon", "Snake", "Horse", "Goat", "Monkey", "Rooster", "Dog", "Pig");
const array string: elements is [0] ("Wood", "Fire", "Earth", "Metal", "Water");
const array string: animalChars is [0] ("子", "丑", "寅", "卯", "辰", "巳", "午", "未", "申", "酉", "戌", "亥");
const array array string: elementChars is [0] ([0] ("甲", "丙", "戊", "庚", "壬"), [0] ("乙", "丁", "己", "辛", "癸"));

const proc: main is func
  local
    var integer: year is 0;
    var integer: eIdx is 0;
    var integer: aIdx is 0;
  begin
    OUT := STD_CONSOLE;
    for year range {1935, 1938, 1968, 1972, 1976, 1984, 1985, 2017} do
      eIdx := (year - 4) rem 10 div 2;
      aIdx := (year - 4) rem 12;
      writeln(year <& " is the year of the " <& elements[eIdx] <& " " <& animals[aIdx] <& " (" <&
              ([0] ("yang", "yin"))[year rem 2] <& "). " <& elementChars[year rem 2][eIdx] <& animalChars[aIdx]);
    end for;
  end func;
