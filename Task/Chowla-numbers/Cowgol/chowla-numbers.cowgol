include "cowgol.coh";

sub chowla(n: uint32): (sum: uint32) is
    sum := 0;
    var i: uint32 := 2;

    while i*i <= n loop
        if n % i == 0 then
            sum := sum + i;
            var j := n / i;
            if i != j then
                sum := sum + j;
            end if;
        end if;
        i := i + 1;
    end loop;
end sub;

var n: uint32 := 1;
while n <= 37 loop
    print("chowla(");
    print_i32(n);
    print(") = ");
    print_i32(chowla(n));
    print("\n");
    n := n + 1;
end loop;

n := 2;
var power: uint32 := 100;
var count: uint32 := 0;
while n <= 10000000 loop
    if chowla(n) == 0 then
        count := count + 1;
    end if;
    if n % power == 0 then
        print("There are ");
        print_i32(count);
        print(" primes < ");
        print_i32(power);
        print_nl();
        power := power * 10;
    end if;
    n := n + 1;
end loop;

count := 0;
const LIMIT := 35000000;
var k: uint32 := 2;
var kk: uint32 := 3;
loop
    n := k * kk;
    if n > LIMIT then break; end if;
    if chowla(n) == n-1 then
        print_i32(n);
        print(" is a perfect number.\n");
        count := count + 1;
    end if;
    k := kk + 1;
    kk := kk + k;
end loop;

print("There are ");
print_i32(count);
print(" perfect numbers < ");
print_i32(LIMIT);
print_nl();
