table
chowla(1) = 0.
chowla(2) = 0.
chowla(3) = 0.
chowla(N) = C, N>3 =>
    Max = floor(sqrt(N)),
    Sum = 0,
    foreach (X in 2..Max, N mod X == 0)
        Y := N div X,
        Sum := Sum + X + Y
    end,
    if (N == Max * Max) then
        Sum := Sum - Max
    end,
    C = Sum.

main =>
    foreach (I in 1..37)
        printf("chowla(%d) = %d\n", I, chowla(I))
    end,
    Ranges = {100, 1000, 10000, 100000, 1000000, 10000000},
    foreach (Range in Ranges)
        Count = 0,
        foreach (I in 2..Range)
            if (chowla(I) == 0) then
                Count := Count + 1
            end
        end,
        printf("There are %d primes less than %d.\n", Count, Range)
    end,
    Limit = 35000000,
    Count = 0,
    foreach (I in 2..Limit)
        if (chowla(I) == I-1) then
            printf("%d is a perfect number\n", I),
            Count := Count + 1
        end
    end,
    printf("There are %d perfect numbers less than %d.\n", Count, Limit).
