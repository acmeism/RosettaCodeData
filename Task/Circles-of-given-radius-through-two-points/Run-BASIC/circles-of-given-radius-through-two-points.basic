html "<TABLE border=1>"
html "<tr bgcolor=wheat align=center><td>No.</td><td>x1</td><td>y1</td><td>x2</td><td>y2</td><td>r</td><td>cir x1</td><td>cir y1</td><td>cir x2</td><td>cir y2</td></tr>"
for i = 1 to 5
    read x1, y1, x2, y2,r
html "<tr align=right><td>";i;"</td><td>";x1;"</td><td>";y1;"</td><td>";x2;"</td><td>";y2;"</td><td>";r;"</td>"
    gosub [twoCircles]
next
html "</table>"
end

'p1                p2           r
data 0.1234, 0.9876,    0.8765, 0.2345,    2.0
data 0.0000, 2.0000,    0.0000, 0.0000,    1.0
data 0.1234, 0.9876,    0.1234, 0.9876,    2.0
data 0.1234, 0.9876,    0.8765, 0.2345,    0.5
data 0.1234, 0.9876,    0.1234, 0.9876,    0.0

[twoCircles]

    if x1=x2 and y1=y2 then '2.If the points are coincident
        if r=0 then ' unless r==0.0
            html "<td colspan=4 align=left>It will be a single point (";x1;",";y1;") of radius 0</td></tr>"
            RETURN
        else
            html "<td colspan=4 align=left>There are any number of circles via single point (";x1;",";y1;") of radius ";r;"</td></tr>"
            RETURN
        end if
    end if
    r2 = sqr((x1-x2)^2+(y1-y2)^2)/2			'half distance between points
    if r<r2 then
        html "<td colspan=4 align=left>Points are too far apart (";2*r2;") - there are no circles of radius ";r
        RETURN
    end if

    'else, calculate two centers
    cx=(x1+x2)/2 					'middle point
    cy=(y1+y2)/2
    'should move from middle point along perpendicular by dd2
    dd2=sqr(r^2-r2^2)					'perpendicular distance
    dx1=x2-cx   					'vector to middle point
    dy1=y2-cy
    dx = 0-dy1/r2*dd2   				'perpendicular:
    dy = dx1/r2*dd2     				'rotate and scale
    html "<td>";cx+dy;"</td><td>";cy+dx;"</td>"   	'two points, with (+)
    html "<td>";cx-dy;"</td><td>";cy-dx;"</td></TR>" 	'and (-)
RETURN
