  [ $ "turtleduck.qky" loadfile ] now!

  [ [ 50 dup * 2 * 1
    10 vsqrt drop
    join ] constant
    do ]                 is diag       (   --> n/d )

  [ stack 1 ]            is side       (   --> s   )

  [ 0 side take
    - side put ]         is otherside  (   -->     )

  [ 150 1 walk
    -150 1 fly ]         is trunk      (   -->     )

  [ 50 1 fly ]           is inset      (   -->     )

  [ -50 1 fly ]          is outset     (   -->     )

  [ 150 1 fly
    1 2 turn ]           is otherend   (   -->     )

  [ ]                    is zero       (   -->     )

  [ -1 4 turn
    50 side share *
    dup 1 walk
    negate 1 fly
    1 4 turn ]           is one        (   -->     )

  [ inset one outset ]   is two        (   -->     )

  [ -1 side share *
    8 turn
    diag walk
    diag -v fly
    1 side share *
    8 turn ]             is three      (   -->     )

  [ inset
    -3 side share *
    8 turn
    diag walk
    diag -v fly
    3 side share *
    8 turn
    outset ]             is four       (   -->     )

  [ one four ]           is five       (   -->     )

  [ 1 side share *
    4 turn outset
    one
    inset
    -1 side share *
    4 turn ]             is six        (   -->     )

  [ one six ]            is seven      (   -->     )

  [ two six ]            is eight      (   -->     )

  [ one two six ]        is nine       (   -->     )

  [ [ table
      zero one two
      three four five
      six seven eight
      nine ] do ]        is thousands  ( n -->     )

  [ otherend
    thousands
    otherend ]           is units      ( n -->     )

  [ otherside
    units
    otherside ]          is tens       ( n -->     )

  [ otherside
    thousands
    otherside ]          is hundreds   ( n -->     )

  [ inset
    -1 4 turn
    trunk
    ' [ units tens
        hundreds
        thousands ]
    witheach
      [ dip
          [ 10 /mod ]
        do ]
    drop
    1 4 turn
    outset ]             is cistercian ( n -->     )

   [ dup witheach
       [ cistercian
         3 times inset ]
     size 3 * times
       outset ]          is task       ( [ -->     )


  turtle 5 wide -600 1 fly
 ' [ 0 1 20 300 4000 5555 6789 1234 ] task
