Dim Shared As Uinteger largest = 0

Function isColorful(n As Uinteger) As Uinteger
    ' A colorful number cannot be greater than 98765432
    If n < 0 Or n > 98765432 Then Return 0
    If n >= 0 And n < 10 Then Return 1    ' Single digit number is colorful

    Dim As Uinteger i, j, k, tmp = n
    Dim As String s = Str(n)
    Dim As Uinteger digits(Len(s))
    For i = 1 To Len(s)
        digits(i) = Val(Mid(s, i, 1))
        If digits(i) = 0 Or digits(i) = 1 Then Return 0
    Next i

    Dim As Uinteger uniqueDigits(9)
    For i = 1 To Len(s)
        uniqueDigits(digits(i)) += 1
    Next i
    For i = 0 To 9
        If uniqueDigits(i) > 1 Then Return 0
    Next i

    Dim As Uinteger products(Len(s) * (Len(s) + 1) / 2)
    Dim As Uinteger pIndex = 0
    For i = 1 To Len(s)
        products(pIndex) = digits(i)
        pIndex += 1
    Next i
    For i = 2 To Len(s)
        For j = 0 To Len(s) - i
            Dim As Uinteger product = 1
            For k = j To (j + i) - 1
                product *= digits(k + 1)
            Next k
            For k = 0 To pIndex - 1
                If products(k) = product Then Return 0
            Next k
            products(pIndex) = product
            pIndex += 1
        Next j
    Next i

    If tmp > largest Then largest = tmp
    Return 1
End Function

Sub TestColorfuls()
    Dim As Uinteger i, j, k, cnt, x, csum, pow

    Print "Colorful numbers less than 100:"
    cnt = 0
    For i = 0 To 99
        If isColorful(i) Then
            cnt +=1
            Print Using "###"; i;
            If cnt Mod 10 = 0 Then Print
        End If
    Next i

    Print !"\n\nCount of colorful numbers for each order of magnitude:"
    csum = 0
    pow = 10
    For i = 0 To 7
        j = Iif(i = 0, 0, 10 ^ i)
        k = 10 ^ (i + 1) - 1
        cnt = 0
        For x = j To k
            cnt += isColorful(x)
        Next x

        csum += cnt
        Print Using " # digit colourful number count: ####,# - ###.###%"; i+1; cnt; 100 * cnt/pow
        pow = Iif(pow = 10, 90, pow * 10)
    Next i

    Print !"\nLargest magnitude colorful number: "; largest
    Print "Total colorful numbers :"; csum
End Sub

Dim As Double t0 = Timer
TestColorfuls()
Print Using !"\nTook ###.### seconds on i5 @ 3.20 GHz"; Timer - t0

Sleep
