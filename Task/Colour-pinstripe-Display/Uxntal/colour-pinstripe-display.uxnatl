|00 @System [ &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2
	&b $2 &debug $1 &state $1 ]

|20 @Screen [ &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2
	&addr $2 &pixel $1 &sprite $1 ]

|100

( set theme )
#0f00 .System/r DEO2
#00f0 .System/g DEO2
#000f .System/b DEO2

( store screen width )
.Screen/width DEI2 ,draw-layer/width STR2
( store a quarter of the screen height )
.Screen/height DEI2 #02 SFT2 ,/quarter-height STR2

( draw the four stripe layers )
#00
&loop
    ( update y coordinate )
	#00 OVR [ LIT2 &quarter-height $2 ] MUL2 .Screen/y DEO2
    ( draw a layer )
	INCk draw-layer
    ( do it four times )
	INC DUP #04 LTH ?/loop
	POP

BRK

@draw-layer ( step -: )
    ( extend step to short, create counter )
    #00 SWP #0000
    &loop
        ( update x coordinate )
        MUL2k .Screen/x DEO2
        ( fill a region )
        DUP #03 AND #80 ORA .Screen/pixel DEO
        ( loop until the end of the screen )
        INC2 MUL2k [ LIT2 &width $2 ] LTH2 ?/loop
    POP2 POP2 JMP2r
