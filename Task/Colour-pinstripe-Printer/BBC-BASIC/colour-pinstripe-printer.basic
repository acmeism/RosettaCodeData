      PD_RETURNDC = 256
      _LOGPIXELSY = 90

      DIM pd{lStructSize%, hwndOwner%, hDevMode%, hDevNames%, \
      \      hdc%, flags%, nFromPage{l&,h&}, nToPage{l&,h&}, \
      \      nMinPage{l&,h&}, nMaxPage{l&,h&}, nCopies{l&,h&}, \
      \      hInstance%, lCustData%, lpfnPrintHook%, lpfnSetupHook%, \
      \      lpPrintTemplateName%, lpSetupTemplateName%, \
      \      hPrintTemplate%, hSetupTemplate%}
      pd.lStructSize% = DIM(pd{})
      pd.hwndOwner% = @hwnd%
      pd.flags% = PD_RETURNDC

      SYS "PrintDlg", pd{} TO ok%
      IF ok%=0 THEN QUIT
      SYS "DeleteDC", @prthdc%
      @prthdc% = pd.hdc%
      *MARGINS 0,0,0,0

      dx% = @vdu%!236-@vdu%!232
      dy% = @vdu%!244-@vdu%!240
      SYS "GetDeviceCaps", @prthdc%, _LOGPIXELSY TO dpi%

      DIM rc{l%,t%,r%,b%}
      DIM colour%(7)
      colour%() = &000000, &0000FF, &00FF00, &FF0000, \
      \           &FF00FF, &FFFF00, &00FFFF, &FFFFFF

      VDU 2,1,32,3
      pitch% = 1
      FOR y% = 0 TO dy% STEP dpi%
        col% = 0
        FOR x% = 0 TO dx%-pitch% STEP pitch%
          rc.l% = x% : rc.r% = x% + pitch%
          rc.t% = y% : rc.b% = y% + dpi%
          SYS "CreateSolidBrush", colour%(col% MOD 8) TO brush%
          SYS "FillRect", @prthdc%, rc{}, brush%
          SYS "DeleteObject", brush%
          col% += 1
        NEXT
        pitch% += 1
      NEXT y%
      VDU 2,1,12,3
