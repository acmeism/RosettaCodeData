#
# list-compare.icn
#
link fullimag

procedure main()
   L1 := ["aa"]
   L2 := ["aa", "aa", "aa"]
   L3 := ["", "aa", "ab", "ac"]
   L4 := ["aa", "bb", "cc"]
   L5 := ["cc", "bb", "aa"]

   every L := (L1 | L2 | L3 | L4 | L5) do {
      writes(fullimage(L))
      writes(": equal ")
      writes(if allequal(L) then "true" else "false")
      writes(", ascending ")
      write(if ascending(L) then "true" else "false")
   }
end

# test for all identical
procedure allequal(L)
   if *L < 2 then return
   a := L[1]
   every b := L[2 to *L] do {
      if a ~== b then fail
      a := b
   }
   return
end

# test for strictly ascending
procedure ascending(L)
   if *L < 2 then return
   a := L[1]
   every b := L[2 to *L] do {
      if a >>= b then fail
      a := b
   }
   return
end
