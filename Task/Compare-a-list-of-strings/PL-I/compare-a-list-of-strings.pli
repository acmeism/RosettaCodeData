*process source xref attributes or(!);
 /*--------------------------------------------------------------------
 * 01.07.2014 Walter Pachl
 *-------------------------------------------------------------------*/
 clist: Proc Options(main);
 Dcl (hbound) Builtin;
 Dcl sysprint Print;
 Dcl abc(3) Char(2) Init('AA','BB','CC');
 Dcl aaa(3) Char(2) Init('AA','AA','AA');
 Dcl acb(3) Char(2) Init('AA','CC','BB');
 Call test('ABC',ABC);
 Call test('AAA',AAA);
 Call test('ACB',ACB);

 test: Procedure(name,x);
 Dcl name Char(*);
 Dcl x(*) Char(*);
 Dcl (all_equal,increasing) Bit(1) Init('1'b);
 Dcl (i,i1) Bin Fixed(31);
 Dcl txt Char(50) Var;
 Do i=1 To hbound(x)-1 While(all_equal ! increasing);
  i1=i+1;
  Select;
    When(x(i1)=x(i)) increasing='0'b;
    When(x(i1)<x(i)) Do;
                     increasing='0'b;
                     all_equal='0'b;
                     End;
    Otherwise /* x(i1)>x(i) */
                     all_equal='0'b;
    End;
  End;
  Select;
    When(all_equal)  txt='all elements are equal';
    When(increasing) txt='elements are in increasing order';
    Otherwise        txt='neither equal nor in increasing order';
    End;
  Put Skip List(name!!': '!!txt);
  End;
  End;
