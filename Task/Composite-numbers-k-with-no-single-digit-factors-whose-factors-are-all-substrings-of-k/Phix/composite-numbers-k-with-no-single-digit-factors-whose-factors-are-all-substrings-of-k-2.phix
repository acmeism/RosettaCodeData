with javascript_semantics
integer count = 0, n = 11*11,
        limit = iff(platform()=JS?10:20)
atom t0 = time(), t1 = time()
while count<limit do
    string s = sprintf("%d",n)
    integer l = floor(sqrt(n)), k = n, f = 3
    bool valid = true
    while true do
        if remainder(k,f)=0 then
            if f<10 or not match(sprintf("%d",f),s) then
                valid = false
                exit
            end if
            if f=k then exit end if
            k /= f
        else
            f += 2
            if f>l then
                if k=n or not match(sprintf("%d",k),s) then
                    valid = false
                end if
                exit
            end if
        end if
    end while
    if valid then
        count += 1;
        string t = join(apply(prime_factors(n,true,-1),sprint),"x"),
               e = elapsed(time()-t1)
        printf(1,"%2d: %,10d = %-17s (%s)\n",{count,n,t,e})
        t1 = time()
    end if
    n += 2
end while
printf(1,"Total time:%s\n",{elapsed(time()-t0)})
