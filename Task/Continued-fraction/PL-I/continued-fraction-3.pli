/* Derived from continued fraction in Wiki Ada program */

continued_fractions:                         /* 6 Sept. 2012 */
   procedure options (main);
   declare (Sqrt2 initial (1), napier initial (2), pi initial (3)) fixed (1);

Get_Coeffs: procedure (form, n, coefA, coefB);
      declare form fixed (1), n fixed, (coefA, coefB) float (18);

      select (form);
         when (Sqrt2) do;
               if n > 0 then coefA = 2; else coefA = 1;
               coefB = 1;
            end;
         when (Napier) do;
               if n > 0 then coefA = n; else coefA = 2;
               if n > 1 then coefB = n - 1; else coefB = 1;
            end;
         when (Pi) do;
               if n > 0 then coefA = 6; else coefA = 3;
               coefB = (2*n - 1)**2;
            end;
      end;
   end Get_Coeffs;

   Calc: procedure (form, n) returns (float (18));
      declare form fixed (1), n fixed;
      declare (A, B) float (18);
      declare Temp float (18) initial (0);
      declare ni fixed;

      do ni = n to 1 by -1;
         call Get_Coeffs (form, ni, A, B);
         Temp = B/(A + Temp);
      end;
      call Get_Coeffs (form, 0, A, B);
      return (A + Temp);
   end Calc;

   put      edit ('SQRT2=',  calc(sqrt2,  200)) (a(10), f(20,17));
   put skip edit ('NAPIER=', calc(napier, 200)) (a(10), f(20,17));
   put skip edit ('PI=',     calc(pi,   99999)) (a(10), f(20,17));

end continued_fractions;
