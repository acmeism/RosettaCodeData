( ( exact
  =   integerPart decimalPart z
    .     @(!arg:?integerPart "." ?decimalPart)
        &   !integerPart
          + (   @( !decimalPart
                 : (? ((%@:~0) ?:?decimalPart)) [?z
                 )
              & !decimalPart*10^(-1*!z)
            | 0
            )
      | !arg
  )
& ( approximation
  =     integerPart firstDecimals repeatingDecimals
      , x y z z-y x-y numerator denominator
    .   @( !arg
         :   ?integerPart
             "."
             [?x
             ?firstDecimals
             ?repeatingDecimals
             [?y
             !repeatingDecimals
             [?z
         )
      & !z+-1*!y:?z-y
      & !x+-1*!y:?x-y
      & 10:?numerator:?denominator
      & ( !z-y:0&0:?repeatingDecimals
        |   9:?denominator
          &   whl
            ' ( !z+-1:>!y:?z
              & !numerator*10:?numerator
              & !denominator*10+9:?denominator
              )
          & @(!repeatingDecimals:? #?repeatingDecimals)
        )
      & ( @(!firstDecimals:? #?firstDecimals)
        | 0:?firstDecimals
        )
      &   !integerPart
        + !firstDecimals*10^(!x-y+!z-y)
        + !numerator*!denominator^-1*!repeatingDecimals*10^!x-y
  )
&     "0.9054054054"
      "0.5185185185"
      "0.75"
      "0.905405400"
      "0.1428571428"
      "35.000"
      "35.001"
      "0.00000000001"
      "0.000001000001"
      "0.9"
      "0.99"
      "0.909"
      "0.9090"
      "0.90909"
  : ?decs
&   whl
  ' ( !decs:%?dec ?decs
    & approximation$!dec:?approx
    &   out
      $ ( !dec
          "="
          (exact$!dec:?precise)
          ( !approx:!precise&
          | str$("(approx. " !approx ")")
          )
        )
    )
);
