(size, fofl):
Convert_Decimal_To_Rational: procedure options (main); /* 14 January 2014, from Ada */

Real_To_Rational: procedure (R, Bound, Numerator, Denominator) recursive
         options (reorder);
   declare R float (18), Bound float,
          (Numerator, Denominator) fixed binary (31);
   declare Error float;
   declare Best fixed binary initial (1);
   declare Best_Error float initial (huge(error));
   declare I fixed binary (31);

   if R = 0 then
      do;
         Numerator   = 0;
         Denominator = 1;
         return;
      end;
   else if R < 0 then
      do;
         call Real_To_Rational(-R, Bound, Numerator, Denominator);
         Numerator = -Numerator;
         return;
      end;
   else
      do I = 1 to Bound;
         Error = abs(I * R - trunc(I * R + sign(R)*0.5));
         if Error < Best_Error then
            do;
               Best = I;
               Best_Error = Error;
            end;
      end;

   Denominator = Best;
   Numerator   = Denominator * R + sign(R) * 0.5;

end Real_To_Rational;


   declare (Num, Denom) fixed binary (31);
   declare R float (18);
   declare I fixed BINARY;

   do R = 0.75, 0.25,   0.3333333,   0.518518000,  0.905405400,
          0.142857143,  3.141592654, 2.718281828, -0.423310825,
          31.415926536, 0;
      put skip edit(R) (f(13,9));
      do I = 0 to 4;
         call Real_to_Rational(R, 10**I, Num, Denom);
         put edit('   ' || trim(Num) || ' / ' || trim(Denom)) (a);
      end;
   end;
end Convert_Decimal_To_Rational;
