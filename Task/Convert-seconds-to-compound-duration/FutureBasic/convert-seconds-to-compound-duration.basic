include "NSLog.incl"

local fn CompoundDurationString( seconds as long ) as CFStringRef
  long               s = 1, m = s * 60, h = m * 60, d = h * 24, w = d * 7
  long               v(4) : v(0) = w : v(1) = d : v(2) = h : v(3) = m : v(4) = s
  long               i, value
  CFArrayRef         abbr = @[@"wk",@"d",@"hr",@"min",@"sec"]
  CFMutableStringRef string = fn MutableStringWithCapacity(0)

  for i = 0 to 4
    value = seconds / v(i)
    seconds = seconds mod v(i)
    if ( value )
      if ( len(string) ) then MutableStringAppendString( string, @", " )
      MutableStringAppendFormat( string, @"%ld %@", value, abbr[i] )
    end if
  next
end fn = string

NSLog(@"%@",fn CompoundDurationString(7259))
NSLog(@"%@",fn CompoundDurationString(86400))
NSLog(@"%@",fn CompoundDurationString(6000000))

HandleEvents
