10 LET m=60: LET h=60*m: LET d=h*24: LET w=d*7
20 DATA 10,7259,86400,6000000,0,1,60,3600,604799,604800,694861
30 READ n
40 FOR i=1 TO n
50 READ s
60 LET nweek=0: LET nday=0: LET nhour=0: LET nmin=0: LET nsec=0: LET s$=""
70 PRINT s;" = ";
80 IF s>=w THEN LET nweek=INT (s/w): LET s=FN m(s,w)
90 IF s>=d THEN LET nday=INT (s/d): LET s=FN m(s,d)
100 IF s>=h THEN LET nhour=INT (s/h): LET s=FN m(s,h)
110 IF s>=m THEN LET nmin=INT (s/m): LET s=FN m(s,m)
120 LET nsec=INT (s)
130 IF nweek>0 THEN LET s$=s$+STR$ nweek+" wk, "
140 IF nday>0 THEN LET s$=s$+STR$ nday+" d, "
150 IF nhour>0 THEN LET s$=s$+STR$ nhour+" hr, "
160 IF nmin>0 THEN LET s$=s$+STR$ nmin+" min, "
170 IF nsec>0 THEN LET s$=s$+STR$ nsec+" sec"
180 IF s$<>"" THEN IF s$(LEN s$-1)="," THEN LET s$=s$( TO LEN s$-2)
190 PRINT s$
200 NEXT i
210 STOP
220 DEF FN m(a,b)=a-INT (a/b)*b
