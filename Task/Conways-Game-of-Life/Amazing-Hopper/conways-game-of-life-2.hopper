#include <jambo.h>
#define SIZER   90
#define SIZEC   120

Main
    Cls
    Hide cursor

    Dim (SIZER, SIZEC), as zeros 'grid, neighbour_count'
    Dim (SIZER, SIZEC), as fill '" ",disp grid'

    c=0 , Let( c := Utf8(Chr(254)))

    m={}
    Set ' 0,1,1 ' Apend row to 'm'
    Set ' 1,1,0 ' Apend row to 'm'
    Set ' 0,1,0 ' Apend row to 'm'

    [44:46, 59:61] Set 'm', Put 'grid'

    Clr all marks

    radio=1, r=0

    Tok sep '""'

    Locate (1,1)
    Loop
       i=1
       Iterator ( ++i, Less equal(i,SIZER),\
                  j=1, Iterator ( ++j, Less equal(j,SIZEC), \
                           [i,j], Neighbour count (grid,radio), Put 'neighbour_count' ) )

       Cartesian ( Greater equal(grid, 1)---Back up to 'r'--- Mul by 'neighbour_count';\
                                                          Out of range including '1,4' )
       Get range, Set '0," "', Put 'disp grid', Put 'grid', Forget

       Cartesian ( Not( r ); Mul by 'neighbour_count'; Is equal to '3' )
       Get range, Set '1,c', Put 'disp grid', Put 'grid', Forget
       Clr range

       Clr all marks
       Print table 'disp grid'

       Break if ( Key pressed )
    Back
    Pause
    Show cursor
End
