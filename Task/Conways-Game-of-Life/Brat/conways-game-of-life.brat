width = 3
height = 3
rounds = 3

universe = [[0 1 0]
            [0 1 0]
            [0 1 0]]

next = height.of({width.of(0)})

cell = { x, y |
  true? x < width && { x >= 0 && { y >= 0 && { y < height }}}
  {
    universe[y][x]
  }
  { 0 }
}

neighbors = { x, y |
  cell(x - 1, y - 1) +
  cell(x, y - 1) +
  cell(x + 1, y - 1) +
  cell(x + 1, y) +
  cell(x + 1, y + 1) +
  cell(x, y + 1) +
  cell(x - 1, y + 1) +
  cell(x - 1, y)
}

set_next = { x, y, v |
  next[y][x] = v
}

step = {
  universe.each_with_index { row, y |
    row.each_with_index { c, x |
      n = neighbors(x, y)

      when { n < 2 } { set_next x,y, 0 }
           { n > 3 } { set_next x, y, 0 }
           { n == 3 } { set_next x, y, 1 }
           { true } { set_next x, y, c }
    }
  }

  u2 = universe
  universe = next
  next = u2
}

display = {
  p universe.map({ r |
    r.map({ n | true? n == 0, '-', "O" }).join
  }).join("\n")
}

rounds.times {
  display
  p
  step
}
