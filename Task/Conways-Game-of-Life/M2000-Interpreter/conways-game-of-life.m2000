Module Life {
	Font "courier new"
	Form 40, 18
	Cls 3,0
	Double
	Report 2,  "Game of Life"
	Normal
	Cls 5, 2
	Const Mx=20, My=10
	Dim A(0 to Mx+1, 0 to My+1)=0
	Flush
REM	Data (2,2),(2,3),(3,3),(4,3),(5,4),(,3,4),(5,3),(6,2),(8,5),(5,8)
	Data (5,3)
	Data (5,4)
	Data (5,5)
	generation=1
	While not empty
		(A, B)=Array
		A(A,B)=1
	End While
	Display()
	Do
		k$=Key$
		If k$=chr$(13) Then exit
		A()=@GetNext()
		refresh 500
		Display()
	Until A()#Sum()=0
	K$=Key$
	Cls, 0
	End
	Function GetNext()
		Local B()
		B()=A()  ' copy array
		Local B=B()  ' get a pointer
		B*=0  ' make all element zero
		Local i, j, tot
		For j=1 to My
			For i=1 to Mx
				tot=-A(i,j)
				For k=j-1 to j+1
					For m=i-1 to i+1
						tot+=A(m, k)
					Next
				Next
				If A(i,j)=1 Then
					If tot=2 or tot=3 Then B(i,j)=1
				Else.if tot=3 Then
					B(i,j)=1
				End If
			Next
		Next
		=B()
	End Function
	Sub Display()
		Cursor 0,2
		move !    ' move graphic to character cursor
		Fill scale.x, scale.y-pos.Y, 1, 5
		Print "Generation:";Generation
		Generation++
		Local i, j
		For j=1 to My
			Print @(width div 2-Mx div 2);
			For i=1 to Mx
				Print If$(A(I,J)=1->"■", "□");
			Next
			Print
		Next
		Print
		Report 2, "Press enter to exit or any other key for Next generation"
	End Sub
	
}
Life
