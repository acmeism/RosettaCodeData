10 DEFINT A-Z
20 DIM L(16,16), N(16,16)
30 M = 16
40 CLS
50 PRINT "PRESS A KEY TO START...";
60 W = RND(1)
70 IF INKEY$ = "" THEN 60
80 CLS
100 FOR I=1 TO M
110   FOR J=1 TO M
120     IF RND(1)>=.7 THEN N(I,J) = 1 ELSE N(I,J) = 0
130   NEXT J
140 NEXT I
1000 FOR I=0 TO M+1
1010   LOCATE I,0 : PRINT "+";
1020   LOCATE I,M+1 : PRINT "+";
1030   LOCATE 0,I : PRINT "+";
1040   LOCATE M+1,I : PRINT "+";
1050 NEXT I
1080 G=0
1090 LOCATE 1,M+3 : PRINT USING "#####";G
1100 FOR I=1 TO M
1110   FOR J=1 TO M
1115     W = N(I,J) : L(I,J) = W
1120     LOCATE I,J
1130     IF W=0 THEN PRINT " "; ELSE PRINT "*";
1160   NEXT J
1170 NEXT I
1180 FOR I=1 TO M
1190   FOR J=1 TO M
1200     NC=0
1210     FOR K=I-1 TO I+1
1215       IF K=0 OR K>M THEN 1260
1220       FOR W=J-1 TO J+1
1230         IF W=0 OR W>M OR (K=I AND W=J) THEN 1250
1240         NC = NC + L(K,W)
1250       NEXT W
1260     NEXT K
1270     IF NC=2 THEN N(I,J)=L(I,J) : GOTO 1300
1280     IF NC=3 THEN N(I,J)=1 ELSE N(I,J)=0
1300   NEXT J
1310 NEXT I
1350 G=G+1
1360 GOTO 1090
