SCREEN 9, 0, 0, 1

RANDOMIZE TIMER

WINDOW (0, 0)-(80, 80)

'La matrizA es la actual, la matrizB es la siguiente iteración
'ArrayA is current, arrayB is next iteration
DIM matrizA(-1 TO 81, -1 TO 81)
DIM matrizB(-1 TO 81, -1 TO 81)

'Aleatorizar las celdas de matrizA,
'Randomize cells in arrayA,
'y establecer las de matrizB a 0
'and set those of matrixB to 0
y = 0
DO
	x = 0
	DO
		x = x + 1
		matrizA(x, y) = INT(RND + .5)
        matrizB(x, y) = 0
    LOOP UNTIL x > 80

    y = y + 1
LOOP UNTIL y > 80

''--- Bucle Principal ---
'' --- Main Loop ---
DO
    CLS
    'Dibuja la matriz
    'Draw the matrix
    y = 0
    DO
        x = 0			
        DO
            IF matrizA(x, y) = 1 THEN LINE (x, y)-(x + 1, y + 1), 1, BF
            x = x + 1
        LOOP UNTIL x > 80

        y = y + 1
    LOOP UNTIL y > 80

    'Cuenta el recuento de la celda circundante
    'Counts the count of the surrounding cell
     'Luego aplica la operación a la celda
     'Then apply the operation to the cell
    y = 0
    DO
        x = 0
        DO
            'Cuenta las células circundantes
            'Count the surrounding cells
            cuenta = 0

            IF matrizA(x - 1, y + 1) = 1 THEN cuenta = cuenta + 1
            IF matrizA(x, y + 1) = 1 THEN cuenta = cuenta + 1
            IF matrizA(x + 1, y + 1) = 1 THEN cuenta = cuenta + 1
            IF matrizA(x - 1, y) = 1 THEN cuenta = cuenta + 1
            IF matrizA(x + 1, y) = 1 THEN cuenta = cuenta + 1
            IF matrizA(x - 1, y - 1) = 1 THEN cuenta = cuenta + 1
            IF matrizA(x, y - 1) = 1 THEN cuenta = cuenta + 1
            IF matrizA(x + 1, y - 1) = 1 THEN cuenta = cuenta + 1

            'Aplica las operaciones
            'Apply the operations
            'Muerte
            'Death
            IF matrizA(x, y) = 1 THEN
                IF cuenta = 2 OR cuenta = 3 THEN matrizB(x, y) = 1 ELSE matrizB(x, y) = 0
            END IF

            'Nacimiento
            'Birth
            IF matrizA(x, y) = 0 THEN
                IF cuenta = 3 THEN matrizB(x, y) = 1 ELSE matrizB(x, y) = 0
            END IF
            x = x + 1
        LOOP UNTIL x > 80

        y = y + 1
    LOOP UNTIL y > 80

    'Actualiza la matriz con la nueva matriz que hemos calculado.
    'Update the matrix with the new matrix that we have calculated.
    y = 0
    DO
        x = 0
        DO
            x = x + 1
            matrizA(x, y) = matrizB(x, y)
        LOOP UNTIL x > 80
        y = y + 1

    LOOP UNTIL y > 80
    PCOPY 0, 1
LOOP WHILE INKEY$ = ""
