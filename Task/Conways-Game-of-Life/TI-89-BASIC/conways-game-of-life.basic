Define life(pattern) = Prgm
  Local x,y,nt,count,save,xl,yl,xh,yh
  Define nt(y,x) = when(pxlTest(y,x), 1, 0)

  {}→save
  setGraph("Axes", "Off")→save[1]
  setGraph("Grid", "Off")→save[2]
  setGraph("Labels", "Off")→save[3]
  FnOff
  PlotOff
  ClrDraw

  If pattern = "blinker" Then
    36→yl
    40→yh
    78→xl
    82→xh
    PxlOn  36,80
    PxlOn  38,80
    PxlOn  40,80
  ElseIf pattern = "glider" Then
    30→yl
    40→yh
    76→xl
    88→xh
    PxlOn  38,76
    PxlOn  36,78
    PxlOn  36,80
    PxlOn  38,80
    PxlOn  40,80
  ElseIf pattern = "r" Then
    38-5*2→yl
    38+5*2→yh
    80-5*2→xl
    80+5*2→xh
    PxlOn  38,78
    PxlOn  36,82
    PxlOn  36,80
    PxlOn  38,80
    PxlOn  40,80
  EndIf

  While getKey() = 0
    © Expand upper-left corner to whole cell
    For y,yl,yh,2
      For x,xl,xh,2
        If pxlTest(y,x) Then
          PxlOn y+1,x
          PxlOn y+1,x+1
          PxlOn y,  x+1
        Else
          PxlOff y+1,x
          PxlOff y+1,x+1
          PxlOff y,  x+1
        EndIf
      EndFor
    EndFor

    © Compute next generation
    For y,yl,yh,2
      For x,xl,xh,2
        nt(y-1,x-1) + nt(y-1,x) + nt(y-1,x+2) + nt(y,x-1) + nt(y+1,x+2) + nt(y+2,x-1) + nt(y+2,x+1) + nt(y+2,x+2) → count
        If count = 3 Then
          PxlOn y,x
        ElseIf count ≠ 2 Then
          PxlOff y,x
        EndIf
      EndFor
    EndFor
  EndWhile

  © Restore changed options
  setGraph("Axes", save[1])
  setGraph("Grid", save[2])
  setGraph("Labels", save[3])
EndPrgm
