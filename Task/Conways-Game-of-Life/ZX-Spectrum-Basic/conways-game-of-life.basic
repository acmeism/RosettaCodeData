10 REM Initialize
20 LET w=32*22
30 DIM w$(w): DIM n$(w)
40 FOR n=1 TO 100
50 LET w$(RND*w+1)="O"
60 NEXT n
70 REM Loop
80 FOR i=34 TO w-34
90 LET p$="": LET d=0
100 LET p$=p$+w$(i-1)+w$(i+1)+w$(i-33)+w$(i-32)+w$(i-31)+w$(i+31)+w$(i+32)+w$(i+33)
110 LET n$(i)=w$(i)
120 FOR n=1 TO LEN p$
130 IF p$(n)="O" THEN LET d=d+1
140 NEXT n
150 IF (w$(i)=" ") AND (d=3) THEN LET n$(i)="O": GO TO 180
160 IF (w$(i)="O") AND (d<2) THEN LET n$(i)=" ": GO TO 180
170 IF (w$(i)="O") AND (d>3) THEN LET n$(i)=" "
180 NEXT i
190 PRINT AT 0,0;w$
200 LET w$=n$
210 GO TO 80
