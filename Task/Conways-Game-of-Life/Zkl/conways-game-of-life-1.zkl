class Life{
   fcn init(n, r1,c1, r2,c2, etc){
      var N=n, cells=Data(n*n), tmp=Data(n*n),
        ds=T(T(-1,-1),T(-1,0),T(-1,1), T(0,-1),T(0,1), T(1,-1),T(1,0),T(1,1));
      icells:=vm.arglist[1,*];
      (N*N).pump(Void,cells.append.fpM("1-",0));	// clear board
      icells.pump(Void,Void.Read,fcn(row,col){ cells[row*N+col]=1 });
   }
   fcn get(row,col){
      if((0<=row<N) and (0<=col<N)) return(cells[row*N+col]);
      return(0);
   }
   fcn postToastie(row,col){
      n:=ds.reduce('wrap(n,[(r,c)]){n+get(r+row,c+col)},0);
      c:=get(row,col);
      ((n==2 and c==1) or n==3).toInt()
   }
   fcn cycle{
      tmp.clear();
      foreach row in (N){ foreach col in (N) {
         tmp.append(postToastie(row,col)) } }
      t:=cells; cells=tmp; tmp=t;
   }
   fcn toString{
      cells.pump(0,String,fcn(c,rn){
         (if(c)"*" else "-") + (if(rn.inc()%N) "" else "\n")
      }.fp1(Ref(1)));
   }
   fcn toAnsi{
      cells.pump(0,"\e[H",fcn(c,rn){
         (if(c)"\e[07m  \e[m" else "  ") + (if(rn.inc()%N) "" else "\e[E")
      }.fp1(Ref(1)));
   }
   fcn dance(n=300){ do(n){ toAnsi().print(); Atomic.sleep(0.2); cycle(); } }
}
