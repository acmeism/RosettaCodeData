include c:\cxpl\codes;  \intrinsic 'code' declarations
string 0;               \use zero-terminated strings, instead of MSb terminated


func    StrNCmp(A, B, N); \Compare string A to string B up to N bytes long
\This returns:
\       >0 if A > B
\       =0 if A = B
\       <0 if A < B
char    A, B;           \strings to be compared
int     N;              \number of bytes to compare
int     I;
[for I:= 0 to N-1 do
    if A(I) # B(I) then
        return A(I) - B(I);
return 0;               \they're equal
];      \StrNCmp


func    StrLen(Str);    \Return the number of characters in an ASCIIZ string
char    Str;
int     I;
for I:= 0 to -1>>1-1 do
        if Str(I) = 0 then return I;


func    SubStr(A, B);   \Count number of times string B occurs in A
char    A, B;
int     LA, LB, C, I;
[LA:= StrLen(A);  LB:= StrLen(B);
C:= 0;  I:= 0;
while I < LA do
        if StrNCmp(B, A+I, LB) = 0 then [C:= C+1;  I:= I+LB]
        else I:= I+1;
return C;
];


[IntOut(0, SubStr("the three truths", "th"));  CrLf(0);
 IntOut(0, SubStr("ababababab", "abab"));  CrLf(0);
]
