*        Create an HTML table      19/02/2017
CREHTML  CSECT
         USING  CREHTML,R13
         B      72(R15)
         DC     17F'0'
         STM    R14,R12,12(R13)
         ST     R13,4(R15)
         ST     R15,8(R13)
         LR     R13,R15            end of prolog
         LA     R8,RND
         XPRNT  PGBODY,64          <html><head></head><body>
         XPRNT  PGTAB,64           <table border=1 ... cellspacing=0>
         SR     R6,R6              row=0
       DO WHILE=(C,R6,LE,NROWS)    do row=0 to nrows
       IF LTR,R6,Z,R6 THEN           if row=0
         XPRNT  PGTRTH,64              <tr><th></th>
       ELSE     ,                    else
         XDECO  R6,XDEC                edit row
         MVC    PGTR+8(1),XDEC+11      output row heading
         XPRNT  PGTR,64                <tr><th>.</th>
       ENDIF    ,                    endif
         LA     R7,1                 col=1
       DO WHILE=(C,R7,LE,NCOLS)      do col=1 to ncols
       IF LTR,R6,Z,R6 THEN             if row=0
         LR     R1,R7                    col
         LA     R4,TCAR-1(R1)            tcar(col)
         MVC    PGTH+4(1),0(R4)          output heading
         XPRNT  PGTH,64                  <th>.</th>
       ELSE     ,                      else
         L      R2,0(R8)                 value
         XDECO  R2,XDEC                  edit value
         MVC    PGTD+18(4),XDEC+8        output cell value
         XPRNT  PGTD,64                  <td align="right">....</td>
         LA     R8,4(R8)                 next value
       ENDIF    ,                      endif
         LA     R7,1(R7)               col++
       ENDDO    ,                    enddo col
         XPRNT  PGETR,64             </tr>
         LA     R6,1(R6)             row++
       ENDDO    ,                  enddo row
         XPRNT  PGETAB,64          </table>
         XPRNT  PGEBODY,64         </body></html>
         L      R13,4(0,R13)       epilog
         LM     R14,R12,12(R13)
         XR     R15,R15
         BR     R14                exit
NROWS    DC     F'4'               number of rows
NCOLS    DC     F'3'               number of columns
TCAR     DC     CL3'XYZ'
RND      DC     F'7055',F'5334',F'5795',F'2895',F'3019',F'7747'
         DC     F'140',F'7607',F'8144',F'7090',F'475',F'4140'
PGBODY   DC     CL64'<html><head></head><body>'
PGTAB    DC     CL64'<table border=1 cellpadding=10 cellspacing=0>'
PGTRTH   DC     CL64'<tr><th></th>'
PGTH     DC     CL64'<th>.</th>'
PGETR    DC     CL64'</tr>'
PGTR     DC     CL64'<tr><th>.</th>'
PGTD     DC     CL64'<td align="right">....</td>'
PGETAB   DC     CL64'</table>'
PGEBODY  DC     CL64'</body></html>'
XDEC     DS     CL12
         YREGS
         END    CREHTML
