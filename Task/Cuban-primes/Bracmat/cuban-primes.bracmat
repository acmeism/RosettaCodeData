( ( cubanprimes
  =
    .   !arg:(?N.?bigN)
      & :?cubans
      & (0.1.1):(?cube100k.?cube1.?count)
      & 0:?i
      &   whl
        ' ( 1+!i:?i
          & !i+1:?j
          & !j^3:?cube2
          & !cube2+-1*!cube1:?diff
          & ( !diff^1/2:~(!diff^?)
            |   (   !count:~>!N
                  & !diff !cubans:?cubans
                |
                )
              & ( !count:<!bigN
                | !diff:?cube100k&~
                )
              & 1+!count:?count
            )
          & !cube2:?cube1
          )
      & ( columnwidth
        =   cols
          .   !arg:(%@:@(?:? [?cols)) ?
            & div$(-1+!cols.3)*4+1
        )
      & columnwidth$!cubans:?colwidth
      & \n:?table
      & 0:?col
      & ( format
        =   n col cif R
          .   !arg:(?n.?col)
            & -1:?cif
            &     vap
                $ ( (
                    =
                      .   (   mod$(1+!cif:?cif.3):0
                            & -2+!col:?col
                            & ","
                          | -1+!col:?col&
                          )
                          !arg
                    )
                  . rev$!n
                  )
              : "," ?R
            & rev$(str$!R):?R
            &   whl
              ' ( !col+-1:?col:>-2
                & " " !R:?R
                )
            & str$!R
        )
      &   whl
        ' ( !cubans:%?cuban ?cubans
          & mod$(1+!col:?col.10):?col
          &     (!col:0&\n|" ")
                format$(!cuban.!colwidth)
                !table
            : ?table
          )
      & out$(str$("The first " !N " cuban primes are: " !table))
      &   out
        $ ( str
          $ ( "The 100,000th cuban prime is "
              format$(!cube100k.columnwidth$!cube100k)
            )
          )
  )
& cubanprimes$(200.100000)
)
