Module Currency_Task {
      Locale 1033
      Font "Courier New"
      Form 80,32
      hamburgers=4000000000000000@
      hamburger_price=5.5#
      milkshakes=2#
      milkshake_price=2.86#
      tax_rate=0.0765#
      PrHeadLine("Item","price","quantity", "value")
      PrLine("hamburger",hamburger_price,hamburgers,hamburgers*hamburger_price)
      PrLine("milkshake", milkshake_price,milkshakes,milkshakes*milkshake_price)
      PrResults( "subtotal", hamburgers*hamburger_price+milkshakes*milkshake_price)
      PrResults("tax", (hamburgers*hamburger_price+milkshakes*milkshake_price)*tax_rate)
      PrResults("total", (hamburgers*hamburger_price+milkshakes*milkshake_price)*(tax_rate+1))

      h_p_q=hamburgers*hamburger_price
      m_p_q=milkshakes*milkshake_price
      Document Doc$=format$("{0:15}{1:-8}{2:-25}{3:-30}","Item", "price", "quantity", "value")+{
      }+format$("{0:15}{1:-8}{2:-25}{3:-30}","hamburger",str$(hamburger_price,"$#,##0.00"),str$(hamburgers, "#,##0"), Str$(h_p_q,"$#,##0.00"))+{
      }+format$("{0:15}{1:-8}{2:-25}{3:-30}","milkshake", str$(milkshake_price,"$#,##0.00"),Str$(milkshakes, "#,##0"), Str$(m_p_q,"$#,##0.00"))+{
      }+format$("{0:-48}{1:-30}","subtotal", Str$(h_p_q+m_p_q,"$#,##0.00"))+{
      }+format$("{0:-48}{1:-30}","tax", Str$((h_p_q+m_p_q)*tax_rate,"$#,##0.00"))+{
      }+format$("{0:-48}{1:-30}","total", Str$((h_p_q+m_p_q)*(tax_rate+1),"$#,##0.00"))+{
      }
      clipboard Doc$


      Sub PrHeadLine(a$,b$,c$,d$)
            Print Part  $(1,15),a$,$(3,8),b$, $(3,25),c$, $(3,30),d$
            Print
      End Sub
      Sub PrLine(a$,b,c,d)
            Print Part  $(1,15),a$,$("$#,###.00"),$(3,8),b, $("#,##0"),$(3,25),c,$("$#,###.00"), $(3,30),d
            Print
      End Sub
      Sub PrResults(a$,b)
            Print Part  $(3,48),a$,$("$#,###.00"),$(3,30),b
            Print
      End Sub
}
Currency_Task
