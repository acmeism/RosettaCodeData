string  0;              \use zero-terminated strings

func Damm(Input);       \Return 'true' if checksum is correct
char Input;
int  Interim, Table;
[Table:= [
    [0, 3, 1, 7, 5, 9, 8, 6, 4, 2],
    [7, 0, 9, 2, 1, 5, 4, 8, 6, 3],
    [4, 2, 0, 6, 8, 7, 1, 3, 5, 9],
    [1, 7, 5, 0, 9, 8, 3, 4, 2, 6],
    [6, 1, 2, 3, 0, 4, 5, 9, 7, 8],
    [3, 6, 7, 4, 2, 0, 9, 5, 8, 1],
    [5, 8, 6, 9, 7, 2, 0, 1, 3, 4],
    [8, 9, 4, 5, 3, 6, 2, 0, 1, 7],
    [9, 4, 3, 8, 6, 1, 7, 2, 0, 5],
    [2, 5, 8, 1, 4, 3, 6, 7, 9, 0] ];
Interim:= 0;
while Input(0) do
    [Interim:= Table(Interim, Input(0)-^0);
    Input:= Input+1;
    ];
return Interim = 0;
];

int String, I;
[String:= ["5724", "5727", "112946", "112949"];
for I:= 0 to 4-1 do
    [Text(0, String(I));  ChOut(0, 9\tab\);
    Text(0, if Damm(String(I)) then "true" else "false");
    CrLf(0);
    ];
]
