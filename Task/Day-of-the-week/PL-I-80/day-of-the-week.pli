/* Test of PL/I-80 routine to determine day of the week */

sunday_christmas:
    proc options (main);
    %replace
      sunday by 0;
    dcl
        (year, w) fixed bin(15);
    put skip list ('Christmas will fall on Sunday in these years:');
    do year = 2008 to 2121;
        w = weekday((year),12,25);
        if w = sunday then
           put skip edit (year) (f(4));
    end;

    stop;

/*
*  Return day of week (Sun=0, Mon=1, etc.) for a given
*  yr, mo, da using Zeller's congruence
*/
weekday:
    proc (yr, mo, da) returns (fixed bin(15));
    dcl (yr, mo, da) fixed bin(15);
    dcl (c, y, m, d, z) fixed bin(15);
    y = yr;  /* make local copies */
    m = mo;
    d = da;
    if m < 3 then
        do;
            m = m + 10;
            y = y - 1;
        end;
    else m = m - 2;
    c = y / 100;
    y = mod(y, 100);
    z = (26 * m - 2) / 10;
    z = z + d + y + (y/4) + (c/4) - 2 * c + 777;
    return (mod(z, 7));
end weekday;

end sunday_christmas;
