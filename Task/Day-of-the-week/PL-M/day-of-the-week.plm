100H: /* FIND YEARS WHERE CHRISTMAS DAY FALLS ON A SUNDAY                    */

   /* CP/M BDOS SYSTEM CALL AND I/O ROUTINES                                 */
   BDOS: PROCEDURE( FN, ARG ); DECLARE FN BYTE, ARG ADDRESS; GOTO 5; END;
   PR$CHAR:   PROCEDURE( C ); DECLARE C BYTE;    CALL BDOS( 2, C );  END;
   PR$STRING: PROCEDURE( S ); DECLARE S ADDRESS; CALL BDOS( 9, S );  END;
   PR$NL:     PROCEDURE;   CALL PR$CHAR( 0DH ); CALL PR$CHAR( 0AH ); END;
   PR$NUMBER: PROCEDURE( N ); /* PRINTS A NUMBER IN THE MINIMUN FIELD WIDTH  */
      DECLARE N ADDRESS;
      DECLARE V ADDRESS, N$STR ( 6 )BYTE, W BYTE;
      V = N;
      W = LAST( N$STR );
      N$STR( W ) = '$';
      N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      DO WHILE( ( V := V / 10 ) > 0 );
         N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      END;
      CALL PR$STRING( .N$STR( W ) );
   END PR$NUMBER;

   /* TASK                                                                   */

   /* RETURNS THE DAY OF THE WEEK CORRESPONDING To D/M/Y                     */
   DAY$OF$WEEK: PROCEDURE( D, M, Y )BYTE;
      DECLARE ( D, M, Y ) ADDRESS;
      DECLARE ( J, K, MM, YY ) ADDRESS;
      MM = M;
      YY = Y;
      IF MM <= 2 THEN DO;
         MM = MM + 12;
         YY = YY - 1;
      END;
      J = YY  / 100;
      K = YY MOD 100;
      RETURN ( D + ( ( MM + 1 ) * 26 ) / 10 + K + K / 4 + J / 4 + 5 * J )
             MOD 7;
   END DAY$OF$WEEK ;

   DECLARE ( YEAR, MONTH, DAY, COUNT ) ADDRESS;
   CALL PR$STRING( .'25TH OF DECEMBER IS A SUNDAY IN$' );CALL PR$NL;
   COUNT = 0;
   DO YEAR = 2008 TO 2121;
      DAY = DAY$OF$WEEK( 25, 12, YEAR );
      IF DAY = 1 THEN DO;
         CALL PR$CHAR( ' ' );CALL PR$NUMBER( YEAR );
         IF ( COUNT := COUNT + 1 ) MOD 10= 0 THEN CALL PR$NL;
      END;
   END;

EOF
