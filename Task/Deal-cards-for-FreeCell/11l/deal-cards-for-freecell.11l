F randomGenerator(=seed, n)
   [Int] r
   -V max_int32 = 7FFF'FFFF
   seed = seed [&] max_int32

   L r.len < n
      seed = (seed * 214013 + 2531011) [&] max_int32
      r [+]= seed >> 16

   R r

F deal(seed)
   V nc = 52
   V cards = Array((nc - 1 .< -1).step(-1))
   V rnd = randomGenerator(seed, nc)
   L(r) rnd
      V j = (nc - 1) - r % (nc - L.index)
      swap(&cards[L.index], &cards[j])
   R cards

F show(cards)
   V l = cards.map(c -> ‘A23456789TJQK’[Int(c / 4)]‘’‘CDHS’[c % 4])
   L(i) (0 .< cards.len).step(8)
      print((l[i .< i + 8]).join(‘ ’))

:start:
V seed = I :argv.len == 2 {Int(:argv[1])} E 11982
print(‘Hand #.’.format(seed))
V deck = deal(seed)
show(deck)
