      *FLOAT 64

      hand% = 617

      REM Initialise card library:
      SYS "LoadLibrary", "CARDS.DLL" TO cards%
      IF cards% = 0 ERROR 100, "No CARDS library"
      SYS "GetProcAddress", cards%, "cdtInit" TO cdtInit%
      SYS "GetProcAddress", cards%, "cdtDraw" TO cdtDraw%
      SYS cdtInit%, ^dx%, ^dy%
      VDU 23,22,8*dx%;5*dy%;8,16,16,128

      REM Initialise deck:
      DIM card&(51)
      FOR I% = 0 TO 51 : card&(I%) = I% : NEXT

      REM Shuffle deck:
      dummy% = FNrng(hand%)
      FOR I% = 51 TO 0 STEP -1
        C% = FNrng(-1) MOD (I% + 1)
        SWAP card&(C%), card&(I%)
      NEXT

      REM Display deck:
      FOR I% = 0 TO 51
        C% = card&(51 - I%)
        X% = (I% MOD 8) * dx%
        Y% = (I% DIV 8) * dy% * 2 / 3
        SYS cdtDraw%, @memhdc%, X%, Y%, C%, 0, 0
      NEXT
      SYS "InvalidateRect", @hwnd%, 0, 0
      *GSAVE freecell
      END

      DEF FNrng(seed)
      PRIVATE state, M%
      IF seed >= 0 THEN
        state = seed
      ELSE
        state = (state * 214013 + 2531011)
        FOR M% = 52 TO 31 STEP -1
          IF state >= 2^M% state -= 2^M%
        NEXT
      ENDIF
      = state >> 16
