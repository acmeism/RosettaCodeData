(  ( createArray
  =   array rank ranks suit suits
    .   A 2 3 4 5 6 7 8 9 T J Q K:?ranks
      & :?array
      &   whl
        ' ( !ranks:%?rank ?ranks
          & ♣ ♦ ♥ ♠:?suits
          &   whl
            ' ( !suits:%?suit ?suits
              & !array str$(!rank !suit):?array
              )
          )
      & !array
  )
& ( deal
  =     A B D L Z pick card dealt deck
      , i last rand row state
    .   !arg:(?deck:? [?L.?state)
      & 8:?row
      & :?dealt
      & ( pick
        =   sep
          .   (   -1+!row:>0:?row
                & " ":?sep
              | \n:?sep&8:?row
              )
            & !dealt !arg !sep:?dealt
        )
      & 2^31:?B
      & 2^16:?D
      & "
        'Hard code' the numbers B and D into the rand function using
        macro expansion. (Gives a marginally faster execution speed.)
        "
      &
          ' (
            .   mod$(!state*214013+2531011.$B):?state
              & div$(!state.$D)
            )
        : (=?rand)
      & !L+1:?L
      &   whl
        ' ( mod$(rand$.!L+-1:?L):?i
          & !deck:?A [!i %?card ?Z
          &   ( !Z:?Z %@?last&!A !last !Z
              | !A
              )
            : ?deck
          & pick$!card
          )
      & pick$\n
      & str$!dealt
  )
& createArray$:?deck
& put$("Game #1\n","dealt.txt",NEW)
& put$(deal$(!deck.1),"dealt.txt",APP)
& put$("

Game #617
","dealt.txt",APP)
& put$(deal$(!deck.617),"dealt.txt",APP)
&
)
