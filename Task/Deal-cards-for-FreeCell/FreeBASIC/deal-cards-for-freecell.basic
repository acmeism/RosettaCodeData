' version 04-11-2016
' compile with: fbc -s console

' to seed ms_lcg(seed > -1)
' to get random number ms_lcg(-1) or ms_lcg() or just ms_lcg
Function ms_lcg(seed As Integer = -1) As UInteger

    Static As UInteger ms_state

    If seed <> -1 Then
        ms_state = seed Mod 2 ^ 31
    Else
        ms_state = (214013 * ms_state + 2531011) Mod 2 ^ 31
    End If

    Return ms_state Shr 16

End Function

' ------=< MAIN >=------

Dim As UByte card(51)
Dim As String suit = "CDHS", value = "A23456789TJQK"
Dim As Long i, c, s, v, game = 1
Dim As ULong game_nr(1 To 2) = { 1, 617}

Do

    ms_lcg(game_nr(game))       ' seed generator
    Print "game #"; game_nr(game)
    game = game + 1

    For i = 0 To 51             ' set up the cards
        card(i) = i
    Next

    For i = 51 To 0 Step -1     ' shuffle
        c = ms_lcg Mod (i +1)
        Swap card(i), card(c)
    Next

    c = 0
    Do
        For i = 0 To 7
            s = card(51 - c) Mod 4
            v = card(51 - c) \ 4
            Print Chr(value[v]); Chr(suit[s]); " ";
            c = c +1
            If c > 51 Then Exit Do
        Next
        Print
    Loop
    Print : Print

Loop Until game > UBound(game_nr)


' empty keyboard buffer
While Inkey <> "" : Wend
Print : Print "hit any key to end program"
Sleep
End
