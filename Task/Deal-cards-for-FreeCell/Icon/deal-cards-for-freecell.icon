procedure main(A)          # freecelldealer
   freecelldealer(\A[1] | &null)      # seed from command line
end

procedure newDeck()                   #: return a new unshuffled deck
   every D := list(52) & i := 0 & r := !"A23456789TJQK" & s := !"CDHS" do
      D[i +:= 1] := r || s   # initial deck AC AD ... KS
   return D
end

procedure freecelldealer(gamenum)     #: deal a freecell hand
   /gamenum := 11982
   return showHand(freecellshuffle(newDeck(),gamenum))
end

procedure showHand(D)                 #: show a freecell hand
   write("Hand:\n")
   every writes("  ",(1 to 8) | "\n")
   every writes(" ",D[i := 1 to *D]) do
      if i%8 = 0 then write()
   write("\n")
   return D
end

procedure freecellshuffle(D,gamenum)  #: freecell shuffle

   srand_freecell(gamenum)                        # seed random number generator
   D2 := []
   until *D = 0 do {                              # repeat until all dealt
      D[r := rand_freecell() % *D + 1] :=: D[*D]  # swap random & last cards
      put(D2,pull(D))                             # remove dealt card from list
      }
   return D2
end

procedure srand_freecell(x)           #: seed random
static seed
   return seed := \x | \seed | 0      # parm or seed or zero if none
end

procedure rand_freecell()             #: lcrng
   return ishift(srand_freecell((214013 * srand_freecell() + 2531011) % 2147483648),-16)
end
