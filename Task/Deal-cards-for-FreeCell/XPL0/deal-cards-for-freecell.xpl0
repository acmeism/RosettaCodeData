include c:\cxpl\codes;                  \intrinsic 'code' declarations
string 0;                               \use zero-terminated string convention
int  RandState;

func Rand;                              \Random number in range 0 to 32767
[RandState:= (214013*RandState + 2531011) & $7FFF_FFFF;
return RandState >> 16;
];

int  Card, Deck(52), Size;
char Suit, Rank;
[RandState:= IntIn(8);                  \seed RNG with number from command line
for Card:= 0 to 52-1 do Deck(Card):= Card; \create array of 52 cards
Rank:= "A23456789TJQK";
Suit:= "CDHS";
Size:= 52;
repeat  Card:= rem(Rand/Size);          \choose a random card
        ChOut(0, Rank(Deck(Card)/4));   \deal it by showing it
        ChOut(0, Suit(rem(0)));
        if rem(Size/8)=5 then CrLf(0) else ChOut(0, ^ );
        Size:= Size-1;                  \one less card in deck
        Deck(Card):= Deck(Size);        \replace dealt card with last card
until   Size = 0;                       \all cards have been dealt
]
