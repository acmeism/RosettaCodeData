PROC PrintBH(BYTE a)
  BYTE ARRAY hex=['0 '1 '2 '3 '4 '5 '6 '7 '8 '9 'A 'B 'C 'D 'E 'F]

  Put(hex(a RSH 4))
  Put(hex(a&$0F))
RETURN

PROC Test(CHAR ARRAY s)
  BYTE i,pos

  pos=0
  FOR i=2 TO s(0)
  DO
    IF s(i)#s(1) THEN
      pos=i
      EXIT
    FI
  OD

  PrintF("""%S"" (len=%B) -> ",s,s(0))
  IF pos=0 THEN
    PrintE("all characters are the same.")
  ELSE
    PrintF("""%C"" (hex=$",s(pos))
    PrintBH(s(pos))
    PrintF(") is the first difference at pos. %B.%E",pos)
  FI
  PutE()
RETURN

PROC Main()
  Test("")
  Test("   ")
  Test("2")
  Test("333")
  Test(".55")
  Test("tttTTT")
  Test("4444 444k")
RETURN
