  [ 0 swap
    dup size 0 = iff
      drop done
    behead swap witheach
      [ over != if
          [ drop i^ 1+
            swap conclude ] ]
    drop ]                    is allsame ( [ --> n )

  ( 0 indicates all items are the same,
    otherwise n is first different item )

  [ say ' String: "' dup echo$
    say '" length ' dup size echo
    dup allsame dup 0 = iff
      [ say ', all characters are the same.'
        2drop ]
    else
      [ say ', first difference is item '
        dup echo
        say ', char "'
        peek dup emit
        say '", hex: '
        16 base put echo base release
        say '.' ]
    cr ]                      is task    ( $ -->   )

  $ ""          task
  $ "   "       task
  $ "2"         task
  $ ".55"       task
  $ "tttTTT"    task
  $ "4444 444k" task
