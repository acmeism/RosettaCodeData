≪ DUP SIZE → s n
   ≪ "Examining [" s + "], length=" + n →STR + +
      1 CF
      IF n THEN
         s 1 1 SUB 1
         DO
            s OVER DUP SUB
            IF 3 PICK OVER ≠ THEN
               1 SF
               ROT DROP SWAP
            ELSE DROP
            END
         1 +
         UNTIL DUP n ≥ 1 FS? OR END
         DROP
      ELSE DUP
      END
      IF 1 FC? THEN
         DROP
         "All chars are the same."
      ELSE
         "'" OVER +
         "' (hex=" + OVER NUM HEX R→B →STR +
         ") is different at pos " + s 4 ROLL POS →STR +
      END
   ≫
≫
'ALLSAME?' STO
≪ { "" "   " "2" "333" ".55" "tttTTT" "4444 444k" }
   → tl ≪
    1 tl SIZE FOR j
      tl j GET ALLSAME?
   NEXT
   ≫
≫
EVAL
