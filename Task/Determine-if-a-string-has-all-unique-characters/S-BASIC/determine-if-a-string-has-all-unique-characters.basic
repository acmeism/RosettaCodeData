comment
  Return 0 if there are no repeated characters in s
  (including the special case of an empty string),
  otherwise the first position where a character
  duplicates one previously encountered
end
function repeated_chars(s = string) = integer
   var i, j, slen, result = integer
   slen = len(s)
   if slen < 2 then
      result = 0
   else
      begin
         result = 0
         for i = 1 to slen - 1
            for j = i + 1 to slen
               if mid(s,i,1) = mid(s,j,1) and result = 0 then
                  result = j
            next j
         next i
      end
end = result

procedure report(str = string)
   var p, first = integer
   p = repeated_chars(str)
   if p = 0 then
      print chr(34); str; chr(34); " (length ="; len(str); \
      ") has no duplicate characters"
   else
      begin
         first = instr(1, str, mid(str,p,1))
         print chr(34); str; chr(34); " (length ="; len(str); \
         ") has duplicate characters:"
         print "'"; mid(str,p,1); "' (= "; right$(hex$(asc(mid(str,p,1))),2); \
         "h)"; " appears at positions"; first; " and"; p

      end
   print
end

rem - test cases

report ""
report "."
report "abcABC"
report "XYZ ZYX"
report "1234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ"

end
