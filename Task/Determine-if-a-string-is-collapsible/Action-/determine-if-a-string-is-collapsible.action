PROC Collapse(CHAR ARRAY in,out)
  BYTE i,j
  CHAR c

  j=1 c=0
  FOR i=1 TO in(0)
  DO
    IF in(i)#c THEN
      c=in(i)
      out(j)=c
      j==+1
    FI
  OD
  out(0)=j-1
RETURN

PROC Test(CHAR ARRAY s)
  CHAR ARRAY c(100)
  BYTE CH=$02FC ;Internal hardware value for last key pressed

  Collapse(s,c)
  PrintF("<<<%S>>> (len=%B)%E",s,s(0))
  PrintF("<<<%S>>> (len=%B)%E",c,c(0))
  PutE()
  PrintE("Press any key to continue")
  PutE()

  DO UNTIL CH#$FF OD
  CH=$FF
RETURN

PROC Main()
  Test("")
  Test("""If I were two-faced, would I be wearing this one?"" --- Abraham Lincoln ")
  Test("..1111111111111111111111111111111111111111111111111111111111111117777888")
  Test("I never give 'em hell, I just tell the truth, and they think it's hell. ")
  Test("                                                    --- Harry S Truman  ")
RETURN
