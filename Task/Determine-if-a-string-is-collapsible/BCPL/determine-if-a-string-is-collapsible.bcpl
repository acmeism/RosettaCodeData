get "libhdr"

// Collapse a string
let collapse(in, out) = valof
$(  let o = 0
    for i = 1 to in%0
        unless i>1 & in%i = in%(i-1)
        $(  o := o + 1
            out%o := in%i
        $)
    out%0 := o
    resultis out
$)

// Print string with brackets and length
let brackets(s) be
    writef("%N: <<<%S>>>*N", s%0, s)

// Print original and collapsed version
let show(s) be
$(  let v = vec 1+255/BYTESPERWORD
    brackets(s)
    brackets(collapse(s, v))
    wrch('*N')
$)

let start() be
$(  show("")
    show("*"If I were two-faced, would I be wearing this one?*" --- Abraham Lincoln ")
    show("..1111111111111111111111111111111111111111111111111111111111111111111788")
    show("I never give 'em hell, I just tell the truth, and they think it's hell. ")
    show("                                                     --- Harry S Truman ")
$)
