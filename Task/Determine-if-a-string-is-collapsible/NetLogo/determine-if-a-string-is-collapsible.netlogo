to-report split [ string ]
  ;; utility reporter to split a string into a list
  report n-values length string [ [ n ] -> item n string ]
end

to-report collapse [ string ]
  ;; reporter that actually does the collapse function
  ifelse ( string = "" )
  [ report "" ]
  [ report reduce [ [ a b ] -> (word a ifelse-value last a != b [ b ] [ "" ] ) ] split string ]
end

to-report format [ string ]
  ;; reporter to format the output as required
  report ( word "<<<" string ">>> " length string )
end

to demo-collapse [ string ]
  ;; procedure to display the required output
  output-print format string
  output-print format collapse string
end

to demo
  ;; procedure to perform the test cases
  foreach
  [ ""
    "\"If I were two-faced, would I be wearing this one?\" --- Abraham Lincoln "
    "..1111111111111111111111111111111111111111111111111111111111111117777888"
    "I never give 'em hell, I just tell the truth, and they think it's hell. "
    "                                                    --- Harry S Truman  "
  ]
  demo-collapse
end
