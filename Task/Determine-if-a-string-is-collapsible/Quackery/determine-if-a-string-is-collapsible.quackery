  [ false -1 rot
    witheach
      [ 2dup = iff
          [ drop dip not
            conclude ]
        else nip ]
    drop ]                       is collapsible ( $ --> b )

  [ [] -1 rot
    witheach
      [ 2dup = iff drop
        else
          [ nip dup dip join ] ]
    drop ]                       is collapse    ( $ --> $ )

  [ dup collapsible iff
      [ dup collapse
        swap 2 ]
    else
      [ say "(Not collapsible.)" cr
        1 ]
    times
      [ say "<<<"
        dup echo$
        say ">>>" cr
        say "   Length: "
        size echo
        say " characters" cr cr ]
    cr ]                         is task     ( $ -->   )

$ "" task
$ '"If I were two-faced, would I be wearing this one?" --- Abraham Lincoln ' task
$ "..1111111111111111111111111111111111111111111111111111111111111117777888" task
$ "I never give 'em hell, I just tell the truth, and they think it's hell. " task
$ "                                                    --- Harry S Truman  " task
