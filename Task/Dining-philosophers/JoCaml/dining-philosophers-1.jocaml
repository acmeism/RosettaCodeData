let random_wait n = Unix.sleep (Random.int n);;
let print s m = Printf.printf "philosopher %s is %s\n" s m; flush(stdout);;
let will_eat s = print s "eating"; random_wait 10;;
let will_think s = print s "thinking"; random_wait 20; print s "hungry";;

  (* a,b,c,d,e are thinking philosophers; ah,bh,ch,dh,eh are the same philosophers when hungry;
     fab is the fork located between philosophers a and b; similarly for fbc, fcd, ... *)

def  ah() & fab() & fea() = will_eat "Aristotle"; a() & fab() & fea()
 or  bh() & fab() & fbc() = will_eat "Kant";      b() & fab() & fbc()
 or  ch() & fbc() & fcd() = will_eat "Spinoza";   c() & fbc() & fcd()
 or  dh() & fcd() & fde() = will_eat "Marx";      d() & fcd() & fde()
 or  eh() & fde() & fea() = will_eat "Russell";   e() & fde() & fea()

 and a() = will_think "Aristotle"; ah()
 and b() = will_think "Kant";      bh()
 and c() = will_think "Spinoza";   ch()
 and d() = will_think "Marx";      dh()
 and e() = will_think "Russell";   eh()
;;
spawn fab() & fbc() & fcd() & fde() & fea() & a() & b() & c() & d() & e();;
