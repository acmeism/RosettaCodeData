            NORMAL MODE IS INTEGER

            VECTOR VALUES FMT = $ I7*$
            AMOUNT = 19

            THROUGH TEST, FOR CAND=0, 1, AMOUNT.E.0
            WHENEVER DISARI.(CAND)
                PRINT FORMAT FMT,CAND
                AMOUNT = AMOUNT-1
            END OF CONDITIONAL
TEST        CONTINUE

            INTERNAL FUNCTION(N)
            ENTRY TO LENGTH.
            L = 0
            THROUGH COUNT, FOR NN=N, 0, NN.E.0
            L = L+1
COUNT       NN = NN/10
            FUNCTION RETURN L
            END OF FUNCTION

            INTERNAL FUNCTION(BASE,EXP)
            ENTRY TO RAISE.
            R = 1
            THROUGH MUL, FOR E=EXP, -1, E.E.0
MUL         R = R*BASE
            FUNCTION RETURN R
            END OF FUNCTION

            INTERNAL FUNCTION(N)
            ENTRY TO DISARI.
            L = LENGTH.(N)
            POWSUM = 0
            THROUGH DGTLP, FOR NN=N, 0, NN.E.0
            NX = NN/10
            DG = NN-NX*10
            POWSUM = POWSUM+RAISE.(DG,L)
            L = L-1
DGTLP       NN = NX
            FUNCTION RETURN POWSUM.E.N
            END OF FUNCTION
            END OF PROGRAM
