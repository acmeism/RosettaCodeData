Dim Shared As Integer zc(1 To 999)

Sub initZC()
    For x As Integer = 1 To 9
        zc(x) = 2        ' 00x
        zc(10 * x) = 2   ' 0x0
        zc(100 * x) = 2  ' x00
        For y As Integer = 10 To 90 Step 10
            zc(     y + x)  = 1  ' 0yx
            zc(10 * y + x)  = 1  ' y0x
            zc(10 *(y + x)) = 1  ' yx0
        Next y
    Next x
End Sub

Sub main()
    initZC()

    Dim As Double t0 = Timer
    Dim As Integer trail = 1, first = 0, f
    Dim As Double total = 0.0, ratio
    Dim As Uinteger rfs()
    Redim rfs(1 To 1000)
    rfs(1) = 1             ' reverse factorial(1) in base 1000
    Dim As Integer rfs_len = 1

    For f = 2 To 50000
        Dim As Integer carry = 0
		Dim As Integer j = trail
		Dim As Integer d999
		Dim As Integer zeroes = (trail - 1) * 3
        Dim As Integer new_len = rfs_len

        While j <= new_len Or carry > 0
            If j <= new_len Then carry += rfs(j) * f

            d999 = carry Mod 1000
            If j <= new_len Then
                rfs(j) = d999
            Else
                If j > Ubound(rfs) Then Redim Preserve rfs(1 To Ubound(rfs) + 1000)
                rfs(j) = d999
                new_len = j
            End If

			zeroes += Iif(d999 = 0, 3, zc(d999))

            carry \= 1000
            j += 1
        Wend

        rfs_len = new_len

        While trail <= rfs_len Andalso rfs(trail) = 0
            trail += 1
        Wend

		' d999 := Quick correction for length and zeroes
        d999 = rfs(rfs_len)
        If d999 < 100 Then
			zeroes -= Iif(d999 < 100, Iif(d999 < 10, 2, 1), 0)
        End If

        Dim As Integer digits = rfs_len * 3 - (3 - Len(Str(d999)))
        total += zeroes / digits
        ratio = total / f

        If ratio >= 0.16 Then
            first = 0
        Elseif first = 0 Then
            first = f
        End If

        If f = 100 Or f = 1000 Or f = 10000 Then
            Print Using "Mean proportion of zero digits in factorials to ##### is 0.##########"; f; ratio;
            Print Using " (#.## seconds)"; Timer - t0
        End If
    Next f

    Print Using !"\nThe mean proportion dips permanently below 0.16 at #####"; first;
    Print Using ". (##.## seconds)"; Timer - t0
End Sub

main()

Sleep
