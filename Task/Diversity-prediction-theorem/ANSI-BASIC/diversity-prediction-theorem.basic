100 PROGRAM DiversityPredictionTheorem
110 OPTION BASE 0
120 DIM Estimates(1, 4)
130 FOR I = 0 TO 1
140    LET J = 0
150    READ Estimates(I, J)
160    DO WHILE Estimates(I, J) <> 0
170       LET J = J + 1
180       READ Estimates(I, J)
190    LOOP
200 NEXT I
210 DATA 48.0, 47.0, 51.0, 0.0
220 DATA 48.0, 47.0, 51.0, 42.0, 0.0
230 LET TrueVal = 49
240 FOR I = 0 TO 1
250    LET Sum = 0
260    LET J = 0
270    DO WHILE Estimates(I, J) <> 0
280       LET Sum = Sum + (Estimates(I, J) - TrueVal) ^ 2
290       LET J = J + 1
300    LOOP
310    LET AvgErr = Sum / J
320    PRINT USING "Average error : ##.###": AvgErr
330    LET Sum = 0
340    LET J = 0
350    DO WHILE Estimates(I, J) <> 0
360       LET Sum = Sum + Estimates(I, J)
370       LET J = J + 1
380    LOOP
390    LET Avg = Sum / J
400    LET CrowdErr = (TrueVal - Avg) ^ 2
410    PRINT USING "Crowd error   : ##.###": CrowdErr
420    PRINT USING "Diversity     : ##.###": AvgErr - CrowdErr
430    PRINT
440 NEXT I
450 END
