CARD EndProg ;required for ALLOCATE.ACT

INCLUDE "D2:ALLOCATE.ACT" ;from the Action! Tool Kit. You must type 'SET EndProg=*' from the monitor after compiling, but before running this program!

DEFINE PTR="CARD"
DEFINE NODE_SIZE="6"
TYPE ListNode=[INT data PTR prv,nxt]

ListNode POINTER listBegin,listEnd

PROC Append(INT v)
  ListNode POINTER n

  n=Alloc(NODE_SIZE)
  n.data=v
  n.prv=listEnd
  n.nxt=0
  IF listEnd THEN
    listEnd.nxt=n
  ELSE
    listBegin=n
  FI
  listEnd=n
RETURN

PROC Clear()
  ListNode POINTER n,next

  n=listBegin
  WHILE n
  DO
    next=n.nxt
    Free(n,NODE_SIZE)
    n=next
  OD
  listBegin=0
  listEnd=0
RETURN

PROC ForwardTraverse()
  ListNode POINTER n

  n=listBegin
  PrintE("Forward traverse:")
  Print("(")
  WHILE n
  DO
    PrintI(n.data)
    IF n.nxt THEN
      Print(", ")
    FI
    n=n.nxt
  OD
  PrintE(")")
RETURN

PROC BackwardTraverse()
  ListNode POINTER n

  n=listEnd
  PrintE("Backward traverse")
  Print("(")
  WHILE n
  DO
    PrintI(n.data)
    IF n.prv THEN
      Print(", ")
    FI
    n=n.prv
  OD
  PrintE(")")
RETURN

PROC Main()
  INT i
  Put(125) PutE() ;clear screen

  AllocInit(0)
  listBegin=0
  listEnd=0

  FOR i=0 TO 50
  DO
    Append(i*i)
  OD
  ForwardTraverse()
  PutE()
  BackwardTraverse()

  Clear()
RETURN
