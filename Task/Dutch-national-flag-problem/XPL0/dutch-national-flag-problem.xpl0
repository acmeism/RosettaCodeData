def  Red, White, Blue;
def  Size = 10;
int  A(Size), N;

proc ShowOrder;
[for N:= 0 to Size-1 do
    case A(N) of
      Red:  Text(0, "Red ");
      Blue: Text(0, "Blue ")
    other   Text(0, "White ");
CrLf(0);
];

proc Part3Ways;         \Partition array A three ways (code from Wikipedia)
def  Mid = White;
int  I, J, K, T;
[I:= 0;  J:= 0;  K:= Size-1;
while J <= K do
    if A(J) < Mid then
            [T:= A(I);  A(I):= A(J);  A(J):= T;
            I:= I+1;
            J:= J+1;
            ]
    else if A(J) > Mid then
            [T:= A(J);  A(J):= A(K);  A(K):= T;
            K:= K-1;
            ]
    else    J:= J+1;
];

[for N:= 0 to Size-1 do A(N):= Ran(3);
Text(0, "Original order : ");
ShowOrder;
Part3Ways;
Text(0, "Sorted order   : ");
ShowOrder;
]
