program emirp
	args n
	qui clear
	qui mata: build(`n')
	qui save temp, replace
	qui replace p=real(strreverse(strofreal(p)))
	qui merge 1:1 p using temp, keep(3) nogen
	qui drop if real(strreverse(strofreal(p)))==p
end

mata
real colvector sieve(real scalar n) {
	real colvector a
	real scalar i,j
	if (n<2) return(J(0,1,.))
	a=J(n,1,1)
	a[1]=0
	for (i=1; i<=n; i++) {
		if (a[i]) {
			j=i*i
			if (j>n) return(select(1::n,a))
			for (; j<=n; j=j+i) a[j]=0
		}
	}
}

function build(n) {
	a=sieve(n)
	st_addobs(rows(a))
	st_addvar("long","p")
	st_store(.,1,a)
}
end
