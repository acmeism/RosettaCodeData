  [ [] swap
    [ base share /mod
      rot join swap
      dup 0 = until ]
    drop ]                        is digits    ( n --> [ )

  [ dup 0 = iff
      [ drop false ]
      done
    digits
    true swap
    behead swap
    witheach
       [ tuck - abs 1 != if
           [ dip not conclude ] ]
    drop ]                        is esthetic ( n --> b )

  15 times
    [ i^ 2 +
      say "Base " dup echo cr
      base put
      [] 0
      [ 1+
        dup esthetic if
          [ tuck join swap ]
        over size
        base share 6 * = until ]
      drop
      base share 4 * 1 - split nip
      echo cr cr
      base release ]

  say "Decimal between 1000 and 9999: "
  cr
  0 temp put
  9000 times
    [ i^ 1000 + dup
      esthetic iff
        [ echo
          1 temp tally
          temp share
          10 mod iff
          sp else cr ]
      else drop ]
  temp release
