 10  GOSUB 100" EULER'S SUM OF POWERS CONJECTURE
 20  PRINT I"^5 = "Z"^5 + "Y"^5 + "X"^5 + "W"^5";
 30  END

 REM EULER'S SUM OF POWERS CONJECTURE
 100  GOSUB 300
 110  FOR W = T TO MAX:X1$ = X$(W)
 120  FOR X = T TO W:C$ = X1$:D$ = X$(X): GOSUB 600:S2$ = E$
 130      FOR Y = T TO X:C$ = S2$:D$ = X$(Y): GOSUB 600:S3$ = E$
 140          FOR Z = T TO Y:C$ = S3$:D$ = X$(Z): GOSUB 600
 150              FOR I = W TO MAX: IF X$(I) = E$ THEN  RETURN
 160  NEXT I,Z,Y,X,W: STOP

 300  IF T THEN  RETURN
 310  LET MAX = 255: DIM X$(MAX)
 320  FOR N = 0 TO MAX: GOSUB 350:X$(N) = E$: NEXT N:T = 1:N =  FRE (0)
 330  RETURN

 REM N ^ 5 RETURNS E$
 350  A$ =  STR$ (N):B$ = A$: GOSUB 400
 360  A$ = E$:B$ = E$: GOSUB 400
 370  A$ =  STR$ (N):B$ = E$

 400  REM MULTIPLY A$ * B$
 410  C$ = "":D$ = "0"
 420  FOR I =  LEN (B$) TO 1 STEP  - 1
 430      C = 0:B =  VAL ( MID$ (B$,I,1))
 440      FOR J =  LEN (A$) TO 1 STEP  - 1
 450          V = B *  VAL ( MID$ (A$,J,1)) + C
 460          C =  INT (V / 10):V = V - C * 10
 470          C$ =  STR$ (V) + C$
 480      NEXT J
 490      IF C THEN C$ =  STR$ (C) + C$
 510      GOSUB 600"ADD C$ + D$
 520      D$ = E$:C$ = "0":J =  LEN (B$) - I
 530      IF J THEN J = J - 1:C$ = C$ + "0": GOTO 530
 550  NEXT I
 560  RETURN

 600  REM ADD C$ + D$
 610  E =  LEN (D$):E$ = "":C = 0
 620  FOR J =  LEN (C$) TO 1 STEP  - 1
 630      IF E THEN D =  VAL ( MID$ (D$,E,1))
 640      V =  VAL ( MID$ (C$,J,1)) + D + C
 650      C = V > 9:V = V - 10 * C
 660      E$ =  STR$ (V) + E$
 670      IF E THEN E = E - 1:D = 0
 680  NEXT J
 700  IF E THEN V =  VAL ( MID$ (D$,E,1)) + C:C = V > 9:V = V - 10 * C:E$ =  STR$ (V) + E$:E = E - 1: GOTO 700
 710  IF C THEN E$ = "1" + E$
 720  RETURN
