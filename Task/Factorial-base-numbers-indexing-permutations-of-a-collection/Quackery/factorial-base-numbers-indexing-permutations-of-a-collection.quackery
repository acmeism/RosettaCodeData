  [ 1 swap times [ i 1+ * ] ]   is !            (   n --> n )

  [ dup 0 = iff [ drop 2 ] done
    0
    [ 1+ 2dup ! / 0 = until ]
    nip ]                       is figits       (   n --> n )

  [ [] unrot 1 - times
      [ i 1+ ! /mod
        dip join ] drop ]       is factoradic   ( n n --> [ )

  [ 0 swap
    witheach [ i 1+ ! * + ] ]   is unfactoradic (   [ --> n )

  [ [] unrot witheach
      [ pluck
        rot swap nested join
        swap ]
    join ]                       is inversion   ( [ [ --> [ )

  [ over size
    factoradic inversion ]       is nperm       ( [ n --> [ )

  [ 0 unrot swap witheach
      [ over find
        dup dip [ pluck drop ]
        rot i 1+ * + swap ]
    drop ]                      is permnum      ( [ [ --> n )

  say 'The 1236880662123rd permutation of' cr
  say '"uncopyrightable" is "'
  $ 'uncopyrightable' 1236880662123 nperm echo$
  say '".' cr cr

  say 'The factorial base representation of' cr
  say  '1236880662123 is '
  1236880662123 dup figits factoradic echo
  say '.' cr cr

  say '"lucentbiography" is permutation' cr
  say '#' $ 'lucentbiography' $ 'uncopyrightable' permnum echo
  say ' of "uncopyrightable".'
