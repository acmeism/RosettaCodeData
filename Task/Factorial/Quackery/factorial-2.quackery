  [ 1 & ]                   is odd       ( n --> b )

  [ odd not ]               is even      ( n --> b )

  [ 1 >> ]                  is 2/        ( n --> n )

  [ [] swap
    witheach
      [ i^ swap - join ] ]  is [i^-]     ( [ --> [ )

 [ 1 split
    witheach
      [ over -1 peek
        * join ] ]          is [prod]    ( [ --> [ )

  [ 1 -  ' [ 0 ]
    swap times
      [ dup i^ 1+
        dup dip
          [ 2/ peek ]
        odd +
        join ] ]            is [A000120] ( n --> [ )


  [ [A000120] [i^-] ]       is [A011371] ( n --> [ )

  [ ' [ 0 ] swap
    1 - times
      [ i^ 1+ dup even if
        [ dip dup 2/ peek ]
        join ]
    behead drop ]           is [A000265] ( n --> [ )

 [ ' [ 1 ] swap dup
   [A000265] [prod]
   swap [A011371]
   swap  witheach
     [ over i^ 1+ peek
       << rot swap join
       swap ] drop ]        is [A000142] ( n --> [ )

[ 1+ [A000142] -1 peek ]    is !
