Module Factors_of_a_Mersenne_number{
	Dim q(1 To 10)
	q(1)= 11, 23, 29, 37, 41, 43, 47, 53, 59, 67, 71, 73, 79, 83, 97, 929
	For k = 1 To 10
		If @isPrime(q(k)) Then
			long p, r, d, r=q(k)
			long long i
			d = 2*q(k)+1
			while r>0: r=sint(binary.add(r,r)): end while
			Do
				i=1
				p=r
				While p<>0
					i = i**2@ Mod d
					If p<0 Then i*=2
					If i>d Then i-=d
					p=sint(binary.add(p,p))
				End While
				If i=1 Then Exit
				d+=2*q(k)
			Always
			Print "2^"; q(k); @(6); " - 1 = 0 (mod"; d; ")"
		Else
			Print q(k); " is not prime"
		End If
	Next
	
	Function isPrime(n As long)
		If n Mod 2 = 0 Then = n=2 : Exit Function
		If n Mod 3 = 0 Then = n=3 : Exit Function
		Local d As long = 5
		While d * d <= n
			If n Mod d = 0 Then =False: exit function
			d += 2
			If n Mod d = 0 Then = False: exit function
			d += 4
		End While
		=True
	End Function
}
Factors_of_a_Mersenne_number
