DEFINE MAX="15"

PROC GenerateSeq(CARD ARRAY init BYTE nInit CARD ARRAY seq BYTE nSeq)
  CARD next
  BYTE i,j,n

  IF nInit<nSeq THEN
    n=nInit
  ELSE
    n=nSeq
  FI

  FOR i=0 TO n-1
  DO
    seq(i)=init(i)
  OD

  FOR i=n TO nSeq-1
  DO
    next=0
    FOR j=i-nInit TO i-1
    DO
      next==+seq(j)
    OD
    seq(i)=next
  OD
RETURN

PROC PrintSeq(CHAR ARRAY name CARD ARRAY seq BYTE n)
  BYTE i

  PrintF("%S=[",name)
  FOR i=0 TO n-1
  DO
    PrintC(seq(i))
    IF i<n-1 THEN
      Print(" ")
    ELSE
      PrintE("]")
    FI
  OD
RETURN

PROC SetInverseVideo(CHAR ARRAY text)
  BYTE i

  FOR i=1 TO text(0)
  DO
    text(i)=text(i) OR $80
  OD
RETURN

PROC Test(CHAR ARRAY name CARD ARRAY init CARD ARRAY nInit BYTE nSeq)
  CARD ARRAY seq(MAX)

  SetInverseVideo(name)
  GenerateSeq(init,nInit,seq,nSeq)
  PrintSeq(name,seq,nSeq)
RETURN

PROC Main()
  CARD ARRAY fibInit=[1 1 2 4 8 16 32 64 128 256 512]
  CARD ARRAY lucInit=[2 1]

  Test("lucas",lucInit,2,MAX)
  Test("fibonacci",fibInit,2,MAX)
  Test("tribonacci",fibInit,3,MAX)
  Test("tetranacci",fibInit,4,MAX)
  Test("pentanacci",fibInit,5,MAX)
  Test("hexanacci",fibInit,6,MAX)
  Test("heptanacci",fibInit,7,MAX)
  Test("octanacci",fibInit,8,MAX)
  Test("nonanacci",fibInit,9,MAX)
  Test("decanacci",fibInit,10,MAX)
RETURN
