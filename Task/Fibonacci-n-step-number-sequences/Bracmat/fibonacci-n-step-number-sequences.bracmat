( ( nacci
  =   Init Cnt N made tail
    .   ( plus
        =   n
          .   !arg:#%?n ?arg&!n+plus$!arg
            | 0
        )
      & !arg:(?Init.?Cnt)
      & !Init:? [?N
      & !Init:?made
      & !Cnt+-1*!N:?times
      & -1+-1*!N:?M
      &   whl
        ' ( !times+-1:~<0:?times
          & !made:? [!M ?tail
          & !made plus$!tail:?made
          )
      & !made
  )
& ( pad
  =   len w
    .   @(!arg:? [?len)
      & @("          ":? [!len ?w)
      & !w !arg
  )
&     (fibonacci.1 1)
      (tribonacci.1 1 2)
      (tetranacci.1 1 2 4)
      (pentanacci.1 1 2 4 8)
      (hexanacci.1 1 2 4 8 16)
      (heptanacci.1 1 2 4 8 16 32)
      (octonacci.1 1 2 4 8 16 32 64)
      (nonanacci.1 1 2 4 8 16 32 64 128)
      (decanacci.1 1 2 4 8 16 32 64 128 256)
      (lucas.2 1)
  : ?L
&   whl
  ' ( !L:(?name.?Init) ?L
    & out$(str$(pad$!name ": ") nacci$(!Init.12))
    )
);
