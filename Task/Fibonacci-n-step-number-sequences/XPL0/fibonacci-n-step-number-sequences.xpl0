include c:\cxpl\codes;          \intrinsic 'code' declarations

proc Nacci(N, F0);              \Generate Fibonacci N-step sequence
int N,                          \step size
    F0;                         \array of first N values
int I, J;
def M = 10;                     \number of members in the sequence
int F(M);                       \Fibonacci sequence
[for I:= 0 to M-1 do            \for all the members of the sequence...
    [if I < N then F(I):= F0(I) \initialize sequence
    else [F(I):= 0;             \sum previous members to get member I
         for J:= 1 to N do F(I):= F(I) + F(I-J);
         ];
    IntOut(0, F(I)); ChOut(0, ^ );
    ];
CrLf(0);
];

[Text(0, " Fibonacci: ");  Nacci(2, [1, 1]);
 Text(0, "Tribonacci: ");  Nacci(3, [1, 1, 2]);
 Text(0, "Tetranacci: ");  Nacci(4, [1, 1, 2, 4]);
 Text(0, "     Lucas: ");  Nacci(2, [2, 1]);
]
