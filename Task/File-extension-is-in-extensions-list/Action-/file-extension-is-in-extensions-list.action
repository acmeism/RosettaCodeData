DEFINE PTR="CARD"

CHAR FUNC ToLower(CHAR c)
  IF c>='A AND c<='Z THEN
    c==+'a-'A
  FI
RETURN (c)

BYTE FUNC CheckExt(CHAR ARRAY file,ext)
  BYTE i,j
  CHAR c1,c2

  i=file(0) j=ext(0)
  IF i<j THEN RETURN (0) FI

  WHILE j>0
  DO
    c1=ToLower(file(i))
    c2=ToLower(ext(j))
    IF c1#c2 THEN RETURN (0) FI
    i==-1
    j==-1
  OD
  IF file(i)#'. THEN
    RETURN (0)
  FI
RETURN (1)

BYTE FUNC Check(CHAR ARRAY file PTR ARRAY exts BYTE count)
  BYTE i

  FOR i=0 TO count-1
  DO
    IF CheckExt(file,exts(i)) THEN
      RETURN (1)
    FI
  OD
RETURN (0)

PROC Main()
  PTR ARRAY exts(7),files(8)
  BYTE i

  exts(0)="zip"
  exts(1)="rar"
  exts(2)="7z"
  exts(3)="gz"
  exts(4)="archive"
  exts(5)="A##"
  exts(6)="tar.bz2"

  files(0)="MyData.a##"
  files(1)="MyData.tar.Gz"
  files(2)="MyData.gzip"
  files(3)="MyData.7z.backup"
  files(4)="MyData..."
  files(5)="MyData"
  files(6)="MyData_v1.0.tar.bz2"
  files(7)="MyData_v1.0.bz2"

  FOR i=0 to 7
  DO
    Print(files(i)) Print(" -> ")
    IF Check(files(i),exts,7) THEN
      PrintE("true")
    ELSE
      PrintE("false")
    FI
  OD
RETURN
