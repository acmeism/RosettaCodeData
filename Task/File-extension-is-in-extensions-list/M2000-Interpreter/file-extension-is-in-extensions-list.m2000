module File_extension_is_in_extensions_list {
	extensions=List:="zip","rar", "7z","gz", "archive", "a##"
	filenames=("MyData.a##", "MyData.tar.gz", "MyData.gzip", "MyData.7z.backup", "MyData...", "MyData")
	scanExtensions(each(filenames))
	
	' extra
	Append extensions, "tar.bz2"
	extra_filenames=("MyData_v1.0.tar.bz2", "MyData_v1.0.bz2")
	scanExtensions(each(extra_filenames))
	
	sub scanExtensions(m)
		local b as boolean, ext, n
		while m
			ext=@possible_extensions(array$(m))
			n=each(ext)
			b=false
			while n
				b=exist(extensions, array$(n))
				if b then exit
			end while
			Print array$(m), b
		end while
	end sub
	function possible_extensions(a as string)
		stack new {
			local string k=rightpart$(a,".")
			do
				data lcase$(k)
				k=rightpart$(k,".")
			until k=""
			=array([])
		}	
	end function
}
File_extension_is_in_extensions_list
