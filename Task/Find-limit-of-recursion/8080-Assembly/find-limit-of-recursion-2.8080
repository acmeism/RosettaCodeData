	org	100h
	lxi	h,-top	; Subtract highest used location from stack pointer
	dad	sp	
	xra	a	; This gives bytes, but a call takes two bytes;
	ora	h	; so HL should be divided by two to give the actual
	rar		; number.
	mov	h,a
	mov	a,l
	rar
	mov	l,a
	;;;	The number of free stack words is now in HL, output it
	lxi	d,num
	push	d
	lxi	b,-10
dgt:	lxi	d,-1
clcdgt:	inx	d
	dad	b
	jc	clcdgt
	mov	a,l
	adi	10+'0'
	xthl
	dcx	h
	mov	m,a
	xthl
	xchg
	mov	a,h
	ora	l
	jnz	dgt
	;;;	Use CP/M routine to print the string
	pop	d
	mvi	c,9
	call 	5
	rst	0
	;;;	Placeholder for numeric output
	db	'00000'
num:	db	'$'	
	;;;	The program does not need any memory beyond this point.
	;;;	This means anything from this place up to SP is free for the
	;;;	stack.
top:	equ	$		
