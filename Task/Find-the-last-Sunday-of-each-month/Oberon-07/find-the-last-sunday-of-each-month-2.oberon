MODULE LastDays;
IMPORT Dates;

VAR    daysInMonth : ARRAY 12 OF INTEGER;

(* sets the elements of lastWD to the day of the last weekDay of each month in year *)
PROCEDURE DayOfLastWeekDay* ( year, weekDay : INTEGER; VAR lastWD : ARRAY OF INTEGER ) ;
    VAR   endOfMonth, dow, mPos, offset : INTEGER ;
    BEGIN
        FOR mPos := 0 TO 11 DO
            endOfMonth := daysInMonth[ mPos ];
            IF mPos = 1 THEN
               (* February *)
               IF Dates.isLeapYear( year ) THEN INC( endOfMonth ) END
            END;
            dow := Dates.dayOfWeek( endOfMonth, mPos + 1, year );
            (* offset the last day to the required day of the week *)
            IF dow >= weekDay THEN
               offset := dow - weekDay
            ELSE
               offset := 7 - ( weekDay - dow )
            END;
            lastWD[ mPos ] := endOfMonth - offset
        END
    END DayOfLastWeekDay ;

BEGIN
    daysInMonth[ 0 ] := 31; daysInMonth[  1 ] := 28; daysInMonth[  2 ] := 31;
    daysInMonth[ 3 ] := 30; daysInMonth[  4 ] := 31; daysInMonth[  5 ] := 30;
    daysInMonth[ 6 ] := 31; daysInMonth[  7 ] := 31; daysInMonth[  8 ] := 30;
    daysInMonth[ 9 ] := 31; daysInMonth[ 10 ] := 30; daysInMonth[ 11 ] := 31
END LastDays.
