include "cowgol.coh";

sub width(n: uint16): (w: uint8) is
    w := 1;
    while n >= 10 loop
        n := n / 10;
        w := w + 1;
    end loop;
end sub;

sub print_fixed(n: uint16, w: uint8) is
    w := w - width(n);
    while w > 0 loop
        print_char(' ');
        w := w - 1;
    end loop;
    print_i16(n);
end sub;

sub floyd(rows: uint16) is
    var maxno := rows * (rows+1)/2;
    var num: uint16 := 1;
    var row: uint16 := 1;
    while row <= rows loop
        var col: uint16 := 1;
        while col <= row loop
            print_fixed(num, 1 + width(maxno - rows + col));
            num := num + 1;
            col := col + 1;
        end loop;
        print_nl();
        row := row + 1;
    end loop;
end sub;

floyd(5);
print_nl();
floyd(14);
