/* NetRexx */
options replace format comments java crossref symbols binary
/* REXX ***************************************************************
* 12.07.2012 Walter Pachl  - translated from Python
**********************************************************************/
Parse Arg rowcount .
if rowcount.length() == 0 then rowcount = 1
say 'Rows:' rowcount
say
col = 0
len = Rexx ''
ll = ''                               -- last line of triangle
Loop j = rowcount * (rowcount - 1) / 2 + 1 to rowcount * (rowcount + 1) / 2
  col = col + 1                       -- column number
  ll = ll j                           -- build last line
  len[col] = j.length()               -- remember length of column
  End j
Loop i = 1 To rowcount - 1            -- now do and output the rest
  ol = ''
  col = 0
  Loop j = i * (i - 1) / 2 + 1 to i * (i + 1) / 2 -- elements of line i
    col = col + 1
    ol=ol j.right(len[col])           -- element in proper length
    end
  Say ol                              -- output ith line
  end i
Say ll                                -- output last line
