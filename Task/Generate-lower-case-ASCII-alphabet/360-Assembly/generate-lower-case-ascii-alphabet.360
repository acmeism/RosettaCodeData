*        Generate lower case alphabet - 15/10/2015
LOWER    CSECT
         USING  LOWER,R15          set base register
         LA     R7,PG              pgi=@pg
         SR     R6,R6              clear
         IC     R6,=C'a'           char='a'
         BCTR   R6,0               char=char-1
LOOP     LA     R6,1(R6)           char=char+1
         STC    R6,CHAR
         CLI    CHAR,C'i'          if char>'i'
         BNH    OK
         CLI    CHAR,C'j'          and char<'j'
         BL     SKIP               then skip
         CLI    CHAR,C'r'          if char>'r'
         BNH    OK
         CLI    CHAR,C's'          and char<'s'
         BL     SKIP               then skip
OK       MVC    0(1,R7),CHAR       output char
         LA     R7,1(R7)           pgi=pgi+1
SKIP     CLI    CHAR,C'z'          if char='z'
         BNE    LOOP               loop
         XPRNT  PG,26              print buffer
         XR     R15,R15            set return code
         BR     R14                return to caller
CHAR     DS     C                  character
PG       DS     CL26               buffer
         YREGS
         END    LOWER
