	org	100h
	jmp	test

	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;; Store the lowercase alphabet as a CP/M string
	;; ($-terminated), starting at HL.
	;; Destroys: b, c
	
alph:	lxi	b,611ah	; set B='a' and C=26 (counter)
aloop:	mov	m,b	; store letter in memory
	inr	b	; next letter
	inx	h	; next memory position
	dcr	c	; one fewer letter left
	jnz	aloop	; go do the next letter if there is one
	mvi	m,'$'	; terminate the string
	ret
	
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;; Test code
test:	lxi	h,buf	; select buffer
	call	alph	; generate alphabet
	
	lxi	d,buf	; print string in buffer
	mvi	c,9
	call	5
	rst	0


buf:	ds	27	; buffer to keep the alphabet in
