100H: /* FIND SOME GIUGA NUMBERS, COMPOSITES N SUCH THAT ALL THEIR DISTINCT */
      /*                       PRIME FACTORS F EXACTLY DIVIDE ( N / F ) - 1 */

   /* CP/M BDOS SYSTEM CALL AND I/O ROUTINES                                */
   BDOS: PROCEDURE( FN, ARG ); DECLARE FN BYTE, ARG ADDRESS; GOTO 5; END;
   PR$CHAR:   PROCEDURE( C ); DECLARE C BYTE;    CALL BDOS( 2, C );  END;
   PR$STRING: PROCEDURE( S ); DECLARE S ADDRESS; CALL BDOS( 9, S );  END;
   PR$NL:     PROCEDURE;   CALL PR$CHAR( 0DH ); CALL PR$CHAR( 0AH ); END;
   PR$NUMBER: PROCEDURE( N ); /* PRINTS A NUMBER IN THE MINIMUN FIELD WIDTH  */
      DECLARE N ADDRESS;
      DECLARE V ADDRESS, N$STR ( 6 )BYTE, W BYTE;
      V = N;
      W = LAST( N$STR );
      N$STR( W ) = '$';
      N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      DO WHILE( ( V := V / 10 ) > 0 );
         N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      END;
      CALL PR$STRING( .N$STR( W ) );
   END PR$NUMBER;

   /* TASK                                                                  */

   /* FIND THE FIRST THREE GIUGA NUMBERS (THE FOURTH IS > 65535)            */
   /* EACH PRIME FACTOR CAN ONLY APPEAR ONCE, E.G.,: FOR 2:                 */
   /* (( N / 2 ) - 1) MOD 2 = 0  => N / 2 IS ODD => N NOT DIVISIBLE BY 4    */
   /* SIMILARLY FOR OTHER PRIMES                                            */
   DECLARE ( N, V, FCOUNT, F ) ADDRESS;
   DECLARE IS$GIUGA            BYTE;
   N     = 2;
   DO WHILE N < 65000;                   /* ASSUME THE NUMEBRS ARE ALL EVEN */
      V        = N / 2;
      IS$GIUGA = 1;
      FCOUNT   = 1;
      F        = 1;
      DO WHILE ( F := F + 2 ) <= V AND IS$GIUGA;
         IF V MOD F = 0 THEN DO;
            /* HAVE A PRIME FACTOR                                          */
            FCOUNT   = FCOUNT + 1;
            IS$GIUGA = ( ( N / F ) - 1 ) MOD F = 0;
            V = V / F;
         END;
      END;
      IF IS$GIUGA THEN DO;
         IF FCOUNT > 1 THEN DO;
            /* N IS NOT PRIME, SO IS GIUGA                                  */
            CALL PR$CHAR( ' ' );CALL PR$NUMBER( N );
         END;
      END;
      N = N + 4;
   END;

EOF
