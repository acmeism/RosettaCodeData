  [ $ "bigrat.qky" loadfile ] now!

  0 temp put
  1 n->v
  [ 1 temp tally
    2dup 1/v 1 n->v v+
    2over 2over
    5 approx= not while        ( i.e. to five digits after the decimal point    )
    2swap 2drop again ]
  say " After "
  temp take echo
  say " iterations: "
  63 point$ echo$ cr
  say "As a vulgar fraction: "
  2dup vulgar$ echo$ cr
  2dup
  [ 2dup 1/v 1 n->v v+         ( Continue to 63 digits after the decimal point  )
    2over 2over                ( to find the approximate error to same accuracy )
    63 approx= not while       ( as Wolfram Alpha.                              )
    2swap 2drop again ]
  2drop 2swap v-
  say "   Approximate error: "
  63 point$ echo$ cr
