import ballerina/io;

function gss(int[] s) returns [int[], int] {
    int best = 0;
    int strt = 0;
    int end = 0;
    int sum = 0;
    int sumStart = 0;
    foreach var [i, x] in s.enumerate() {
        sum += x;
        if sum > best {
            best = sum;
            strt = sumStart;
            end = i + 1;
        } else if sum < 0 {
            sum = 0;
            sumStart = i + 1;
        }
    }
    return [s.slice(strt, end), best];
}

function A(any[] a) returns string {
    return re `,`.replaceAll(a.toString(), ", ");
}

public function main() {
    var tests = [
        [-1, -2, 3, 5, 6, -2, -1, 4, -4, 2, -1],
        [-1, 1, 2, -5, -6],
        [],
        [-1, -2, -1]
    ];
    foreach var test in tests {
        io:println("Input:   ", A(test));
        var res = gss(test);
        var subSeq = res[0];
        int sum = res[1];
        io:println("Sub seq: ", A(subSeq));
        io:println("Sum:     ", sum, "\n");
    }
}
