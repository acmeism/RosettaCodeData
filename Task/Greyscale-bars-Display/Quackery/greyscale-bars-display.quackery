  [ $ "turtleduck.qky" loadfile ] now!

  [ 1280 1 ]                  is width          (     --> s )
  [ 720 1 ]                   is height         (     --> s )

  [ dip [ 1 - dup ]
    255 * swap /
    1 swap
    3 of dup colour fill
      [ 2 times
        [ width 2over v/ walk
          -1 4 turn
          height 4 1 v/ walk
          -1 4 turn ]
        2drop ] ]             is bar            ( n n -->   )

  [ 1 over times
      [ over i bar
        width 2over v/ fly ]
    2drop ]                   is line           (   n -->   )

  [ turtle
    10 frames
    width 2 1 v/ fly
    1 4 turn
    height 2 1 v/ fly
    1 2 turn
    height fly
    -1 4 turn
    ' [ 8 16 32 64 ]
    2 times
      [ behead line
        1 4 turn
        height -2 1 v/ fly
        1 4 turn
        behead line
        1 2 turn ]
    drop
    1 frames ]                is greyscale-bars (     -->   )

  greyscale-bars
