$ include "seed7_05.s7i";
  include "draw.s7i";
  include "keybd.s7i";

const proc: main is func
  local
    var integer: barHeight is 0;
    var integer: barNumber is 0;
    var integer: colCount is 0;
    var integer: deltaX is 0;
    var integer: x is 0;
    var integer: col is 0;
  begin
    screen(640, 480);
    KEYBOARD := GRAPH_KEYBOARD;
    barHeight := height(curr_win) div 4;
    for barNumber range 0 to 3 do
      colCount := 8 << barNumber;
      deltaX := width(curr_win) div colCount;
      for x range 0 to pred(colCount) do
        if barNumber rem 2 = 0 then
          col := 65535 - 65535 div pred(colCount) * x;
        else
          col := 65535 div pred(colCount) * x;
        end if;
        rect(deltaX * x, barHeight * barNumber, deltaX, barHeight,
            color(col, col, col));
      end for;
    end for;
    ignore(getc(KEYBOARD));
  end func;
