procedure main ()
  lower_limit := 1
  higher_limit := 100

  write ("Think of a number between 1 and 100")
  write ("Press 'enter' when ready")
  read ()

  repeat {
    if (higher_limit < lower_limit)
      then { # check that player is not cheating!
        write ("Something has gone wrong ... I give up")
        exit ()
      }
    my_guess := (higher_limit + lower_limit) / 2
    write ("My guess is ", my_guess)
    write ("Enter 'H' if your number is higher, 'L' if lower, or 'E' if equal")
    reply := map(trim(read ()))
    case (reply) of {
      "e" : {
        write ("I got it correct - thankyou!")
        exit () # game over
      }
      "h" : lower_limit := my_guess + 1
      "l" : higher_limit := my_guess - 1
      default : write ("Pardon? Let's try that again")
    }
  }
end
