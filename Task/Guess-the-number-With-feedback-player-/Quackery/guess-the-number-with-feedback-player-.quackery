  [ [ $ "lower higher equal" nest$ ]
    constant ]                       is responses (   --> $ )

  [  trim reverse
     trim reverse
     $ "" swap witheach
       [ lower join ] ]              is cleanup   ( $ --> $ )

  [ $ "Think of a number from 1 to"
    $ " 100 and press enter." join
    input drop
    0 temp put
    [] 100 times [ i 1+ join ]
    [ dup size 0 = iff
        [ say "Impossible!" cr 0 ]
        done
      dup size 2 / split behead
      dup temp replace
      say "I guess " echo say "." cr
      $ "lower, higher or equal? "
      input cleanup responses find
      [ table
        [ nip false ]
        [ drop false ]
        [ say "I guessed it!"
          cr true ]
        [ say "I do not understand."
          temp share swap join join
          cr false ] ]
      do until ]
  2drop temp release ]               is play      (   -->   )
