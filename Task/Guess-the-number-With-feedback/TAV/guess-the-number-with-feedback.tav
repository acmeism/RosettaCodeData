\( Guess a number with feedback (and cheat prevention)
\)
do a guess upto (n):
    x =: random next integer n+1
    o =: integer x+100 as string base 8
    o =: string o from -1 step -1
    print format "Guess a number between 1 and %d; (scrambled %s;)," n, o
    msg =: 'Ok,'
    used =: new row
    used[x] =: '< x <'
    while true
        a =: terminal read line msg _ " you guess: "
        ? a = () || a = 'q' || a = 'Q'
            print "Bye."
            :> ?-
        ? a = '?'
            print join used
            ?^
        b =: string a as integer else -1
        ? b < 1 || b > n
            msg =:  "Bad input, "
            ?^
        ? b = x
            print "You guessed correct."
            :>
        ? b < x
            msg =: b __ "is less,"
        else
            msg =: b __ "is greater,"
        used[b] =: a

main (parms):+
    random set seed ()
    print "Guessing a number."
    print "The number is shown, but plus 100 in octal, digits reversed"
    print "Type Q to quit, ? to tell your guesses so far"
    n =: string parms[1] as integer else 100
    ?* do a guess upto n
        ()

\+ stdlib
\+ readline
