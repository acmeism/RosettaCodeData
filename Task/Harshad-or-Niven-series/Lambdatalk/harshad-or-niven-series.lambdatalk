{def harshad?
 {def harshad?.sum
  {lambda {:n}
  {if {W.empty? {W.rest :n}}
   then {W.first :n}
   else {+ {W.first :n}
           {harshad?.sum {W.rest :n}}} }}}
 {lambda {:n}
  {= {% :n {harshad?.sum :n}} 0} }}
-> harshad?

{harshad? 12}
-> true
{harshad? 13}
-> false

{def harshads
 {def harshads.loop
  {lambda {:a :n :i}
   {if {> {A.length :a} :n}
    then :a
    else {harshads.loop {if {harshad? :i}
                         then {A.addlast! :i :a}
                         else :a}
                        :n
                        {+ :i 1}} }}}
 {lambda {:n}
  {harshads.loop {A.new} :n 0} }}
-> harshads

{harshads 20}
-> [0,1,2,3,4,5,6,7,8,9,10,12,18,20,21,24,27,30,36,40,42]

{def firstharshadafter
 {def firstharshadafter.loop
  {lambda {:i}
   {if {harshad? :i}
    then :i
    else {firstharshadafter.loop {+ :i 1}} }}}
 {lambda {:n}
  {firstharshadafter.loop {+ :n 1}} }}
-> firstharshadafter

{firstharshadafter 1000}
-> 1002
