( ( a
  =
    .   !arg:(1|2)&1
      | (as..find)$!arg:(?.?arg)&!arg
      |     (as..insert)
          $ ( !arg
            . a$(a$(!arg+-1))+a$(!arg+-1*a$(!arg+-1)):?arg
            )
        & !arg
  )
& new$hash:?as
& 0:?n:?maxan/n
& 1:?pow
&   whl
  ' ( 1+!n:?n
    & !pow:~>20
    & ( 2^!pow:~!n
      |     out
          $ ( str
            $ ( "Between 2^"
                !pow+-1
                " and 2^"
                !pow
                " the maximum value of a(n)/n is reached for n = "
                !maxn
                " with the value "
                !maxan/n
              )
            )
        & 0:?maxan/n
        & 1+!pow:?pow
      )
    & a$!n*!n^-1:?an/n
    & (   !an/n:>!maxan/n:?maxan/n
        & !n:?maxn
      |
      )
    & ( !an/n:~<11/20:?Man/n&!n:?Mallows
      |
      )
    )
&   out
  $ ( str
    $ ( "Mallows number is "
        !Mallows
        ", where a("
        !Mallows
        ")/"
        !Mallows
        " == "
        !Man/n
        ", which is greater than 0.55 by "
        !Man/n+-11/20
      )
    )
)
