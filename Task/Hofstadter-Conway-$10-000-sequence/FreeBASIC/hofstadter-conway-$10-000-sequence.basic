' version 13-07-2018
' compile with: fbc -s console

Dim As UInteger a(), pow2 = 2, p2 = 2 ^ pow2, peakpos, n, mallows
Dim As Double peak = 0.5, r
ReDim a(2 ^ 20)
a(1) = 1
a(2) = 1

For n = 3 To 2 ^ 20
    a(n) = a(a(n -1)) + a(n - a(n -1))
    r = a(n) / n
    If r >= 0.55 Then mallows = n
    If r > peak Then peak = r : peakpos = n
    If n = p2 Then
        Print Using "Maximum between 2 ^ ## and 2 ^ ## is"; pow2 -1; pow2;
        Print Using " #.##### at n = "; peak;
        Print peakpos
        pow2 += 1
        p2 = 2 ^ pow2
        peak = 0.5
    End If
Next

Print
Print "Mallows number is "; mallows

' empty keyboard buffer
While Inkey <> "" : Wend
Print : Print "hit any key to end program"
Sleep
End
