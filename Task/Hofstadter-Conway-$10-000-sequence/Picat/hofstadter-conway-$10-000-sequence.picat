go =>
   foreach(N in 0..19)
      [Val,Ix] = argmax({a(I)/I : I in 2**N..2**(N+1)}),
      printf("Max from 2^%2d..2^%-2d is %0.8f at %d\n",N,N+1,Val,Ix+2**N-1)
   end,
   println(mallows_number=mallows_number()),
   nl.

% The sequence definition
table
a(1) = 1.
a(2) = 1.
a(N) = a(a(N-1))+a(N-a(N-1)).

% argmax: find the (first) index for the max value(s) of L.
argmax(L) = [Max,MaxIxFirst] =>
  Max = max(L),
  MaxIxFirst = {I : I in 1..L.length, L[I] == Max}.first.

% Calculate the Mallows number separately.
mallows_number() = Mallow =>
   Mallow = _,
   foreach(M in 1..19)
      Min = 2**M,
      Max = Min*2,
      MaxRatio = 0,
      NVal = 0,
      foreach(N in Min..Max)
         Ratio = a(N)/N,
         if Ratio > MaxRatio then
           MaxRatio := Ratio,
           NVal := N
         end,
         if Ratio > 0.55 then
           Mallow := N
         end
      end
  end.
