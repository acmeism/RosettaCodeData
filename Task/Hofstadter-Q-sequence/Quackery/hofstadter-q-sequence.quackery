[ 2dup swap size dup negate swap within
    not if
      [ drop size 1+ number$
        $ "Term " swap join
        $ " of the Q sequence is not defined."
        join message put bail ]
    peek ]                                is qpeek  ( [ n --> x )

  [ dup dup -1 qpeek negate qpeek
    dip [ dup dup -2 qpeek negate qpeek ]
    + join ]                              is next-q (   [ --> [ )

  [ dup size 2 < iff
      [ drop 0 ] done
    0 swap behead swap
    witheach
      [ tuck > if [ dip 1+ ] ]
    drop ]                                is drops  (   [ --> n )

  0 backup
    [ ' [ 1 1 ]
      998 times next-q
      dup
      -1 split swap 10 split drop
      witheach [ echo sp ]
      say "... "
      0 peek echo cr
      99000 times next-q
      drops echo
      say " decreasing terms" ]
  bailed if
    [ message take cr echo$ cr ]
