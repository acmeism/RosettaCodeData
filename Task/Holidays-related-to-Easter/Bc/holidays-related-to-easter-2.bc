scale = 0

/* Format d days after March 21. Assumes March, April, May or June. */
define format(d) {
	if (d > 80) {		print "     Jun ",  d - 71
	} else if (d > 71) { 	print "     Jun 0", d - 71
	} else if (d > 49) {	print "     May ",  d - 40
	} else if (d > 40) {	print "     May 0", d - 40
	} else if (d > 19) {	print "     Apr ",  d - 10
	} else if (d > 10) {	print "     Apr 0", d - 10
	} else {		print "     Mar ",  d + 21
	}
}

/*
 * For year n, print Easter and related holidays.
 * Assumes n >= 1, scale = 0.
 */
define easter(n) {
	auto a, b, d, e, j

	/*
	 * Calculate e = day of Easter, following the 1886 paper,
	 * Kalender-Formeln (Calendar Formulae) by Chr. Zeller.
	 *   http://www.merlyn.demon.co.uk/zel-1886.htm
	 */
	b = (19 * (n % 19) + 15) % 30
	d = (b + n + n / 4) % 7
	e = b + 7 - d		/* This counts days after 21 March. */

	/*
	 * Starting at 1924 March 10 (Julian) / March 23 (Gregorian),
	 * change to the Gregorian calendar. Easter 1924 is April 27,
	 * after this change.
	 */
	if (n >= 1924) {
		/* This formula only works from March to December. */
		e += n / 100 - n / 400 - 2
	}

	if(n < 1000) " "
	print n
	z = format(e)		/* Easter */
	z = format(e + 39)	/* Ascension Thursday */
	z = format(e + 49)	/* Pentecost */
	z = format(e + 56)	/* All Saints' Sunday */
	print "\n"
}

print "         Easter     Ascension             All\n"
print "  AD     Sunday     Thursday   Pentecost  Saints\n"
for (year =  400; year <= 2000; year += 100) z = easter(year)
for (year = 2010; year <= 2020; year +=   1) z = easter(year)
z = easter(2100)
quit
