/*
 * Normalized Julian Day Number from date (base 1899-12-30 00:00:00)
 * D = Vec [year, month, day]
 * return day number
 */
njd(D) =
{
  my (m, y);

  if (D[2] > 2, y = D[1]; m = D[2] + 1, y = D[1] - 1; m = D[2] + 13);

  (1461 * y) \ 4 + (306001 * m) \ 10000 + D[3] - 694024

/* Calendar reform ? */
    + if (100 * (100 * D[1] + D[2]) + D[3] > 15821004, 2 - y \ 100 + y \ 400)
}

/*
 * Date from Normalized Julian Day Number (base 1899-12-30 00:00:00)
 * n = Normalized Julian Day Number
 * return Vec [year, month, day]
 */
njdate(n) =
{
  my (a = n + 2415019, b, c, d, m, D, M, Y);

/* Calendar reform ? */
  if (a >= 2299161, b = (4 * a - 7468865) \ 146097; a += 1 + b - b \ 4);

  a += 1524;
  b = (20 * a - 2442) \ 7305;
  c = (1461 * b) \ 4;
  d = ((a - c) * 10000) \ 306001;
  m = d - 1 - 12 * (d > 13);

  [b - 4715 - (m > 2), m, a - c - (306001 * d) \ 10000]
}

/*
 * Date of Easter
 * Y = year
 * return Vec [year, month, day]
 */
easter(y) =
{
  my (a, b, d, m);

  if (y > 1582,           /* calendar reform ? */
/* Gregorian Easter */
    a = y % 19;
    b = y % 100;
    d = y \ 100;
    m = (19 * a + d - d \ 4 - (d - (d + 8) \ 25 + 1) \ 3 + 15) % 30;
    d = (32 + (d % 4) * 2 + (b \ 4) * 2 - m - b % 4) % 7;
    m += d - (a + 11 * m + 22 * d) \ 451 * 7 + 114;
  ,
/* Julian Easter */
    d = ((y % 19) * 19 + 15) % 30;
    m = d + ((y % 4) * 2 + (y % 7) * 4 - d + 34) % 7 + 114;
  );

  [y, m \ 31, m % 31 + 1]
}

holiday(y) =
{
  my (e = njd(easter(y)), n);

  n = njdate(e   ); printf("%4d: Easter: %02d-%02d, ", y, n[2], n[3]);
  n = njdate(e+39); printf("Ascension: %02d-%02d, ", n[2], n[3]);
  n = njdate(e+49); printf("Pentecost: %02d-%02d, ", n[2], n[3]);
  n = njdate(e+56); printf("Trinity: %02d-%02d, ", n[2], n[3]);
  n = njdate(e+60); printf("Corpus: %02d-%02d\n", n[2], n[3]);
}

print("Christian holidays, related to Easter, for years from 400 to 2100 CE:");
forstep (y = 400, 2100, 100, holiday(y));

print("\nChristian holidays, related to Easter, for years from 2010 to 2020 CE:");
for (y = 2010, 2020, holiday(y));
