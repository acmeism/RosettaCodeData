(subscriptrange, size, fofl):
Easter: procedure options (main);
   declare months(12) character (9) varying static initial (
           'January', 'February', 'March',     'April',   'May',      'June',
           'July',    'August',   'September', 'October', 'November', 'December');
   declare (year, month, day) fixed binary;

   do year = 2000 to 2020;
      call Easter_Sunday (year, month, day);
      put skip edit ('In ', year, ' Easter occurs on ', day, ' of ', months(month))
         (a, f(4), a, f(2), a, a);
   end;

/* Given the year, this procedure computes the month and day when Easter Sunday falls. */
Easter_Sunday: procedure (year,month,day);
/*
  See:
  Jean Meeus, "Astronomical Formulae for Calculators",
  4th edition, Willmann-Bell, 1988, p.31
*/

   declare (year nonassignable, month, day) fixed binary;
   declare (a, b, c, d, e, f, g, h, i, j, k, l, m, n) fixed binary;

   a = mod(year, 19);
   b = year/100;
   c = mod(year, 100);
   d = b/4;
   e = mod(b,4);
   f = (b+8)/25;
   g = (b-f+1)/3;
   h = mod(19*a+b-d-g+15, 30);
   i = c/4;
   k = mod(c,4);
   l = mod(32+2*e+2*i-h-k, 7);
   m = (a+11*h+22*l)/451;
   n = h+l-7*m+114;
   month = n/31;
   day = mod(n, 31)+1;
end Easter_Sunday;

end Easter;
