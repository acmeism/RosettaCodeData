--
-- demo\rosetta\Easter.exw
-- =======================
--
function easter(integer year)
-- from https://en.wikipedia.org/wiki/Computus#Anonymous_Gregorian_algorithm
    integer a = mod(year,19),
            b = floor(year/100),
            c = mod(year,100),
            d = floor(b/4),
            e = mod(b,4),
            f = floor((b+8)/25),
            g = floor((b-f+1)/3),
            h = mod(19*a+b-d-g+15,30),
            i = floor(c/4),
            k = mod(c,4),
            l = mod(32+2*e+2*i-h-k,7),
            m = floor((a+11*h+22*l)/451),
            n = h+l-7*m+114,
            month = floor(n/31),
            day = mod(n,31)+1
    return {year,month,day,0,0,0,0,0,0}
end function

constant dates = {{"Easter   ",0},
                  {"Ascension",39},
                  {"Pentecost",49},
                  {"Trinity  ",56},
                  {"Corpus   ",60}}

constant fmt = join(repeat(" %12s",length(dates))),
         fmt0 = "   "&fmt&"\n",
         fmtN = "%4d"&fmt&"\n"

include builtins\timedate.e

set_timedate_formats({"Ddd ddth Mmm"})

procedure show(integer year)
    if year=0 then
        printf(1,fmt0, columnize(dates,1)[1])
    else
        timedate e = easter(year)
        sequence args = {year}
        for i=1 to length(dates) do
            string d = format_timedate(adjust_timedate(e,timedelta(days:=dates[i][2])))
            args = append(args,d)
        end for
        printf(1,fmtN, args)
    end if
end procedure
show(0)
for year=400 to 2000 by 100 do
    show(year)
end for
show(0)
for year=2010 to 2020 do
    show(year)
end for
