DataSection
  C_DAYS:
  Data.i 0,39,49,56,60
  END_C_DAYS:
  DAYS_MT:
  Data.i 0,31,28,31,30,31,30,31,31,30,31,30,31
  END_DAYS_MT:
EndDataSection

Dim m.s{3}(12)  : PokeS(@m(),"___JanFebMarAprMayJunJulAugSepOctNovDec")
Dim dom.i(12)   : CopyMemory(?DAYS_MT,@dom(),?END_DAYS_MT-?DAYS_MT)
Structure tDate : yyyy.i : mm.i : dd.i : EndStructure
Define.tDate DateTime, BufDateTime

Procedure.b IsLeap(y.i)
  ProcedureReturn Bool( y % 4 = 0 ) & Bool( y % 100 <> 0 ) | Bool( y % 400 = 0 )
EndProcedure

Procedure.i DayOfMt(mm.i,yyyy.i)
  Shared dom()
  If mm=2 And IsLeap(yyyy) : ProcedureReturn 29 : Else : ProcedureReturn dom(mm) : EndIf
EndProcedure

Procedure delta(*pDate.tDate,AddDays.i)
  *pDate\dd+AddDays
  dom.i=DayOfMt(*pDate\mm,*pDate\yyyy)
  While *pDate\dd-dom>0
    *pDate\mm+1
    If *pDate\mm>12 : *pDate\mm=1 : *pDate\yyyy+1 : EndIf
    *pDate\dd-dom
    dom=DayOfMt(*pDate\mm,*pDate\yyyy)
  Wend
EndProcedure

Procedure dt(y.i)
  a.i=y%19
  b.i=y/100
  c.i=y%100
  d.i=b/4
  e.i=b%4
  f.i=(b+8)/25
  g.i=(b-f+1)/3
  h.i=(19*a+b-d-g+15)%30
  i.i=c/4
  k.i=c%4
  l.i=(32+2*e+2*i-h-k)%7
  m.i=(a+11*h+22*l)/451
  num.i=h+l-7*m+114
  mt.i=num/31
  dy.i=(num%31)+1
  Shared DateTime
  DateTime\yyyy=y
  DateTime\mm=mt
  DateTime\dd=dy
EndProcedure

Macro PutTab(i)
  dt(i) : BufDateTime=DateTime : r$=RSet(Str(DateTime\yyyy),4)
  *p_C_Days=?C_DAYS
  While *p_C_Days<?END_C_DAYS
    delta(@DateTime,PeekI(*p_C_Days))
    r$+Space(5)+RSet(Str(DateTime\dd),2,"0")+" "+m(DateTime\mm)
    DateTime=BufDateTime
    *p_C_Days+SizeOf(Integer)
  Wend
EndMacro

OpenConsole()
PrintN("Year"+Space(5)+"Easter  Ascension  Pentecost    Trinity  C/Christi")
PrintN(" CE "+Space(5)+"Sunday  Thursday    Sunday      Sunday   Thursday")
PrintN("----"+Space(5)+"------  --------- ----------    -------  ---------")
For i=400 To 2100 Step 100 : PutTab(i) : PrintN(r$) : Next
PrintN("")
For i=2010 To 2020 : PutTab(i) : PrintN(r$) : Next
Input()
