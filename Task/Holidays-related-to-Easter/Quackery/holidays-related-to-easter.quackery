  [ dup 19 mod 19 *
    15 + 30 mod
    swap dup 4 mod 2 *
    swap 7 mod 4 * +
    over - 34 + 7 mod +
    114 + 31 /mod 1+ ]       is julian    (   y --> m d )

  [ dup 19 mod 19 *
    over 100 / +
    over 400 / -
    swap 100 / 8 * 13 +
    25 / - 15 + 30 mod ]     is hge       (   y --> n   )

  [ dup 100 / 4 mod 2 * 32 +
    over 100 mod 4 / 2 * +
    over hge -
    swap 4 mod - 7 mod ]     is lge       (   y --> n   )

  [ dup 19 mod
    over hge 11 * +
    swap lge 19 * + 443 / ]  is mge       (   y --> n   )

  [ dup hge
    over lge +
    swap mge 7 * -
    90 + 25 / ]              is nge       (   y --> n   )

  [ dup hge
    over lge +
    over mge 7 * +
    swap nge 33 * +
    19 + 32 mod ]            is pge       (   y --> n   )

  [ dup nge swap pge ]       is gregorian (   y --> m d )

  [ dup 1583 < iff
      julian
    else gregorian ]         is easter    (   y --> m d )

   [ dip 1+ 9 +
    dup 31 > if
      [ dip 1+ 31 - ] ]      is ascension ( m d -->     )

  [ dip 1+ 19 +
    dup 31 > if
      [ dip 1+ 31 - ] ]      is pentecost ( m d -->     )

  [ dip 1+ 26 +
    dup 31 > if
      [ dip 1+ 31 - ] ]      is trinity   ( m d -->     )

  [ dip 1+ 30 +
    dup 31 > if
      [ dip 1+ 31 - ] ]      is corpus    ( m d -->     )

  [ swap 3 -
    [ table
      $ "Mar" $ "Apr"
      $ "May" $ "Jun" ]
    do echo$ sp
    dup 10 < if [ say "0" ]
    echo ]                   is echodate  ( m d -->     )

  [ dup 1000 < if sp
    dup echo say "   "
    easter 2dup echodate
    say "    "
    2dup ascension echodate
    say "    "
    2dup pentecost echodate
    say "    "
    2dup trinity echodate
    say "    "
    corpus echodate cr ]     is echodates (   y -->     )

  say "Year   Easter   Ascension            Trinity  Corpus"
  cr
  say "(CE)   Sunday   Thursday  Pentecost  Sunday   Christi"
  cr cr
  18 times
    [ i^ 100 * 400 + echodates ]
  cr
  11 times
    [ i^ 2010 + echodates ]
