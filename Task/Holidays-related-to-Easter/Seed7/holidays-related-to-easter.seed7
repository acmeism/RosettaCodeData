$ include "seed7_05.s7i";
  include "time.s7i";
  include "duration.s7i";

const func time: easterDate (in integer: year) is func
  result
    var time: result is time.value;
  local
    var integer: H1 is 0;
    var integer: H2 is 0;
    var integer: M is 0;
    var integer: N is 0;
    var integer: a is 0;
    var integer: b is 0;
    var integer: c is 0;
    var integer: d is 0;
    var integer: e is 0;
    var integer: f is 0;
  begin
    H1 := year mdiv 100;
    H2 := year mdiv 400;
     M := 15 + H1 - H2 - (8 * H1 + 13) mdiv 25;
     N := 4 + H1 - H2;

    # Gauss formula:
     a := year mod 19;
     b := year mod 4;
     c := year mod 7;
     d := (19 * a + M) mod 30;
     e := (2 * b + 4 * c + 6 * d + N) mod 7;
     f := 22 + d + e;

    if f = 57 then
      f := 50;
    end if;
    if d = 28 and e = 6 and a > 10 then
      f := 49;
    end if;

    result.year := year;
    if f <= 31 then
      result.month := 3;
    else
      result.month := 4;
      f -:= 31;
    end if;
    result.day := f;
  end func;

const array string: weekday is [] ("Mon", "Tue", "Wed", "Thu", "Fri", "Sat", "Sun");
const array string: month is [] ("Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec");
const array string: holiday is [] ("Easter", "Ascension", "Pentecost", "Trinity", "Corpus");
const array integer: delta is [] (0, 39, 49, 56, 60);

const func string: usDate (in time: aTime) is
  return weekday[dayOfWeek(aTime)] <& aTime.day lpad 3 <& " " <& month[aTime.month];

const func string: holiday (in integer: index, in time: easter) is
  return holiday[index] <& ": " <& usDate(easter + delta[index] . DAYS);

const proc: writeHolidays (in integer: year) is func
  local
	var time: easter is time.value;
  begin
    easter := easterDate(year);
    writeln(year lpad 4 <& " " <&
        holiday(1, easter) <& ", " <&
        holiday(2, easter) <& ", " <&
        holiday(3, easter) <& ", " <&
        holiday(4, easter) <& ", " <&
        holiday(5, easter));
  end func;

const proc: main is func
  local
    var integer: year is 0;
  begin
    writeln("Christian holidays, related to Easter, for each centennial from 400 to 2100 CE:");
    for year range 400 to 2100 step 100 do
	  writeHolidays(year);
    end for;
    writeln;
	writeln("Christian holidays, related to Easter, for years from 2010 to 2020 CE:");
    for year range 2010 to 2020 do
	  writeHolidays(year);
    end for;
  end func;
