10  ' Horizontal sundial calculations
20  PRINT "Enter latitude       => ";
30  INPUT LAT
40  PRINT "Enter longitude      => ";
50  INPUT LNG
60  PRINT "Enter legal meridian => ";
70  INPUT REF
80  PRINT
90  LET PI = 4 * ATN(1)
100 LET SLAT = SIN(LAT * PI / 180)
110 PRINT "    sine of latitude:   "; USING "#.##^^^^"; SLAT
120 PRINT "    diff longitude:     "; USING "####.###"; LNG - REF
130 PRINT
140 PRINT "Hour, sun hour angle, dial hour line angle from 6am to 6pm"
150 FOR H% = -6 TO 6
160  LET HRA = 15 * H%
170  LET HRA = HRA - (LNG - REF): ' correct for longitude difference
180  LET HLA = ATN(SLAT * TAN(HRA * PI / 180)) * 180 / PI
190  PRINT "HR="; USING "+##"; H%;
200  PRINT "; HRA="; USING "+###.###"; HRA;
210  PRINT "; HLA="; USING "+###.###"; HLA
220 NEXT H%
230 END
