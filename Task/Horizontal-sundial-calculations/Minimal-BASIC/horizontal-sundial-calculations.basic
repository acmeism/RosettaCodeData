10 REM Horizontal sundial calculations
20 DEF FNM(X) = INT(X*1000+0.5)/1000
30 PRINT "Enter latitude";
40 INPUT L
50 PRINT "Enter longitude";
60 INPUT L1
70 PRINT "Enter legal meridian";
80 INPUT R
90 PRINT
100 LET P = 4*ATN(1)
110 LET S1 = SIN(L*P/180)
120 PRINT "    sine of latitude:"; S1
130 PRINT "    diff longitude:  "; FNM(L1-R)
140 PRINT
150 PRINT "Hour, sun hour angle, dial hour line angle from 6am to 6pm"
160 FOR H = -6 TO 6
170 LET A1 = 15*H
180 REM Correct for longitude difference:
190 LET A1 = A1-(L1-R)
200 LET A2 = ATN(S1*TAN(A1*P/180))*180/P
210 PRINT "HR ="; H;
220 PRINT TAB(9); "HRA ="; FNM(A1);
230 PRINT TAB(24); "HLA ="; FNM(A2)
240 NEXT H
250 END
