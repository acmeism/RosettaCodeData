$ include "seed7_05.s7i";
  include "float.s7i";
  include "math.s7i";

const float: radianToDegrees is 57.295779513082320876798154814114;
const float: degreesToRadian is 0.017453292519943295769236907684883;

const proc: main is func
  local
    var float: lat is 0.0;
    var float: slat is 0.0;
    var float: lng is 0.0;
    var float: meridian is 0.0;
    var float: hla is 0.0;
    var float: hra is 0.0;
    var integer: h is 0;
  begin
    write("Enter latitude: ");
    readln(lat);
    write("Enter longitude: ");
    readln(lng);
    write("Enter legal meridian: ");
    readln(meridian);
    writeln;
    slat := sin(degreesToRadian * lat);
    writeln("sine of latitude: " <& slat digits 3);
    writeln("diff longitude: " <& lng - meridian digits 3);
    writeln;
    writeln("Hour, sun hour angle, dial hour line angle from 6am to 6pm");
    for h range -6 to 6 do
      hra := 15.0 * flt(h);
      hra := hra - lng + meridian;
      hla := radianToDegrees * atan(slat * tan(degreesToRadian * hra));
      writeln("HR= " <& h lpad 2 <& "; HRA= " <& hra digits 3 lpad 7 <&
              "; HLA= " <& hla digits 3 lpad 7);
    end for;
  end func;
