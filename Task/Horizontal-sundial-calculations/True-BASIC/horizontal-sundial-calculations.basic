FUNCTION rad2deg(theta)
    LET rad2deg = theta*180/PI
END FUNCTION
FUNCTION deg2rad(theta)
    LET deg2rad = theta*PI/180
END FUNCTION
FUNCTION signo(x)
    IF x > 0 THEN LET signo = 1 ELSE LET signo = -1
END FUNCTION
INPUT prompt "Enter latitude  (degrees)     : ": latitude  !    -4.95
INPUT prompt "Enter longitude (degrees)     : ": longitude !   -150.5
INPUT prompt "Enter legal meridian (degrees): ": meridian  !   -150.0
PRINT
PRINT "Time     Sun hora angle   Dial hora line angle"
FOR hora = 6 TO 18
    LET hra = (15*hora)-longitude+meridian-180
    LET hla = rad2deg(ATN(SIN(deg2rad(latitude))*TAN(deg2rad(hra))))
    IF abs(hra) > 90 THEN LET hla = hla+180*signo(hra*latitude)
    PRINT  USING "##         ####.##         ####.###": hora, hra, hla
NEXT hora
END
