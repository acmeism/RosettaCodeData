inc  c:\cxpl\codes;
def  Pi = 3.14159265358979323846,
     Deg2Rad = Pi/180.0,
     Rad2Deg = 180.0/Pi,
     Tab = $09;
real Lat, SinLat, Long, Mer;
real HA, HLA;                   \hour angle and hour line angle
int  H, T;                      \hour, time
[Text(0, "Latitude:       ");  Lat:=  RlIn(0);
 Text(0, "Longitude:      ");  Long:= RlIn(0);
 Text(0, "Legal meridian: ");  Mer:=  RlIn(0);
Text(0, "
Hour  Sun hour angle   Dial hour line angle
");
Format(4, 3);
SinLat:= Sin(Lat*Deg2Rad);
for H:= -6 to 6 do
   [HA:= float(15 * H);         \hour angle is 15 times the hour
    HA:= HA - (Long-Mer);       \ but corrected for longitude difference
    HLA:= ATan2( SinLat * Sin(HA*Deg2Rad), Cos(HA*Deg2Rad) ) * Rad2Deg;
    T:= H+12;  if T>12 then T:= T-12;
    if T<10 then ChOut(0, ^ );  IntOut(0, T);
    Text(0, if H>=0 then "pm    " else "am      ");
    RlOut(0, HA);  ChOut(0, Tab);  RlOut(0, HLA);  CrLf(0);
   ];
]
