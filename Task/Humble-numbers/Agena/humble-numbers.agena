scope # find some Humble numbers - numbers with no prime factors above 7
    local constant maxHumble, constant maxShownHumble := 2048, 50;
    local h, hCount      := seq(), [ 0, 0, 0, 0, 0, 0 ]; hCount[ 0 ] := 0;
    local p2, p3, p5, p7 := 2, 3, 5, 7;
    local last2, last3, last5, last7 := 1, 1, 1, 1;
    # 1 is the first humble number ( 2^0 * 3^0 * 5^0 * 7^0 ) and has 1 digit
    h[ 1 ], hCount[ 1 ]  := 1, 1;
    io.write( "1" );
    for n from 2 to maxHumble do
        # the next humble number is the lowest of the next multiples of 2, 3, 5, 7
        local constant m := math.min( p2, p3, p5, p7 );
        h[ n ] := m;
        if n <= maxShownHumble then io.write( " ", m ) fi;
        if m = p2 then last2 +:= 1;  p2 := 2 * h[ last2 ] fi;
        if m = p3 then last3 +:= 1;  p3 := 3 * h[ last3 ] fi;
        if m = p5 then last5 +:= 1;  p5 := 5 * h[ last5 ] fi;
        if m = p7 then last7 +:= 1;  p7 := 7 * h[ last7 ] fi;
        hCount[ if   m <        10 then 1
                elif m <       100 then 2
                elif m <     1_000 then 3
                elif m <    10_000 then 4
                elif m <   100_000 then 5
                elif m < 1_000_000 then 6
                else                    0
                fi
              ] +:= 1
    od;
    print();
    for i to 6 do
        printf( "There are %4d Humble numbers with %d digits\n", hCount[ i ], i )
    od
end
