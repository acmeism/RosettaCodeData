$ include "seed7_05.s7i";

const func boolean: isISBN13 (in var string: input) is func
  result
    var boolean: isbn is FALSE;
  local
    var char: c is ' ';
    var integer: digit is 0;
    var integer: i is 1;
    var integer: sum is 0;
  begin
    input := replace(input, " ", "");
    input := replace(input, "-", "");
    if length(input) = 13 then
      for c range input do
        digit := ord(c) - 48;
        if not odd(i) then
          digit *:= 3;
        end if;
        sum +:= digit;
        incr(i);
      end for;
      isbn := sum rem 10 = 0;
    end if;
  end func;

const proc: main is func
  local
    var string: str is "";
  begin
    for str range [] ("978-0596528126", "978-0596528120", "978-1788399081", "978-1788399083") do
      writeln(str <& ": " <& isISBN13(str));
    end for;
  end func;
