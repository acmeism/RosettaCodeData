10 rem input loop - rosetta code
11 rem open command channel, clear screen, switch to lower case
12 open 15,8,15
15 print chr$(147);chr$(14):f$=""

20 input "Enter filename";f$
25 if f$="" then end

30 open 5,8,5,f$+",s,r"
40 gosub 1000
50 if er=62 then print "That file is not found... Try again.":close 5:goto 20
60 if er<>0 then print "There was an unexpected error.":close 5:gosub 1100

70 get#5,a$
80 if st and 64 then close 5:close 15:end
90 print a$;:goto 70

1000 rem check command channel for error
1005 rem error number, error msg$, track number, sector number
1010 input#15,er,er$,tk,sc
1020 return

1100 rem print error
1110 print:print er;"- ";er$;" track:";tk;"sector:";sc
1120 return
