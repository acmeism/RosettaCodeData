/* Integer square root using quadratic residue method */
proc nonrec isqrt(ulong x) ulong:
    ulong q, z, r;
    long t;

    q := 1;
    while q <= x do q := q << 2 od;

    z := x;
    r := 0;
    while q > 1 do
        q := q >> 2;
        t := z - r - q;
        r := r >> 1;
        if t >= 0 then
            z := t;
            r := r + q
        fi
    od;

    r
corp

proc nonrec main() void:
    byte x;
    ulong pow7;

    /* print isqrt(0) ... isqrt(65) */
    for x from 0 upto 65 do
        write(isqrt(x):2);
        if x % 11 = 10 then writeln() fi
    od;

    /* print isqrt(7^0) thru isqrt(7^10) */
    pow7 := 1;
    for x from 0 upto 10 do
        writeln("isqrt(7^", x:2, ") = ", isqrt(pow7):5);
        pow7 := pow7 * 7
    od
corp
