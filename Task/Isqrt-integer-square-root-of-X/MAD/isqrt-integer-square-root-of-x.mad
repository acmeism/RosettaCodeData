            NORMAL MODE IS INTEGER

          R  INTEGER SQUARE ROOT OF X

            INTERNAL FUNCTION(X)
            ENTRY TO ISQRT.
            Q = 1
FNDPW4      WHENEVER Q.LE.X
                Q = Q * 4
                TRANSFER TO FNDPW4
            END OF CONDITIONAL
            Z = X
            R = 0
FNDRT       WHENEVER Q.G.1
                Q = Q / 4
                T = Z - R - Q
                R = R / 2
                WHENEVER T.GE.0
                    Z = T
                    R = R + Q
                END OF CONDITIONAL
                TRANSFER TO FNDRT
            END OF CONDITIONAL
            FUNCTION RETURN R
            END OF FUNCTION

          R  PRINT INTEGER SQUARE ROOTS OF 0..65

            THROUGH SQ65, FOR N=0, 11, N.G.65
SQ65        PRINT FORMAT N11, ISQRT.(N), ISQRT.(N+1), ISQRT.(N+2),
          0    ISQRT.(N+3), ISQRT.(N+4), ISQRT.(N+5), ISQRT.(N+6),
          1    ISQRT.(N+7), ISQRT.(N+8), ISQRT.(N+9), ISQRT.(N+10)
            VECTOR VALUES N11 = $11(I1,S1)*$

          R  MACHINE WORD SIZE ON IBM 704 IS 36 BITS
          R  PRINT UP TO AND INCLUDING ISQRT(7^12)

            POW7 = 1
            THROUGH SQ7P12, FOR I=0, 1, I.G.12
            PRINT FORMAT SQ7, I, ISQRT.(POW7)
SQ7P12      POW7 = POW7 * 7
            VECTOR VALUES SQ7 = $9HISQRT.(7^,I2,4H) = ,I6*$

            END OF PROGRAM
