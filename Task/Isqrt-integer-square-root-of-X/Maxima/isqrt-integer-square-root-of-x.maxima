/* -*- Maxima -*- */

/*

The Rosetta Code integer square root task, in Maxima.

I have not tried to make the output conform quite to the task
description, because Maxima is not a general purpose programming
language. Perhaps someone else will care to do it.

I *do* check that the Rosetta Code routine gives the same results as
the built-in function.

*/

/* pow4gtx -- find a power of 4 greater than x. */
pow4gtx (x) := block (
  [q],
  q : 1, while q <= x do q : bit_lsh (q, 2),
  q
  ) $

/* rosetta_code_isqrt -- find the integer square root. */
rosetta_code_isqrt (x) := block (
  [q, z, r, t],
  q : pow4gtx (x),
  z : x,
  r : 0,
  while 1 < q do (
    q : bit_rsh (q, 2),
    t : z - r - q,
    r : bit_rsh (r, 1),
    if 0 <= t then (
      z : t,
      r : r + q
      )
    ),
  r
  ) $

for i : 0 thru 65 do (
  display (rosetta_code_isqrt (i),
    is (equal (rosetta_code_isqrt (i), isqrt (i))))
  ) $
for i : 1 thru 73 step 2 do (
  display (7**i, rosetta_code_isqrt (7**i),
    is (equal (rosetta_code_isqrt (7**i), isqrt (7**i))))
  ) $
