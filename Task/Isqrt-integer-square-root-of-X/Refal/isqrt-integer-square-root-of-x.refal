$ENTRY Go {
    = <Isqrt0To65>
      <Prout>
      <IsqrtOddPow7>;
};

Isqrt0To65 {
    = <Prout 'isqrt of 0..65:'>
      <Each (Prout) <Split 11 <Each (Isqrt) <Iota 0 65>>>>;
};

IsqrtOddPow7 {
    , <Each (Add 1) <Each (Mul 2) <Iota 0 36>>>: e.Pows
    = <Each (IsqrtPow7) e.Pows>;
};

IsqrtPow7 {
    s.Pow, <Pow (7) s.Pow>: e.Pow7,
           <Isqrt e.Pow7>: e.Sqrt =
        <Prout 'isqrt(7^' <Symb s.Pow> ') = ' <Commatize e.Sqrt>>;
};

Isqrt {
    e.X, <Isqrt1 e.X>: e.Q = <Isqrt2 (e.Q) (e.X) (0)>;
};

Isqrt1 {
    (e.Q) e.X, <Compare (e.Q) e.X>: '+' = e.Q;
    (e.Q) e.X = <Isqrt1 (<Mul (e.Q) 4>) e.X>;
    e.X = <Isqrt1 (1) e.X>;
};

Isqrt2 {
    (1)   (e.Z) (e.R) = e.R;
    (e.Q) (e.Z) (e.R),
        <Div (e.Q) 4>: e.Q2,
        <Sub (<Sub (e.Z) e.R>) e.Q2>: e.T,
        <Div (e.R) 2>: e.R2,
        e.T: {
            '-' e.T2 = <Isqrt2 (e.Q2) (e.Z) (e.R2)>;
            e.T      = <Isqrt2 (e.Q2) (e.T) (<Add (e.R2) e.Q2>)>;
        };
};

Pow {
    (e.N) 0 = 1;
    (e.N) e.P, <Divmod (e.P) 2>: {
        (e.P2) 0, <Pow (e.N) e.P2>: e.X = <Mul (e.X) e.X>;
        (e.P2) 1, <Pow (e.N) e.P2>: e.X = <Mul (e.N) <Mul (e.X) e.X>>;
    };
};

Commatize {
    e.N = <Commatize1 <Symb e.N>>;
};

Commatize1 {
    e.X s.F s.1 s.2 s.3 = <Commatize1 e.X s.F> ',' s.1 s.2 s.3;
    e.X = e.X;
}

Iota { s.E s.E = s.E; s.S s.E = s.S <Iota <Add 1 s.S> s.E>; };
Each { (e.F) = ; (e.F) t.X e.XS = <Mu e.F t.X> <Each (e.F) e.XS>; };
Split { s.G = ; s.G e.X, <First s.G e.X>: (e.F) e.R = (e.F) <Split s.G e.R>; };
