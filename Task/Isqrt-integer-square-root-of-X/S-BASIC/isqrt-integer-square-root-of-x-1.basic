comment
  return integer square root of n using quadratic
  residue algorithm. WARNING: the function will fail
  for x > 16,383.
end
function isqrt(x = integer) = integer
  var q, r, t = integer
  q = 1
  while q <= x do
    q = q * 4       rem overflow may occur here!
  r = 0
  while q > 1 do
    begin
      q = q / 4
      t = x - r - q
      r = r / 2
      if t >= 0 then
        begin
          x = t
          r = r + q
        end
    end
end = r

rem - Exercise the function

var n, pow7 = integer
print "Integer square root of first 65 numbers"
for n=1 to 65
  print using "#####";isqrt(n);
next n
print
print "Integer square root of odd powers of 7"
print "  n    7^n   isqrt"
print "------------------"
for n=1 to 3 step 2
  pow7 = 7^n
  print using "###  ####  ####";n; pow7; isqrt(pow7)
next n

end
