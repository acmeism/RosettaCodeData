func Jacobi(A, N);
int A, N, Result, T;
[if A >= N then A:= rem(A/N);
Result:= 1;
while A do
    [while (A&1) = 0 do
        [A:= A >> 1;
        if (N&7) = 3 or (N&7) = 5 then Result:= -Result;
        ];
    T:= A;  A:= N;  N:= T;
    if (A&3) = 3 and (N&3) = 3 then Result:= -Result;
    A:= rem(A/N);
    ];
if N = 1 then return Result;
return 0;
];

proc PrintTable(KMax, NMax);
int  KMax, NMax, K, N;
[Text(0, "N\K|");
Format(3, 0);
for K:= 0 to KMax do RlOut(0, float(K));
CrLf(0);
Text(0, "----");
for K:= 0 to KMax do Text(0, "---");
CrLf(0);
for N:= 1 to NMax do
    [Format(2, 0);
    RlOut(0, float(N));
    Text(0, " |");
    Format(3, 0);
    for K:= 0 to KMax do
        RlOut(0, float(Jacobi(K, N)));
    CrLf(0);
    N:= N+1;
    ];
];

PrintTable(20, 21);
