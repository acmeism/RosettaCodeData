        .TITLE  JWLSTN
        .MCALL  .TTYOUT,.EXIT
JWLSTN::JMP     DEMO

        ; COUNT JEWELS R1 IN STONES R0, ANSWER IN R2
COUNT:  MOV     #200,R2         ; CLEAR JEWELS
        MOV     #6$,R3
1$:     CLR     (R3)+
        SOB     R2,1$
        BR      3$
2$:     MOVB    #1,6$(R2)       ; MARK JEWELS
3$:     MOVB    (R1)+,R2
        BNE     2$
        BR      5$
4$:     MOVB    6$(R1),R3       ; COUNT JEWELS IN STONES
        ADD     R3,R2
5$:     MOVB    (R0)+,R1
        BNE     4$
        RTS     PC
6$:    .BLKB   400

DEMO:   MOV     #TESTS,R4
1$:     MOV     (R4)+,R1        ; READ TEST CASE
        MOV     (R4)+,R0
        BEQ     2$              ; DONE?
        JSR     PC,COUNT        ; COUNT
        MOV     R2,R0           ; DISPLAY RESULT
        ADD     #60,R0
        .TTYOUT
        MOV     #15,R0
        .TTYOUT
        MOV     #12,R0
        .TTYOUT
        BR      1$              ; NEXT
2$:     .EXIT

TESTS:  .WORD   1$,2$,3$,4$,0,0
1$:     .ASCIZ  /aA/
2$:     .ASCIZ  /aAAbbbb/
3$:     .ASCIZ  /z/
4$:     .ASCIZ  /ZZ/
        .END    JWLSTN
