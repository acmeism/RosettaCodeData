isKaprekar[n, base=10] :=
{
   if n==1
      return [1, 1, 1]

   s = base[n^2, base]
   for i=1 to length[s]-1
   {
      ls = left[s,i]
      l = parseInt[ls, base]
      rs = right[s,-i]
      r = parseInt[rs, base]

      if isPositive[l] and isPositive[r] and l+r == n
         return [n, s, "$ls + $rs"]
   }

   return undef
}

f = {|x| isKaprekar[x] != undef}
println[formatTable[select[1 to 9999, f], "right"]]

println[]
print[length[select[1 to 999_999, f]]]
println[" Kaprekar numbers less than 1,000,000"]

println["\nKaprekar numbers in base 17:"]
results = new array
for i = 1 to 999_999
{
   r = isKaprekar[i, 17]
   if r != undef
      results.push[r]
}
println[formatTable[results, "right"]]
