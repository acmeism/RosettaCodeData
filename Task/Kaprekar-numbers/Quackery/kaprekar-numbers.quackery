  [ dup 1 = if done
    dup temp put
    dup *
    false swap 1
    [ base share *
      2dup /mod
      over 0 = iff
        2drop done
      dup 0 = iff
        2drop again
      + temp share = iff
        [ rot not unrot ]
        done
      again ]
    2drop
    temp release ]        is kaprekar ( n --> b )

  say "Kaprekar numbers less than one thousand: "
  []
  1000 times
    [ i^ kaprekar if
      [ i^ join ] ]
  echo cr cr

  say "Number of Kaprekar numbers less than one million: "
  0
  1000000 times
    [ i^ kaprekar if 1+ ]
  echo cr cr

  say "Base 17 Kaprekar numbers less than one million." cr cr
  17 base put
    []
  1000000 times
    [ i^ kaprekar if
      [ i^ join ] ]
  say "In base 17: "
  dup echo cr
  base release
  say "In decimal: "
  echo
