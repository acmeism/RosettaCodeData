  [ table ]                     is !        (   n --> n )
  1
  101 times
    [ dup ' ! put
      i^ 1+ * ]
  drop

  [ 2dup = iff [ 2drop 1 ] done
    2dup * 0 = iff * done
    dup 1 = iff [ drop ! ] done
    2dup - ! unrot
    dup !
    swap 1 - ! *
    swap dup !
    swap 1 - ! *
    swap /
    swap / ]                    is lah      (   n --> n )

  [ dip number$
    over size -
    space swap of
    swap join echo$ ]           is justify  ( n n -->   )

  [ table ]                     is colwidth (   n --> n )
  13 times
   [ 12 i^ lah number$
     size 2 + ' colwidth put ]

  say "  k| "
  13 times
    [ i^ dup colwidth justify ] cr
  say " n |"
  char - 115 of echo$ cr
  13 times
    [ i^ dup
      dup 2 justify say " | "
      1+ times
        [ dup i^ lah
          i^ colwidth justify ]
      drop cr ]
  cr
  0
  101 times
    [ 100 i^ lah
      2dup < iff nip else drop ]
  echo
