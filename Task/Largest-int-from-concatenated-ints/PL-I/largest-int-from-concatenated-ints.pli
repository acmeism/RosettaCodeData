/* Largest catenation of integers            16 October 2013 */
/* Sort using method 2, comparing pairs of adjacent integers. */

Largest: procedure options (main);
   declare s(*) char (20) varying controlled, n fixed binary;
   get (n);
   allocate s(n);
   get list (s);
   s = trim(s);
   put skip edit (s) (a, x(1));
   put skip list ('Largest integer=', Largest_integer());

largest_integer: procedure () returns (char(100) varying);
   declare sorted bit (1);
   declare (true value ('1'b), false value ('0'b)) bit (1);
   declare i fixed binary;
   declare temp character(20) varying;

   do until (sorted);
      sorted = true;
      do i = 1 to n-1;
         if char(s(i)) || char(s(i+1)) < char(s(i+1)) || char(s(i)) then
            do;
               temp = s(i); s(i) = s(i+1); s(i+1) = temp; sorted = false;
            end;
      end;
   end;
   return (string(s));
end largest_integer;
end Largest;
