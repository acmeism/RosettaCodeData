func Cat(A, B);         \Concatenate integers A and B
int  A, B, T;
[T:= B;
while T do
    [A:= A*10;
     T:= T/10;
    ];
return A+B;
];

proc CatSort(A, Len);   \Sort array A by concatenating adjacent values
int  A, Len;
int  I, J, T, X, Y;
[for J:= Len-1 downto 0 do
    for I:= 0 to J-1 do
        [X:= Cat(A(I), A(I+1));
         Y:= Cat(A(I+1), A(I));
         if X > Y then
            [T:= A(I);  A(I):= A(I+1);  A(I+1):= T];
        ];
];

proc Largest(Ints, Len); \Show the largest int from concatenated ints
int  Ints, Len;
int  I;
[CatSort(Ints, Len);
for I:= Len-1 downto 0 do
    IntOut(0, Ints(I));
CrLf(0);
];

[Largest([1, 34, 3, 98, 9, 76, 45, 4], 8);
 Largest([54, 546, 548, 60], 4);
]
