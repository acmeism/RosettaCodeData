(   audino bagon baltoy banette bidoof braviary bronzor
    carracosta charmeleon cresselia croagunk darmanitan deino
    emboar emolga exeggcute gabite girafarig gulpin haxorus
    heatmor heatran ivysaur jellicent jumpluff kangaskhan
    kricketune landorus ledyba loudred lumineon lunatone machamp
    magnezone mamoswine nosepass petilil pidgeotto pikachu
    pinsir poliwrath poochyena porygon2 porygonz registeel
    relicanth remoraid rufflet sableye scolipede scrafty seaking
    sealeo silcoon simisear snivy snorlax spoink starly
    tirtouga trapinch treecko tyrogue vigoroth vulpix wailord
    wartortle whismur wingull yamask
  : ?names
& 1:?newnames
&   whl
  ' ( !names:@(%?name:%@?first ? @?last) ?names
    & !first^(.!last)\L!name*!newnames:?newnames
    )
& !newnames:?names
& 0:?max
& :?sequence
& ( lalefile
  =     done todo A M Z Length first
      , Ms a z last candidates
    .   !arg:(!done.)&!done:?sequence
      |   !arg:(.?todo)
        & (   !todo
            :   ?A
              * %?first^?candidates
              * ( ?Z
                &   !candidates
                  :   ?a
                    + ?last\L(%?M*?Ms)
                    + ( ?z
                      & lalefile$(!M.!A*!first^(!a+!last\L!Ms+!z)*!Z)
                      & ~
                      )
                )
          |
          )
      |   !arg:(@(%:? [-2 ?first) ?:?done.?todo)
        & :?M
        & (   !todo:?A*!first^%?candidates*?Z
            &   !candidates
              :   ?a
                + ?last\L(%?M*?Ms)
                + ( ?z
                  &   lalefile
                    $ (!M !done.!A*!first^(!a+!last\L!Ms+!z)*!Z)
                  & ~
                  )
          |   !M:
            & !done:? [?Length
            & !Length:>!max:?max
            & !done:?sequence
          |
          )
  )
& lalefile$(.!names)
& out$("Length:" !max "Sequence:" !sequence)
);
