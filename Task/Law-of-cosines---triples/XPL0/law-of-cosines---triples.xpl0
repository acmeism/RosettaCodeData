proc LawCos(Eqn);
int  Eqn;
int  Cnt, A, B, C;

    proc Show;
    [Cnt:= Cnt+1;
    IntOut(0, A);  ChOut(0, ^ );
    IntOut(0, B);  ChOut(0, ^ );
    IntOut(0, C);  CrLf(0);
    ];

[Cnt:= 0;
for A:= 1 to 13 do
    for B:= 1 to A do
        for C:= 1 to 13 do
            case Eqn of
             1: if A*A + B*B       = C*C then Show;
             2: if A*A + B*B - A*B = C*C then Show;
             3: if A*A + B*B + A*B = C*C then Show
            other [];
IntOut(0, Cnt);  Text(0, " results^m^j");
];

proc ExtraCredit;
int  Cnt, A, B, C, C2;
[Cnt:= 0;
for A:= 1 to 10_000 do
    for B:= 1 to A-1 do
        [C2:= A*A + B*B - A*B;
        C:= sqrt(C2);
        if C*C = C2 then Cnt:= Cnt+1;
        ];
Text(0, "Extra credit: ");
IntOut(0, Cnt);
CrLf(0);
];

int Case;
[for Case:= 1 to 3 do LawCos(Case);
ExtraCredit;
]
