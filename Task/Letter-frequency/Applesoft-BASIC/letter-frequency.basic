 100  LET F$ = "TEXT FILE"
 110  LET D$ =  CHR$ (4)
 120  DIM C(255)
 130  PRINT D$"OPEN "F$
 140  FOR Q = 0 TO 1 STEP 0
 150      PRINT D$"READ "F$
 160      ONERR  GOTO 240
 170      GET C$
 180      POKE 216,0
 190      LET C =  ASC (C$)
 200      LET C(C) = C(C) + 1
 210      PRINT
 220  NEXT
 230  STOP
 240  POKE 216,0
 250  LET E =  PEEK (222)
 260  PRINT D$"CLOSE "F$
 270  IF E <  > 5 THEN  RESUME
 280  FOR I = 0 TO 255
 290      IF C(I) THEN  GOSUB 320
 300  NEXT I
 310  END
 320  IF I < 32 THEN  PRINT "^" CHR$ (64 + I);
 330  IF I >  = 32 AND I < 128 THEN  PRINT  CHR$ (I);
 340  IF I > 127 THEN  PRINT "CHR$("I")";
 350  PRINT "="C(I)"  ";
 360  RETURN
