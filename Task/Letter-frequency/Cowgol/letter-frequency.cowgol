include "cowgol.coh";
include "argv.coh";
include "file.coh";

# Get filename from command line
ArgvInit();
var file := ArgvNext();
if file == (0 as [uint8]) then
    print("error: no file name\n");
    ExitWithError();
end if;

# Open the file
var fcb: FCB;
if FCBOpenIn(&fcb, file) != 0 then
    print("error: cannot open file\n");
    ExitWithError();
end if;

# Counters for each letter
var letterCount: uint32[26];
MemZero(&letterCount as [uint8], @bytesof letterCount);

# Count every letter
var len := FCBExt(&fcb);
while len != 0 loop
    len := len - 1;
    var ch := (FCBGetChar(&fcb) | 32) - 'a';
    if ch >= @sizeof letterCount then
        continue;
    end if;
    letterCount[ch] := letterCount[ch] + 1;
end loop;

# Close the file
var foo := FCBClose(&fcb);

# Print value for each letter
ch := 0;
while ch < @sizeof letterCount loop
    print_char(ch + 'A');
    print(": ");
    print_i32(letterCount[ch]);
    print_nl();
    ch := ch + 1;
end loop;
