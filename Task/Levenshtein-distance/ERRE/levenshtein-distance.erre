PROGRAM LEVENSHTEIN

!$DYNAMIC
  DIM D%[0,0]

PROCEDURE LEVENSHTEIN(S$,T$->RES%)
      LOCAL I%,J%,M%
      FOR I%=0 TO LEN(S$) DO
        D%[I%,0]=I%
      END FOR
      FOR J%=0 TO LEN(T$) DO
        D%[0,J%]=J%
      END FOR
      FOR J%=1 TO LEN(T$) DO
        FOR I%=1 TO LEN(S$) DO
          IF MID$(S$,I%,1)=MID$(T$,J%,1) THEN
            D%[I%,J%]=D%[I%-1,J%-1]
          ELSE
            M%=D%[I%-1,J%-1]
            IF D%[I%,J%-1]<M% THEN M%=D%[I%,J%-1] END IF
            IF D%[I%-1,J%]<M% THEN M%=D%[I%-1,J%] END IF
            D%[I%,J%]=M%+1
          END IF
        END FOR
      END FOR
      RES%=D%[I%-1,J%-1]
END PROCEDURE

BEGIN
   S$="kitten"  T$="sitting"
   PRINT("'";S$;"' -> '";T$;"' has distance ";)
   !$DIM D%[LEN(S$),LEN(T$)]
   LEVENSHTEIN(S$,T$->RES%)
   PRINT(RES%)
   !$ERASE D%

   S$="rosettacode" T$="raisethysword"
   PRINT("'";S$;"' -> '";T$;"' has distance ";)
   !$DIM D%[LEN(S$),LEN(T$)]
   LEVENSHTEIN(S$,T$->RES%)
   PRINT(RES%)
   !$ERASE D%
END PROGRAM
