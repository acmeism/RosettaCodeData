10 REM Levenshtein distance
20 PRINT "The Levenshtein distance..."
30 S$ = "kitten": T$ = "sitting": GOSUB 1000
40 PRINT "between '"; S$; "' and '"; T$; "' is"; LEV.DIST
50 S$ = "rosettacode": T$ = "raisethysword": GOSUB 1000
60 PRINT "between '"; S$; "' and '"; T$; "' is"; LEV.DIST
70 END
1000 REM ** Calculate distance for strings S$ and T$
1010 M = LEN(S$): N = LEN(T$)
1020 DIM D(M, N)
1030 FOR I = 0 TO M
1040  D(I, 0) = I
1050 NEXT I
1060 FOR J = 0 TO N
1070  D(0, J) = J
1080 NEXT J
1090 FOR J = 1 TO N
1100  FOR I = 1 TO M
1110   IF MID$(S$, I, 1) <> MID$(T$, J, 1) THEN 1140
1120   D(I, J) = D(I - 1, J - 1)
1130   GOTO 1160
1140   IF D(I, J - 1) + 1 < D(I - 1, J - 1) + 1 THEN MIN = D(I, J - 1) + 1 ELSE MIN = D(I - 1, J - 1) + 1
1150   IF D(I - 1, J) + 1 < MIN THEN D(I, J) = D(I - 1, J) + 1 ELSE D(I, J) = MIN
1160  NEXT I
1170 NEXT J
1180 LEV.DIST = D(M, N)
1190 ERASE D
1200 RETURN
