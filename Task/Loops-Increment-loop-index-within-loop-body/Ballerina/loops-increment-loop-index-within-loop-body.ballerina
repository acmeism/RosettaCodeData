import ballerina/io;

function isPrime(int n) returns boolean {
    if n < 2 { return false; }
    if n % 2 == 0 { return n == 2; }
    if n % 3 == 0 { return n == 3; }
    int d = 5;
    while d * d <= n {
        if n % d == 0 { return false; }
        d += 2;
        if n % d == 0 { return false; }
        d += 4;
    }
    return true;
}

function commatize(int n) returns string {
    string s = n.toString();
    if n < 0 { s = s.substring(1); }
    int le = s.length();
    foreach int i in int:range(le - 3, 0, -3) {
        s = s.substring(0, i) + "," + s.substring(i);
    }
    if n >= 0 { return s; }
    return "-" + s;
}

public function main() {
    int c = 0;
    int i = 42;
    while c < 42 {
        if isPrime(i) {
            c += 1;
            io:println(commatize(c).padStart(2), ": ", commatize(i).padStart(18));
            i = 2 * i - 1;
        }
        i += 1;
    }
}
