def  Size = 25000;
char Sieve(1+Size);
int  I, Count, Ludic;
[\Make sieve for Ludic numbers
for I:= 1 to Size do Sieve(I):= true;
Ludic:= 2;
loop    [I:= Ludic;  Count:= Ludic;
        loop    [repeat I:= I+1;
                    if I > Size then quit;
                until   Sieve(I);
                Count:= Count-1;
                if Count = 0 then
                    [Sieve(I):= false;
                    Count:= Ludic;
                    ];
                ];
        repeat  Ludic:= Ludic+1;
                if Ludic > Size then quit;
        until   Sieve(Ludic);
        ];
\Show first 25 Ludic numbers
Count:= 0;  I:= 1;
loop    [if Sieve(I) then
            [IntOut(0, I);  ChOut(0, ^ );
            Count:= Count+1;
            if Count >= 25 then quit;
            ];
        I:= I+1;
        ];
CrLf(0);
\Show how many Ludic numbers are <= 1000
Count:= 0;
for I:= 1 to 1000 do
    if Sieve(I) then Count:= Count+1;
IntOut(0, Count);
CrLf(0);
\Show Ludic numbers from 2000 to 2005
Count:= 0;  I:= 1;
loop    [if Sieve(I) then
            [Count:= Count+1;
            if Count >= 2000 then
                [IntOut(0, I);  ChOut(0, ^ )];
            if Count >= 2005 then quit;
            ];
        I:= I+1;
        ];
CrLf(0);
\Show triplets of Ludic numbers < 250
for I:= 1 to 250-1-6 do
    if Sieve(I) & Sieve(I+2) & Sieve(I+6) then
        [ChOut(0, ^();
        IntOut(0, I);  ChOut(0, ^ );
        IntOut(0, I+2);  ChOut(0, ^ );
        IntOut(0, I+6);  Text(0, ") ");
        ];
CrLf(0);
]
