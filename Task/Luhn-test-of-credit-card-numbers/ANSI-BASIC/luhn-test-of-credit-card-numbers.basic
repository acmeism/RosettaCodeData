100 REM Luhn test of credit card numbers
110 DATA "49927398716", "49927398717", "1234567812345678", "1234567812345670"
120 DECLARE EXTERNAL FUNCTION LuhnTest
130 FOR J = 1 TO 4
140    READ C$
150    PRINT C$;
160    IF LuhnTest(C$) = 1 THEN PRINT " is valid." ELSE PRINT " is invalid."
170 NEXT J
180 END
190 REM
200 EXTERNAL FUNCTION LuhnTest(C$)
210 LET S = 0
220 FOR I = LEN(C$) TO 1 STEP -2
230    LET S = S + VAL(C$(I:I))
240 NEXT I
250 FOR I = LEN(C$) - 1 TO 1 STEP -2
260    LET B = VAL(C$(I:I)) * 2
270    IF B >= 10 THEN LET B = B - 9
280    LET S = S + B
290 NEXT I
300 IF MOD(S, 10) = 0 THEN LET LuhnTest = 1 ELSE LET LuhnTest = 0
310 END FUNCTION
