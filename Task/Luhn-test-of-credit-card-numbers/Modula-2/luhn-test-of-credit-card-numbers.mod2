MODULE LuhnTestCreditCard;
(* Luhn test of credit card numbers *)

FROM STextIO IMPORT
  WriteString, WriteLn;

CONST
  MaxLen = 16;

TYPE
  TCardNum = ARRAY[0 .. MaxLen] OF CHAR;
  TCards = ARRAY[0 .. 3] OF TCardNum;

CONST
  Cards = TCards{"49927398716", "49927398717",
                 "1234567812345678", "1234567812345670"};
VAR
  J: CARDINAL;

PROCEDURE LuhnTest(C: ARRAY OF CHAR): BOOLEAN;
VAR
  S, I, B, LastIndex: CARDINAL;
BEGIN
  S := 0;
  LastIndex := LENGTH(C) - 1;
  FOR I := LastIndex TO 0 BY -2 DO
    S := S + (ORD(C[I]) - ORD("0"))
  END;
  FOR I := LastIndex - 1 TO 0 BY -2 DO
    B := (ORD(C[I]) - ORD("0")) * 2;
    IF B >= 10 THEN
      B := B - 9
    END;
    S := S + B
  END;
  RETURN S MOD 10 = 0
END LuhnTest;

BEGIN
  FOR J := 0 TO 3 DO
    WriteString(Cards[J]);
    IF LuhnTest(Cards[J]) THEN
      WriteString(" is valid.")
    ELSE
      WriteString(" is invalid.")
    END;
    WriteLn;
  END;
END LuhnTestCreditCard.
