test: procedure options (main);

   declare (cardnumber, rcn) character (20) varying;
   declare (i, k, s1, s2) fixed binary;

   get edit (cardnumber) (L);
   cardnumber = trim(cardnumber);
   rcn = reverse(cardnumber);
   s1, s2 = 0;
   /* Sum the odd-numbered digits */
   do i = 1 to length(rcn) by 2;
      s1 = s1 + substr(rcn, i, 1);
   end;
   /* Twice the even-numbered digits. */
   do i = 2 to length(rcn) by 2;
      k = 2 * substr(rcn, i, 1);
      s2 = s2 + mod(k,10) + trunc(k/10);
   end;
   if mod(s1 + s2, 10) = 0 then
      put skip edit (cardnumber, ' passes the Luhn test' )(a);
   else
      put skip edit (cardnumber, ' does not pass the Luhn test' )(a);
   put skip list (s1 + s2);
end test;
