string 0;               \use zero-terminated strings

func    Valid(Str);     \Return 'true' if digits in Str pass Luhn test
char    Str;
int     Len, Sum, I, Dig;
[Len:= 0;               \find length of Str
while Str(Len) do Len:= Len+1;
Sum:= 0;                \sum even and odd digits
for I:= 0 to Len-1 do   \(no need to reverse)
        [if (I xor Len) and 1 then
                Sum:= Sum + Str(I) - ^0
        else    [Dig:= Str(I) - ^0;
                Dig:= Dig*2;
                Sum:= Sum + Dig/10 + rem(0);
                ];
        ];
return rem(Sum/10) = 0;
];

int     Luhn, N;
[Luhn:= ["49927398716",
         "49927398717",
         "1234567812345678",
         "1234567812345670"];
for N:= 0 to 4-1 do
        [Text(0, Luhn(N));
         Text(0, if Valid(Luhn(N))
                then " is valid"
                else " is not valid");
        CrLf(0);
        ];
]
