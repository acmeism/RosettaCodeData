var BN=Import("zklBigNum");

   // 192-->"887\01675\07436\013783\0..." ~60k bytes
fcn lychrel(n,returnListof=False){ n=BN(n); //-->Bool|Data(==StringBuf)
   sink:=(if(returnListof) Data(0,String) else Void);
   nls:=(500).pump(sink,'wrap(){
      ns:=n.add(BN(n.toString().reverse())).toString();
      if(ns==ns.reverse()) return(Void.Stop,False);  // not a Lychrel number
      ns
   });
   if(nls) if(returnListof) return(sink.mode(Int).howza(2)) else return(True);
   False;
}
fcn isPalindrome(n){ n==n.toString().reverse().toInt() }
fcn findSeeds(lychrels){
   seeds:=List(lychrels[0]);
   foreach n,lnns in ([1..].zip(lychrels[1,*])){ ln,seq:=lnns;
      foreach _,s2 in (seeds){
	 foreach s3 in (seq){
	    if(Void!=(z:=s2.findString(s3)) and 0==s2[z-1]) break(2);
	 }
      }
      fallthrough{ seeds.append(lnns) }
   }
   seeds.apply("get",0)
}
