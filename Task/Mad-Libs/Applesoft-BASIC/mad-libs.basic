 100  DIM L$(100),W$(50),T$(50)
 110  LET M$ =  CHR$ (13)
 120  FOR L = 1 TO 1E9
 130      READ L$(L)
 140      IF  LEN (L$(L)) THEN  NEXT L
 150  FOR I = 1 TO L
 160      LET M = 0
 170      LET N$(0) = ""
 180      FOR J = 1 TO  LEN (L$(I))
 190          LET C$ =  MID$ (L$(I),J,1)
 200          IF C$ = "<" AND  NOT M THEN M = 1:C$ = "":N$(M) = ""
 210          IF C$ = ">" AND M THEN  GOSUB 300:M = 0:C$ = W$(Z)
 220      LET N$(M) = N$(M) + C$
 230      NEXT J
 240      LET L$(I) = N$(0)
 250  NEXT I
 260  FOR I = 1 TO L
 270      PRINT M$L$(I);
 280  NEXT
 290  END
 300  FOR Z = 0 TO T
 310      IF T$(Z) = N$(M) THEN  RETURN
 320  NEXT Z
 330  LET T = Z
 340  LET T$(Z) = N$(M)
 350  PRINT M$"ENTER A "T$(Z);
 360  INPUT "? ";W$(Z)
 370  RETURN
 380  DATA "<NAME> WENT FOR A WALK IN THE PARK. <HE OR SHE>"
 390  DATA "FOUND A <NOUN>. <NAME> DECIDED TO TAKE IT HOME."
 990  DATA
