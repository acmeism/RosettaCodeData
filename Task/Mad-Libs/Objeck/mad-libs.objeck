use Collection, Query.RegEx;

class MadLibs {
  function : Main(args : String[]) ~ Nil {
    input : String;
    if(args->Size() = 1) {
      input := System.IO.Console->ReadLine();
    }
    else {
      input := "<name> went for a walk in the park. <he or she> found a <noun>. <name> decided to take it home.";
    };

    replace_sets := Hash->New()<String, String>;
    results := Query.RegEx.RegEx->New("\\<(\\w| )*\\>")->Find(input)<Result>;
    each(result in results) {
      temp_word := input->SubString(result->GetStart(), result->GetLength());
      if(<>replace_sets->Has(temp_word)) {
        "What word would you like to use to replace '{$temp_word}'? "->Print();
        replace_word := System.IO.Console->ReadLine();
        replace_sets->Insert(temp_word, replace_word)<String, String>;
      };
    };

    replace_pairs := replace_sets->GetKeyValues()<Pair<String, String>>;
    each(replace_pair in replace_pairs) {
      temp_word := replace_pair->GetFirst();
      replace_word := replace_pair->GetSecond();

      input := input->ReplaceAll(temp_word, replace_word);
    }

    input->PrintLine();
  }
}
