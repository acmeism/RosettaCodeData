import system'routines;
import extensions;
import extensions'routines;

MagicSquareDoublyEven(int n)
{
    if(n < 4 || n.mod(4) != 0)
        { InvalidArgumentException.new("base must be a positive multiple of 4").raise() };

    int bits := 09669h;
    int size := n * n;
    int mult := n / 4;

    var result := IntMatrix.allocate(n,n);
    int i := 0;
    for (int r := 0; r < n; r += 1)
    {
        for(int c := 0; c < n; c += 1; i += 1)
        {
            int bitPos := c / mult + (r / mult) * 4;

            result[r][c] := ((bits & (1 $shl bitPos)) != 0).iif(i+1,size - i)
        }
    };

    ^ result
}

public program()
{
    int n := 8;

    console.printLine(MagicSquareDoublyEven(n));

    console.printLine().printLine("Magic constant: ",(n * n + 1) * n / 2)
}
