mata
function magic(n) {
    if (mod(n,2)==1) {
        p = (n+1)/2-2
        a = J(n,n,.)
        for (i=1; i<=n; i++) {
            for (j=1; j<=n; j++) {
                a[i,j] = n*mod(i+j+p,n)+mod(i+2*j-2,n)+1
            }
        }
    } else if (mod(n,4)==0) {
        p = n^2+n+1
        a = J(n,n,.)
        for (i=1; i<=n; i++) {
            for (j=1; j<=n; j++) {
                a[i,j] = mod(floor(i/2)-floor(j/2),2)==0 ? p-n*i-j : n*(i-1)+j
            }
        }
    } else {
        p = n/2
        q = p*p
        k = (n-2)/4+1
        a = magic(p)
        a = a,a:+2*q\a:+3*q,a:+q
        i = 1..p
        j = 1..k-1
        if (k>2) j = j,n-k+3..n
        m = a[i,j]; a[i,j] = a[i:+p,j]; a[i:+p,j] = m
        j = 1,k
        m = a[k,j]; a[k,j] = a[k:+p,j]; a[k:+p,j] = m
    }
    return(a)
}
end
