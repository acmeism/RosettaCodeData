arraybase 1
global m

call magicSquare(5)
call magicSquare(17)
end

subroutine magicSquare(n)
	redim m(n,n)
	inc = 1
	cont = 1
	row = 1
	col = (n+1) / 2
	while cont <= n*n
		m[row,col] = cont
		cont += 1
		if inc < n then
			inc += 1
			row -= 1
			col += 1
			if row <> 0 then
				if col > n then col = 1
			else
				row = n
			end if
		else
			inc = 1
			row += 1
		end if
	end while
	call printSquare(n)
end subroutine

subroutine printSquare(n)
	#Arbitrary limit to fit width of A4 paper
	if n < 23 then
		print
		print n; " x "; n; " Magic Square --- ";
		print "Magic constant is "; int((n*n+1)/2*n)
		for row = 1 to n
			for col = 1 to n
				print rjust(string(m[row,col]),4);
			next col
			#print
			print
		next row
	else
		print "Magic Square will not fit on one sheet of paper."
	end if
end subroutine
