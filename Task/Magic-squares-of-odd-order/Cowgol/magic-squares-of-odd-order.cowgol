include "cowgol.coh";

sub magic(n: uint16) is
    sub f(x: uint16, y: uint16): (r: uint16) is
        r := (x + y*2 + 1) % n;
    end sub;

    sub cell(x: uint16, y: uint16): (c: uint16) is
        c := f(n-x-1, y)*n + f(x, y) + 1;
    end sub;

    var y: uint16 := 0;
    while y < n loop
        var x: uint16 := 0;
        loop
            print_i16(cell(x, y));
            x := x + 1;
            if x == n then
                print_nl();
                break;
            else
                print_char('\t');
            end if;
        end loop;
        y := y + 1;
    end loop;
    print_nl();
end sub;

var n: uint16 := 1;
while n <= 7 loop
    print("Magic square of order ");
    print_i16(n);
    print(" with constant ");
    print_i16((n*n+1)/2*n);
    print(":\n");
    magic(n);
    n := n + 2;
end loop;
