magic: procedure options (main);  /* 18 April 2014 */
   declare n fixed binary;

   put skip list ('What is the order of the magic square?');
   get list (n);
   if n < 3 | iand(n, 1) = 0 then
      do; put skip list ('The value is out of range'); stop; end;
   put skip list ('The order is ' || trim(n));

   begin;
      declare m(n, n) fixed, (i, j, k) fixed binary;

      on subrg snap put data (i, j, k);
      m = 0;
      i = 1; j = (n+1)/2;

      do k = 1 to n*n;
         if m(i,j) = 0 then
            m(i,j) = k;
         else
            do;
               i = i + 2; j = j + 1;
               if i > n then i = mod(i,n);
               if j > n then j = 1;
               m(i,j) = k;
            end;
         i = i - 1; j = j - 1;
         if i < 1 then i = n;
         if j < 1 then j = n;
      end;

      do i = 1 to n;
         put skip edit (m(i, *)) (f(4));
      end;

      put skip list ('The magic number is' || sum(m(1,*)));
   end;
end magic;
