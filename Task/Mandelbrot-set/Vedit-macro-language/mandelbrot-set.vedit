#1 =-21000              // left edge = -2.1
#2 = 15000              // right edge = 1.5
#3 = 15000              // top edge = 1.5
#4 =-15000              // bottom edge = -1.5
#5 = 200                // max iteration depth
#6 = 350                // x step size
#7 = 750                // y step size

Buf_Switch(Buf_Free)
for (#12 = #3; #12 > #4; #12 -= #7) {                   // y0
    for (#11 = #1; #11 < #2; #11 += #6) {               // x0
        #22 = 0                                         // y
        #21 = 0                                         // x
        #9 = ' '                                        // char to be displayed
        for (#15 = 0; #15 < #5; #15++) {                // iteration
            #31 = (#21/10 * #21) / 1000                 // x*x
            #32 = (#22/10 * #22) / 1000                 // y*y
            if (#31 + #32 > 40000) {
                #9 = '0' + #15                          // print digit 0...9
                if (#15 > 9) {                          // if iteration count > 9,
                    #9 = '@'                            //  print '@'
                }
                break
            }
            #33 = #31 - #32 + #11                       // temp = x*x - y*y + x0
            #22 = (#21/10 * #22) / 500 + #12            // y = 2*x*y + y0
            #21 = #33                                   // x = temp
        }
        Ins_Char(#9)
    }
    Ins_Newline
}
BOF
