DECLARE FUNCTION IntToStr$ (num AS LONG, ancho AS INTEGER)
DECLARE FUNCTION JulianDate# (y AS LONG, m AS LONG, d AS LONG)
DECLARE FUNCTION g2m$ (dateStr AS STRING)

Dim dates(6) As String
dates(0) = "2004-06-19": dates(1) = "2012-12-18": dates(2) = "2012-12-21"
dates(3) = "2019-01-19": dates(4) = "2019-03-27": dates(5) = "2020-02-29"
dates(6) = "2020-03-01"

Dim i As Integer
For i = 0 To 6
    Print dates(i); " "; g2m$(dates(i))
Next i
'sleep
End

Function IntToStr$ (num As Long, ancho As Integer)
    IntToStr$ = Right$("00" + LTrim$(Str$(num)), ancho)
End Function

Function JulianDate# (y As Long, m As Long, d As Long)
    Dim a As Long
    a = (14 - m) \ 12
    y = y + 4800 - a
    m = m + 12 * a - 3
    JulianDate# = d + ((153 * m + 2) \ 5) + 365 * y + (y \ 4) - (y \ 100) + (y \ 400) - 32045
End Function

Function g2m$ (dateStr As String)
    Dim longDate As String
    Dim roundDate As String
    Dim correlation As Double
    correlation = 584283 'GTM correlation

    Dim longCountDays(4) As Double
    longCountDays(0) = 144000: longCountDays(1) = 7200
    longCountDays(2) = 360: longCountDays(3) = 20: longCountDays(4) = 1

    Dim tzolkinMonths(19) As String
    tzolkinMonths(0) = "Imix'": tzolkinMonths(1) = "Ik'"
    tzolkinMonths(2) = "Ak'bal": tzolkinMonths(3) = "K'an"
    tzolkinMonths(4) = "Chikchan": tzolkinMonths(5) = "Kimi"
    tzolkinMonths(6) = "Manik'": tzolkinMonths(7) = "Lamat"
    tzolkinMonths(8) = "Muluk": tzolkinMonths(9) = "Ok"
    tzolkinMonths(10) = "Chuwen": tzolkinMonths(11) = "Eb"
    tzolkinMonths(12) = "Ben": tzolkinMonths(13) = "Hix"
    tzolkinMonths(14) = "Men": tzolkinMonths(15) = "K'ib'"
    tzolkinMonths(16) = "Kaban": tzolkinMonths(17) = "Etz'nab'"
    tzolkinMonths(18) = "Kawak": tzolkinMonths(19) = "Ajaw"

    Dim haadMonths(18) As String
    haadMonths(0) = "Pop": haadMonths(1) = "Wo'"
    haadMonths(2) = "Sip": haadMonths(3) = "Sotz'"
    haadMonths(4) = "Sek": haadMonths(5) = "Xul"
    haadMonths(6) = "Yaxk'in": haadMonths(7) = "Mol"
    haadMonths(8) = "Ch'en": haadMonths(9) = "Yax"
    haadMonths(10) = "Sak'": haadMonths(11) = "Keh"
    haadMonths(12) = "Mak": haadMonths(13) = "K'ank'in"
    haadMonths(14) = "Muwan": haadMonths(15) = "Pax"
    haadMonths(16) = "K'ayab": haadMonths(17) = "Kumk'u"
    haadMonths(18) = "Wayeb'"

    Dim y As Long, m As Long, d As Long
    y = Val(Mid$(dateStr, 1, 4))
    m = Val(Mid$(dateStr, 6, 2))
    d = Val(Mid$(dateStr, 9, 2))

    Dim julianDays As Double
    julianDays = JulianDate#(y, m, d)

    Dim remainder As Double
    remainder = julianDays - correlation
    Dim longParts(4) As Long

    For i = 0 To 4
        longParts(i) = Int(remainder / longCountDays(i))
        remainder = remainder - (longParts(i) * longCountDays(i))
    Next i

    longDate = ""
    For i = 0 To 4
        If i > 0 Then longDate = longDate + "."
        longDate = longDate + IntToStr$(longParts(i), 2)
    Next i

    Dim tzolkinMonth As Long, tzolkinDay As Long
    Dim haabMonth As Long, haabDayNum As Long
    Dim lordNumber As Long

    tzolkinMonth = Int((julianDays + 16) Mod 20)
    tzolkinDay = Int(((julianDays + 5) Mod 13)) + 1

    haabMonth = Int(((julianDays + 65) Mod 365) / 20)
    haabDayNum = Int(((julianDays + 65) Mod 365) Mod 20)

    Dim haabDay As String
    If haabDayNum = 0 Then
        haabDay = "Chum"
    Else
        haabDay = LTrim$(Str$(haabDayNum))
    End If

    lordNumber = Int((julianDays - correlation) Mod 9)
    If lordNumber = 0 Then lordNumber = 9

    roundDate = LTRIM$(STR$(tzolkinDay)) + " " + _
                tzolkinMonths(tzolkinMonth) + " " + _
                haabDay + " " + _
                haadMonths(haabMonth) + " G" + _
                LTRIM$(STR$(lordNumber))

    g2m$ = longDate + " " + roundDate
End Function
