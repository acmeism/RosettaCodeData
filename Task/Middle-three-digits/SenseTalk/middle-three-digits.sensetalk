set inputs to [123, 12345, 1234567, 987654321,
	10001, -10001, -123, -100, 100, -12345,
	1, 2, -1, -10, 2002, -2002, 0]

repeat with each num in inputs
	put num into output
	put middle3digits of num into character 15 of output -- will autofill with "."s
	put output
end repeat

to handle middle3digits of number
	put every occurrence of <digit> in number into digits
	if the number of items in digits is less than 3 then return "Error: too few digits"
	if the number of items in digits is an even number then return "Error: even number of digits"
	repeat while the number of items in digits is greater than 3
		delete the first item of digits
		delete the last item of digits
	end repeat
	return digits joined by empty
end middle3digits
