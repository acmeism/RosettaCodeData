fun isPrime = logic by int n
  if n <= 1 do return false end
  for int i = 2; i <= int!sqrt(n); ++i
    if n % i == 0 do return false end
  end
  return true
end
fun format = text by var n, int max do return " " * (max - length(text!n)) + n end
fun motzkin = List by int n
  List m = int[].with(n)
  m[0] = 1
  m[1] = 1
  for int i = 2; i < m.length; ++i
    m[i] = (m[i - 1] * (2 * i + 1) + m[i - 2] * (3 * i - 3)) / (i + 2)
  end
  return m
end
int n = 0
writeLine(format("n", 2) + format("motzkin(n)", 20) + format("prime", 6))
for each int m in motzkin(42)
  writeLine(format(n, 2) + format(m, 20) + format(isPrime(m), 4))
  ++n
end
