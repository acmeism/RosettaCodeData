*process source xref attributes or(!);
 narn3: Proc Options(main);
 Dcl (i,j,k,l,nn,n,sum) Dec Fixed(15)init(0);
 Dcl s  Char(15) Var;
 dcl t  Char(15);
 Dcl p9(15) Pic'9' Based(addr(t));
 Dcl (ms,msa,ela) Dec Fixed(15);
 Dcl tim Char(12);
 n=30;
 Dcl power(0:9,1:9) Dec Fixed(15);
 Do i=0 To 9;
   Do j=1 To 9;
     Power(i,j)=i**j;
     End;
   End;
 ms=milliseconds();
 Do j=0 By 1 Until(nn=n);
   s=dec2str(j);
   t=s;
   l=length(s);
   sum=power(p9(1),l);
   Do k=2 To l;
     sum=sum+power(p9(k),l);
     If sum>j Then Leave;
     End;
   If sum=j Then Do;
     nn=nn+1;
     msa=milliseconds();
     ela=msa-ms;
     ms=msa;                                /*yyyymmddhhmissmis*/
     tim=translate('ij:kl:mn.opq',datetime(),'abcdefghijklmnopq');
     Put Edit(nn,' narcissistic:',j,ela,tim)
             (Skip,f(9),a,f(12),f(15),x(2),a(12));
     End;
   End;

 dec2str: Proc(x) Returns(char(15) var);
 Dcl x Dec Fixed(15);
 Dcl ds Pic'(14)z9';
 ds=x;
 Return(trim(ds));
 End;

 milliseconds: Proc Returns(Dec Fixed(15));
 Dcl c17 Char(17);
 dcl 1 * Def C17,
      2 * char(8),
      2 hh Pic'99',
      2 mm Pic'99',
      2 ss Pic'99',
      2 ms Pic'999';
 Dcl result Dec Fixed(15);
 c17=datetime();
 result=(((hh*60+mm)*60)+ss)*1000+ms;
 Return(result);
 End;
 End;
