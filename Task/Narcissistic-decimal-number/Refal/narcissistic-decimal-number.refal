$ENTRY Go {
    = <Each (Prout) <Gen 25 Narcissist>>;
};

Gen {
    s.N s.F = <Gen s.N s.F 0>;
    0   s.F s.C = ;
    s.N s.F s.C, <Mu s.F s.C>: {
        True = s.C <Gen <- s.N 1> s.F <+ 1 s.C>>;
        False = <Gen s.N s.F <+ 1 s.C>>;
    };
};

Narcissist {
    s.N, <Digits s.N>: e.D,
         <Lenw e.D>: s.P e.D,
         <Sum <Each (Pow s.P) e.D>>: s.N = True;
    s.N = False;
};

Digits {
    s.N = <Each (Numb) <Symb s.N>>;
};

Each {
    (e.F) = ;
    (e.F) t.I e.X = <Mu e.F t.I> <Each (e.F) e.X>;
};

Pow {
    0   s.N = 1;
    s.P s.N = <Mul s.N <Pow <- s.P 1> s.N>>;
};

Sum {
    = 0;
    s.N e.X = <+ s.N <Sum e.X>>;
};
