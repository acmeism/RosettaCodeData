100 PROGRAM "Nim.bas"
110 RANDOMIZE
120 CLEAR SCREEN
130 LET TOKENS=12
140 PRINT "Starting with";TOKENS;"tokens.":PRINT
150 DO
160   PRINT "How many tokens will you take? (1-3) ";
170   DO
180     LET K=VAL(INKEY$)
190   LOOP UNTIL K>0 AND K<4
200   LET TOKENS=MAX(TOKENS-K,0):LET G=0
210   PRINT K:PRINT TAB(19);TOKENS;"remainig.":PRINT
220   IF TOKENS>0 THEN
230     LET L=MOD(TOKENS,4)
240     IF L=0 THEN LET L=MIN(RND(3)+1,TOKENS)
250     LET TOKENS=TOKENS-L:LET G=-1
260     PRINT "Computer takes";L;"tokens.";TOKENS;"remaining.":PRINT
270   END IF
280 LOOP WHILE TOKENS>0
290 IF G THEN
300   PRINT "Computer wins!"
310 ELSE
320   PRINT "You win!"
330 END IF
