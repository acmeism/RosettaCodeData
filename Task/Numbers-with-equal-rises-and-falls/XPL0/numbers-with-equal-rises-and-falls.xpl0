func RiseFall(N);       \Return 'true' if N has equal rises and falls
int  N, R, F, D, D0;
[R:= 0;  F:= 0;
N:= N/10;
D0:= rem(0);
while N do
    [N:= N/10;
    D:= rem(0);
    if D > D0 then R:= R+1;
    if D < D0 then F:= F+1;
    D0:= D;
    ];
return R = F;
];

int N, Cnt;
[N:= 1;
Cnt:= 0;
loop    [if RiseFall(N) then
            [Cnt:= Cnt+1;
            if Cnt <= 200 then
                [IntOut(0, N);
                if rem (Cnt/10) = 0 then CrLf(0)
                                    else ChOut(0, 9\tab\);
                ];
            if Cnt = 10_000_000 then
                [Text(0, "10 millionth number is ");
                IntOut(0, N);  CrLf(0);
                quit;
                ];
            ];
        N:= N+1;
        ];
]
