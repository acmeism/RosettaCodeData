100 REM Old Russian measure of length
110 LET UnitsLength = 13
120 DIM Units$(1 TO 13), Convs(1 TO 13)
130 REM DIM Units$(1 TO UnitsLength), Convs(1 TO UnitsLength) allowed in some implementations
140 FOR I = 1 TO UnitsLength
150    READ Units$(I)
160 NEXT I
170 DATA tochka, liniya, dyuim, vershok, piad, fut
180 DATA arshin, sazhen, versta, milia, centimeter, meter, kilometer
190 REM All expressed in centimeters
200 FOR I = 1 TO UnitsLength
210    READ Convs(I)
220 NEXT I
230 DATA 0.0254, 0.254, 2.54, 4.445, 17.78, 30.48
240 DATA 71.12, 213.36, 106680, 746760, 1, 100, 100000
250 DO
260    PRINT "------------"
270    PRINT
280    FOR I = 1 TO UnitsLength
290       PRINT USING "## ": I;
300       PRINT Units$(I)
310    NEXT I
320    PRINT
330    DO
340       INPUT PROMPT "Please choose a unit 1 to " & STR$(UnitsLength) & "   :": Unit
350    LOOP UNTIL (Unit >= 1) AND (Unit <= UnitsLength)
360    DO
370       INPUT PROMPT "Now enter a value in that unit :": Value
380    LOOP UNTIL Value > 0
390    PRINT
400    PRINT "The equivalent in the remaining units is:"
410    PRINT
420    FOR I = 1 TO UnitsLength
430       IF I <> Unit THEN
440          PRINT USING "#######.######## ": Value * Convs(Unit) / Convs(I);
450          PRINT Units$(I); "(s)"
460       END IF
470    NEXT I
480    PRINT
490    DO
500       INPUT PROMPT "Do another one y/n :": YN$
510       LET YN$ = LCASE$(YN$)
520    LOOP UNTIL (YN$ = "y") OR (YN$ = "n")
530    PRINT
540 LOOP UNTIL YN$ = "n"
550 PRINT "End of story"
560 END
