100 REM Old Russian measure of length
110 MAX.INDEX = 12
120 DIM UNITS$(MAX.INDEX), CONVS(MAX.INDEX)
130 DATA tochka, liniya, dyuim, vershok, piad, fut
140 DATA arshin, sazhen, versta, milia, centimeter, meter, kilometer
150 DATA 0.0254, 0.254, 2.54, 4.445, 17.78, 30.48
160 DATA 71.12, 213.36, 106680, 746760, 1, 100, 100000
170 FOR I = 0 TO MAX.INDEX
180  READ UNITS$(I)
190 NEXT I
200 FOR I = 0 TO MAX.INDEX
210  READ CONVS(I)
220 NEXT I
230 FOR I = 0 TO MAX.INDEX
240  PRINT USING "## "; I + 1;
250  PRINT UNITS$(I)
260 NEXT I
270 PRINT
280 PRINT "Please choose a unit 1 to"; MAX.INDEX + 1;: INPUT " :", UNIT: IF (UNIT < 1) OR (UNIT > MAX.INDEX + 1) THEN 280
290 UNIT = UNIT - 1
300 INPUT "Now enter a value in that unit:", VALUE: IF VALUE <= 0 THEN 300
310 PRINT
320 PRINT "The equivalent in the remaining units is:"
330 PRINT
340 FOR I = 0 TO MAX.INDEX
350  IF I <> UNIT THEN PRINT USING "#######.######## "; VALUE * CONVS(UNIT) / CONVS(I);: PRINT UNITS$(I); "(s)"
360 NEXT I
370 PRINT
380  INPUT "Do another one y/n"; YN$
390  IF UN$ <> "" THEN YN$ = CHR$(ASC(LEFT$(YN$, 1)) OR 32) ' GW-BASIC does not have LCASE$
400 IF (YN$ <> "y") AND (YN$ <> "n") THEN 380
410 PRINT
420 IF YN$ <> "n" THEN 230
430 PRINT "End of story."
440 END
