10 REM Old Russian measure of length
20 M=12
30 DIM U$(12),C(12)
40 DATA tochka,liniya,dyuim,vershok,piad,fut
50 DATA arshin,sazhen,versta,milia,centimeter
60 DATA meter,kilometer
70 DATA 0.0254,0.254,2.54,4.445,17.78,30.48
80 DATA 71.12,213.36,106680,746760,1,100,100000
90 FOR I=0 TO M:READ U$(I):NEXT I
100 FOR I=0 TO M:READ C(I):NEXT I
110 FOR I=0 TO M:PRINT I+1;U$(I):NEXT I
120 PRINT "Please choose a unit 1 to";M+1;
130 INPUT IU
140 IF (IU<1) OR (IU>M+1) GOTO 120
150 IU=IU-1
160 INPUT "Now enter a value in that unit";V
170 IF V<=0 GOTO 160
180 CLS
190 PRINT V;U$(IU);"(s) in the remaining ";
200 PRINT "units is:"
210 FOR I=0 TO M
220 IF I=IU GOTO 240
230 PRINT V*C(IU)/C(I);U$(I);"(s)"
240 NEXT I
250 INPUT "Do another one y/n";YN$
260 IF YN$="" GOTO 250
270 YN$=CHR$(ASC(LEFT$(YN$,1)) OR 32)
280 IF (YN$<>"n") AND (YN$<>"y") GOTO 250
290 IF YN$<>"n" GOTO 110
300 PRINT "End of story."
310 END
