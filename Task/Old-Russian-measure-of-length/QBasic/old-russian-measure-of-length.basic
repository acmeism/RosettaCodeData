DIM units(1 TO 13) AS STRING
FOR i = LBOUND(units) TO UBOUND(units)
READ units(i)
NEXT i
DATA "tochka", "liniya", "dyuim", "vershok", "piad", "fut", "arshin", "sazhen", "versta", "milia", "centimeter", "meter", "kilometer"

' all expressed in centimeters
DIM convs(1 TO 13) AS SINGLE
FOR i = 1 TO 13
READ convs(i)
NEXT i
DATA 0.0254, 0.254, 2.54, 4.445, 17.78, 30.48, 71.12, 213.36, 106680, 746760, 1, 100, 100000
DIM unit AS INTEGER
DIM value AS SINGLE
DIM yn AS STRING

DO
    CLS
    PRINT
    FOR i = 1 TO 13
        PRINT USING "## "; i;
        PRINT units(i)
    NEXT i
    PRINT
    DO
        INPUT "Please choose a unit 1 to 13 : ", unit
    LOOP UNTIL unit >= 1 AND unit <= 13
    PRINT
    DO
        INPUT "Now enter a value in that unit : ", value
    LOOP UNTIL value >= 0
    PRINT
    PRINT "The equivalent in the remaining units is : "
    PRINT
    FOR i = 1 TO 13
        IF i <> unit THEN PRINT " "; units(i), " : "; value * convs(unit) / convs(i)
    NEXT i
    PRINT
    DO
        INPUT "Do another one y/n : ", yn
        yn = LCASE$(yn)
    LOOP UNTIL yn = "y" OR yn = "n"
LOOP UNTIL yn = "n"
