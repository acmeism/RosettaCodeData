int  Units, Unit, I, YN;
real Convs, Value;

[Units:= ["tochka", "liniya", "dyuim", "vershok", "piad", "fut",
        "arshin", "sazhen", "versta", "milia",
        "centimeter", "meter", "kilometer"];

Convs:= [0.0254, 0.254, 2.54, 4.445, 17.78, 30.48,
        71.12, 213.36, 106680., 746760.,
        1., 100., 100000.];

loop    [for I:= 0 to 13-1 do
                [if I+1 < 10 then ChOut(0, ^ );  IntOut(0, I+1);
                ChOut(0, ^ );  Text(0, Units(I));  CrLf(0);
                ];
        CrLf(0);
        loop    [Text(0, "Please choose a unit 1 to 13   : ");
                OpenI(0);
                Unit:= IntIn(0);
                if Unit >= 1 and Unit <= 13 then quit;
                ];
        Unit:= Unit-1;
        loop    [Text(0, "Now enter a value in that unit : ");
                OpenI(0);
                Value:= RlIn(0);
                if Value >= 1. then quit;
                ];
        Text(0, "^m^jThe equivalent in the remaining units is:^m^j^m^j");
        Format(7, 8);
        for I:= 0 to 13-1 do
            [if I # Unit then
                [RlOut(0, Value*Convs(Unit)/Convs(I));
                Text(0, " : ");  Text(0, Units(I));  CrLf(0);
                ];
            ];
        CrLf(0);
        YN:= ^ ;
        while YN # ^y and YN # ^n do
                [Text(0, "Do another one y/n : ");
                OpenI(0);
                YN:= ChIn(0) or $20;
                ];
        if YN = ^n then quit;
        CrLf(0);
        ];
]
