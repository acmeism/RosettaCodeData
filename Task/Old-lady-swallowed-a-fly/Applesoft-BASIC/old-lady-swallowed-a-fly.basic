 100  FOR N = 0 TO 7: READ A$(N),E$(N): NEXT N:N = N - 1
 110  FOR C = 1 TO N
 120      PRINT M$M$"THERE WAS AN OLD LADY WHO SWALLOWED A "A$(C);
 130      LET M$ =  CHR$ (13)
 140      IF C > 1 THEN  PRINT M$E$(C);: IF C < N THEN  FOR B = C TO 2 STEP  - 1: PRINT M$A$(0)A$(B)" TO CATCH THE "A$(B - 1) MID$ (",;",1 + (B = 2),1);: NEXT B
 150      IF C < N THEN  PRINT M$E$(1)E$(0);
 160  NEXT C
 170  END
 200  DATA"  SHE SWALLOWED A "," - PERHAPS SHE'LL DIE!"
 210  DATA"FLY","I DON'T KNOW WHY SHE SWALLOWED A FLY"
 220  DATA"SPIDER","THAT WRIGGLED AND JIGGLED AND TICKLED INSIDE HER;"
 230  DATA"BIRD","HOW ABSURD TO SWALLOW A BIRD."
 240  DATA"CAT","FANCY THAT TO SWALLOW A CAT!"
 250  DATA"DOG","WHAT A HOG, TO SWALLOW A DOG;"
 260  DATA"COW","I DON'T KNOW HOW SHE SWALLOWED A COW,"
 270  DATA"HORSE","SHE'S DEAD, OF COURSE!"
