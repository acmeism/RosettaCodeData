1 rem rosetta code
2 rem old lady who swallowed a fly - c128
5 print chr$(147);chr$(14)
10 dim an$(10),ex$(10),mu$(8,3):a=1
15 for i=1 to 8:read an$(i),ex$(i):next i:gosub 2500
20 for c=1 to 8
30   print chr$(147):print "     Old Lady Who Swallowed A Fly     ":print
40   print "I know an old lady who swallowed"
45   print "  a ";an$(c):m$=mu$(c,1):gosub 2000
50   for bc=c to 1 step -1
55     if bc=c or bc<=2 then print ex$(bc):m$=mu$(bc,2):gosub 2000
57     if bc=8 then for t=1 to 2000:next t
60     if bc>1 then print "She swallowed a "an$(bc)" to catch the "an$(bc-1)";"
61     if bc>1 then m$=mu$(bc,3):gosub 2000
65   next bc
70   print "   ... Perhaps she'll die!"
72   m$=mu$(1,3):gosub 2000
75   get k$:if k$="q" then end
77   if k$>"0" and k$<"9" then c=asc(k$)-49:rem jump to animal
80   print:for t=1 to 1000:next t
90 next c
100 end

1000 rem lyrics
1010 data "fly","I don't know why she swallowed a fly..."
1020 data "spider","That wriggled and jiggled and tickled     inside her!"
1030 data "bird","How absurd to swallow a bird!"
1040 data "cat","Imagine that! She swallowed a cat!"
1050 data "dog","What a hog, to swallow a dog!"
1060 data "goat","She just opened her throat and swallowed  a goat!"
1070 data "cow","I don't know how she swallowed a cow!"
1080 data "horse","...She died, of course!"

1100 rem music (optional)
1110 data "icffaffcffaqf","igggqgifeechc","icqdieqf":rem fly
1120 data "icffaffcffaff","ifgggggfecccqc","icffaffcqfiaqf":rem spider
1130 data "icffaffcffaqf","qgigqgifeechc","icffaqficqfiaiff":rem bird
1140 data "icffaffcffaqf","icqgigqgifeechc","icffaqficqfiaqf":rem cat
1150 data "icffaffcffaqf","qgigqgifeechc","icffaqficqfiaqf":rem dog
1160 data "icffaffcffaqf","iccgggqgifeechc","icffaqficqfiaqf":rem goat
1170 data "icffaffcffaqf","igggqgifeechc","icffaqficqfiaqf":rem cow
1180 data "icffaffcffahf","qchdqehf","icffaqficqfiaqf":rem horse

2000 rem note player
2005 play m$
2010 return

2500 rem set up sid
2510 for i=1 to 8:for j=1 to 3:read mu$(i,j):next j:next i
2520 play "v1o4t0u5"
2599 return
