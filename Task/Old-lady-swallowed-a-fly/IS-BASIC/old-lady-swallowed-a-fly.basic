100 PROGRAM "OldLady.bas"
110 DIM A$(7),E$(7)
120 FOR N=0 TO 7
130   READ A$(N),E$(N)
140 NEXT
150 LET M$=CHR$(13):LET L$="There was an old lady who swallowed a "
160 TEXT 80:PRINT L$;A$(1);CHR$(241)
170 FOR C=1 TO 7
180   PRINT M$;M$;L$;A$(C);
190   IF C>1 THEN
200     PRINT ";";M$;E$(C);
210     IF C<N THEN
220       FOR B=C TO 2 STEP-1
230         PRINT M$;A$(0);A$(B);" to catch the ";A$(B-1);",;"(1+ABS(B=2));
240         IF B=3 THEN PRINT M$;"  ";E$(2);
250       NEXT
260     END IF
270   ELSE
280     PRINT ",";
290   END IF
300   IF C<N THEN PRINT M$ E$(1);E$(0);
310 NEXT
320 PRINT M$;M$;L$;"horse...";M$;"She's dead, of course!"
330 DATA "  She swallowed the "," - perhaps she'll die!"
340 DATA fly,I don't know why she swallowed a fly
350 DATA spider,"That wriggled and jiggled and tickled inside her,"
360 DATA bird,"How absurd to swallow a bird!"
370 DATA cat,"Fancy that to swallow a cat!"
380 DATA dog,"What a hog, to swallow a dog!"
390 DATA goat,"Just opened her throat and swallowwed a goat!"
400 DATA cow,"I don't know how she swallowed a cow!"
