procedure main()  #:  There Was An Old Lady Lyrics

verse := table()   # arglists for printf - [1] long asides and [2] terse joiners
verse["bird"]   := [["%s,\nQuite absurd, %s %s;\n",1,2,1],["%s,\n",1]]
verse["cat"]    := [["%s,\nFancy that, %s %s;\n",1,2,1],["%s,\n",1]]
verse["dog"]    := [["%s,\nWhat a hog, %s %s;\n",1,2,1],["%s,\n",1]]
verse["pig"]    := [["%s,\nHer mouth was so big, %s %s;\n",1,2,1],["%s,\n",1]]
verse["goat"]   := [["%s,\nShe just opened her throat, %s %s;\n",1,2,1],["%s,\n",1]]
verse["cow"]    := [["%s,\nI don't know how, %s %s;\n",1,2,1],["%s,\n",1]]
verse["donkey"] := [["%s,\nIt was rather wonky, %s %s;\n",1,2,1],["%s,\n",1]]

                   # just long versions of these
verse["fly"]    := [["%s,\nBut I don't know why %s %s,\nPerhaps she'll die!\n\n",1,2,1]]
verse["spider"] := [["%s,\nThat wriggled and jiggled and tickled inside her;\n",1]]
verse["horse"]  := [["%s...\nShe's dead, of course!\n",1]]

every (f := verse[k := key(verse)][1|2])[i := 1 to *f] do   # fix every printf args
   f[i] := case f[i] of { 1 : k ; 2 : "she swallowed the"; default : f[i]}

zoofilo := []
"fly,spider,bird,cat,dog,pig,goat,cow,donkey,horse," ?   # order
   while push(zoofilo,tab(find(","))) & move(1) do {
      printf("There was an old lady who swallowed a ")
      every critter := !zoofilo do {
         printf!verse[critter,(critter == (zoofilo[1] | "spider" | "fly"),1)|2]
         if critter == "horse" then stop()               # dead
         printf("She swallowed the %s to catch the ","fly" ~== critter)
         }
      }
end

link printf
