'[RC] Old lady swallowed a fly
'http://rosettacode.org/wiki/Old_lady_swallowed_a_fly
'lyrics
'http://www.timmyabell.com/music/lyrics/ol/oldlady.htm

animalList$="fly spider bird cat dog goat cow horse"
reason$(1)="I don't know why she swallowed the fly,"
reason$(2)="That wriggled and jiggled and tickled inside her."
reason$(3)="How absurd to swallow a bird!"
reason$(4)="Imagine that, to swallow a cat!"
reason$(5)="My, what a hog, to swallow a dog!"
reason$(6)="Just opened her throat and swallowed a goat!"
reason$(7)="I wonder how she swallowed a cow?!"
reason$(8)="She's dead, of course!!"

i=0
while 1
    i=i+1
    animal$ = word$(animalList$, i)
    if animal$ ="" then exit while
    verse$ = "I know an old lady who "
    verse2$ = "swallowed a " +animal$
    print verse$ +verse2$+","
    print reason$(i)
    if i = 8 then end
    '--------------
    animals$=animal$+" "+animals$
    animal2$="*"
    j=1
    while 1
        j=j+1
        animal2$ = word$(animals$, j)
        if animal2$ ="" then exit while
        reason$="She swallowed the "+animal$+" to catch the "+animal2$+","
        animal$ = animal2$
        print reason$
        if animal2$ = "fly" then print reason$(1)
        if animal2$ = "spider" then print reason$(2)
    wend
    '--------------
    print "I guess she'll die."
    print
wend

end
