  [ [ table
      [ say "fly" ]
      [ say "spider" ]
      [ say "bird" ]
      [ say "cat" ]
      [ say "dog" ]
      [ say "goat" ]
      [ say "cow" ] ] do ]                                          is beast       ( n -->   )

  [ [ table
      [ say "I don't know why she swallowed a fly."          ]
      [ say "That wiggled and jiggled and tickled inside her." ]
      [ say "How absurd to swallow a bird." ]
      [ say "Imagine that, she swallowed a cat!" ]
      [ say "What a hog to swallow a dog." ]
      [ say "She just opened her throat and swallowed that goat." ]
      [ say "I don't know how she swallowed a cow." ] ] do ]        is observation ( n -->   )

  [ say "There was an old lady who swallowed a " dup beast
    dup if [ cr dup observation ]
    times
      [ cr say "She swallowed the " i 1+ beast
        say " to catch the " i beast ]
    cr 0 observation
    cr say "Perhaps she'll die." ]                                  is verse       ( n -->   )

  [ say "There was an old lady who swallowed a horse."
    cr say "She's dead, of course." ]                               is coda        (   -->   )

  [ 7 times [ i^ verse cr cr ] coda ]                               is song        (   -->   )

  song
