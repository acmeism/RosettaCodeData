$ include "seed7_05.s7i";

const array array string: verses is [] (
    [] ("fly", "I don't know why she swallowed the fly."),
    [] ("spider", "That wriggled and jiggled and tickled inside her."),
    [] ("bird", "Quite absurd. To swallow a bird."),
    [] ("cat", "Fancy that. To swallow a cat."),
    [] ("dog", "What a hog. To swallow a dog."),
    [] ("pig", "Her mouth was so big. To swallow a pig."),
    [] ("goat", "She just opened her throat. To swallow a goat."),
    [] ("cow", "I don't know how. To swallow a cow."),
    [] ("donkey", "It was rather wonky. To swallow a donkey."),
    [] ("horse", "She's dead, of course!"));

const proc: main is func
  local
    var integer: verseNumber is 0;
    var integer: animal is 0;
  begin
    for key verseNumber range verses do
      writeln("There was an old lady who swallowed a " <& verses[verseNumber][1] <& ",");
      writeln(verses[verseNumber][2]);
      if verseNumber <> length(verses) then
        for animal range verseNumber downto 2 do
          writeln("She swallowed the " <& verses[animal][1] <& " to catch the " <& verses[pred(animal)][1] <& ",");
        end for;
        if verseNumber <> 1 then
          writeln(verses[1][2]);
        end if;
        writeln("Perhaps she'll die!");
        writeln;
      end if;
    end for;
  end func;
