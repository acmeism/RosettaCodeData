F order_disjoint_list_items(&data, items)
   [Int] itemindices
   L(item) Set(items)
      V itemcount = items.count(item)
      V lastindex = [-1]
      L(i) 0 .< itemcount
         lastindex.append(data.index(item, lastindex.last + 1))
      itemindices [+]= lastindex[1..]
   itemindices.sort()
   L(index, item) zip(itemindices, items)
      data[index] = item

F slist(s)
   R Array(s).map(String)

F tostring(l)
   R ‘'’l.join(‘ ’)‘'’

L(data, items) [(‘the cat sat on the mat’.split(‘ ’), (‘mat cat’).split(‘ ’)),
                (‘the cat sat on the mat’.split(‘ ’), (‘cat mat’).split(‘ ’)),
                (slist(‘ABCABCABC’), slist(‘CACA’)),
                (slist(‘ABCABDABE’), slist(‘EADA’)),
                (slist(‘AB’), slist(‘B’)),
                (slist(‘AB’), slist(‘BA’)),
                (slist(‘ABBA’), slist(‘BA’)),
                (slist(‘’), slist(‘’)),
                (slist(‘A’), slist(‘A’)),
                (slist(‘AB’), slist(‘’)),
                (slist(‘ABBA’), slist(‘AB’)),
                (slist(‘ABAB’), slist(‘AB’)),
                (slist(‘ABAB’), slist(‘BABA’)),
                (slist(‘ABCCBA’), slist(‘ACAC’)),
                (slist(‘ABCCBA’), slist(‘CACA’))]
   print(‘Data M: #<24 Order N: #<9’.format(tostring(data), tostring(items)), end' ‘ ’)
   order_disjoint_list_items(&data, items)
   print(‘-> M' #.’.format(tostring(data)))
