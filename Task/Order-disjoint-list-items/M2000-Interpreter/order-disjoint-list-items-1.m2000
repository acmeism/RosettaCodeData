Function Checkit$ {
	Document Ret$
	Flush
	Data "the cat sat on the mat", "mat cat"
	Data "the cat sat on the mat","cat mat"'
	Data "A B C A B C A B C", "C A C A"
	Data "A B C A B D A B E", "E A D A"
	Data "A B", "B"
	Data "A B", "B A"
	Data "A B B A","B A"
	Dim A$()
	while not empty
		read m$, n$
		A$()=piece$(m$, " ")
		Let w=piece$(n$, " ")
		Let z=A$()
		x=each(w)
		while x
			y=z#pos(array$(x))
			if y>-1 then a$(y)=""
		end while
		p=0
		x=each(w)
		while x
			while a$(p)<>"" : p++: end while
			a$(p)=array$(x)
		end while
		ret$=m$+" | "+n$+" -> "+z#str$()+{
		}
	end while
	=ret$
}
Report Checkit$()
Clipboard  Checkit$()
