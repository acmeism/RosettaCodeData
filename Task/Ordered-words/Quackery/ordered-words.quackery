  [ - -1 1 clamp 1+ ]'[ swap peek do ]     is <=>      ( n n -->  )

  [ true swap
    behead swap witheach
      [ tuck > if
          [ dip not conclude ] ]
      drop ]                               is ordered (   [ --> b )

  [ stack ]                                is largest (   [ --> s )

  [ 1 largest put
    [] swap witheach
      [ dup size
        largest share <=>
          [ drop
            [ dup ordered iff
                [ nested join ]
              else drop ]
            [ dup ordered iff
                [ dup size largest replace
                  nip nested ]
              else drop ] ] ]
     largest release ]                     is task    (   [ --> [ )

  $ 'unixdict.txt' sharefile drop nest$
  task
  witheach [ echo$ sp ]
