File_Open("unixdict.txt", BROWSE)
#1 = 2                                  // length of longest word found
Repeat (ALL) {
    #2 = EOL_Pos-Cur_Pos                // length of this word
    if (#2 >= #1) {
        #3 = 1                          // flag: is ordered word
        Char(1)
        While (!At_EOL) {
            if (Cur_Char < Cur_Char(-1)) {
                #3 = 0                  // not an ordered word
                break
            }
            Char(1)
        }
        if (#3) {                       // ordered word found
            if (#2 > #1) {              // new longer word found
                #1 = #2
                Reg_Empty(10)           // clear list
            }
            BOL Reg_Copy(10,1,APPEND)   // add word to list
        }
    }
    Line(1,ERRBREAK)                    // next word
}
Buf_Quit(OK)                            // close file
Reg_Type(10)                            // display results
