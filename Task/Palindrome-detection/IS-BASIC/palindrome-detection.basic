100 PROGRAM "Palindr.bas"
110 LINE INPUT PROMPT "Text: ":TX$
120 PRINT """";TX$;""" is ";
130 IF PALIND(TX$) THEN
140   PRINT "a palindrome."
150 ELSE
160   PRINT "not a palindrome."
170 END IF
180 DEF TRIM$(TX$)
190   LET T$=""
200   FOR I=1 TO LEN(TX$)
210     IF TX$(I)>="A" AND TX$(I)<="Z" THEN LET T$=T$&TX$(I)
220   NEXT
230   LET TRIM$=T$
240 END DEF
250 DEF PALIND(TX$)
260   LET PALIND=-1:LET TX$=TRIM$(UCASE$(TX$))
270   FOR I=1 TO LEN(TX$)/2
280     IF TX$(I)<>TX$(LEN(TX$)-I+1) THEN LET PALIND=0:EXIT FOR
290   NEXT
300 END DEF
