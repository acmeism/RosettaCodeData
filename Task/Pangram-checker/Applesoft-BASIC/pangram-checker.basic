 100  P$ = "11111111111111111111111111"
 110  FOR Q = 1 TO 3
 120      READ S$
 130      GOSUB 200"IS PANGRAM?
 140      PRINT  MID$ ("NO  YES ",P * 4 + 1,4)S$
 150  NEXT Q
 160  END
 170  DATA"THE QUICK BROWN FOX JUMPS OVER THE LAZY DOG."
 180  DATA"THE QUICK BROWN FOX JUMPED OVER THE LAZY DOG."
 190  DATA"THE FIVE BOXING WIZARDS JUMP QUICKLY."
 200  P = 0:L =  LEN (S$): IF  NOT L THEN  RETURN
 210  F$ = "00000000000000000000000000"
 220  FOR I = 1 TO L
 230      C =  ASC ( MID$ (S$,I,1)):C = C - 32 * (C > 95): IF C > 64 AND C < 91 THEN J = C - 64:F$ =  MID$ (F$,1,J - 1) + "1" +  MID$ (F$,J + 1):P = F$ = P$: IF P THEN  RETURN
 240  NEXT I
 250  RETURN
