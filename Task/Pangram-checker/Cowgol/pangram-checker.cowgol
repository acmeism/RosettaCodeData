include "cowgol.coh";

sub pangram(str: [uint8]): (r: uint8) is
    var letters: uint8[26];
    MemZero(&letters[0], 26);

    loop
        var chr := [str];
        if chr == 0 then break; end if;
        str := @next str;
        chr := (chr | 32) - 'a';
        if chr >= 26 then continue; end if;
        letters[chr] := letters[chr] | 1;
    end loop;

    r := 1;
    chr := 0;
    while chr < 26 loop
        r := r & letters[chr];
        if r == 0 then break; end if;
        chr := chr + 1;
    end loop;
end sub;

var yesno: [uint8][] := {": no\n", ": yes\n"};
var test: [uint8][] := {
    "The quick brown fox jumps over the lazy dog.",
    "The five boxing wizards dump quickly."
};

var i: @indexof test := 0;
while i < @sizeof test loop
    print(test[i]);
    print(yesno[pangram(test[i])]);
    i := i + 1;
end loop;
