proc nonrec pangram(*char s) bool:
    ulong letters;
    char c;
    byte b;
    byte A = pretend('a', byte);
    byte Z = pretend('z', byte);

    letters := 0L0;
    while
        c := s*;
        s := s + 1;
        c /= '\e'
    do
        b := pretend(c, byte) | 32;
        if b >= A and b <= Z then
            letters := letters | 0L1 << (b-A)
        fi
    od;
    letters = 0x3FFFFFF
corp

proc nonrec test(*char s) void:
    writeln("\"", s, "\": ",
            if pangram(s) then "yes" else "no" fi)
corp

proc nonrec main() void:
    test("The quick brown fox jumps over the lazy dog.");
    test("The five boxing wizards jump quickly.");
    test("Not a pangram")
corp
