 10 IF RND>=.5 THEN GOTO 100
 20 PRINT "YOU PICK FIRST."
 30 INPUT P$
 40 PRINT "YOU CHOSE ";P$
 50 LET C$="H"
 60 IF P$(2)="H" THEN LET C$="T"
 70 LET C$=C$+P$( TO 2)
 80 PRINT "I CHOOSE  ";C$
 90 GOTO 190
100 PRINT "I PICK FIRST."
110 LET C$=""
120 FOR I=1 TO 3
130 LET C$=C$+"H"
140 IF RND>=.5 THEN LET C$(I)="T"
150 NEXT I
160 PRINT "I CHOOSE  ";C$
170 INPUT P$
180 PRINT "YOU CHOSE ";P$
190 LET G$=""
200 IF RND>=.5 THEN GOTO 230
210 LET G$=G$+"H"
220 GOTO 240
230 LET G$=G$+"T"
240 PRINT AT 5,0;G$
250 IF LEN G$<3 THEN GOTO 200
260 IF G$(LEN G$-2 TO )=P$ THEN GOTO 290
270 IF G$(LEN G$-2 TO )=C$ THEN GOTO 310
280 GOTO 200
290 PRINT "YOU WIN"
300 STOP
310 PRINT "I WIN"
