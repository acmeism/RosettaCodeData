Public Sub Main()

  Dim found As Integer = 0, curr As Integer = 3
  Dim sum As Integer, toti As Integer

  Print "The first 20 perfect totient numbers are:"
  While found < 20
    sum = totient(curr)
    toti = sum
    Do
      toti = totient(toti)
      sum += toti
    Loop While toti <> 1
    If sum = curr Then
      Print sum; ", ";
      found += 1
    End If
    curr += 1
  Wend
  Print Chr(8); Chr(8); " "

End

Function GCD(n As Integer, d As Integer) As Integer

  If d = 0 Then Return n Else Return GCD(d, n Mod d)

End Function

Function Totient(n As Integer) As Integer

  Dim m As Integer, phi As Integer = 0

  For m = 1 To n
    If GCD(m, n) = 1 Then phi += 1
  Next
  Return phi

End Function
