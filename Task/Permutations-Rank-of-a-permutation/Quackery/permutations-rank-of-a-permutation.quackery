  [ 1 swap times [ i 1+ * ] ]   is !          (   n --> n )

  [ [] swap times [ i^ join ] ] is identity   (   n --> [ )

  [ dup identity unrot
    [] unrot 1 - times
      [ i 1+ ! /mod
        dip join ] drop ]       is factoradic ( n n --> [ )

  [ [] unrot witheach
      [ pluck
        rot swap nested join
        swap ]
    join ]                      is inversion  ( [ [ --> [ )

  [ factoradic inversion ]      is rank->perm ( n n --> [ )

  [ [] over size identity
      rot witheach
        [ over find
          dup dip
            [ pluck drop ]
           swap dip join ]
    drop -1 split drop ]        is perm->fdic (   [ --> [ )

  [ 0 swap
    witheach [ + i 1+ * ] ]     is fdic->rank (   [ --> n )

  [ perm->fdic fdic->rank ]     is perm->rank (   [ --> n )

3 ! times
  [ i^
    dup echo say " -> "
    3 rank->perm
    dup echo say " -> "
    perm->rank
    echo cr ]
cr
4 times
  [ 12 ! random
    dup echo say " -> "
    12 rank->perm
    dup echo say " -> "
    perm->rank
    echo cr ]
