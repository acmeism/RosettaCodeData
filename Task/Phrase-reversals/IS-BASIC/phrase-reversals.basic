100 PROGRAM "ReverseS.bas"
110 LET S$="Rosetta Code Pharse Reversal"
120 PRINT S$
130 PRINT REVERSE$(S$)
140 PRINT REVERSEW$(S$)
150 PRINT REVERSEC$(S$)
160 DEF REVERSE$(S$)
170   LET T$=""
180   FOR I=LEN(S$) TO 1 STEP-1
190     LET T$=T$&S$(I)
200   NEXT
210   LET REVERSE$=T$
220 END DEF
230 DEF REVERSEW$(S$)
240   LET T$="":LET PE=LEN(S$)
250   FOR PS=PE TO 1 STEP-1
260     IF PS=1 OR S$(PS)=" " THEN LET T$=T$&" ":LET T$=T$&LTRIM$(S$(PS:PE)):LET PE=PS-1
270   NEXT
280   LET REVERSEW$=LTRIM$(T$)
290 END DEF
300 DEF REVERSEC$(S$)
310   LET T$="":LET PS=1
320   FOR PE=1 TO LEN(S$)
330     IF PE=LEN(S$) OR S$(PE)=" " THEN LET T$=T$&" ":LET T$=T$&REVERSE$(RTRIM$(S$(PS:PE))):LET PS=PE+1
340   NEXT
350   LET REVERSEC$=LTRIM$(T$)
360 END DEF
