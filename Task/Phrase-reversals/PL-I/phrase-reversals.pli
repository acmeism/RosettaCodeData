reverser: procedure options (main);          /* 19 August 2015 */
   declare (phrase, r, word) character (100) varying;
   declare (start, end) fixed binary;

   phrase = 'rosetta code phrase reversal';
   put ('The original phrase is: ' || phrase);

   put skip list ( '1. ' || reverse(phrase) );

   start = 1; r = ''; put skip edit ('2. ') (a);
   do until ( end > length(phrase) );
      end = index(phrase, ' ', start);          /* Find end of the next word.*/
      if end = 0 then end = length(phrase) + 1; /* We're at the last word.   */
      word = substr(phrase, start, end-start);
      put edit ( reverse(word), ' ' ) (a);      /* Append reversed word.     */
      r = word || ' ' || r;                     /* Prepend normal word.      */
      start = end+1;
   end;
   put skip list ('3. ' || r);

end reverser;
