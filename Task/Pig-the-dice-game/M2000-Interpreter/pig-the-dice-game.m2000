Module GamePig {
      Print "Game of Pig"
      dice=-1
      res$=""
      Player1points=0
      Player1sum=0
      Player2points=0
      Player2sum=0
      HaveWin=False
      score()
      \\ for simulation, feed the keyboard buffer with R and H
      simulate$=String$("R", 500)
      For i=1 to 100
            Insert Random(1,Len(simulate$)) simulate$="H"
      Next i
      Keyboard simulate$
      \\ end simulation
      while res$<>"Q" {
            Print "Player 1 turn"
            PlayerTurn(&Player1points, &player1sum)
            if res$="Q" then exit
            Player1sum+=Player1points
            Score()
            Print "Player 2 turn"
            PlayerTurn(&Player2points,&player2sum)
            if res$="Q" then exit
            Player2sum+=Player2points
            Score()
      }
      If HaveWin then {
            Score()
            If Player1Sum>Player2sum then {
                  Print "Player 1 Win"
            } Else Print "Player 2 Win"
      }

      Sub Rolling()
            dice=random(1,6)
            Print "dice=";dice
      End Sub
      Sub PlayOrQuit()
            Print "R -Roling Q -Quit"
            Repeat {
                  res$=Ucase$(Key$)
            } Until Instr("RQ", res$)>0
      End Sub
      Sub PlayAgain()
            Print "R -Roling H -Hold Q -Quit"
            Repeat {
                  res$=Ucase$(Key$)
            } Until Instr("RHQ", res$)>0
      End Sub
      Sub PlayerTurn(&playerpoints, &sum)
            PlayOrQuit()
            If res$="Q" then Exit Sub
            playerpoints=0
            Rolling()
            While dice<>1 and res$="R" {
                  playerpoints+=dice
                  if dice>1 and playerpoints+sum>100 then {
                        sum+=playerpoints
                        HaveWin=True
                        res$="Q"
                  } Else {
                        PlayAgain()
                        if res$="R" then Rolling()
                  }
            }
            if dice=1 then playerpoints=0
      End Sub
      Sub Score()
            Print "Player1 points="; Player1sum
            Print "Player2 points="; Player2sum
      End Sub
}
GamePig
