constant numPlayers = 2,
         maxScore = 100
sequence scores = repeat(0,numPlayers)
printf(1,"\nPig The Dice Game\n\n")
integer points = 0, -- points accumulated in current turn, 0=swap turn
        player = 1  -- start with first player
while true do
    integer roll = rand(6)
    printf(1,"Player %d, your score is %d, you rolled %d. ",{player,scores[player],roll})
    if roll=1 then
        printf(1," Too bad. :(\n")
        points = 0 -- swap turn
    else
        points += roll
        if scores[player]+points>=maxScore then exit end if
        printf(1,"Round score %d. Roll or Hold?",{points})
        integer reply = upper(wait_key())
        printf(1,"%c\n",{reply})
        if reply == 'H' then
            scores[player] += points
            points = 0 -- swap turn
        end if
    end if
    if points=0 then
        player = mod(player,numPlayers) + 1
    end if
end while
printf(1,"\nPlayer %d wins with a score of %d!\n",{player,scores[player]+points})
