  [ stack ]                          is turnscore  ( -->   )
  [ stack 1 ]                        is playernum  ( -->   )

  [ 3 playernum share -
    playernum replace ]              is nextplayer ( -->   )

  [ say "Player "
    playernum share echo
    [ $ " (R)oll or (H)old? " input
      space join
      0 peek upper dup
      char R = iff
        [ drop true ]    done
      char H = iff false done
      say "        "     again ] ]   is choose     ( --> b )

  [ 1 playernum replace
    0 0
    [ 0 turnscore put
      [ choose while
        say "Dice roll is: "
        6 random 1+
        dup echo cr cr
        dup 1 = iff
          [ say "Your turn ends. "
            cr drop
            0 turnscore replace ]
          done
        turnscore tally
        again ]
        turnscore take +
        say "Your score is: "
        dup echo cr
        dup 100 < while
        swap nextplayer
        cr
        again ]
      say "You win." cr
      2drop ]                        is play       ( -->   )
