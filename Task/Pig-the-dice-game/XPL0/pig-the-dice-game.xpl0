include c:\cxpl\codes;          \intrinsic 'code' declarations
integer Player, Die, Points, Score(2);
[Score(0):= 0;  Score(1):= 0;   \starting scores for each player
Player:= 1;                     \second player
repeat  Player:= if Player = 1 then 0 else 1;           \next player
        Points:= 0;                                     \points for current turn
        loop    [Text(0, "Player ");  IntOut(0, Player+1);
                Text(0, " is up. Roll or hold (r/h)? ");
                OpenI(0);       \discard any chars in keyboard buffer (like CR)
                if ChIn(0) = ^h then quit               \default is 'r' to roll
                else    [Die:= Ran(6)+1;                \roll the die
                        Text(0, "You get ");  IntOut(0, Die);  CrLf(0);
                        if Die = 1 then [Points:= 0;  quit];
                        Points:= Points + Die;          \add up points for turn
                        Text(0, "Total points are ");  IntOut(0, Points);
                        Text(0, " for a tentative score of ");
                        IntOut(0, Score(Player)+Points);  CrLf(0);
                        ];
                ];
        Score(Player):= Score(Player) + Points;         \show scores
        Text(0, "Player 1 has ");  IntOut(0, Score(0));
        Text(0, " and player 2 has ");  IntOut(0, Score(1));  CrLf(0);
until   Score(Player) >= 100;
Text(0, "Player ");  IntOut(0, Player+1);  Text(0, " WINS!!!");
]
