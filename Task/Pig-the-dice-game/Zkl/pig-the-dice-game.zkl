const WIN=100, PLAYERS=2;
players,safeScores:=Walker.cycle([0..PLAYERS-1]), PLAYERS.pump(List(),0);
rollDie:=(1).random.fp(7);
yes,player,score,S:=T("","y"),players.next(),0,0;
tally:='wrap(player,score){ w:=safeScores[player]+=score; (w>=WIN) };

while(True){
   print("Player %d: (%d, %d). Rolling? (y/n) ".fmt(player+1,S,score));
   if(yes.holds(ask().strip().toLower())){
      rolled:=rollDie(); println(" Rolled a %d".fmt(rolled));
      if(rolled==1){
	 println(" Bust! You lose %d but keep %d\n".fmt(score,S));
      }else{
	 score+=rolled;
	 if(score + S>=WIN){ tally(player,score); break; }
	 continue;
      }
   }else{
      if(tally(player,score)) break;
      println(" Sticking with %d\n".fmt(safeScores[player]));
   }
   player,score,S=players.next(),0,safeScores[player];
}
println("\n\nPlayer %d wins with a score of %d".fmt(player+1, safeScores[player]));
