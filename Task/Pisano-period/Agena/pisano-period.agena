scope # find the Pisano period of some primes and composites

    local procedure pisano( m :: number ) :: number # returns the Pisano period of m
        local p, c := 0, 1;
        for i from 0 to ( m * m ) - 1 do
            p, c := c, ( p + c ) mod m;
            if p = 0 and c = 1 then return i + 1 fi
        od;
        return 1
    end;

    # returns the Pisano period of p^k or 0 if p is not prime or k < 1
    local procedure pisanoPrime( p :: number, k :: number ) :: number
         return if not numtheory.isprime( p ) or k < 1 then 0 else entier ( p ^ ( k - 1 ) * pisano( p ) ) fi
    end;

    print( "Pisano period of p^2 where p is a prime < 15:" );
    for p to 15 do
        if numtheory.isprime( p ) then printf( " %d:%d", p, pisanoPrime( p, 2 ) ) fi
    od;
    printf( "\nPisano period of primes up to 180, non-primes shown as \"*\":\n" );
    for p to 180 do
        if not numtheory.isprime( p ) then printf( "   *" ) else printf( "%4d", pisanoPrime( p, 1 ) ) fi;
        if p mod 10 = 0 then print() fi
    od;
    print( "\nPisano period of positive integers up to 180:" );
    for n to 180 do
        printf( "%4d", pisano( n ) );
        if n mod 10 = 0 then print() fi
    od

end
