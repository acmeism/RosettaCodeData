100 PROGRAM "PolySp.bas"
110 OPTION ANGLE DEGREES
120 LET CH=2
130 SET VIDEO X 40:SET VIDEO Y 27:SET VIDEO MODE 1:SET VIDEO COLOR 0
140 OPEN #1:"video:"
150 OPEN #2:"video:"
160 WHEN EXCEPTION USE POSERROR
170   FOR ANG=40 TO 150 STEP 2
180     IF CH=2 THEN
190       LET CH=1
200     ELSE
210       LET CH=2
220     END IF
230     CLEAR #CH
240     PLOT #CH:640,468,ANGLE 180;
250     FOR D=12 TO 740 STEP 4
260       PLOT #CH:FORWARD D,RIGHT ANG;
270     NEXT
280     DISPLAY #CH:AT 1 FROM 1 TO 27
290   NEXT
300 END WHEN
310 HANDLER POSERROR
320   LET D=740
330   CONTINUE
340 END HANDLER
