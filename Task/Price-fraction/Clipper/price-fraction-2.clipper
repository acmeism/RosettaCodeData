Procedure Main()
   Local i
   For i := -0.02 to 1.02 STEP 0.03
      ? i, "->", PriceFraction(i), i+0.02, "->", PriceFraction(i+0.02)
   Next
Return


Static Function PriceFraction( nValue )
   Local nResult
   Local n
   // Function is only defined for values 0 to 1.00
   // Return NIL for anything else
   // Table of values {V1, V2} = {Threshhold, Standard value}
   #define TV_THRESHHOLD 1
   #define TV_STD_VALUE  2
   Local aTable := { {0,    NIL },;
                     {0.06, 0.10},;
                     {0.11, 0.18},;
                     {0.16, 0.26},;
                     {0.21, 0.32},;
                     {0.26, 0.38},;
                     {0.31, 0.44},;
                     {0.36, 0.50},;
                     {0.41, 0.54},;
                     {0.46, 0.58},;
                     {0.51, 0.62},;
                     {0.56, 0.66},;
                     {0.61, 0.70},;
                     {0.66, 0.74},;
                     {0.71, 0.78},;
                     {0.76, 0.82},;
                     {0.81, 0.86},;
                     {0.86, 0.90},;
                     {0.91, 0.94},;
                     {0.96, 0.98},;
                     {1.01, 1.00} }
   n := AScan( aTable, {|x| nValue < x[TV_THRESHHOLD] })
   If n > 0
      nResult := aTable[n][TV_STD_VALUE]
   Else
      nResult := NIL
   Endif
Return nResult
