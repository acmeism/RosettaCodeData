use Collection;

class Proper{
  function : Main(args : String[]) ~ Nil {
    for(x := 1; x <= 10; x++;) {
      Print(x, ProperDivs(x));
    };

    x := 0;
    count := 0;

    for(n := 1; n <= 20000; n++;) {
      if(ProperDivs(n)->Size() > count) {
        x := n;
        count := ProperDivs(n)->Size();
      };
    };
    "{$x}: {$count}"->PrintLine();
  }

  function : ProperDivs(n : Int) ~ IntVector {
    divs := IntVector->New();

    if(n = 1) {
      return divs;
    };
    divs->AddBack(1);

    for(x := 2; x < n; x++;) {
      if(n % x = 0) {
        divs->AddBack(x);
      };
    };
    divs->Sort();

    return divs;
  }

  function : Print(x : Int, result : IntVector) ~ Nil {
    "{$x}: "->Print();
    result->ToArray()->ToString()->PrintLine();
  }
}
