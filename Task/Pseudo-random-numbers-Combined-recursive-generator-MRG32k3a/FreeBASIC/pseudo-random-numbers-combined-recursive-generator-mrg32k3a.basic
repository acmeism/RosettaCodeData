Const As Longint a1_1 = 0
Const As Longint a1_2 = 1403580
Const As Longint a1_3 = -810728
Const As Longint m1 = 4294967087  ' 2^32 - 209

Const As Longint a2_1 = 527612
Const As Longint a2_2 = 0
Const As Longint a2_3 = -1370589
Const As Longint m2 = 4294944443  ' 2^32 - 22853

Const d As Double = m1 + 1

Dim Shared As Longint x1(2), x2(2)

Sub seed(seed_state As Longint)
    If seed_state <= 0 Or seed_state >= m1 Then
        Print "Error: Invalid seed state"
        End
    End If
    x1(0) = seed_state : x1(1) = 0 : x1(2) = 0
    x2(0) = seed_state : x2(1) = 0 : x2(2) = 0
End Sub

Function next_int() As Longint
    Dim As Longint x1i = (a1_1 * x1(0) + a1_2 * x1(1) + a1_3 * x1(2)) Mod m1
    If x1i < 0 Then x1i += m1

    Dim As Longint x2i = (a2_1 * x2(0) + a2_2 * x2(1) + a2_3 * x2(2)) Mod m2
    If x2i < 0 Then x2i += m2

    x1(2) = x1(1) : x1(1) = x1(0) : x1(0) = x1i
    x2(2) = x2(1) : x2(1) = x2(0) : x2(0) = x2i

    Dim As Longint z = (x1i - x2i) Mod m1
    If z < 0 Then z += m1

    Return z + 1
End Function

Function next_float() As Double
    Return next_int() / d
End Function

' Main program
Dim As Integer i, rdx

seed(1234567)
For i = 1 To 5
    Print next_int()
Next i
Print

seed(987654321)
Dim As Longint r(4)
For i = 1 To 100000
    rdx = Int(next_float() * 5)
    r(rdx) += 1
Next i

For i = 0 To 4
    Print i & ": " & r(i);
    If i < 4 Then Print ", ";
Next i

Sleep
