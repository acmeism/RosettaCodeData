import system'math;
import extensions;
import extensions'text;

struct Quaternion : BaseValue
{
    rprop real A;
    rprop real B;
    rprop real C;
    rprop real D;

    constructor new(a, b, c, d)
        <= new(cast real(a), cast real(b), cast real(c), cast real(d));

    constructor new(real a, real b, real c, real d)
    {
        A := a;
        B := b;
        C := c;
        D := d
    }

    constructor(real r)
    {
        A := r;
        B := 0.0r;
        C := 0.0r;
        D := 0.0r
    }

    real Norm = (A*A + B*B + C*C + D*D).sqrt();

    Quaternion Negative = Quaternion.new(A.Negative,B.Negative,C.Negative,D.Negative);

    Quaternion Conjugate = Quaternion.new(A,B.Negative,C.Negative,D.Negative);

    Quaternion add(Quaternion q)
        = Quaternion.new(A + q.A, B + q.B, C + q.C, D + q.D);

    Quaternion multiply(Quaternion q)
        = Quaternion.new(
            A * q.A - B * q.B - C * q.C - D * q.D,
            A * q.B + B * q.A + C * q.D - D * q.C,
            A * q.C - B * q.D + C * q.A + D * q.B,
            A * q.D + B * q.C - C * q.B + D * q.A);

    Quaternion add(real r)
        <= add(Quaternion.new(r,0,0,0));

    Quaternion multiply(real r)
        <= multiply(Quaternion.new(r,0,0,0));

    bool equal(Quaternion q)
        = (A == q.A) && (B == q.B) && (C == q.C) && (D == q.D);

    string Printable
        = new StringWriter().printFormatted("Q({0}, {1}, {2}, {3})",A,B,C,D);
}

public program()
{
    auto q := Quaternion.new(1,2,3,4);
    auto q1 := Quaternion.new(2,3,4,5);
    auto q2 := Quaternion.new(3,4,5,6);
    real r := 7;

    console.printLine("q = ", q);
    console.printLine("q1 = ", q1);
    console.printLine("q2 = ", q2);
    console.printLine("r = ", r);

    console.printLine("q.Norm() = ", q.Norm);
    console.printLine("q1.Norm() = ", q1.Norm);
    console.printLine("q2.Norm() = ", q2.Norm);

    console.printLine("-q = ", q.Negative);
    console.printLine("q.Conjugate() = ", q.Conjugate);

    console.printLine("q + r = ", q + r);
    console.printLine("q1 + q2 = ", q1 + q2);
    console.printLine("q2 + q1 = ", q2 + q1);

    console.printLine("q * r = ", q * r);
    console.printLine("q1 * q2 = ", q1 * q2);
    console.printLine("q2 * q1 = ", q2 * q1);

    console.printLineFormatted("q1*q2 {0} q2*q1", ((q1 * q2) == (q2 * q1)).iif("==","!="))
}
