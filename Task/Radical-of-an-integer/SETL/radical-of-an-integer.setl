program radicals_of_an_integer;
    init factab := distinct_factor_table(1000000);

    print("Radicals of the first 50 integers:");
    loop for n in [1..50] do
        nprint(lpad(str radical n, 5));
        if n mod 10=0 then print; end if;
    end loop;

    print;
    loop for n in [99999, 499999, 999999] do
        print("Radical of " + lpad(str n, 6) + ": " + lpad(str radical n, 6));
    end loop;

    print;
    print("Distribution of factor counts:");

    distr := {};
    loop for f = factab(n) do
        distr(#f) +:= 1;
    end loop;
    loop for amount = distr(n) do
        print(str n + ": " + lpad(str amount, 6));
    end loop;

    op radical(n);
        return 1 */ factab(n);
    end op;

    proc distinct_factor_table(n);
        factab := [{}] * n;

        loop init i := 2; while i <= n step i +:= 1; do
            if factab(i) = {} then
                loop init c := i; while c <= n step c +:= i; do
                    factab(c) with:= i;
                end loop;
            end if;
        end loop;

        return factab;
    end proc;
end program;
