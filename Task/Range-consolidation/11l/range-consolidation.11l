F consolidate(ranges)
   F normalize(s)
      R sorted(s.filter(bounds -> !bounds.empty).map(bounds -> sorted(bounds)))

   V norm = normalize(ranges)
   L(&r1) norm
      V i = L.index
      I !r1.empty
         L(j) i + 1 .< norm.len
            V& r2 = norm[j]
            I !r2.empty & r1.last >= r2[0]
               r1 = [r1[0], max(r1.last, r2.last)]
               r2.clear()
   R norm.filter(rnge -> !rnge.empty)

L(s) [[[1.1, 2.2]],
      [[6.1, 7.2], [7.2, 8.3]],
      [[4.0, 3.0], [2.0, 1.0]],
      [[4.0, 3.0], [2.0, 1.0], [-1.0, -2.0], [3.9, 10.0]],
      [[1.0, 3.0], [-6.0, -1.0], [-4.0, -5.0], [8.0, 2.0], [-6.0, -6.0]]]
   print(String(s)[1 .< (len)-1]‘ => ’String(consolidate(s))[1 .< (len)-1])
