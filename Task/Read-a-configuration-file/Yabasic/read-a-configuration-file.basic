a = open("rosetta_read.cfg")

while(not eof(#a))
    FLAG = true : REMARK = false
    line input #a line$
    line$ = trim$(line$)
    ll = len(line$)
    c$ = left$(line$, 1)
    switch(c$)
        case "": case "#": REMARK = true : break
        case ";": FLAG = false : line$ = trim$(right$(line$, ll - 1)) : break
        default: MULTI = instr(line$, ",")
    end switch

    if not REMARK then
        GAP = instr(line$, "=") : if not GAP GAP = instr(line$, " ")
        if not GAP then
            print line$, " = ";
            if FLAG then print "true" else print "false" end if
        else
            if MULTI then
                count = 1 : SG = GAP
                repeat
                    print left$(line$, GAP - 1), "(", count, ") = ", trim$(mid$(line$, SG + 1, MULTI - SG - 1))
                    count = count + 1
                    SG = MULTI + 1 : MULTI = instr(line$, ",", SG)
                    if not MULTI MULTI = ll + 1
                until(SG > ll)
            else
                print left$(line$, GAP - 1), " = ", trim$(right$(line$, ll - GAP))
            end if
        end if
    end if
wend

close #a
