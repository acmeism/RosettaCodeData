BYTE FUNC ReadLine(CHAR ARRAY fname CARD index CHAR ARRAY result)
  CHAR ARRAY line(255)
  CARD curr
  BYTE status,dev=[1]

  Close(dev)
  Open(dev,fname,4)
  curr=1 status=1
  WHILE Eof(dev)=0
  DO
    InputSD(dev,line)
    IF curr=index THEN
      SCopy(result,line)
      status=0
      EXIT
    FI
    curr==+1
  OD
  Close(dev)
RETURN (status)

PROC Test(CHAR ARRAY fname CARD index)
  CHAR ARRAY result(255)
  BYTE status

  PrintF("Reading %U line...%E",index)
  status=ReadLine(fname,index,result)
  IF status=0 THEN
    IF result(0)=0 THEN
      PrintF("%U line is empty.%E%E",index)
    ELSE
      PrintF("%U line is:%E""%S""%E%E",index,result)
    FI
  ELSEIF status=1 THEN
    PrintF("File contains less than %U lines.%E%E",index)
  FI
RETURN

PROC Main()
  CHAR ARRAY fname="H6:READ__96.ACT"

  PrintF("Reading ""%S""...%E%E",fname)
  Test(fname,7)
  Test(fname,24)
  Test(fname,50)
RETURN
