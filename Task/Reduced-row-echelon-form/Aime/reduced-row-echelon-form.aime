rref(list l, integer rows, columns)
{
    integer e, f, i, j, lead, r;
    list u, v;

    lead = r = 0;
    while (r < rows && lead < columns) {
        i = r;
        while (!l.q_list(i)[lead]) {
            i += 1;
            if (i == rows) {
                i = r;
                lead += 1;
                if (lead == columns) {
                    break;
                }
            }
        }
        if (lead == columns) {
            break;
        }

        u = l[i];

        l.spin(i, r);
        e = u[lead];
        if (e) {
            for (j, f in u) {
                u[j] = f / e;
            }
        }

        for (i, v in l) {
            if (i != r) {
                e = v[lead];
                for (j, f in v) {
                    v[j] = f - u[j] * e;
                }
            }
        }

        lead += 1;

        r += 1;
    }
}

display_2(list l)
{
    for (, list u in l) {
        u.ucall(o_winteger, -1, 4);
        o_byte('\n');
    }
}

main(void)
{
    list l;

    l = list(list(1, 2, -1, -4),
             list(2, 3, -1, -11),
             list(-2, 0, -3, 22));
    rref(l, 3, 4);
    display_2(l);

    0;
}
