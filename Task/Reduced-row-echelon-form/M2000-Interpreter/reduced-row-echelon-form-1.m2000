Module Base1 {
      dim base 1, A(3, 4)
      A(1, 1)= 1,    2,   -1,   -4,  2 ,   3,   -1,   -11,  -2  ,  0 ,  -3,    22
      lead=1
      rowcount=3
      columncount=4
      gosub disp()
      for r=1 to rowcount {
            if columncount<lead then exit
            i=r
            while A(i,lead)=0 {
                  i++
                  if rowcount=i then i=r : lead++ : if columncount<lead then exit
            }
            for c =1 to columncount {
                  swap A(i, c), A(r, c)
            }
              if A(r, lead)<>0 then {
                  div1=A(r,lead)
                  For c =1 to columncount {
                      A( r, c)/=div1
                  }
            }
            for i=1 to rowcount {
                  if i<>r then {
                        mult=A(i,lead)
                        for j=1 to columncount {
                                 A(i,j)-=A(r,j)*mult
                        }
                  }
            }
            lead=lead+1
      }
      disp()
      sub disp()
            local i, j
            for i=1 to rowcount
                  for j=1 to columncount
                        Print A(i, j),
                  Next j
                  if pos>0 then print
            Next i
      End sub
}
Base1
