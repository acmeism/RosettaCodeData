proc ReducedRowEchelonForm(M, Rows, Cols);
\Replace M with its reduced row echelon form
real M;  int Rows, Cols;
int  Lead, R, C, I;
real RLead, ILead, T;
[Lead:= 0;
for R:= 0 to Rows-1 do
    [if Lead >= Cols then return;
    I:= R;
    while M(I, Lead) = 0. do
        [I:= I+1;
        if I = Rows-1 then
            [I:= R;
            Lead:= Lead+1;
            if Lead = Cols-1 then return;
            ];
        ];
    \Swap rows I and R
    T:= M(I);  M(I):= M(R);  M(R):= T;

    if M(R, Lead) # 0. then
        \Divide row R by M[R, Lead]
        [RLead:= M(R, Lead);
        for C:= 0 to Cols-1 do
            M(R, C):= M(R, C) / RLead;
        ];

    for I:= 0 to Rows-1 do
        [if I # R then
            \Subtract M[I, Lead] multiplied by row R from row I
            [ILead:= M(I, Lead);
            for C:= 0 to Cols-1 do
                M(I, C):= M(I, C) - ILead * M(R, C);
            ];
        ];
    Lead:= Lead+1;
    ];
];

real M;
int  R, C;
[M:= [ [ 1.,  2., -1., -4.],
       [ 2.,  3., -1.,-11.],
       [-2.,  0., -3., 22.] ];
ReducedRowEchelonForm(M, 3, 4);
Format(4,1);
for R:= 0 to 3-1 do
        [for C:= 0 to 4-1 do
                RlOut(0, M(R,C));
        CrLf(0);
        ];
]
