10   ' Remove Duplicates
20   OPTION BASE 1
30   LET MAXI% = 7
40   DIM D(7), R(7): ' data, result
50   ' Set the data.
60   FOR I% = 1 TO 7
70    READ D(I%)
80   NEXT I%
90   ' Remove duplicates.
100  LET R(1) = D(1)
110  LET LRI% = 1: ' last index of result
120  LET P% = 1: ' position
130  WHILE P% < MAXI%
140   LET P% = P% + 1
150   LET ISNEW = 1: ' is a new number?
160   LET RI% = 1: ' current index of result
170   WHILE (RI% <= LRI%) AND ISNEW
180    IF D(P%) = R(RI%) THEN LET ISNEW = 0
190    LET RI% = RI% + 1
200   WEND
210   IF ISNEW THEN LET LRI% = LRI% + 1: LET R(LRI%) = D(P%)
220  WEND
230  FOR RI% = 1 TO LRI%
240   PRINT R(RI%)
250  NEXT RI%
260  END
1000 DATA 1, 2, 2, 3, 4, 5, 5
