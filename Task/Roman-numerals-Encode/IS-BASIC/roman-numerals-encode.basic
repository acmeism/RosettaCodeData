100 PROGRAM "Roman.bas"
110 DO
120   PRINT :INPUT PROMPT "Enter an arabic number: ":N
130   IF N<1 THEN EXIT DO
140   PRINT TOROMAN$(N)
150 LOOP
160 DEF TOROMAN$(X)
170   IF X>3999 THEN
180     LET TOROMAN$="Too big."
190     EXIT DEF
200   END IF
210   RESTORE
220   LET SUM$=""
230   FOR I=1 TO 13
240     READ ARABIC,ROMAN$
250     DO WHILE X>=ARABIC
260       LET SUM$=SUM$&ROMAN$
270       LET X=X-ARABIC
280     LOOP
290   NEXT
300   LET TOROMAN$=SUM$
310 END DEF
320 DATA 1000,"M",900,"CM",500,"D",400,"CD",100,"C",90,"XC"
330 DATA 50,"L",40,"XL",10,"X",9,"IX",5,"V",4,"IV",1,"I"
