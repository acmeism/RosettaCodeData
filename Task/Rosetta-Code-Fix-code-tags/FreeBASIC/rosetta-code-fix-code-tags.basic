Function ReplaceAll(Byval source As String, Byval find As String, Byval replace As String) As String
    Dim As String result = source
    Dim As Integer posic = Instr(result, find)
    Dim As Integer offset = 1
    While posic > 0
        result = Left(result, posic - 1) & replace & Mid(result, posic + Len(find))
        offset = posic + Len(replace)
        posic = Instr(offset, result, find)
    Wend
    Return result
End Function

Function ProcessSource(source As String) As String
    Dim As String result = source
    Dim As String sh = "syntaxhighlight"
    Dim As String linea, newContent
    Dim As Integer nlPos

    ' Process line by line
	While Len(result) > 0
        nlPos = Instr(result, Chr(10))
        If nlPos = 0 Then
            linea = result
            result = ""
        Else
            linea = Left(result, nlPos - 1)
            result = Mid(result, nlPos + 1)
        End If

		' Check for <lang at start of linea
        If Left(linea, 5) = "<lang" Then
            Dim As Integer closePos = Instr(6, linea, ">")
            If closePos > 0 Then
                Dim As String lang = Trim(Mid(linea, 6, closePos - 6))
                If lang = "" Then lang = "text"
                linea = "<" & sh & " lang=""" & lang & """>" & Mid(linea, closePos + 1)
            End If
        End If

        newContent &= linea & Chr(10)
    Wend

	' Replace plain <lang> with <syntaxhighlight lang="text">
    newContent = ReplaceAll(newContent, "<lang>", "<" & sh & " lang=""text"">")
	' Replace closing tags
    newContent = ReplaceAll(newContent, "</lang>", "</" & sh & ">")

    Return newContent
End Function

' Main program
Dim As String filename = "input.txt"
Dim As Integer ff = Freefile
Dim As String linea, content

If Open(filename For Input As #ff) = 0 Then
    While Not Eof(ff)
        Line Input #ff, linea
        content &= linea & Chr(10)
    Wend
    Close #ff

    Print ProcessSource(content)
End If

Sleep
