Const max = 10000000
Dim i As Long, j As Long
Dim sc1 As Integer, usc1 As Long, sc2 As Integer, usc2 As Long
Dim safeprimes As String, unsafeprimes As String
ReDim criba(max) As Integer
criba(0) = 1
criba(1) = 1

For i = 4 To max Step 2
    criba(i) = 1
Next i
For i = 3 To Sqr(max) + 1 Step 2
    If criba(i) = 0 Then
        For j = i * i To max Step i * 2
            criba(j) = 1
        Next j
    End If
Next

usc1 = 1
unsafeprimes = "2"
For i = 3 To 3001 Step 2
    If criba(i) = 0 Then
        If criba(i \ 2) = 0 Then
            sc1 = sc1 + 1
            If sc1 <= 35 Then safeprimes = safeprimes + " " + Str$(i)
        Else
            usc1 = usc1 + 1
            If usc1 <= 40 Then unsafeprimes = unsafeprimes + " " + Str$(i)
        End If
    End If
Next i

For i = 3003 To max \ 10 Step 2
    If criba(i) = 0 Then
        If criba(i \ 2) = 0 Then
            sc1 = sc1 + 1
        Else
            usc1 = usc1 + 1
        End If
    End If
Next i

sc2 = sc1
usc2 = usc1
For i = max \ 10 + 1 To max Step 2
    If criba(i) = 0 Then
        If criba(i \ 2) = 0 Then
            sc2 = sc2 + 1
        Else
            usc2 = usc2 + 1
        End If
    End If
Next i

Print "the first 35 Safeprimes are: "; safeprimes
Print
Print "the first 40 Unsafeprimes are:  "; unsafeprimes
Print
Print "                  Safeprimes     Unsafeprimes"
Print "    Below         ---------------------------"
Print Using "##########,      "; max \ 10; sc1; usc1
Print Using "##########,      "; max; sc2; usc2
