( ( divmod
  =   a b
    . !arg:(?a.?b)&(div$(!a.!b).mod$(!a.!b))
  )
& ( overnight
  =   ns nn result s q r
    .   !arg:(?ns.?nn)
      & :?result
      & 0:?s
      &   whl
        ' ( !s+1:?s:~>!ns
          & divmod$(!nn.!ns):(?q.?r)
          & !r:1
          & !q*(!ns+-1):?nn
          & !result (!s.!q.!r.!nn):?result
          )
      & !s:>!ns
      & divmod$(!nn.!ns):(?q.0)
      & !result
  )
& ( minnuts
  =   nsailors nnuts result sailor takes gives leaves
    .   !arg:?nsailors
      & 0:?nnuts
      &   whl
        ' ( 1+!nnuts:?nnuts
          & ~(overnight$(!nsailors.!nnuts):?result)
          )
      & out$(!nsailors ": " !nnuts)
      &   whl
        ' ( !result:(?sailor.?takes.?gives.?leaves) ?result
          &   out
            $ ( str
              $ ( " Sailor #"
                  !sailor
                  " takes "
                  !takes
                  ", giving "
                  !gives
                  " to the monkey and leaves "
                  !leaves
                )
              )
          )
      &   out
        $ ( str
          $ ("In the morning, each sailor gets " !leaves*!nsailors^-1 " nuts")
          )
  )
& 4:?n
&   whl
  ' ( 1+!n:~>6:?n
    & out$("Solution with " !n " sailors:")
    & minnuts$!n
    )
)
