EnableExplicit
#SPC=Chr(32)
#TB=~"\t"
#TBLF=~"\t\n"
Define.i a,b,l,n,count=0
Define *count.Integer=@count

Procedure.i AddCount(*c.Integer) ; *counter: by Ref
  *c\i+1
  ProcedureReturn *c\i
EndProcedure

Procedure.s FormatStr(tx$,l.i)
  Shared *count
  If AddCount(*count)%10=0
    ProcedureReturn RSet(tx$,l,#SPC)+#TBLF
  Else
    ProcedureReturn RSet(tx$,l,#SPC)+#TB
  EndIf
EndProcedure

Procedure.b Trial(n.i)
  Define.i i
  For i=3 To Int(Sqr(n)) Step 2
    If n%i=0 : ProcedureReturn #False : EndIf
  Next
  ProcedureReturn #True
EndProcedure

Procedure.b isPrime(n.i)
  If (n>1 And n%2<>0 And Trial(n)) Or n=2 : ProcedureReturn #True : EndIf
  ProcedureReturn #False
EndProcedure

OpenConsole("Sequence of primes by Trial Division")
PrintN("Input (n1<n2 & n1>0)")
Print("n1 : ") : a=Int(Val(Input()))
Print("n2 : ") : b=Int(Val(Input()))
l=Len(Str(b))
If a<b And a>0
  PrintN(~"\nPrime numbers between "+Str(a)+" and "+Str(b))
  For n=a To b
    If isPrime(n)
      Print(FormatStr(Str(n),l))
    EndIf
  Next
  Print(~"\nPrimes= "+Str(*count\i))
  Input()
EndIf
