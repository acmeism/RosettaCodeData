  [ say "evaluating "
    ]this[ echo cr ]       is ident    (     -->   )

  [ iff say "true"
    else say "false" ]     is echobool (   b -->   )

  [ swap iff drop done
    times drop
    false ]done[ ]         is SC-and   ( b n -->   )

  [ swap not iff drop done
    times drop
    true ]done[ ]          is SC-or    ( b n -->   )

  [ ident
    2 times not ]          is a        (   b --> b )

  [ ident
    4 times not ]          is b        (   b --> b )

  [ say "i = "
    dup echobool
    say " AND j = "
    dup echobool
    cr
    [ a 1 SC-and b ]
    say "result is "
    echobool cr cr ]       is AND-demo (     -->   )

  [ say "i = "
    dup echobool
    say " OR j = "
    dup echobool
    cr
    [ a 1 SC-or b ]
    say "result is "
    echobool
    cr cr ]                is OR-demo  (     -->   )

  true  true  AND-demo
  true  false AND-demo
  false true  AND-demo
  false false AND-demo
  cr
  true  true  OR-demo
  true  false OR-demo
  false true  OR-demo
  false false OR-demo
