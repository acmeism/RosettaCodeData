100 PROGRAM "Sierpin2.bas"
110 LET LEVEL=5
120 OPTION ANGLE DEGREES
130 SET VIDEO MODE 1:SET VIDEO COLOUR 0:SET VIDEO X 40:SET VIDEO Y 27
140 OPEN #101:"video:"
150 SET PALETTE BLACK,YELLOW
160 DISPLAY #101:AT 1 FROM 1 TO 27
170 PLOT 1180,20,ANGLE 180;
180 CALL SIERP(560,LEVEL)
190 DEF CURVE(D,A,LEV)
200   IF LEV=0 THEN
210     PLOT FORWARD D;
220   ELSE
230     CALL CURVE(D/2,-A,LEV-1)
240     PLOT RIGHT A;
250     CALL CURVE(D/2,A,LEV-1)
260     PLOT RIGHT A;
270     CALL CURVE(D/2,-A,LEV-1)
280   END IF
290 END DEF
300 DEF SIERP(D,LEV)
310   CALL CURVE(D,60,LEV)
320   PLOT LEFT 60;
330   CALL CURVE(D,-60,LEV)
340   PLOT LEFT 60;
350   CALL CURVE(D,60,LEV)
360 END DEF
