  [ $ "turtleduck.qky" loadfile ] now!

  [ stack ]                      is switch.arg (   --> [ )

  [ switch.arg put ]             is switch     ( x -->   )

  [ switch.arg release ]         is otherwise  (   -->   )

  [ switch.arg share
    != iff ]else[ done
    otherwise ]'[ do ]done[ ]    is case       ( x -->   )

  [ $ "" swap witheach
      [ nested quackery join ] ] is expand     ( $ --> $ )

  [ $ "L" ]                      is L          ( $ --> $ )

  [ $ "R" ]                      is R          ( $ --> $ )

  [ $ "BLALB" ]                  is A          ( $ --> $ )

  [ $ "ARBRA" ]                  is B          ( $ --> $ )

  $ "A"

  6 times expand

  turtle
  10 frames
  witheach
    [ switch
        [ char L case [ -1 6 turn ]
          char R case [  1 6 turn ]
          otherwise   [  4 1 walk ] ] ]
  1 frames
