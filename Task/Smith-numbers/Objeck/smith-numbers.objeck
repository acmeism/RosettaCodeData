use Collection;

class Test {
  function : Main(args : String[]) ~ Nil {
    for(n := 1; n < 10000; n+=1;) {
      factors := PrimeFactors(n);
      if(factors->Size() > 1) {
        sum := SumDigits(n);
        each(i : factors) {
          sum -= SumDigits(factors->Get(i));
        };

        if(sum = 0) {
          n->PrintLine();
        };
      };
    };
  }

  function : PrimeFactors(n : Int) ~ IntVector {
    result := IntVector->New();

    for(i := 2; n % i = 0; n /= i;) {
      result->AddBack(i);
    };

    for(i := 3; i * i <= n; i += 2;) {
      while(n % i = 0) {
        result->AddBack(i);
        n /= i;
      };
    };

    if(n <> 1) {
      result->AddBack(n);
    };

    return result;
  }

  function : SumDigits(n : Int) ~ Int {
    sum := 0;
    while(n > 0) {
      sum += (n % 10);
      n /= 10;
    };

    return sum;
  }
}
