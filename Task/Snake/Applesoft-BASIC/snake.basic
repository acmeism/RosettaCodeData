REM === VARIABLES ===
REM
REM D(4)   X DELTAS
REM E(4)   Y DELTAS
REM K(255) KEYCODE BEHESTS
REM FN X   X COORDINATES OF SNAKE FROM MEMORY
REM FN Y   Y COORDINATES OF SNAKE FROM MEMORY
REM A      <UNUSED>
REM B      BEHEST: 0=NOTHING 1=RIGHT 2=LEFT 3=DOWN 4=UP 5=QUIT
REM C      SCRN COLOR
REM D      X-DELTA
REM E      Y-DELTA
REM F      FOOD COLOR= 4 DARK GREEN
REM H      HIT= 127
REM I      KEYBOARD= 49152
REM J      CLEAR KEYBOARD= 49168
REM K      KEYCODE
REM L      LENGTH WIDTH&HEIGHT= 40
REM M      MAX= 1599 UPPER BOUND OF SNAKE X AND Y COORDINATES
REM N      NEW POSITION OF HEAD OF SNAKE
REM O      OLD POSITION OF TAIL OF SNAKE
REM P      ADDRESS OF 1600 X COORDINATES OF SNAKE
REM Q      QUIT= 5
REM R      REMAINING COLOR= 9 ORANGE
REM S      SNAKE HEAD COLOR= 13 YELLOW
REM T      ONE= 1
REM U      X FOOD COORDINATE
REM V      Y FOOD COORDINATE
REM W      WALL COLOR= 10 GREY
REM X      X SNAKE HEAD COORDINATE
REM Y      Y SNAKE HEAD COORDINATE
REM Z      ADDRESS OF 1600 Y COORDINATES OF SNAKE
REM Q$     QUIT MESSAGE

REM === KEYBOARD ===
REM
REM UPPER  LOWER KEY BEHEST
REM  155         ESC  QUIT
REM  139         UP   UP
REM  193    225   A   UP
REM  201    233   I   UP
REM  138         DOWN DOWN
REM  218    250   Z   DOWN
REM  203    235   K   DOWN
REM  136         LEFT LEFT
REM  202    234   J   LEFT
REM  149        RIGHT RIGHT
REM  204    236   L   RIGHT

 0  ON B = Q GOTO 5: IF B THEN D = D(B):E = E(B)
 1 X = X + D:Y = Y + E:C =  SCRN( X,Y): COLOR= S: PLOT X,Y: COLOR= R: PLOT  FN X(N), FN Y(N):N = N - T + (N = 0) * M: POKE P + N,X: POKE Z + N,Y: ON C(C) GOTO 4: IF  NOT C THEN  COLOR= 0: PLOT  FN X(O), FN Y(O):O = O - T + (O = 0) * M
 2  IF C THEN U =  INT ( RND (T) * L):V =  INT ( RND (T) * L): ON  SCRN( U,V) > 0 GOTO 2: COLOR= F: PLOT U,V
 3 K =  PEEK (I):B =  PEEK (J * (K > H)):B = K(K): GOTO
 4  COLOR= T: PLOT X,Y: READ Q$: DATA5,20,20,1,-1,1,-1,13,9,4,10,1,40,1599,127,49152,49168,4608,6400,5,4,4,4,4,4,3,3,3,3,3,2,2,2,1,1,1,DONE,GAME OVER
 5  IF Q = 5 THEN  PRINT Q$;: END : HOME :Q = Q * (B = Q):B = 0: ON Q = 5 GOTO : RUN
 6  LOMEM: 24576
 7  DIM K(255),D(4),E(4),C(15): READ Q,X,Y,D(1),D(2),E(3),E(4),S,R,F,W,T,L,M,H,I,J,P,Z,K(155),K(139),K(193),K(225),K(201),K(233),K(138),K(218),K(250),K(203),K(235),K(136),K(202),K(234),K(149),K(204),K(236),Q$
 8  DEF  FN X(I) =  PEEK (P + I): DEF  FN Y(I) =  PEEK (Z + I):B =  RND (0):C(S) = T:C(R) = T:C(W) = T:B =  INT ( RND (T) * 4) + T:D = D(B):E = E(B): POKE P + O,X: POKE Z + O,Y
 9  GR : HOME : COLOR= W: VLIN 0,39 AT 0: VLIN 0,39 AT 39: HLIN 1,38 AT 0: HLIN 1,38 AT 39: COLOR= F: PLOT X + D,Y + E: GOTO
