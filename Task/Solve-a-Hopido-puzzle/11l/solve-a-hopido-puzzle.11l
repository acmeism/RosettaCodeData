V neighbours = [[2, 2], [-2, 2], [2, -2], [-2, -2], [3, 0], [0, 3], [-3, 0], [0, -3]]
V cnt = 0
V pWid = 0
V pHei = 0

F is_valid(a, b)
   R -1 < a & a < :pWid & -1 < b & b < :pHei

F iterate(&pa, x, y, v)
   I v > :cnt
      R 1

   L(i) 0 .< :neighbours.len
      V a = x + :neighbours[i][0]
      V b = y + :neighbours[i][1]
      I is_valid(a, b) & pa[a][b] == 0
         pa[a][b] = v
         V r = iterate(&pa, a, b, v + 1)
         I r == 1
            R r
         pa[a][b] = 0
   R 0

F solve(pz, w, h)
   V pa = [[-1] * h] * w
   V f = 0
   :pWid = w
   :pHei = h
   L(j) 0 .< h
      L(i) 0 .< w
         I pz[f] == ‘1’
            pa[i][j] = 0
            :cnt++
         f++

   L(y) 0 .< h
      L(x) 0 .< w
         I pa[x][y] == 0
            pa[x][y] = 1
            I 1 == iterate(&pa, x, y, 2)
               R (1, pa)
            pa[x][y] = 0
   R (0, pa)

V r = solve(‘011011011111111111111011111000111000001000’, 7, 6)
I r[0] == 1
   L(j) 6
      L(i) 7
         I r[1][i][j] == -1
            print(‘   ’, end' ‘’)
         E
            print(‘ #02’.format(r[1][i][j]), end' ‘’)
      print()
E
   print(‘No solution!’, end' ‘’)
