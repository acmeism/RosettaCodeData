Function formatOID(oid As String) As String
    Dim As String result = ""
    Dim As String segment = ""
    Dim As Integer i = 1, lenOID = Len(oid)

	While i <= lenOID
        If Mid(oid, i, 1) = "." Then
            result &= Space(5 - Len(segment)) & segment & "."
            segment = ""
        Else
            segment &= Mid(oid, i, 1)
        End If
        i += 1
    Wend
    result &= Space(5 - Len(segment)) & segment
    Return result
End Function

Sub quickSort(arr() As String, first As Integer, last As Integer)
    If first >= last Then Exit Sub

    Dim As Integer i = first, j = last
    Dim As String pivot = arr((first + last) \ 2)

    Do
        While arr(i) < pivot: i += 1: Wend
        While arr(j) > pivot: j -= 1: Wend

        If i <= j Then
            Swap arr(i), arr(j)
            i += 1
            j -= 1
        End If
    Loop Until i > j

    If first < j Then quickSort(arr(), first, j)
    If i < last Then quickSort(arr(), i, last)
End Sub

' Main program
Dim As Integer i, j
Dim As String oids(5) = { _
"1.3.6.1.4.1.11.2.17.19.3.4.0.10", _
"1.3.6.1.4.1.11.2.17.5.2.0.79", _
"1.3.6.1.4.1.11.2.17.19.3.4.0.4", _
"1.3.6.1.4.1.11150.3.4.0.1", _
"1.3.6.1.4.1.11.2.17.19.3.4.0.1", _
"1.3.6.1.4.1.11150.3.4.0" }

For i = 0 To Ubound(oids)
    oids(i) = formatOID(oids(i))
Next

quickSort(oids(), 0, Ubound(oids))

For i = 0 To Ubound(oids)
    Dim As String tmp = ""
    For j = 1 To Len(oids(i))
        If Mid(oids(i), j, 1) <> " " Then tmp &= Mid(oids(i), j, 1)
    Next
    Print tmp
Next

Sleep
