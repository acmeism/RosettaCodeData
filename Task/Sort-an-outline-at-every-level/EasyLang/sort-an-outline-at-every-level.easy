sortdir = 1
proc sort . d$[][] .
   for i = 1 to len d$[][] - 1
      for j = i + 1 to len d$[][]
         if sortdir * strcmp d$[j][1] d$[i][1] < 0 : swap d$[j][] d$[i][]
      .
   .
.
repeat
   s$ = input
   until s$ = ""
   inp$[] &= s$
.
subr init
   stdind = -1
   linpos = 0
   lin$ = ""
   linind = 0
.
istab = -1
#
proc nextline . .
   if linpos = -1 : return
   linpos += 1
   if linpos > len inp$[]
      linpos = -1
      return
   .
   lin$ = inp$[linpos]
   linind = 0
   for c$ in strchars lin$
      if istab = -1
         if c$ = " "
            istab = 0
         elif c$ = "\t"
            stdind = 1
            istab = 1
         .
      .
      if c$ <> " " and c$ <> "\t"
         if stdind = -1 and linind > 0 : stdind = linind
         break 1
      .
      if istab = 0 and c$ = " " or istab = 1 and c$ = "\t"
         linind += 1
      else
         print "mix of tab and space - line " & linpos
         linpos = -1
         return
      .
   .
.
func$[] outline .
   curind = linind
   repeat
      #
      until linpos = -1 or linind < curind
      if linind = curind
         r$[][] &= [ lin$ ]
      elif linind = curind + stdind
         h$[] = outline
         for h$ in h$[] : r$[$][] &= h$
      else
         if linpos <> -1 : print "indentation error - line " & linpos
         linpos = -1
      .
      nextline
   .
   sort r$[][]
   for i to len r$[][]
      for r$ in r$[i][] : r$[] &= r$
   .
   if linpos <> -1 : linpos -= 1
   return r$[]
.
proc run dir . .
   sortdir = dir
   call init
   nextline
   for s$ in outline : print s$
   print ""
.
run 1
run -1
#
input_data
zeta
    beta
    gamma
        lambda
        kappa
        mu
    delta
alpha
    theta
    iota
    epsilon
