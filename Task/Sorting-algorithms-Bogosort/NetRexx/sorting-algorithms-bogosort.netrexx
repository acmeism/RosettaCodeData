/* NetRexx */
options replace format comments java crossref savelog symbols nobinary

import java.util.List

method isSorted(list = List) private static returns boolean

  if list.isEmpty then
    return isTrue

  it = list.iterator
  last = Comparable it.next
  loop label i_ while it.hasNext
    current = Comparable it.next
    if last.compareTo(current) > 0 then
      return isFalse
    last = current
    end i_

  return isTrue

method bogoSort(list = List) private static
  loop label s_ while \isSorted(list)
    Collections.shuffle(list)
    end s_

  return

method main(args = String[]) public constant
  samples = [int 31, 41, 59, 26, 53, 58, 97, 93, 23, 84]
  alst = ArrayList(samples.length)
  loop iv = 0 to samples.length - 1
    alst.add(Integer(samples[iv]))
    end iv

  say 'unsorted:' alst.toString
  bogoSort(alst)
  say 'sorted:  ' alst.toString

  return

method isTrue public static returns boolean
  return 1 == 1

method isFalse public static returns boolean
  return \isTrue
