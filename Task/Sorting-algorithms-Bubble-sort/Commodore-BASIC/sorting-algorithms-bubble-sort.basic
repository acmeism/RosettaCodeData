 5 REM ===============================================
10 REM HTTP://ROSETTACODE.ORG/
20 REM TASK=SORTING ALGORITHMS/BUBBLE SORT
30 REM LANGUAGE=COMMODORE 64 BASIC V2
40 REM DATE=2020-08-17
50 REM CODING BY=ALVALONGO
60 REM FILE=BUBLE.PRG
70 REM ============================================

100 PRINT "SORTING ALGORITHMS/BUBBLE SORT"
110 GOSUB 700
120 GOSUB 800
130 PRINT "RESULT:"
140 GOSUB 600
150 END

600 REM DISPLAY DATA================================
610 FOR K=1 TO N
620 PRINT A(K);
630 NEXT K
640 PRINT
650 RETURN

700 REM LOAD DATA ==================================
720 READ N
730 DIM A(N)
740 FOR I=1 TO N
750 READ A(I)
760 NEXT I
770 RETURN

800 REM BUBBLE SORT ================================
810 FOR I=1 TO N
815 PRINT "I=";I
820 GOSUB 600
830 SW=-1
840 FOR J=1 TO N-I
850 IF A(J)>A(J+1) THEN T=A(J):A(J)=A(J+1):A(J+1)=T:SW=0
860 NEXT J
865 PRINT "SW=";SW
870 IF SW THEN I=N
880 NEXT I
890 RETURN

900 REM DATA==========================================
910 DATA 15
920 DATA 64,34,25,12,22,11,90,13,59,47,19,89,10,17,31
