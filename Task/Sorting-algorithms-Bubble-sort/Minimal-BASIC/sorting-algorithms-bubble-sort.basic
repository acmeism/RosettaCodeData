100 REM Sorting algorithms/Bubble sort
110 REM Prepare data
120 REM N - size; A - array of nums
130 LET N = 10
140 OPTION BASE 1
150 DIM A(10)
160 RANDOMIZE
170 PRINT "Before: ";
180 FOR I = 1 TO N
190  LET A(I) = INT(RND*100)
200  PRINT A(I);
210 NEXT I
220 PRINT
230 REM Sort
240 REM C - counter; H - has changed
250 LET C = N
260  LET H = 0
270  FOR I = 1 TO C-1
280   IF A(I) <= A(I+1) THEN 330
290    LET T = A(I)
300    LET A(I) = A(I+1)
310    LET A(I+1) = T
320    LET H = 1
330  NEXT I
340  LET C = C-1
350 IF H = 1 THEN 260
360 REM Display result
370 PRINT "After: ";
380 FOR I = 1 TO N
390  PRINT A(I);
400 NEXT I
410 PRINT
420 END
