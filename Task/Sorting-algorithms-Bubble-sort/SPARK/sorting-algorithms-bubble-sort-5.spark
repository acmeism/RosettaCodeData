package body Bubble with SPARK_Mode is

   procedure Sort (A : in out Arr)
   is
      Prev : Arr (A'Range) with Ghost;
      Done : Boolean;
   begin
      for I in reverse A'First .. A'Last - 1 loop
         Prev := A;
         Done := True;
         for J in A'First .. I loop
            if A(J) > A(J + 1) then
               declare
                  TMP : Integer := A(J);
               begin
                  A(J) := A(J + 1);
                  A(J + 1) := TMP;
                  Done := False;
               end;
            end if;
            pragma Loop_Invariant (if Done then Sorted (A(A'First .. J + 1)));
            pragma Loop_Invariant (Bubbled (A(A'First .. J + 1)));
            pragma Loop_Invariant (A(J + 2 .. A'Last) = Prev(J + 2 .. A'Last));
            pragma Loop_Invariant (for some K in A'First .. J + 1 =>
                                     A(J + 1) = Prev(K));
         end loop;
         exit when Done;
         pragma Loop_Invariant (if Done then Sorted (A));
         pragma Loop_Invariant (Bubbled (A(A'First .. I + 1)));
         pragma Loop_Invariant (Sorted (A(I + 1 .. A'Last)));
      end loop;
   end Sort;

end Bubble;
