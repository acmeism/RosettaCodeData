// Animated sort.
// Original idea by William Tang, obtained from MicroHobby 25 Years (https://microhobby.speccy.cz/zxsf/MH-25Years.pdf)

clear screen

n=15 : m=18 : y=9 : t$=chr$(17)+chr$(205)+chr$(205)
dim p(n), p$(n)

for x=1 TO n
    p(x)=ran(15)+1
    p$(x)=str$(p(x),"##.######")
    print at(0,x) p$(x)
next x

for j=1 to n-1
    for i=j+1 to n
        l=n+j-i+1
        if p(j) > p(l) then
            print color("yellow","red") at(0,j) p$(j)
            if l<>m then
                for x=m to l step sig(l-m): print at(18,x) t$ : print at (18,x+sig(m-l)) "   " : pause .02 : next x
            end if
            for x=17 TO y step -1 : print at(x,l) t$+" " : pause .02 : next x
            for x=0 TO 10 : print at(x,l) " "+p$(l)+t$ : pause .02 : next x
            for x=l TO j STEP -1 : print at(11,x) p$(l)+t$ : print at(11,x+1) "            " : pause .02 : next x
            print at(0,j) "            "
            for x=j+1 TO l-1 : print color("yellow","red") at(0,x) p$(j) : pause .02 : print at(0,x) p$(x) : pause .02 : next x
            print at(0,l) p$(j)
            for x=10 TO 0 step -1 : print at(x,j) p$(l)+t$+" " : pause .02 : next x
            for x=y TO 17 : print at(x,j) " "+t$ : pause .02 : next x
            m=j
            t=p(l) : tem$=p$(l)
            p(l)=p(j) : p$(l)=p$(j)
            p(j)=t : p$(j)=tem$
        end if
        pause .02
    next i
next j

for x=m TO 18 : print at(18,x-1) "   " : print at(18,x) t$ : pause .02 : next x
