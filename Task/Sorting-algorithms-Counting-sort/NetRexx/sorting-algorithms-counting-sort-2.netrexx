/* NetRexx */
options replace format comments java crossref symbols nobinary

runSample(arg)
return

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
method countingSort(icounts) public constant

  parse getMinMax(icounts) amin amax
  array = 0
  loop ix = 1 to icounts.words
    iw = icounts.word(ix) + 0
    array[iw] = array[iw] + 1
    end ix
  ocounts = ''
  loop ix = amin to amax
    if array[ix] = 0 then iterate ix
    loop for array[ix]
      ocounts = ocounts ix
      end
    end ix
  return ocounts.space

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
method getMinMax(icounts) public constant

  amin = Long.MAX_VALUE
  amax = Long.MIN_VALUE
  loop x_ = 1 to icounts.words
    amin = icounts.word(x_).min(amin)
    amax = icounts.word(x_).max(amax)
    end x_

  return amin amax

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
method runSample(arg) public static
parse arg icounts
if icounts = '' then -
  icounts = -
    ' 1   3   6   2   7  13  20  12  21  11  22  10  23   9  24   8  25  43  62  42' -
    '63  41  18  42  17  43  16  44  15  45  14  46  79 113  78 114  77  39  78  38' -
    '0  -200 -6  -10 -0' -
    ''

say icounts.space
say countingSort(icounts)

return
