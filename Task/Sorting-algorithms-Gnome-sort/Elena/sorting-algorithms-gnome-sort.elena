import extensions;
import system'routines;

extension op
{
    gnomeSort()
    {
        var list := self.clone();
        int i := 1;
        int j := 2;

        while (i < list.Length)
        {
            if (list[i-1]<=list[i])
            {
                i := j;
                j += 1
            }
            else
            {
                list.exchange(i-1,i);
                i -= 1;
                if (i==0)
                {
                    i := 1;
                    j := 2
                }
            }
        };

        ^ list
    }
}

public program()
{
    var list := new int[]::(3, 9, 4, 6, 8, 1, 7, 2, 5);

    console.printLine("before:", list.asEnumerable());
    console.printLine("after :", list.gnomeSort().asEnumerable())
}
