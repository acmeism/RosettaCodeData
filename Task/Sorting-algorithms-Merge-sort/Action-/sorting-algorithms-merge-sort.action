DEFINE MAX_COUNT="100"

PROC PrintArray(INT ARRAY a INT size)
  INT i

  Put('[)
  FOR i=0 TO size-1
  DO
    IF i>0 THEN Put(' ) FI
    PrintI(a(i))
  OD
  Put(']) PutE()
RETURN

PROC Merge(INT ARRAY a INT first,mid,last)
  INT ARRAY left(MAX_COUNT),right(MAX_COUNT)
  INT leftSize,rightSize,i,j,k

  leftSize=mid-first+1
  rightSize=last-mid

  FOR i=0 TO leftSize-1
  DO
    left(i)=a(first+i)
  OD
  FOR i=0 TO rightSize-1
  DO
    right(i)=a(mid+1+i)
  OD
  i=0 j=0
  k=first
  WHILE i<leftSize AND j<rightSize
  DO
    IF left(i)<=right(j) THEN
      a(k)=left(i)
      i==+1
    ELSE
      a(k)=right(j)
      j==+1
    FI
    k==+1
  OD

  WHILE i<leftSize
  DO
    a(k)=left(i)
    i==+1 k==+1
  OD

  WHILE j<rightSize
  DO
    a(k)=right(j)
    j==+1 k==+1
  OD
RETURN

PROC MergeSort(INT ARRAY a INT size)
  INT currSize,first,mid,last

  currSize=1
  WHILE currSize<size
  DO
    first=0
    WHILE first<size-1
    DO
      mid=first+currSize-1
      IF mid>size-1 THEN
        mid=size-1
      FI
      last=first+2*currSize-1
      IF last>size-1 THEN
        last=size-1
      FI
      Merge(a,first,mid,last);

      first==+2*currSize
    OD
    currSize==*2
  OD
RETURN

PROC Test(INT ARRAY a INT size)
  PrintE("Array before sort:")
  PrintArray(a,size)
  MergeSort(a,size)
  PrintE("Array after sort:")
  PrintArray(a,size)
  PutE()
RETURN

PROC Main()
  INT ARRAY
    a(10)=[1 4 65535 0 3 7 4 8 20 65530],
    b(21)=[10 9 8 7 6 5 4 3 2 1 0
      65535 65534 65533 65532 65531
      65530 65529 65528 65527 65526],
    c(8)=[101 102 103 104 105 106 107 108],
    d(12)=[1 65535 1 65535 1 65535 1
      65535 1 65535 1 65535]

  Test(a,10)
  Test(b,21)
  Test(c,8)
  Test(d,12)
RETURN
