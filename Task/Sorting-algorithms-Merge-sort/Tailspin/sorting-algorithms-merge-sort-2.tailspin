templates mergesort
  templates merge
    @: $(2);
    $(1)... -> \(
      when <?($@merge<[](0)>)
      | ..$@merge(1)> do
        $ !
      otherwise
        ^@merge(1) !
        $ -> #
     \) !
     $@... !
  end merge

  templates mergePairs
    when <[](1)> do
      $(1) !
    when <[](2..)> do
      [$(1..2) -> merge] !
      $(3..last) -> #
  end mergePairs

  templates mergeAll
    when <[](0)> do
      $ !
    when <[](1)> do
      $(1) !
    otherwise
      [ $ -> mergePairs ] -> #
  end mergeAll

  $ -> [ $... -> [ $ ] ] -> mergeAll !
end mergesort

[4,5,3,8,1,2,6,7,9,8,5] -> mergesort -> !OUT::write
