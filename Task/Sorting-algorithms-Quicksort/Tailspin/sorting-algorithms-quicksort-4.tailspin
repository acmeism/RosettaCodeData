quicksort templates
  partial templates
    first is $(1);
    last is $(2);
    pivot is $@quicksort($first);
    @ set $first + 1;
    $last -> # !

    when <|..~$@> do
      limit is $;
      @quicksort($first) set $@quicksort($limit);
      @quicksort($limit) set $pivot;
      [ $first, $limit - 1 ] !
      [ $limit + 1, $last ] !

    when <|?($@quicksort($) matches <|$pivot~..>)> do
      $ - 1 -> # !

    when <|?($@quicksort($@) matches <|..$pivot>)> do
      @ set $@ + 1; $ -> # !

    otherwise
      temp is $@quicksort($@);
      @quicksort($@) set $@quicksort($);
      @quicksort($) set $temp;
      @ set $@ + 1; $ - 1 -> # !
  end partial

  @ set $;
  [1, $@::length] -> !#
  $@ !

  when <|?($(1) matches <|..~$(2)>)> do
    $ -> partial -> !#
end quicksort

[4,5,3,8,1,2,6,7,9,8,5] -> quicksort !
