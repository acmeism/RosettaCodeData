dim array(15)
a = 0
b = arraysize(array(),1)

for i = a to b
    array(i) = ran(1000)
next i

print "unsort ";
for i = a to b
	print array(i) using("####");
	if i = b then print ""; else print ", "; : fi
next i

quickSort(array(), a, b)

print "\n  sort ";
for i = a to b
    print array(i) using("####");
    if i = b then print ""; else print ", "; : fi
next i
print
end

sub quickSort(array(), l, r)
    local asize, i, j, pivot

    size = r - l +1
    if size < 2  return

    i = l
	j = r
    pivot = array(l + int(size / 2))

    repeat
        while array(i) < pivot
            i = i + 1
        wend
        while pivot < array(j)
            j = j - 1
        wend
        if i <= j then
            temp = array(i)
            array(i) = array(j)
            array(j) = temp
            i = i + 1
            j = j - 1
        fi
    until i > j

    if l < j  quickSort(array(), l, j)
    if i < r  quickSort(array(), i, r)
end sub
