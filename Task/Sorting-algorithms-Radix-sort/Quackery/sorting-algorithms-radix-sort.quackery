  [ stack ]                    is digit     (   --> s )

  [ behead swap witheach min ] is smallest  ( [ --> n )

  [ [] over smallest
    rot witheach
      [ over -
        rot swap join swap ]
    swap
    0 digit put
    dup size temp put
    [ ' [ [ ] ] 16 of
      constant
      swap witheach
        [ dup dip
            [ digit share
              >> 15 &
          2dup peek ]
          join
          unrot poke ]
      dup 0 peek size
      temp share != while
      behead swap
      witheach join
      4 digit tally again ]
    behead nip
    temp release
    digit release
    [] unrot
    witheach
      [ over +
        rot swap join swap ]
    drop ]                     is radixsort ( [ --> [ )

  [] 256 times
    [ 1999 random 999 - join ]
  radixsort
  16 times
     [ 16 times
         [ behead
           dup 0 > if sp
           dup abs dup
           10 < if sp
           100 < if sp
           echo sp ] cr ]
  drop
