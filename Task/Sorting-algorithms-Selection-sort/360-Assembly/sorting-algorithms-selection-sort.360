*        Selection sort            26/06/2016
SELECSRT CSECT
         USING  SELECSRT,R13       base register
         B      72(R15)            skip savearea
         DC     17F'0'             savearea
         STM    R14,R12,12(R13)    prolog
         ST     R13,4(R15)         "
         ST     R15,8(R13)         "
         LR     R13,R15            "
         LA     RJ,1               j=1
         DO WHILE=(C,RJ,LE,N)      do j=1 to n
         LR     RK,RJ                k=j
         LR     R1,RJ                j
         SLA    R1,2                 .
         LA     R3,A-4(R1)           @a(j)
         L      RT,0(R3)             temp=a(j)
         LA     RI,1(RJ)             i=j+1
         DO WHILE=(C,RI,LE,N)        do i=j+1 to n
         LR     R1,RI                  i
         SLA    R1,2                   .
         L      R2,A-4(R1)             a(i)
         IF CR,RT,GT,R2 THEN           if temp>a(i) then
         LR     RT,R2                    temp=a(i)
         LR     RK,RI                    k=i
         ENDIF  ,                      end if
         LA     RI,1(RI)               i=i+1
         ENDDO  ,                    end do
		 L      R0,0(R3)             a(j)
         LR     R1,RK                k
         SLA    R1,2                 .
         ST     R0,A-4(R1)           a(k)=a(j)
         ST     RT,0(R3)             a(j)=temp;
         LA     RJ,1(RJ)             j=j+1
         ENDDO  ,                  end do
         LA     R3,PG              pgi=0
         LA     RI,1               i=1
         DO     WHILE=(C,RI,LE,N)  do i=1 to n
         LR     R1,RI                i
         SLA    R1,2                 .
         L      R2,A-4(R1)           a(i)
         XDECO  R2,XDEC              edit a(i)
         MVC    0(4,R3),XDEC+8       output a(i)
         LA     R3,4(R3)             pgi=pgi+4
         LA     RI,1(RI)             i=i+1
         ENDDO  ,                  end do
         XPRNT  PG,L'PG            print buffer
         L      R13,4(0,R13)       epilog
         LM     R14,R12,12(R13)    "
         XR     R15,R15            "
         BR     R14                exit
A     DC F'4',F'65',F'2',F'-31',F'0',F'99',F'2',F'83',F'782',F'1'
      DC F'45',F'82',F'69',F'82',F'104',F'58',F'88',F'112',F'89',F'74'
N        DC     A((N-A)/L'A)       number of items of a
PG       DC     CL80' '            buffer
XDEC     DS     CL12               temp for xdeco
         YREGS
RI       EQU    6                  i
RJ       EQU    7                  j
RK       EQU    8                  k
RT       EQU    9                  temp
         END    SELECSRT
