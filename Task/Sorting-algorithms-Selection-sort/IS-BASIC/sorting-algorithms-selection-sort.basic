100 PROGRAM "SelecSrt.bas"
110 RANDOMIZE
120 NUMERIC ARRAY(-5 TO 14)
130 CALL INIT(ARRAY)
140 CALL WRITE(ARRAY)
150 CALL SELECTIONSORT(ARRAY)
160 CALL WRITE(ARRAY)
170 DEF INIT(REF A)
180   FOR I=LBOUND(A) TO UBOUND(A)
190     LET A(I)=RND(98)+1
200   NEXT
210 END DEF
220 DEF WRITE(REF A)
230   FOR I=LBOUND(A) TO UBOUND(A)
240     PRINT A(I);
250   NEXT
260   PRINT
270 END DEF
280 DEF SELECTIONSORT(REF A)
290   FOR I=LBOUND(A) TO UBOUND(A)-1
300     LET MN=A(I):LET INDEX=I
310     FOR J=I+1 TO UBOUND(A)
320       IF MN>A(J) THEN LET MN=A(J):LET INDEX=J
330     NEXT
340     LET A(INDEX)=A(I):LET A(I)=MN
350   NEXT
360 END DEF
