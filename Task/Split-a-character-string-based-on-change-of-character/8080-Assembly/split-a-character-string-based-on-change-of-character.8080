	org	100h
	jmp	demo
	;;;	Split the string under DE on changing characters,
	;;;	and store the result at HL.
split:	ldax	d	; Load character from string
spcopy:	mov	m,a	; Store in output
	cpi	'$'	; CP/M string terminator
	rz		; Stop when the end is reached
	mov	b,a	; Store previous character in B
	inx	d	; Increment input pointer
	inx	h	; Increment output pointer
	ldax	d	; Get next character
	cmp	b	; Same as previous character?
	jz	spcopy	; Then just copy it
	cpi	'$'	; Otherwise, if it is the en
	jz	spcopy	; Then just copy it as well
	mvi	m,','	; Otherwise, add a comma and a space
	inx	h
	mvi	m,' '
	inx	h
	jmp	spcopy
	;;;	Demo code
demo:	lxi	d,string
	lxi	h,out
	call	split	; Split the string
	lxi	d,out
	mvi	c,9	; And print it using CP/M
	jmp	5
string:	db	'gHHH5YY++///',5Ch,'$'
out:	equ	$
