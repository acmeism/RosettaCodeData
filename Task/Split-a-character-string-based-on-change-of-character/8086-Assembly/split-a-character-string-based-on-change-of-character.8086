	cpu	8086
	org	100h
section	.text
	jmp	demo
	;;;	Split the string at DS:SI on changing characters,
	;;;	and store the result at ES:DI.
split:	lodsb		; Load character	
.copy:	stosb		; Store in output
	cmp	al,'$'	; Done yet?
	je	.out	; If so, stop.
	mov	ah,al	; Store previous character
	lodsb		; Get next character
	cmp	al,ah	; Same character?
	je	.copy	; Then just copy it
	cmp	al,'$'	; End of string?
	je	.copy	; Then just copy it too
	mov	dl,al
	mov	ax,', '	; Otherwise, add a comma and a space
	stosw
	mov	al,dl
	jmp	.copy
.out:	ret
	;;;	Demo code
demo:	mov	si,string
	mov	di,buf
	call	split	; Split the string
	mov	dx,buf
	mov	ah,9
	int	21h	; And print the result using DOS
	ret
section	.data
string:	db	'gHHH5YY++///\$'
section	.bss
buf:	resb	32
