PrintChar equ &BB5A ;Amstrad CPC BIOS call
Terminator equ 0    ;marks the end of a string
        org &8000

	LD HL,StringA
loop:
	ld a,(HL)		;load a char from (HL)
	cp Terminator	        ;is it the terminator?
	ret z			;if so, exit
	ld e,a			;store this char in E temporarily
	inc hl			;next char
	ld a,(HL)		;get next char
	cp Terminator	        ;is the next char the terminator?
	jp z,StringDone	        ;if so, print E and exit.

	;needed to prevent the last char from getting a comma and space.
	
	dec hl			;go back one so we don't skip any chars
	cp e                    ;does (HL) == (HL+1)?
	
	push af
	ld a,e
	call PrintChar		;either way, print E to screen.
	pop af			;retrieve the results of the last compare.
	
	jr z,SkipComma		;if A=E, no comma or space. Just loop again.
	ld a,','
	call PrintChar
	ld a,' '
	call PrintChar
SkipComma:
	inc hl			;next char
	jp loop			;back to start
StringDone:
	ld a,e			;last character in string is printed here.
	jp PrintChar
	
ReturnToBasic:
	RET
	

StringA:
	byte "gHHH5YY++///\",0
