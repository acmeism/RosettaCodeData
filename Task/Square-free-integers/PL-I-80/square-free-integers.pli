SquareFreeDemo: proc options (main);

    %replace
       false by '0'b,
       true by '1'b;

    dcl (i, found) fixed bin;

    put skip list ('Square-free integers from 1 to 145');
    put skip;
    found = 0;
    do i = 1 to 145;
        if (square_free(i)) then
            do;
                put edit (i) (f(4));
                found = found + 1;
		if (mod(found, 16) = 0) then put skip;
            end;
    end;
    put skip edit (found, ' were found') (f(3), a);

    call report_number_found(100);
    call report_number_found(1000);
    call report_number_found(10000);

    stop;

/* report number of square-free integers from 1 to limit */
report_number_found: proc(limit);
   dcl (limit, i, found);
   put skip edit ('Number of square-free integers from 1 to',
      limit, ':') (a, f(6), a);
   found = 0;
   do i = 1 to limit;
      if (square_free(i)) then found = found + 1;
   end;
   put edit (found) (f(5));
end report_number_found;

/* return true if n has no square divisors other than 1 */
square_free: proc (n) returns (bit(1));
    dcl (n, i, sq) fixed bin;
    i = 2;
    sq = i * i;
    do while (sq <= n);
	if (mod(n, sq) = 0) then return (false);
        i = i + 1;
        sq = i * i;
    end;
    return (true);
end square_free;

end SquareFreeDemo;
