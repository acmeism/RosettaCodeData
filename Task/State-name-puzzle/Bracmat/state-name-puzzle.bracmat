(     Alabama
      Alaska
      Arizona
      Arkansas
      California
      Colorado
      Connecticut
      Delaware
      Florida
      Georgia
      Hawaii
      Idaho
      Illinois
      Indiana
      Iowa
      Kansas
      Kentucky
      Louisiana
      Maine
      Maryland
      Massachusetts
      Michigan
      Minnesota
      Mississippi
      Missouri
      Montana
      Nebraska
      Nevada
      "New Hampshire"
      "New Jersey"
      "New Mexico"
      "New York"
      "North Carolina"
      "North Dakota"
      Ohio
      Oklahoma
      Oregon
      Pennsylvania
      "Rhode Island"
      "South Carolina"
      "South Dakota"
      Tennessee
      Texas
      Utah
      Vermont
      Virginia
      Washington
      "West Virginia"
      Wisconsin
      Wyoming
  : ?states
& "New Kory" "Wen Kory" "York New" "Kory New" "New Kory":?extrastates
& ( "State name puzzle"
  =     allStates State state statechars char
      , A Z S1 S2 S3 S4 L1 L2 L3 L4 L12
    .   0:?allStates
      &   whl
        ' ( !arg:%?State ?arg
          & low$!State:?state
          & 0:?statechars
          &   whl
            ' ( @(!state:? (%@:~" ":?char) ?state)
              & !char+!statechars:?statechars
              )
          & (!State.!statechars)+!allStates:?allStates
          )
      & (   !allStates
          :   ?
            + ?*(?S1.?L1)
            + ?A
            + ?*(?S2.?L2)
            + ( ?Z
              & !L1+!L2:?L12
              &   !A+!Z
                :   ?
                  + ?*(?S3.?L3&!L12+-1*!L3:?L4)
                  + ?
                  +   ?
                    * ( ?S4
                      .   !L4
                        & out$(!S1 "+" !S2 "=" !S3 "+" !S4)
                        & ~
                      )
                  + ?
              )
        | out$"No more solutions"
        )
  )
& "State name puzzle"$!states
& "State name puzzle"$(!states !extrastates)
);
