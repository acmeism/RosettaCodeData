100 HOME : rem  100 CLS for Chipmunk Basic, GW-BASIC and MSX BASIC
110 CLEAR : n = 100 : GOSUB 150 : rem no se requiere CLEAR
120 CLEAR : n = 1000 : GOSUB 150
130 CLEAR : n = 10000 : GOSUB 150
140 END
150 rem SUB sample(n)
160  DIM samp(n)
170  FOR i = 1 TO n
180   samp(i) = RND(1)
190  NEXT i
200  rem calculate mean, standard deviation
210  sum = 0
220  sumsq = 0
230  FOR i = 1 TO n
240   sum = sum+samp(i)
250   sumsq = sumsq+samp(i)^2
260  NEXT i
270  PRINT "Sample size ";n
280  mean = sum/n
290  PRINT
300  PRINT "  Mean    = ";mean
310  PRINT "  Std Dev = ";(sumsq/n-mean^2)^0.5
320  PRINT
330  rem------- Show histogram
340  scal = 10
350  DIM bins(scal)
360  FOR i = 1 TO n
370   z = INT(scal*samp(i))
380   bins(z) = bins(z)+1
390  NEXT i
400  FOR b = 0 TO scal-1
410  PRINT "  ";b;" : ";
420   FOR j = 1 TO INT(scal*bins(b))/n*70
430    PRINT "*";
440   NEXT j
450   PRINT
460  NEXT b
470  PRINT
480 RETURN
