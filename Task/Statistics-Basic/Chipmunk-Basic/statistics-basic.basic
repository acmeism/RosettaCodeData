100 sub sample(n)
110  dim samp(n)
120  for i = 1 to n
130   samp(i) = rnd(1)
140  next i
150  rem calculate mean, standard deviation
160  sum = 0
170  sumsq = 0
180  for i = 1 to n
190   sum = sum+samp(i)
200   sumsq = sumsq+samp(i)^2
210  next i
220  print "Sample size ";n
230  mean = sum/n
240  print
250  print "  Mean    = ";mean
260  print "  Std Dev = ";(sumsq/n-mean^2)^0.5
270  print
280  rem------- Show histogram
290  scal = 10
300  dim bins(scal)
310  for i = 1 to n
320   z = int(scal*samp(i))
330   bins(z) = bins(z)+1
340  next i
350  for b = 0 to scal-1
360  print "  ";b;" : ";
370   for j = 1 to int(scal*bins(b))/n*70
380    print "*";
390   next j
400   print
410  next b
420  print
430 end sub
440 cls
450 sample(100)
460 sample(1000)
470 sample(10000)
480 end
