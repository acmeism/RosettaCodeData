[(Int, Int) = BigInt] computed

F sterling1(n, k)
   V key = (n, k)

   I key C :computed
      R :computed[key]
   I n == k == 0
      R BigInt(1)
   I n > 0 & k == 0
      R BigInt(0)
   I k > n
      R BigInt(0)
   V result = sterling1(n - 1, k - 1) + (n - 1) * sterling1(n - 1, k)
   :computed[key] = result
   R result

print(‘Unsigned Stirling numbers of the first kind:’)
V MAX = 12
print(‘n/k’.ljust(10), end' ‘’)
L(n) 0 .. MAX
   print(String(n).rjust(10), end' ‘’)
print()
L(n) 0 .. MAX
   print(String(n).ljust(10), end' ‘’)
   L(k) 0 .. n
      print(String(sterling1(n, k)).rjust(10), end' ‘’)
   print()
print(‘The maximum value of S1(100, k) = ’)
BigInt previous = 0
L(k) 1 .. 100
   V current = sterling1(100, k)
   I current > previous
      previous = current
   E
      print("#.\n(#. digits, k = #.)\n".format(previous, String(previous).len, k - 1))
      L.break
