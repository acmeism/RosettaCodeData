		org	100h
		jmp	demo
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; Strip a set of chracters from a string, in place.
		;;; Input:
		;;;   DE = $-terminated string to be stripped
		;;;   HL = $-terminated string containing characters to strip
stripchars:	push	h	; Store characters to strip on stack.
		mov	b,d	; Copy input string pointer to BC. This will be
		mov	c,e	; the target pointer.
stripchr:	ldax	d	; Copy current character from [DE] to [BC]
		stax	b
		cpi	'$'	; Done?
		jz	stripdone
		pop	h	; Get string of characters to strip.
		push 	h
stripsrch:	mvi	a,'$'	; At the end?
		cmp	m
		jz	srchdone
		ldax	d	; Does it match the character in the input?
		cmp	m
		jz	srchfound
		inx	h	; Look at next character to strip
		jmp	stripsrch
srchfound:	dcx	b	; Found: copy next character over it later.
srchdone:	inx	b	; Increment both pointers
		inx	d
		jmp	stripchr
stripdone:	pop	h	; Remove temporary variable from stack
		ret		; Done
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
demo:		lxi	d,string	; Strip from the string,
		lxi	h,remove	; the characters to remove.
		call	stripchars
		lxi	d,string	; Print the result.
		mvi	c,9
		jmp	5
string:		db	'She was a soul stripper. She took my heart!$'
remove:		db	'aei$'
