		bits	16
		cpu	8086
section		.text
		org	100h
		jmp	demo
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; Strip a set of characters from a string, in place.
		;;; Input:
		;;;    DS:DI = $-terminated string to be stripped.
		;;;    DS:SI = $-terminated string containing chars to strip
stripchars:	mov	bx,di	; Copy string ptr to use as target ptr
		mov	dx,si	; Copy ptr to characters to strip	
.char:		mov	al,[di]	; Copy character
		mov	[bx],al
		cmp	al,'$'	; Done?
		je	.done
		mov	si,dx	; See if character should be stripped
.search:	mov	ah,[si]
		cmp	ah,'$'	; End of characters to strip?
		je	.srchdone
		cmp	ah,al	; Does it match the current character?
		je	.srchfound
		inc	si	; Try next character
		jmp	.search
.srchfound:	dec	bx	; Found - decrement target pointer
.srchdone:	inc	bx	; Increment both pointers
		inc	di
		jmp	.char
.done:		ret
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
demo:		mov	di,string	; Strip from the string,
		mov	si,remove	; the characters to remove.
		call	stripchars
		mov	dx,string	; Print the result
		mov	ah,9
		int	21h
		ret
section		.data
string:		db	'She was a soul stripper. She took my heart!$'
remove:		db	'aei$'
