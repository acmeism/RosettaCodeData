stripper: proc options (main);
   declare s character (100) varying;
   declare i fixed binary;

   s = 'the quick brown fox jumped';
   /* A loop to replace blanks with control characters */
   do i = 1 to length(s);
      if substr(s, i, 1) = ' ' then
         substr(s, i, 1) = '01'x;
   end;
   put skip list (s);

   call stripcc (s);
   put skip list (s);

   s = 'now is the time for all good men';
   /* A loop to replace blanks with control characters */
   do i = 1 to length(s);
      if substr(s, i, 1) = ' ' then
         substr(s, i, 1) = 'A1'x;
   end;
   put skip list (s);

   call stripex (s);
   put skip list (s);

/* Strip control codes. */
stripcc: procedure (s);
   declare s character (*) varying;
   declare w character (length(s));
   declare c character (1);
   declare (i, j) fixed binary;

   j = 0;
   do i = 1 to length (s);
      c = substr(s, i, 1);
      if unspec(c) >= '00100000'b | unspec(c) = '01111111'b then
         do;
            j = j + 1;
            substr(w, j, 1) = c;
         end;
   end;
   s = substr(w, 1, j);
end stripcc;

/* Strips control codes and extended characters. */
stripex: procedure (s);
   declare s character (*) varying;
   declare w character (length(s));
   declare c character (1);
   declare (i, j) fixed binary;

   j = 0;
   do i = 1 to length (s);
      c = substr(s, i, 1);
      if unspec(c) >= '00100000'b & unspec(c) < '01111111'b then
         do;
            j = j + 1;
            substr(w, j, 1) = c;
         end;
   end;
   s = substr(w, 1, j);
end stripex;

end stripper;
