DEFINE SPACE="32"
DEFINE TAB="127"
DEFINE NEWLINE="155"

BYTE FUNC IsWhitespace(CHAR c)
  IF c=SPACE OR c=TAB OR c=NEWLINE THEN
    RETURN (1)
  FI
RETURN (0)

PROC Strip(CHAR ARRAY src,dst BYTE head,tail)
  BYTE i,first,last

  first=1
  last=src(0)

  IF head THEN
    WHILE first<=last
    DO
      IF IsWhitespace(src(first))=0 THEN
        EXIT
      FI
      first==+1
    OD
  FI

  IF tail THEN
    WHILE last>=first
    DO
      IF IsWhitespace(src(last))=0 THEN
        EXIT
      FI
      last==-1
    OD
  FI

  IF first>last THEN
    dst(0)=0
  ELSE
    SCopyS(dst,src,first,last)
  FI
RETURN

PROC Main()
  CHAR ARRAY src="    Action!    ",dst(20)

  src(2)=NEWLINE src(13)=TAB

  PrintF("Original string:%E""%S""%E%E",src)
  Strip(src,dst,1,0)
  PrintF("Trim leading whitespaces:%E""%S""%E%E",dst)
  Strip(src,dst,0,1)
  PrintF("Trim trailing whitespaces:%E""%S""%E%E",dst)
  Strip(src,dst,1,1)
  PrintF("Trim leading and trailing whitespaces:""%S""%E%E",dst)
RETURN
