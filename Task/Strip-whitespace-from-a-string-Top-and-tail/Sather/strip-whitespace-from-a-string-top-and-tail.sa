class MAIN is
    ltrim(s :STR) :STR is
      i ::= 0;
      loop while!(i < s.size);
        if " \t\f\v\n".contains(s[i]) then
           i := i + 1;
        else
           break!;
        end;
      end;
      return s.tail(s.size - i);
    end;

    rtrim(s :STR) :STR is
      i ::= s.size-1;
      loop while!(i >= 0);
        if " \t\f\v\n".contains(s[i]) then
           i := i - 1;
        else
           break!;
        end;
      end;
      return s.head(i+1);
    end;

    trim(s :STR) :STR is
       return ltrim(rtrim(s));
    end;


    main is
      p ::= "     this is a string     ";
      #OUT + ltrim(p).pretty + "\n";
      #OUT + rtrim(p).pretty + "\n";
      #OUT + trim(p).pretty + "\n";
    end;
end;
