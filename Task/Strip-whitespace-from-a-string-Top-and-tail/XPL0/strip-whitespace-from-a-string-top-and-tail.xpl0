code ChOut=8, CrLf=9, Text=12;
string 0;                       \use zero-terminated string convention

func StripLead(S0);             \Strip leading whitespace (<=$20) from string
char S0;
char S1(80);                    \BEWARE: very temporary string space returned
int  I, J, C, Stripped;
[I:= 0;  J:= 0;  Stripped:= false;
repeat  C:= S0(I);  I:= I+1;
        if Stripped ! C>$20 ! C=0 then
            [S1(J):= C;  J:= J+1;  Stripped:= true];
until   C=0;
return S1;
];

func StripTrail(S0);            \Strip trailing whitespace (<=$20) from string
char S0;
char S1(80);                    \BEWARE: very temporary string space returned
int  I, SI;
[I:= 0;
loop    [if S0(I) <= $20 then
             [SI:= I;
             while S0(I) <= $20 do
                [if S0(I) = 0 then [S1(SI):= 0;  return S1];
                S1(I):= S0(I);  I:= I+1;
                ];
             ]
        else [S1(I):= S0(I);  I:= I+1];
        ];
];

char String;
[String:= "  Hello World  ";
Text(0, StripLead(String));  ChOut(0, ^!);  CrLf(0);
Text(0, StripTrail(String));  ChOut(0, ^!);  CrLf(0);
Text(0, StripTrail(StripLead(String)));  ChOut(0, ^!);  CrLf(0);
]
