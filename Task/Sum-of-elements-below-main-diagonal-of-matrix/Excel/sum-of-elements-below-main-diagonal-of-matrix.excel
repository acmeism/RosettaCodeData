=LAMBDA(isUpper,
    LAMBDA(matrix,
        LET(
            nCols, COLUMNS(matrix),
            nRows, ROWS(matrix),
            ixs, SEQUENCE(nRows, nCols, 0, 1),
            x, MOD(ixs, nCols),
            y, QUOTIENT(ixs, nRows),

            IF(nCols=nRows,
                LET(
                    p, LAMBDA(x, y,
                        IF(isUpper, x > y, x < y)
                    ),

                    IF(p(x, y),
                        INDEX(matrix, 1 + y, 1 + x),
                        0
                    )
                ),
                "Matrix not square"
            )
        )
    )
)
