integer b, i, j, k, l, p, s, z;
index r, w;

i = 0;
while (i < 512) {
    b = i.bcount;
    j = 0;
    while (j < 1 << b) {
        data e;

        j += 1;

        k = s = p = 0;
        l = j;
        z = 1;
        while (k < 9) {
            if (i & 1 << k) {
                e.append("-+"[l & 1]);
                s += p * z;
                z = (l & 1) * 2 - 1;
                l >>= 1;
                p = 0;
            }
            e.append('1' + k);
            p = p * 10 + 1 + k;

            k += 1;
        }

        s += p * z;

        if (e[0] != '+') {
            if (s == 100) {
                o_(e, "\n");
            }

            w[s] += 1;
        }
    }

    i += 1;
}

w.wcall(i_fix, 1, 1, r);

o_(r.back, "\n");

k = 0;
for (+k in w) {
    if (!w.key(k + 1)) {
        o_(k + 1, "\n");
        break;
    }
}

i = 10;
for (k of w) {
    o_(k, "\n");
    if (!(i -= 1)) {
        break;
    }
}
