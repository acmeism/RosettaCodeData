module TaxiCab (f as long){
	cls,0
	Print Part "Taxicab numbers"
	Print Under
	profiler
	var Cubes=list, 	Sums=list, 	Ret=list
	var st=0, en=1200
	st=@Proc(0, en)
	sort ret as number
	Display(1, 25)
	Display(2000, 2006)
	Print timecount
	print "done"
	end
	sub Display(from, to)
		local k=each(ret, from,to), s=""
		while k
			s= format$("{0:-6} {1:-12}",K^+1, val(eval$(k!)+"&&"))+eval$(k)
		 	Print s : if f>-1 then print #f, s
		end while
	end sub
	function Proc(ia, ib)
		local i, cube as long long, s as long long
		for i=ia to ib
			if i mod 10=1 then print over $("#0.00"), "Working..";(i-ia)/ib*100;"%"
			cube=i^3
			Append Cubes, cube
			k=each(cubes)
			while k
				s=cube+eval(k)
				if not exist(Sums, s) then
					append Sums,s:=(i)+"^3 + "+(k^)+"^3"
				else.if not exist(Ret, s) then
					append Ret, s:=" = "+(i)+"^3 + "+(k^)+"^3 = "+eval$(Sums)
				end if
			end while
		next
		print over $("#0.00"), "Working..";100;"%"
		print
		=i
	end function
}
file2export="TaxiCabNumbers.txt"
open file2export for wide output as #f
TaxiCab f
close #f
win dir$+"TaxiCabNumbers.txt"
