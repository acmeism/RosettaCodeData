_window = 1
begin enum 1
  _kLabel
  _kFld
  _cLabel
  _cFld
  _fLabel
  _fFld
  _rLabel
  _rFld
end enum

void local fn BuildWindow
  window 1, @"Temperature conversion", (0,0,200,133), NSWindowStyleMaskTitled
  textlabel _kLabel, @"K:", (58,94,18,16)
  textfield _kFld,,, (81,92,60,21)
  textlabel _cLabel, @"C:", (57,68,18,16)
  textlabel _cFld,, (81,68,62,16)
  textlabel _fLabel, @"F:", (59,44,18,16)
  textlabel _fFld,, (81,44,62,16)
  textlabel _rLabel, @"R:", (58,20,18,16)
  textlabel _rFld,, (81,20,62,16)
end fn

void local fn CalcTemperatures
  double k = fn ControlDoubleValue( _kFld )
  ControlSetDoubleValue( _cFld, k - 273.15 )
  ControlSetDoubleValue( _fFld, k * 1.8 - 459.67 )
  ControlSetDoubleValue( _rFld, k * 1.8 )
end fn

void local fn DoDialog( ev as long )
  select ( ev )
    case _textFieldDidEndEditing : fn CalcTemperatures
  end select
end fn

fn BuildWindow

on dialog fn DoDialog

HandleEvents
