  [ $ "bigrat.qky" loadfile ] now!

  [ 5 9 v* ]                        is r->k ( n/d --> n/d )
  [ 1/v r->k 1/v ]                  is k->r ( n/d --> n/d )

  [ 45967 100 v- ]                  is r->f ( n/d --> n/d )
  [ -v r->f -v ]                    is f->r ( n/d --> n/d )

  [ 5463 20 v- ]                    is k->c ( n/d --> n/d )
  [ -v k->c -v ]                    is c->k ( n/d --> n/d )

  [ k->r r->f ]                     is k->f ( n/d --> n/d )
  [ f->r r->k ]                     is f->k ( n/d --> n/d )

  [ r->k k->c ]                     is r->c ( n/d --> n/d )
  [ c->k k->r ]                     is c->r ( n/d --> n/d )

  [ f->k k->c ]                     is f->c ( n/d --> n/d )
  [ c->r r->f ]                     is c->f ( n/d --> n/d )

  [ $->v drop
         2dup 10 point$ echo$
    say " Kelvins is equal to" cr
    k->c 2dup 10 point$ echo$
    say " degrees Celcius"     cr
    c->f 2dup 10 point$ echo$
    say " degrees Fahrenheit"  cr
    f->r      10 point$ echo$
    say " degrees Rankine"     cr ] is task (   $ -->     )

  $ "21.00" task
