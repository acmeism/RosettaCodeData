void local fn BuildWindow
  window 1, @"The Name Game", (0,0,300,200), NSWindowStyleMaskTitled
  textlabel 1, @"Name:", (18,161,44,16)
  textfield 2,,, (68,159,212,21)
  WindowMakeFirstResponder( 1, 2 )
end fn

void local fn OutputLyrics( name as CFStringRef )
  CFStringRef b = @"b", f = @"f", m = @"m", name2 = lcase(name)
  BOOL vowel = NO
  select ( ucc(name2) )
    case _"a", _"e", _"i", _"o", _"u" : vowel = YES
    case _"b" : b = @""
    case _"f" : f = @""
    case _"m" : m = @""
  end select
  if ( !vowel ) then name2 = fn StringSubstringFromIndex( name, 1 )

  CFStringRef lyrics = concat(name,@", ",name,@", bo-",b,name2,@"\n")
  lyrics = concat(lyrics,@"Banana-fana fo-",f,name2,@"\n")
  lyrics = concat(lyrics,@"Fee-fi-mo-",m,name2,@"\n",name,@"!")

  cls : print %(3,70)lyrics
end fn

void local fn DoDialog( ev as long, tag as long )
  select ( ev )
    case _textFieldDidEndEditing : fn OutputLyrics( textfield(tag) )
  end select
end fn

fn BuildWindow

on dialog fn DoDialog

HandleEvents
