get "libhdr"

let ordinal(n) =
    n=1 -> "first",     n=2 -> "second",    n=3 -> "third",
    n=4 -> "fourth",    n=5 -> "fifth",     n=6 -> "sixth",
    n=7 -> "seventh",   n=8 -> "eighth",    n=9 -> "ninth",
    n=10 -> "tenth",    n=11 -> "eleventh", n=12 -> "twelfth",
    valof finish

let gift(n) =
    n=1 -> "A partridge in a pear tree.",
    n=2 -> "Two turtle doves, and",
    n=3 -> "Three french hens,",
    n=4 -> "Four calling birds,",
    n=5 -> "Five golden rings,",
    n=6 -> "Six geese a-laying,",
    n=7 -> "Seven swans a-swimming,",
    n=8 -> "Eight maids a-milking,",
    n=9 -> "Nine ladies dancing,",
    n=10 -> "Ten lords a-leaping,",
    n=11 -> "Eleven pipers piping,",
    n=12 -> "Twelve drummers drumming,",
    valof finish

let verse(n) be
$(  writef("On the %S day of Christmas,*N", ordinal(n))
    writes("My true love gave to me:*N")
    for i=n to 1 by -1 do writef("%S*N", gift(i))
    wrch('*N')
$)

let start() be for n=1 to 12 do verse(n)
