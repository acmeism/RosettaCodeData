        .TITLE  CAROL
        .MCALL  .TTYOUT,.EXIT
CAROL:: CLR     R5
1$:     MOV     #ONTHE,R1
        JSR     PC,PRINT
        MOV     ORDTAB(R5),R1
        JSR     PC,PRINT
        MOV     #DAYOF,R1
        JSR     PC,PRINT
        MOV     VRSTAB(R5),R1
        JSR     PC,PRINT
        ADD     #2,R5
        CMP     R5,#^D24
        BLT     1$
        .EXIT

PRINT:  MOVB    (R1)+,R0
        .TTYOUT
        BNE     PRINT
        RTS     PC

ONTHE:  .ASCIZ  /On the /
ORDTAB: .WORD   1$,2$,3$,4$,5$,6$,7$,8$,9$,10$,11$,12$
1$:     .ASCIZ  /first/
2$:     .ASCIZ  /second/
3$:     .ASCIZ  /third/
4$:     .ASCIZ  /fourth/
5$:     .ASCIZ  /fifth/
6$:     .ASCIZ  /sixth/
7$:     .ASCIZ  /seventh/
8$:     .ASCIZ  /eighth/
9$:     .ASCIZ  /ninth/
10$:    .ASCIZ  /tenth/
11$:    .ASCIZ  /eleventh/
12$:    .ASCIZ  /twelfth/
DAYOF:  .ASCIZ  / day of Christmas, my true love gave to me:/<15><12>
VRSTAB: .WORD   1$,2$,3$,4$,5$,6$,7$,8$,9$,10$,11$,12$
12$:    .ASCII  /Twelve drummers drumming/<15><12>
11$:    .ASCII  /Eleven pipers piping/<15><12>
10$:    .ASCII  /Ten lords a-leaping/<15><12>
9$:     .ASCII  /Nine ladies dancing/<15><12>
8$:     .ASCII  /Eight maids a-milking/<15><12>
7$:     .ASCII  /Seven swans a-swimming/<15><12>
6$:     .ASCII  /Six geese a-laying/<15><12>
5$:     .ASCII  /Five golden rings/<15><12>
4$:     .ASCII  /Four calling birds/<15><12>
3$:     .ASCII  /Three French hens/<15><12>
2$:     .ASCII  /Two turtle doves, and/<15><12>
1$:     .ASCIZ  /A partridge in a pear tree./<15><12><15><12>
        .END    CAROL
