void
Add_Employee(record employees, text name, text id, integer salary,
	     text department)
{
    employees[name] = l_effect(name, id, salary, department);
}

void
collect(record top, record employees)
{
    text name;
    list l;

    for (name, l in employees) {
        list department;
        integer i, salary;

        department = r_v_list(top, l[3]);

        salary = l[2];
        i = 0;
        while (i < ~department) {
            if (__list(department[i])[2] < salary) {
                break;
            }
            i += 1;
        }

        l_l_list(department, i, l);

        if (~department == N + 1) {
            l_delete(department, N);
        }
    }
}

void
print(record top)
{
    text department;
    list employees;

    for (department, employees in top) {
        integer i;
        list l;

        o_plan("Department ", department, "\n");

        for (i, l in employees) {
            o_form("  ~ | ~ | ~\n", l[0], l[1], l[2]);
        }
    }
}

integer
main(void)
{
    record employees, top;

    Add_Employee(employees, "Tyler Bennett  ", "E10297", 32000, "D101");
    Add_Employee(employees, "John Rappl     ", "E21437", 47000, "D050");
    Add_Employee(employees, "George Woltman ", "E00127", 53500, "D101");
    Add_Employee(employees, "Adam Smith     ", "E63535", 18000, "D202");
    Add_Employee(employees, "Claire Buckman ", "E39876", 27800, "D202");
    Add_Employee(employees, "David McClellan", "E04242", 41500, "D101");
    Add_Employee(employees, "Rich Holcomb   ", "E01234", 49500, "D202");
    Add_Employee(employees, "Nathan Adams   ", "E41298", 21900, "D050");
    Add_Employee(employees, "Richard Potter ", "E43128", 15900, "D101");
    Add_Employee(employees, "David Motsinger", "E27002", 19250, "D202");
    Add_Employee(employees, "Tim Sampair    ", "E03033", 27000, "D101");
    Add_Employee(employees, "Kim Arlich     ", "E10001", 57000, "D190");
    Add_Employee(employees, "Timothy Grove  ", "E16398", 29900, "D190");

    collect(top, employees);

    print(top);

    return 0;
}
