proc Sort(Array, Field, Size);  \Sort Array in descending order by Field
int  Array, Field, Size, I, J, T;
[for J:= Size-1 downto 0 do
    for I:= 0 to J-1 do
        if Array(I,Field) < Array(I+1,Field) then
            [T:= Array(I);  Array(I):= Array(I+1);  Array(I+1):= T];
];

int Data, I, I0, Dept, N, Cnt;
[Data:=[["Tyler Bennett",   "E10297", 32000, 101],
        ["John Rappl",      "E21437", 47000, 050],
        ["George Woltman",  "E00127", 53500, 101],
        ["Adam Smith",      "E63535", 18000, 202],
        ["Claire Buckman",  "E39876", 27800, 202],
        ["David McClellan", "E04242", 41500, 101],
        ["Rich Holcomb",    "E01234", 49500, 202],
        ["Nathan Adams",    "E41298", 21900, 050],
        ["Richard Potter",  "E43128", 15900, 101],
        ["David Motsinger", "E27002", 19250, 202],
        ["Tim Sampair",     "E03033", 27000, 101],
        ["Kim Arlich",      "E10001", 57000, 190],
        ["Timothy Grove",   "E16398", 29900, 190],
        [0,                  0,       0,     000]];     \sentinel
I:= 0;                  \find number of employees = Data size
while Data(I,0) # 0 do I:= I+1;
Sort(Data, 3, I);       \sort by department field (3)
I:= 0;                  \sort by salary within each department
while Data(I,0) do
        [Dept:= Data(I,3);
        I0:= I;
        repeat I:= I+1 until Data(I,3) # Dept;
        Sort(@Data(I0), 2, I-I0);
        ];
N:= IntIn(0);                           \get parameter
I:= 0;
loop    [Dept:= Data(I,3);              \for each department
        Text(0, "Department D");
        if Dept < 100 then ChOut(0, ^0);  IntOut(0, Dept);  Text(0, ":^m^j");
        Cnt:= 0;
        loop    [if Cnt >= N then
                    while Data(I,3) = Dept do I:= I+1;  \skip any remaining
                if Data(I,3) # Dept then quit;
                IntOut(0, Data(I,2)); Text(0, "   ");   \salary
                Text(0, Data(I,1));   Text(0, "   ");   \ID
                Text(0, Data(I,0));   CrLf(0);          \name
                Cnt:= Cnt+1;
                I:= I+1;
                ];
        if Data(I,0) = 0 then quit;
        ];
]
