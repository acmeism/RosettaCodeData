import ballerina/io;

class Hanoi {
    private int moves;

    function init(int disks) {
        self.moves = 0;
        io:println("Towers of Hanoi with ", disks, " disks:\n");
        self.move(disks, "L", "C", "R");
        io:println("\nCompleted in ", self.moves, " moves\n");
    }

    private function move(int n, string frm, string to, string via) {
        if n > 0 {
            self.move(n - 1, frm, via, to);
            self.moves += 1;
            io:println("Move disk ", n, " from ", frm, " to ", to);
            self.move(n - 1, via, to, frm);
        }
    }
}

public function main() {
    _ = new Hanoi(3);
    _ = new Hanoi(4);
}
