%newline { [ LIT2 0a -Console/write ] DEO }

|18 @Console/write

|0100

#0102 #0304 hanoi
POP2 POP2

BRK

@hanoi ( from spare to count -- from spare to count )
    ( moving 0 disks is no-op )
    DUP ?{ JMP2r }

    ( move disks 1..count-1 to the spare peg )
    #01 SUB ROT SWP hanoi
    ( from to spare count-1 )

    ( print the current move )
    ;dict/move print/str
    INCk #30 ORA .Console/write DEO
    STH2
    ;dict/from print/str
    OVR #30 ORA .Console/write DEO
    ;dict/to print/str
    DUP #30 ORA .Console/write DEO
	newline
    STH2r

    ( move disks 1..count-1 from the spare peg to the goal peg )
    STH ROT ROT STHr hanoi

    ( restore original parameters for convenient recursion )
    STH2 SWP STH2r INC

    JMP2r

@print/str ( str* -- )
    LDAk .Console/write DEO
    INC2 LDAk ?/str
    POP2 JMP2r

@dict
    &move "Move 20 "disk 2000
    &from 20 "from 20 "pole 2000
    &to 20 "to 20 "pole 2000
