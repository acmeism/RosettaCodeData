V MAX_PRIME = 1000000
V primes = [1B] * MAX_PRIME
primes[0] = primes[1] = 0B

V i = 2
L i * i < MAX_PRIME
   L(j) (i * i .< MAX_PRIME).step(i)
      primes[j] = 0B
   i++
   L i < MAX_PRIME & !primes[i]
      i++

F left_trunc(=n)
   V tens = 1
   L tens < n
      tens *= 10

   L n != 0
      I !:primes[n]
         R 0B
      tens I/= 10
      I n < tens
         R 0B
      n %= tens

   R 1B

F right_trunc(=n)
   L n != 0
      I !:primes[n]
         R 0B
      n I/= 10
   R 1B

L(n) (MAX_PRIME - 1 .< 0).step(-2)
   I left_trunc(n)
      print(‘Left: ’n)
      L.break

L(n) (MAX_PRIME - 1 .< 0).step(-2)
   I right_trunc(n)
      print(‘Right: ’n)
      L.break
