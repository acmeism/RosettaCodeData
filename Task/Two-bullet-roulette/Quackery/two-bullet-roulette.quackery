[ $ "bigrat.qky" loadfile ] now!

[ 0 6 of ]                      is empty    (   --> [   )

[ stack 0 ]                     is chamber  (   --> s   )

[ chamber take 1+ 6 mod
  chamber put ]                 is rotate1  (   -->     )

[ 1 swap chamber share poke
  rotate1 ]                     is /load    ( [ --> [   )

[ dup chamber share peek
  if rotate1 /load ]            is load     ( [ --> [   )

[ 6 random chamber replace ]    is spin     (   -->     )

[ dup dip
  [ chamber share peek ]
  rotate1 ]                     is fire     ( [ --> n [ )

[ ' empty swap join
  ' [ drop or ] join ]          is makesim  ( [ --> [   )

[ 0 ]'[ dup echo sp makesim
  10000 times
  [ dup do swap dip + ] drop
  100 4 point$ echo$ char %
  emit sp say "deaths" cr ]     is sim      (   -->     )

randomise
sim [ load spin load spin fire spin fire ]
sim [ load spin load spin fire fire ]
sim [ load load spin fire spin fire ]
sim [ load load spin fire fire ]
