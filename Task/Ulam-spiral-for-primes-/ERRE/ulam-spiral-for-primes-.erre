PROGRAM SPIRAL

!$INTEGER

CONST RIGHT=1,UP=2,LEFT=3,DOWN=4

!$DYNAMIC
DIM SPIRAL$[0,0]

PROCEDURE PRT_ULAM(N)
  FOR ROW=0 TO N DO
    FOR COL=0 TO N DO
        PRINT(SPIRAL$[ROW,COL];)
    END FOR
    PRINT
  END FOR
  PRINT
  GET(K$)
  FOR ROW=0 TO N DO
    FOR COL=0 TO N DO
        IF VAL(SPIRAL$[ROW,COL])<>0 THEN PRINT("  * ";)  ELSE PRINT(SPIRAL$[ROW,COL];) END IF
    END FOR
    PRINT
  END FOR
END PROCEDURE

PROCEDURE IS_PRIME(A->RES%)
     LOCAL N
     IF A=2 THEN RES%=TRUE EXIT PROCEDURE END IF
     IF A<=1 OR (A MOD 2=0) THEN RES%=FALSE EXIT PROCEDURE END IF
     MAX=SQR(A)
     FOR N=3 TO MAX STEP 2 DO
        IF (A MOD N=0) THEN RES%=FALSE EXIT PROCEDURE END IF
     END FOR
     RES%=TRUE
END PROCEDURE

PROCEDURE GEN_ULAM(N,I)
     DIR=RIGHT
     J=I
     Y=INT(N/2)
     IF (N MOD 2=0) THEN X=Y-1 ELSE X=Y END IF ! shift left for even n's
          WHILE J<=(N*N)-1+I DO

              IS_PRIME(J->RES%)
              IF RES% THEN SPIRAL$[Y,X]=RIGHT$("  "+STR$(J),4) ELSE SPIRAL$[Y,X]=" ---" END IF

              CASE DIR OF
                    RIGHT->
                       IF (X<=(N-1) AND SPIRAL$[Y-1,X]="" AND J>I) THEN DIR=UP END IF
                    END ->
                    UP->
                       IF SPIRAL$[Y,X-1]="" THEN DIR=LEFT END IF
                    END ->
                    LEFT->
                       IF (X=0) OR SPIRAL$[Y+1,X]="" THEN DIR=DOWN END IF
                    END ->
                    DOWN->
                       IF SPIRAL$[Y,X+1]="" THEN DIR=RIGHT END IF
                    END ->
              END CASE

              CASE DIR OF
                    RIGHT-> X=X+1 END ->
                    UP->    Y=Y-1 END ->
                    LEFT->  X=X-1 END ->
                    DOWN->  Y=Y+1 END ->
              END CASE
              J=J+1
          END WHILE
          PRT_ULAM(N)
END PROCEDURE

BEGIN
     N=9
     !$DIM SPIRAL$[N,N]
     GEN_ULAM(N,1)
END PROGRAM
