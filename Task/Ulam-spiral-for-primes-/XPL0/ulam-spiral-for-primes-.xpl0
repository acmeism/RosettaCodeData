func IsPrime(N);        \Return 'true' if N is prime
int  N, I;
[if N <= 2 then return N = 2;
if (N&1) = 0 then \even >2\ return false;
for I:= 3 to sqrt(N) do
    [if rem(N/I) = 0 then return false;
    I:= I+1;
    ];
return true;
];

int N, X, Y, Len, Dir, DX, DY, Side, Step;
[SetVid($13);
X:= 320/2;  Y:= 200/2;
N:= 1;
Len:= 1;
Dir:= 0;        \Rt, Up, Lt, Dn
DX:= [1, 0, -1, 0];
DY:= [0, -1, 0, 1];
repeat  for Side:= 1 to 2 do
            [for Step:= 1 to Len do
                [if IsPrime(N) then Point(X, Y, $F);
                N:= N+1;
                X:= X + DX(Dir); \move in current direction
                Y:= Y + DY(Dir);
                ];
            Dir:= (Dir+1) & 3;   \set next direction
            ];
        Len:= Len+1;
until   Y = 0;                  \reached top
]
