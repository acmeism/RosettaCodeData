var primes =Utils.Generator(Import("sieve.zkl").postponed_sieve);  // lazy
var offsets=Utils.cycle( T(0,1),T(-1,0),T(0,-1),T(1,0) );  // (N,E,S,W), lazy
const BLACK=0, WHITE=0xff|ff|ff, GREEN=0x00|ff|00, EMPTY=0x080|80|80;
fcn uspiral(N){
   if((M:=N).isEven) M+=1;  // need odd width, height
   img,p := PPM(M,M,EMPTY), primes.next(); // 2 .. 250,007: 22,045 primes
   x,y,n := N/2,x,2; img[x,y]=GREEN; x+=1; // start on 2 facing "north"
   while(True){
      ox,oy:=offsets.next(); leftx,lefty:=offsets.peek();  // set direction
      while(True){
	 img[x,y]=( if(n==p){ p=primes.next(); WHITE } else BLACK );
	 if(n==N*N) break(2); // all done
	 n+=1;
	 if(img[x+leftx,y+lefty]==EMPTY) // nothing to my left, turn left
	    { x+=leftx; y+=lefty; break; }
	 x+=ox; y+=oy;	// move in a straight line
      }
   }
   img
}

uspiral(500).write(File("ulamSpiral.ppm","wb"));
