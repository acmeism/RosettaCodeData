$ include "seed7_05.s7i";
  include "float.s7i";

const func integer: randN (in integer: n) is
  return ord(rand(1, n) = 1);

const func integer: unbiased (in integer: n) is func
  result
    var integer: unbiased is 0;
  begin
    repeat
      unbiased := randN(n);
    until unbiased <> randN(n);
  end func;

const proc: main is func
  local
    const integer: tests is 50000;
    var integer: n is 0;
    var integer: sumBiased is 0;
    var integer: sumUnbiased is 0;
    var integer: count is 0;
  begin
    for n range 3 to 6 do
      sumBiased := 0;
      sumUnbiased := 0;
      for count range 1 to tests do
        sumBiased +:= randN(n);
        sumUnbiased +:= unbiased(n);
      end for;
      writeln(n <& ": " <& flt(100 * sumBiased) / flt(tests) digits 3 lpad 6 <&
                   "  " <& flt(100 * sumUnbiased) / flt(tests) digits 3 lpad 6);
    end for;
  end func;
