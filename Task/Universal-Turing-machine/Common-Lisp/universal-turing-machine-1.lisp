(defun turing (initial terminal blank rules tape &optional (verbose NIL))
  (labels ((combine (front back)
             (if front
               (combine (cdr front) (cons (car front) back))
               back))

           (update-tape (old-front old-back new-content move)
             (cond ((eq move 'right)
                    (list (cons new-content old-front)
                          (cdr old-back)))
                   ((eq move 'left)
                    (list (cdr old-front)
                          (list* (car old-front) new-content (cdr old-back))))
                   (T (list old-front
                            (cons new-content (cdr old-back))))))

           (show-tape (front back)
             (format T "狺埝彷狺ア铗弼弪箦篚怏羼骝镱黹卑戾铉翳骝镱舂┅矧ㄣ狎忉汶忪犷氅篚怏羼ㄣ潋忉汶黹卑戾铉翳ㄣ潋忉汶┅┅┅祜镳骘忉汶翎疱翳孱铄鳝忉汶骘骝镱Ж翳孱铄鳝骝镱骘篝狒轭轸獒翳孱铄鳝篝狒骘泔铘孱矧ㄣ狎忉汶忪犷氅骘铄鳝篝狒铄鳝泔铘孱盹鲥ㄧ弭栳箬ㄣ镱篝狒泔铘孱舂蝓戾螬骘铄鳝骝镱铄鳝忉汶躔溽翦翎疱骝镱忉汶铄鳝泔铘孱盹鲥躅糸ㄥ聃犰篝狒翦蝽轭犰滹麒孱鲥蜮矬箬秣翎疱骝镱忉汶┅骈钺祆痱镧麒孱鲥蜮矬箬秣翎疱骝镱忉汶┅蝈趱蝾ㄣ镯忾铄骝镱忉汶┅┅┅