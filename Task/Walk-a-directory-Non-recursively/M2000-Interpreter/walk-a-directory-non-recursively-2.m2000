Module Show_Files {
      Function  get_files$ (folder_path$) {
            \\ we get second argument using letter$ which pop from stack
            pattern$=lcase$(Letter$)
            Declare  objfso "Scripting.FileSystemObject"
            Method objfso, "GetFolder", folder_path$ as fc
            With fc, "files" set files
            \\ from revision 13 - version 9.4
            With files, -4& as EnumFile
            With EnumFile, "Name" as name$
            Dim empty$()
            =empty$()
            Stack New {
                  While EnumFile {
                        If lcase$(name$) ~ pattern$ Then Data name$
                  }
                  \\ get stack values and fill an array
                  =Array$([])
            }
      }
      Dim Name$()
      Name$()=get_files$("C:\Windows","*.exe")
      m=each(Name$())
      While m {
            Print Array$(m)
      }
}
Show_Files
