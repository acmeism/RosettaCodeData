Function PrimeFactors(n As Uinteger) As Uinteger Ptr
    Dim As Uinteger Ptr factors = Callocate(30, Sizeof(Uinteger))
    Dim As Uinteger cnt = 0, d = 2

    While n > 1
        While n Mod d = 0
            n \= d
            factors[cnt] = d
            cnt += 1
        Wend
        d += 1
        If d * d > n Then
            If n > 1 Then
                factors[cnt] = n
                cnt += 1
                Exit While
            End If
        End If
    Wend
    Return factors
End Function

Function DigitsCount(n As Uinteger, b As Uinteger) As Uinteger
    Dim As Uinteger cnt = 0
    Do
        cnt += 1
        n \= b
    Loop Until n = 0
    Return cnt
End Function

Sub Analyze(n As Uinteger, b As Uinteger, Byref digitsN As Uinteger, Byref digitsF As Uinteger)
    Dim As Uinteger Ptr factors = PrimeFactors(n)
    Dim As Uinteger i = 0, indiv = 0, expo = 0

    digitsN = DigitsCount(n, b)
    digitsF = 0
    While factors[i] <> 0
        indiv = factors[i]
        expo = 0
        While factors[i] = indiv
            expo += 1
            i += 1
        Wend
        digitsF += DigitsCount(indiv, b)
        If expo > 1 Then digitsF += DigitsCount(expo, b)
    Wend
    Deallocate(factors)
End Sub

Dim As Uinteger b, n, digitsN, digitsF, wc, ec, fc, wc2, ec2, fc2
Dim As Uinteger w(10000), e(10000), f(10000)

For b = 10 To 11
    wc = 0: ec = 1: fc = 0: wc2 = 0: ec2 = 1: fc2 = 0: n = 2
    e(0) = 1
    Print "FOR BASE "; b; !":\n"
    While fc < 10000 Or ec < 10000 Or wc < 10000
        Analyze(n, b, digitsN, digitsF)
        If digitsN < digitsF Then
            If (wc < 50 Or wc = 9999) Then w(wc) = n
            wc += 1
            If n < 1000000 Then wc2 += 1
        Elseif (digitsN = digitsF) Then
            If ec < 50 Or ec = 9999 Then e(ec) = n
            ec += 1
            If n < 1000000 Then ec2 += 1
        Else
            If fc < 50 Or fc = 9999 Then f(fc) = n
            fc += 1
            If n < 1000000 Then fc2 += 1
        End If
        n += 1
    Wend
    Print "First 50 Wasteful numbers:"
    For n = 0 To 49
        Print Using "#####"; w(n);
        If n Mod 10 = 9 Then Print
    Next

    Print !"\nFirst 50 Equidigital numbers:"
    For n = 0 To 49
        Print Using "#####"; e(n);
        If n Mod 10 = 9 Then Print
    Next n

    Print !"\nFirst 50 Frugal numbers:"
    For n = 0 To 49
        Print Using "#####"; f(n);
        If n Mod 10 = 9 Then Print
    Next n

    Print !"\n10,000th Wasteful number    : "; w(9999)
    Print "10,000th Equidigital number : "; e(9999)
    Print "10,000th Frugal number      : "; f(9999)

    Print !"\nFor natural numbers < 1 million, the breakdown is as follows:"
    Print "  Wasteful numbers    : "; wc2
    Print "  Equidigital numbers : "; ec2
    Print "  Frugal numbers      : "; fc2
    Print
Next b

Sleep
