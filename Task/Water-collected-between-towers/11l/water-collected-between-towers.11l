F water_collected(tower)
   V l = tower.len
   V highest_left = [0] [+] (1 .< l).map(n -> max(@tower[0 .< n]))
   V highest_right = (1 .< l).map(n -> max(@tower[n .< @l])) [+] [0]
   V water_level = (0 .< l).map(n -> max(min(@highest_left[n], @highest_right[n]) - @tower[n], 0))
   print(‘highest_left:   ’highest_left)
   print(‘highest_right:  ’highest_right)
   print(‘water_level:    ’water_level)
   print(‘tower_level:    ’tower)
   print(‘total_water:    ’sum(water_level))
   print(‘’)
   R sum(water_level)

V towers = [
   [1, 5, 3, 7, 2],
   [5, 3, 7, 2, 6, 4, 5, 9, 1, 2],
   [2, 6, 3, 5, 2, 8, 1, 4, 2, 2, 5, 3, 5, 7, 4, 1],
   [5, 5, 5, 5],
   [5, 6, 7, 8],
   [8, 7, 7, 6],
   [6, 7, 10, 7, 6]]

print(towers.map(tower -> water_collected(tower)))
