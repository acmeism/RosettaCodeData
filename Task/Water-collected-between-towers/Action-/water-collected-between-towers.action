PROC PrintArray(BYTE ARRAY a BYTE len)
  BYTE i

  Put('[)
  FOR i=0 TO len-1
  DO
    IF i>0 THEN
      Put(32)
    FI
    PrintB(a(i))
  OD
  Put('])
RETURN

BYTE FUNC Max(BYTE ARRAY a BYTE start,stop)
  BYTE i,res

  res=0
  FOR i=start TO stop
  DO
    IF a(i)>res THEN
      res=a(i)
    FI
  OD
RETURN (res)

BYTE FUNC CalcWater(BYTE ARRAY a BYTE len)
  BYTE water,i,maxL,maxR,lev

  IF len<3 THEN
    RETURN (0)
  FI
  water=0
  FOR i=1 TO len-2
  DO
    maxL=Max(a,0,i-1)
    maxR=Max(a,i+1,len-1)
    IF maxL<maxR THEN
      lev=maxL
    ELSE
      lev=maxR
    FI
    IF a(i)<lev THEN
      water==+lev-a(i)
    FI
  OD
RETURN (water)

PROC Test(BYTE ARRAY a BYTE len)
  BYTE water

  water=CalcWater(a,len)
  PrintArray(a,len)
  PrintF(" holds %B water units%E%E",water)
RETURN

PROC Main()
  DEFINE COUNT="7"
  BYTE ARRAY
    a1=[1 5 3 7 2],
    a2=[5 3 7 2 6 4 5 9 1 2],
    a3=[2 6 3 5 2 8 1 4 2 2 5 3 5 7 4 1],
    a4=[5 5 5 5],
    a5=[5 6 7 8],
    a6=[8 7 7 6],
    a7=[6 7 10 7 6]

  Test(a1,5)
  Test(a2,10)
  Test(a3,16)
  Test(a4,4)
  Test(a5,4)
  Test(a6,4)
  Test(a7,5)
RETURN
