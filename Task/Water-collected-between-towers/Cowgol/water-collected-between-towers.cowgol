include "cowgol.coh";
include "argv.coh";

# Count the amount of water in a given array
sub water(towers: [uint8], length: intptr): (units: uint8) is
    units := 0;
    loop
        var right := towers + length;
        loop
            right := @prev right;
            if right < towers or [right] != 0 then
                break;
            end if;
        end loop;
        if right < towers then break; end if;

        var blocks: uint8 := 0;
        var col := towers;
        while col <= right loop
            if [col] != 0 then
                [col] := [col] - 1;
                blocks := blocks + 1;
            elseif blocks != 0 then
                units := units + 1;
            end if;
            col := @next col;
        end loop;
        if blocks < 2 then
            break;
        end if;
    end loop;
end sub;

# Read list from the command line and print the answer
ArgvInit();
var towers: uint8[256];
var count: @indexof towers := 0;
var n32: int32;
loop
    var argmt := ArgvNext();
    if argmt == 0 as [uint8] then
        break;
    end if;
    (n32, argmt) := AToI(argmt);
    towers[count] := n32 as uint8;
    count := count + 1;
end loop;

if count == 0 then
    print("enter towers on command line\n");
    ExitWithError();
end if;

print_i8(water(&towers[0], count as intptr));
print_nl();
