templates histogramWater
  $ -> \( @: 0"1";
    [$... -> ($)"1"-> { leftMax: $ -> #, value: ($)"1" } ] !
    when <$@..> do @: $; $ !
    otherwise $@ !
  \) -> \( @: { rightMax: 0"1", sum: 0"1" };
    $(last..1:-1)... -> #
    $@.sum !
    when <{ value: <$@.rightMax..> }> do @.rightMax: $.value;
    when <{ value: <$.leftMax..> }> do !VOID
    when <{ leftMax: <..$@.rightMax>}> do @.sum: $@.sum + $.leftMax - $.value;
    otherwise  @.sum: $@.sum + $@.rightMax - $.value;
  \) !
end histogramWater

[[1, 5, 3, 7, 2],
  [5, 3, 7, 2, 6, 4, 5, 9, 1, 2],
  [2, 6, 3, 5, 2, 8, 1, 4, 2, 2, 5, 3, 5, 7, 4, 1],
  [5, 5, 5, 5],
  [5, 6, 7, 8],
  [8, 7, 7, 6],
  [6, 7, 10, 7, 6]]... -> '$ -> histogramWater; water in $;$#10;' -> !OUT::write
