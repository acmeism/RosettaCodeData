import system'routines;
import extensions;

public singleton program
{
    static games := new string[]{"12", "13", "14", "23", "24", "34"};

    static results := "000000";

    nextResult()
    {
        var s := results;

        if (results=="222222") { ^ false };

        results := (results.toInt(3) + 1).toString(3).padLeft($48, 6);

        ^ true
    }

    function()
    {
        var points := IntMatrix.allocate(4, 10);

        int counter := 0;
        do
        {
            var records := new int[]{0,0,0,0};

            for(int i := 0; i < 6; i += 1)
            {
                var r := results[i];
                r =>
                    "2" { records[games[i][0].toInt() - 49] := records[games[i][0].toInt() - 49] + 3 }
                    "1" {
                        records[games[i][0].toInt() - 49] := records[games[i][0].toInt() - 49] + 1;
                        records[games[i][1].toInt() - 49] := records[games[i][1].toInt() - 49] + 1
                    }
                    "0" { records[games[i][1].toInt() - 49] := records[games[i][1].toInt() - 49] + 3 };
            };

            records := records.ascendant();

            for(int i := 0; i <= 3; i += 1)
                {
                    points[i][records[i]] := points[i][records[i]] + 1
                }
        }
        while(program.nextResult());

        new Range(0, 4).zipForEach(new string[]{"1st", "2nd", "3rd", "4th"}, (i,l)
        {
            console.printLine(l,": ", points[3 - i].toArray())
        });
    }
}
