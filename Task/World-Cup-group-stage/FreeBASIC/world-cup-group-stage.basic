Dim Shared As Integer points(3, 9)
Dim Shared As String*2 games(5) = {"12", "13", "14", "23", "24", "34"}
Dim Shared As String result
result = "000000"

Sub SortArray(arr() As Integer)
    Dim As Integer i, j

    For i = 0 To Ubound(arr) - 1
        For j = i + 1 To Ubound(arr)
            If arr(i) > arr(j) Then Swap arr(i), arr(j)
        Next
    Next
End Sub

Function ParseInt(number As String, base_ As Integer) As Integer
    Dim As Integer i, x = 0, pot = 0

    For i = Len(number) To 1 Step -1
        x += base_ ^ pot * Val(Mid(number, i, 1))
        pot += 1
    Next

    Return x
End Function

Function Formato(decimal As Integer, base_ As Integer) As String
    Dim As Integer cociente
    Dim As String conv = ""

    Do
        cociente = decimal \ base_
        conv = Str(decimal Mod base_) + conv
        decimal = cociente
    Loop Until cociente = 0

    Return conv
End Function

Function NextResult() As Boolean
    If result = "222222" Then Return False

    Dim As Integer res = ParseInt(result, 3)
    result = Formato(res + 1, 3)
    While Len(result) < 6
        result = "0" & result
    Wend
    Return True
End Function

Function Compute() As Boolean
    'Dim records(3) As Integer
    Dim As Integer records(3), i, t

    For i = 0 To 5
        Select Case Mid(result, i + 1, 1)
        Case "2"
            t = Val(Mid(games(i), 1, 1)) - 1
            records(t) += 3
        Case "1"
            t = Val(Mid(games(i), 1, 1)) - 1
            records(t) += 1
            t = Val(Mid(games(i), 2, 1)) - 1
            records(t) += 1
        Case "0"
            t = Val(Mid(games(i), 2, 1)) - 1
            records(t) += 3
        End Select
    Next

    SortArray(records())

    For i = 0 To 3
        points(i, records(i)) += 1
    Next

    Return Iif(NextResult(), True, False)
End Function

Do While Compute()
Loop

Print "POINTS         0    1    2    3    4    5    6    7    8    9"
Print String (61, "-")
Dim As Integer i, j
Dim As String places(3) = {"1st", "2nd", "3rd", "4th"}

For i = 0 To 3
    Print places(i); " place  ";
    For j = 0 To 9
        Print Using "#####"; points(3 - i, j);
    Next
    Print
Next

Sleep
