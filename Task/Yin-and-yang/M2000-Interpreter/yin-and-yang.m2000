Module YinYang {
	cls 5,0
	Gradient 0, 5
	Double
	Print Over
	Cursor 0,0
	Report 2,  "阴阳 Yin and yang 음양"
	Normal
	Drawing {
	circle fill 0, 3000,1, 0
	circle fill 15, 3000,1,0, pi/2, -pi/2
	step 0, -1500
	circle fill 15, 1500,1,15
	width 4 {
		circle fill 0, 500,1,0
	}
	step 0, 3000
	circle fill 0, 1500,1,0
	width 4 {
		circle fill 15, 500,1,15
		step 0, -1500
		circle 3000,1,0
	}
	} as A
	Move 6000, 5000-1500
	Image A, 6000
	Move 2000, 5000
	Image A, 3000
	Move 2000+1500, 5000+1500
	hold // hold surface to release by statement release
	Mouse.Icon Hide
	i=0
	every 10 {
		if  inkey$=" " or mouse=2 then exit
		release
		move mouse.x, mouse.y
		Image A, 3000, ,i : i+=5:if i>355 then i=0
		Refresh 20
		if mouse=1 then hold
	}
	Mouse.Icon Show
	filename$="yin and yang.emf"
	// save to file
	Open filename$ for output as #f
	Put #f, A, 1
	Close #f
	// open mspaing
	win "mspaint", quote$(dir$+filename$)
}
YinYang
