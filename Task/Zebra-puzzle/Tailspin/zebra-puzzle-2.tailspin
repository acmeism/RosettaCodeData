templates permutations
  when <=1> do [1] !
  otherwise
    def n: $;
    templates expand
      def p: $;
      1..$n -> \(def k: $;
        [$p(1..$k-1)..., $n, $p($k..last)...] !\) !
    end expand
    $n - 1 -> permutations -> expand !
end permutations

def permutationsOf5: [5 -> permutations];

def nationalities: ['Englishman', 'Swede', 'Dane', 'Norwegian', 'German'];
def colours: ['red', 'green', 'white', 'yellow', 'blue'];
def pets: ['dog', 'birds', 'cats', 'horse', 'zebra'];
def drinks: ['tea', 'coffee', 'milk', 'beer', 'water'];
def smokes: ['Pall Mall', 'Dunhill', 'Blend', 'Blue Master', 'Prince'];

$permutationsOf5... -> $colours($) -> [$... -> {colour: $}]
  -> \(<[(<{colour: <='green'>}>:<{colour: <='white'>}>)]> $! \)
  -> \(def current: $;
       $permutationsOf5... -> $nationalities($) -> \[i]({$current($i), nationality: $}! \) !
     \)
  -> \(<?($(1) <{nationality: <='Norwegian'>}>)> $! \)
  -> \(<[<{nationality: <='Englishman'>, colour: <='red'>}>]> $! \)
  -> \(<[(<{nationality: <='Norwegian'>}>:<{colour: <='blue'>}>)] | [(<{colour: <='blue'>}>:<{nationality: <='Norwegian'>}>)]> $! \)
  -> \(def current: $;
       $permutationsOf5... -> $drinks($) -> \[i]({$current($i), drink: $}! \) !
     \)
  -> \(<?($(3) <{drink: <='milk'>}>)> $! \)
  -> \(<[<{drink: <='coffee'>, colour: <='green'>}>]> $! \)
  -> \(<[<{drink: <='tea'>, nationality: <='Dane'>}>]> $! \)
  -> \(def current: $;
       $permutationsOf5... -> $pets($) -> \[i]({$current($i), pet: $}! \)!
     \)
  -> \(<[<{nationality: <='Swede'>, pet: <='dog'>}>]> $! \)
  -> \(def current: $;
       $permutationsOf5... -> $smokes($) -> \[i]({$current($i), smoke: $}! \)!
     \)
  -> \(<[<{smoke: <='Pall Mall'>, pet: <='birds'>}>]> $! \)
  -> \(<[<{smoke: <='Dunhill'>, colour: <='yellow'>}>]> $! \)
  -> \(<[(<{smoke: <='Blend'>}>:<{pet: <='cats'>}>)] | [(<{pet: <='cats'>}>:<{smoke: <='Blend'>}>)]> $! \)
  -> \(<[(<{smoke: <='Dunhill'>}>:<{pet: <='horse'>}>)] | [(<{pet: <='horse'>}>:<{smoke: <='Dunhill'>}>)]> $! \)
  -> \(<[<{smoke: <='Blue Master'>, drink: <='beer'>}>]> $! \)
  -> \(<[<{smoke: <='Prince'>, nationality: <='German'>}>]> $! \)
  -> \(<[(<{smoke: <='Blend'>}>:<{drink: <='water'>}>)] | [(<{drink: <='water'>}>:<{smoke: <='Blend'>}>)]> $! \)
  -> \[i](when <{pet: <='zebra'>}> do 'The $.nationality; owns the zebra.$#10;' -> !OUT::write $!
          otherwise $! \)
  -> \[i]('$i;: $;'! \) -> '$... -> '$;$#10;';$#10;'
  -> !OUT::write
