  [ stack ]                  is stepcount (     --> s )
  [ stack ]                  is position  (     --> s )
  [ stack ]                  is heading   (     --> s )

  [ heading take
    behead join
    heading put ]            is turn      (     -->   )

  [ heading share 0 peek
    unrot times
      [ position share
        stepcount share
        unrot poke
        over position tally
        1 stepcount tally ]
     nip ]                   is walk      ( [ n --> [ )

  [ dip [ temp put [] ]
    temp share times
       [ temp share split
         dip
           [ nested join ] ]
    drop temp release ]      is matrixify ( n [ --> [ )

  [ 0 stepcount put      ( set up... )
    0 position put
    ' [ 1 ]
    over 1 - join
    over join
    over 1 - negate join
    heading put
    0 over dup * of

    over 1 - times       ( turtle draws first half of zigzag )
      [ 1 walk turn
        i^ 1+ walk turn ]

    heading take         ( reverse the sequence of turns )
    reverse heading put

    over 1 - times       ( turtle draws second half of zigzag )
      [ turn 1 walk
        turn i walk ]
    1 walk

    matrixify            ( ...tidy up )
    heading release
    position release
    stepcount release ]      is zigzag    (   n --> [ )

  10 zigzag
  witheach
    [ witheach
        [ dup 10 < if sp echo sp ]
      cr ]
